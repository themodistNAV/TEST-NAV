OBJECT Table 9087045 Style Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=16/11/16;
    Time=[ 4:48:08 PM];
    Modified=Yes;
    Version List=NS2016.16.1;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Style No.");
               IF (Rec."Sales Type" = Rec."Sales Type"::"All Customers") THEN
                 Rec.VALIDATE("Sales Code", '')
               ELSE
                 Rec.TESTFIELD("Sales Code");
             END;

    CaptionML=[ENU=Style Sales Price;
               ENG=Style Sales Price];
    LookupPageID=Page9087037;
    DrillDownPageID=Page9087037;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Style No.           ;Code20        ;TableRelation=Style;
                                                   OnValidate=BEGIN
                                                                IF (Rec."Style No." <> xRec."Style No.") THEN
                                                                BEGIN
                                                                  Style.GET(Rec."Style No.");
                                                                  Rec.VALIDATE("Unit of Measure Code", Style."Base Unit of Measure");
                                                                END;

                                                                IF "Sales Type" = "Sales Type"::"Customer Price Group" THEN
                                                                  IF CustPriceGr.GET("Sales Code") AND
                                                                     (CustPriceGr."Allow Invoice Disc." = "Allow Invoice Disc.")
                                                                  THEN
                                                                    EXIT;
                                                              END;

                                                   CaptionML=[ENU=Style No.;
                                                              ENG=Style No.] }
    { 15  ;   ;Colour Code         ;Code20        ;TableRelation=Colour;
                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 20  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN BEGIN
                                                                  VALIDATE("Sales Code",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Type;
                                                              ENG=Sales Type];
                                                   OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,Campaign;
                                                                    ENG=Customer,Customer Price Group,All Customers,Campaign];
                                                   OptionString=Customer,Customer Price Group,All Customers,Campaign }
    { 21  ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Price Group)) "Customer Price Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF "Sales Code" <> '' THEN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::"Customer Price Group":
                                                                      BEGIN
                                                                        CustPriceGr.GET("Sales Code");
                                                                        "Price Includes VAT" := CustPriceGr."Price Includes VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := CustPriceGr."VAT Bus. Posting Gr. (Price)";
                                                                        "Allow Line Disc." := CustPriceGr."Allow Line Disc.";
                                                                        "Allow Invoice Disc." := CustPriceGr."Allow Invoice Disc.";
                                                                      END;
                                                                    "Sales Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Sales Code");
                                                                        "Currency Code" := Cust."Currency Code";
                                                                        "Price Includes VAT" := Cust."Prices Including VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := Cust."VAT Bus. Posting Group";
                                                                        "Allow Line Disc." := Cust."Allow Line Disc.";
                                                                      END;
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Sales Code;
                                                              ENG=Sales Code] }
    { 30  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ENG=Currency Code] }
    { 40  ;   ;Price Includes VAT  ;Boolean       ;CaptionML=[ENU=Price Includes VAT;
                                                              ENG=Price Includes VAT] }
    { 41  ;   ;VAT Bus. Posting Gr. (Price);Code10;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Gr. (Price);
                                                              ENG=VAT Bus. Posting Gr. (Price)] }
    { 50  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ENG=Unit of Measure Code] }
    { 60  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                IF "Starting Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ENG=Starting Date] }
    { 61  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                VALIDATE("Starting Date");

                                                                IF "Ending Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ENG=Ending Date] }
    { 70  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[ENU=Minimum Quantity;
                                                              ENG=Minimum Quantity];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 80  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Invoice Disc.;
                                                              ENG=Allow Invoice Disc.] }
    { 81  ;   ;Allow Line Disc.    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Line Disc.;
                                                              ENG=Allow Line Disc.] }
    { 200 ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              ENG=Unit Price];
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Style No.,Colour Code,Sales Type,Sales Code,Starting Date,Currency Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Style@1000000000 : Record 9087012;
      CustPriceGr@1000 : Record 6;
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2;ENG=%1 cannot be after %2';
      Cust@1002 : Record 18;
      Text001@1003 : TextConst 'ENU=%1 must be blank.;ENG=%1 must be blank.';
      Campaign@1005 : Record 5071;
      Text002@1006 : TextConst 'ENU="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card.";ENG="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card."';

    PROCEDURE InitNewRecord@1000000000(pFromStyleSalesPrice@1000000000 : Record 9087045);
    BEGIN
      //DOC NS2016.16.1 - Initialising new record
      //  -> pFromStyleSalesPrice: The record to initialise from

      Rec.VALIDATE("Style No.", pFromStyleSalesPrice."Style No.");
      Rec.VALIDATE("Sales Type", pFromStyleSalesPrice."Sales Type");
      Rec.VALIDATE("Price Includes VAT", pFromStyleSalesPrice."Price Includes VAT");
      Rec.VALIDATE("Allow Invoice Disc.", pFromStyleSalesPrice."Allow Invoice Disc.");
      Rec.VALIDATE("Allow Line Disc.", pFromStyleSalesPrice."Allow Line Disc.");
      Rec.VALIDATE("VAT Bus. Posting Gr. (Price)", pFromStyleSalesPrice."VAT Bus. Posting Gr. (Price)");
    END;

    BEGIN
    {
      //DOC NS2016.16.1 JH 07/10/2016 - Function to initialise a new record
      //DOC NS2016.16.1 PCM 16/11/2016 - Added field "Colour Code" to allow Item pricing.
    }
    END.
  }
}

