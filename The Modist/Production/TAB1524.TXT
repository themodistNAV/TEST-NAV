OBJECT Table 1524 Workflow Rule
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Workflow Rule;
               ENG=Workflow Rule];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=FILTER(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 3   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[ENU=Field No.;
                                                              ENG=Field No.] }
    { 4   ;   ;Operator            ;Option        ;InitValue=Changed;
                                                   CaptionML=[ENU=Operator;
                                                              ENG=Operator];
                                                   OptionCaptionML=[ENU=Increased,Decreased,Changed;
                                                                    ENG=Increased,Decreased,Changed];
                                                   OptionString=Increased,Decreased,Changed }
    { 8   ;   ;Workflow Code       ;Code20        ;TableRelation=Workflow.Code;
                                                   CaptionML=[ENU=Workflow Code;
                                                              ENG=Workflow Code] }
    { 9   ;   ;Workflow Step ID    ;Integer       ;TableRelation="Workflow Step".ID WHERE (Workflow Code=FIELD(Workflow Code));
                                                   CaptionML=[ENU=Workflow Step ID;
                                                              ENG=Workflow Step ID] }
    { 10  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              ENG=Field Caption] }
    { 11  ;   ;Workflow Step Instance ID;GUID     ;TableRelation="Workflow Step Instance".ID;
                                                   CaptionML=[ENU=Workflow Step Instance ID;
                                                              ENG=Workflow Step Instance ID] }
  }
  KEYS
  {
    {    ;Workflow Code,Workflow Step ID,ID       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RuleDisplayTxt@1000 : TextConst '@@@="%1 = The field; %2 = type of operation; (Amount is Increased)";ENU=%1 is %2;ENG=%1 is %2';

    PROCEDURE EvaluateRule@3(RecRef@1000 : RecordRef;xRecRef@1004 : RecordRef) : Boolean;
    VAR
      FieldRef@1001 : FieldRef;
      xFieldRef@1005 : FieldRef;
      Value@1002 : Variant;
      xValue@1003 : Variant;
    BEGIN
      IF NOT RecRef.FIELDEXIST("Field No.") THEN
        EXIT(FALSE);

      FieldRef := RecRef.FIELD("Field No.");
      xFieldRef := xRecRef.FIELD("Field No.");

      Value := FieldRef.VALUE;
      xValue := xFieldRef.VALUE;

      EXIT(CompareValues(xValue,Value));
    END;

    PROCEDURE CompareValues@7(xValue@1000 : Variant;Value@1001 : Variant) : Boolean;
    BEGIN
      IF Value.ISINTEGER OR Value.ISBIGINTEGER OR Value.ISDECIMAL OR Value.ISDURATION THEN
        EXIT(CompareNumbers(xValue,Value));

      EXIT(CompareText(FORMAT(xValue,0,2),FORMAT(Value,0,2)));
    END;

    LOCAL PROCEDURE CompareNumbers@1(xValue@1000 : Decimal;Value@1002 : Decimal) : Boolean;
    BEGIN
      CASE Operator OF
        Operator::Increased:
          EXIT(xValue < Value);
        Operator::Decreased:
          EXIT(xValue > Value);
        Operator::Changed:
          EXIT(xValue <> Value);
        ELSE
          EXIT(FALSE);
      END;
    END;

    LOCAL PROCEDURE CompareText@4(xValue@1000 : Text;Value@1002 : Text) : Boolean;
    BEGIN
      CASE Operator OF
        Operator::Increased:
          EXIT(xValue < Value);
        Operator::Decreased:
          EXIT(xValue > Value);
        Operator::Changed:
          EXIT(xValue <> Value);
        ELSE
          EXIT(FALSE);
      END;
    END;

    PROCEDURE GetDisplayText@2() : Text;
    BEGIN
      CALCFIELDS("Field Caption");
      EXIT(STRSUBSTNO(RuleDisplayTxt,"Field Caption",Operator));
    END;

    BEGIN
    END.
  }
}

