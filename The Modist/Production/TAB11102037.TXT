OBJECT Table 11102037 OM - Project Object
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=Project No.,Object Type,Object No.;
    OnInsert=VAR
               Project@1000 : Record 11102036;
             BEGIN
               TESTFIELD("Object Type");
               TESTFIELD("Object No.");
               TESTFIELD("Project No.");

               Project.GET("Project No.");
               Project.TESTFIELD(Blocked, FALSE);
               "Transport No." := Project."Transport No.";

               UpdateRepository(1);
             END;

    OnModify=BEGIN
               UpdateRepository(2);
             END;

    OnDelete=VAR
               Project@1000 : Record 11102036;
               ctConfirmInTransports@1001 : TextConst 'DEU=This project is already included in a transport.\Are you sure?;ENU=This project is already included in a transport.\Are you sure?;ESP=This project is already included in a transport.\Are you sure?;FRA=This project is already included in a transport.\Are you sure?;NLD=Dit projects is al opgenomen in een transport.\Weet u het zeker?;DEA=This project is already included in a transport.\Are you sure?';
               ctConfirmTransports@1002 : TextConst 'DEU=This project has already been transported.\Are you sure?;ENU=This project has already been transported.\Are you sure?;ESP=This project has already been transported.\Are you sure?;FRA=This project has already been transported.\Are you sure?;NLD=Dit project is al getransporteerd.\Weet u het zeker?;DEA=This project has already been transported.\Are you sure?';
             BEGIN
               Project.GET("Project No.");
               Project.TESTFIELD(Blocked, FALSE);

               IF NOT SkipConfirmDelete THEN
                 IF Project.Transported THEN BEGIN
                   IF NOT CONFIRM(ctConfirmTransports, TRUE) THEN
                     ERROR('');
                 END ELSE
                   IF Project."Transport No." <> '' THEN
                     IF NOT CONFIRM(ctConfirmInTransports, TRUE) THEN
                       ERROR('');

               UpdateRepository(3);
             END;

    OnRename=VAR
               ctNotAllowed@1000 : TextConst 'DEU=Not allowed.;ENU=Not allowed.;ESP=Not allowed.;FRA=Not allowed.;NLD=Niet toegestaan.;DEA=Not allowed.';
             BEGIN
               ERROR(ctNotAllowed);
             END;

    CaptionML=[DEU=Project Object;
               ENU=Project Object;
               ESP=Project Object;
               FRA=Project Object;
               NLD=Projectobject;
               DEA=Project Object];
    LookupPageID=Page11102042;
    DrillDownPageID=Page11102042;
  }
  FIELDS
  {
    { 1   ;   ;Project No.         ;Code20        ;TableRelation="OM - Project";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=[DEU=Project No.;
                                                              ENU=Project No.;
                                                              ESP=Project No.;
                                                              FRA=Project No.;
                                                              NLD=Projectnr.;
                                                              DEA=Project No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Object Type         ;Option        ;OnValidate=BEGIN
                                                                VALIDATE("Object No.", 0);
                                                              END;

                                                   CaptionML=[DEU=Object Type;
                                                              ENU=Object Type;
                                                              ESP=Object Type;
                                                              FRA=Object Type;
                                                              NLD=Objecttype;
                                                              DEA=Object Type];
                                                   OptionCaptionML=[DEU=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query";
                                                                    ENU=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query";
                                                                    ESP=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query";
                                                                    FRA=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query";
                                                                    NLD=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query";
                                                                    DEA=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query"];
                                                   OptionString=[ ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 3   ;   ;Object No.          ;Integer       ;TableRelation=Object.ID WHERE (Type=FIELD(Object Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Name, "Object Version List", "Object Date", "Object Time");
                                                              END;

                                                   CaptionML=[DEU=Object No.;
                                                              ENU=Object No.;
                                                              ESP=Object No.;
                                                              FRA=Object No.;
                                                              NLD=Objectnr.;
                                                              DEA=Object No.];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 4   ;   ;Name                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=FIELD(Object Type),
                                                                                         Company Name=FILTER(''),
                                                                                         ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              ESP=Name;
                                                              FRA=Name;
                                                              NLD=Naam;
                                                              DEA=Name];
                                                   Editable=No }
    { 5   ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("OM - Project".Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[DEU=Project Description;
                                                              ENU=Project Description;
                                                              ESP=Project Description;
                                                              FRA=Project Description;
                                                              NLD=Projectomschrijving;
                                                              DEA=Project Description];
                                                   Editable=No }
    { 6   ;   ;Transport No.       ;Code20        ;TableRelation="OM - Transport";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Transport Description");
                                                              END;

                                                   CaptionML=[DEU=Transport No.;
                                                              ENU=Transport No.;
                                                              ESP=Transport No.;
                                                              FRA=Transport No.;
                                                              NLD=Transportnr.;
                                                              DEA=Transport No.] }
    { 7   ;   ;Transport Description;Text100      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("OM - Transport".Description WHERE (No.=FIELD(Transport No.)));
                                                   CaptionML=[DEU=Transport Description;
                                                              ENU=Transport Description;
                                                              ESP=Transport Description;
                                                              FRA=Transport Description;
                                                              NLD=Transportomschrijving;
                                                              DEA=Transport Description];
                                                   Editable=No }
    { 9   ;   ;Overlap Sorting     ;Integer       ;CaptionML=[DEU=Overlap Sorting;
                                                              ENU=Overlap Sorting;
                                                              ESP=Overlap Sorting;
                                                              FRA=Overlap Sorting;
                                                              NLD=Overlapsortering;
                                                              DEA=Overlap Sorting] }
    { 10  ;   ;Object Version List ;Text248       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object."Version List" WHERE (Type=FIELD(Object Type),
                                                                                                   Company Name=CONST(),
                                                                                                   ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Version List;
                                                              ENU=Version List;
                                                              ESP=Version List;
                                                              FRA=Version List;
                                                              NLD=Versielijst;
                                                              DEA=Version List];
                                                   Editable=No }
    { 11  ;   ;Object Date         ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Date WHERE (Type=FIELD(Object Type),
                                                                                         Company Name=CONST(),
                                                                                         ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Date;
                                                              ENU=Date;
                                                              ESP=Date;
                                                              FRA=Date;
                                                              NLD=Datum;
                                                              DEA=Date];
                                                   Editable=No }
    { 12  ;   ;Object Time         ;Time          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Time WHERE (Type=FIELD(Object Type),
                                                                                         Company Name=CONST(),
                                                                                         ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Time;
                                                              ENU=Time;
                                                              ESP=Time;
                                                              FRA=Time;
                                                              NLD=Tijd;
                                                              DEA=Time];
                                                   Editable=No }
    { 13  ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Object Caption;
                                                              ENU=Object Caption;
                                                              ESP=Object Caption;
                                                              FRA=Object Caption;
                                                              NLD=Object caption;
                                                              DEA=Object Caption];
                                                   Editable=No }
    { 14  ;   ;No. of Comments     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Check Guidelines Comment" WHERE (Object Type=FIELD(Object Type),
                                                                                                            Object No.=FIELD(Object No.)));
                                                   CaptionML=[DEU=No. of Comments;
                                                              ENU=No. of Comments;
                                                              ESP=No. of Comments;
                                                              FRA=No. of Comments;
                                                              NLD=Aantal opmerkingen;
                                                              DEA=No. of Comments];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 15  ;   ;No. of New Comments ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Check Guidelines Comment" WHERE (Object Type=FIELD(Object Type),
                                                                                                            Object No.=FIELD(Object No.),
                                                                                                            Known Comment=CONST(No)));
                                                   CaptionML=[DEU=No. of New Comments;
                                                              ENU=No. of New Comments;
                                                              ESP=No. of New Comments;
                                                              FRA=No. of New Comments;
                                                              NLD=Aantal nieuwe opmerkingen;
                                                              DEA=No. of New Comments];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 16  ;   ;No. of Known Comments;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Check Guidelines Comment" WHERE (Object Type=FIELD(Object Type),
                                                                                                            Object No.=FIELD(Object No.),
                                                                                                            Known Comment=CONST(Yes)));
                                                   CaptionML=[DEU=No. of Known Comments;
                                                              ENU=No. of Known Comments;
                                                              ESP=No. of Known Comments;
                                                              FRA=No. of Known Comments;
                                                              NLD=Aantal bekende opmerkingen;
                                                              DEA=No. of Known Comments];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 17  ;   ;Include in Transport;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Include in Transport;
                                                              ENU=Include in Transport;
                                                              ESP=Include in Transport;
                                                              FRA=Include in Transport;
                                                              NLD=Opnemen in transport;
                                                              DEA=Include in Transport] }
    { 18  ;   ;No. of Critical Comments;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Check Guidelines Comment" WHERE (Object Type=FIELD(Object Type),
                                                                                                            Object No.=FIELD(Object No.),
                                                                                                            Critical=CONST(Yes)));
                                                   CaptionML=[DEU=No. of Critical Comments;
                                                              ENU=No. of Critical Comments;
                                                              ESP=No. of Critical Comments;
                                                              FRA=No. of Critical Comments;
                                                              NLD=Aantal kritieke opmerkingen;
                                                              DEA=No. of Critical Comments];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 19  ;   ;Rollback Date       ;Date          ;CaptionML=[DEU=Rollback Date;
                                                              ENU=Rollback Date;
                                                              ESP=Rollback Date;
                                                              FRA=Rollback Date;
                                                              NLD=Terugroldatum;
                                                              DEA=Rollback Date] }
    { 20  ;   ;Rollback Time       ;Time          ;CaptionML=[DEU=Rollback Time;
                                                              ENU=Rollback Time;
                                                              ESP=Rollback Time;
                                                              FRA=Rollback Time;
                                                              NLD=Terugroltijd;
                                                              DEA=Rollback Time] }
    { 21  ;   ;Used in Action Before;Boolean      ;CaptionML=[DEU=Used in Action Before;
                                                              ENU=Used in Action Before;
                                                              ESP=Used in Action Before;
                                                              FRA=Used in Action Before;
                                                              NLD=Gebruikt in actie vooraf;
                                                              DEA=Used in Action Before] }
  }
  KEYS
  {
    {    ;Project No.,Object Type,Object No.      ;Clustered=Yes }
    {    ;Object Type,Object No.                   }
    {    ;Transport No.                            }
    {    ;Overlap Sorting                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SkipConfirmDelete@1000 : Boolean;

    PROCEDURE RemoveObjects@1(VAR TmpProjectObject@1000 : TEMPORARY Record 11102037);
    VAR
      Project@1001 : Record 11102036;
      TmpProjectPassed@1002 : TEMPORARY Record 11102036;
      ProjectObject@1003 : Record 11102037;
      RepositorySetup@1004 : Record 11102085;
      FirstObjectOfProject@1005 : Boolean;
    BEGIN
      IF RepositorySetup.GET THEN
        ;

      IF TmpProjectObject.FINDSET THEN
        REPEAT

          ProjectObject.GET(TmpProjectObject."Project No.", TmpProjectObject."Object Type", TmpProjectObject."Object No.");

          IF TmpProjectPassed.GET(ProjectObject."Project No.") THEN
            FirstObjectOfProject := FALSE
          ELSE BEGIN
            FirstObjectOfProject := TRUE;

            IF RepositorySetup."Use Repository" THEN
              IF Project.GET(ProjectObject."Project No.") THEN BEGIN
                Project."Repository Busy" := TRUE;
                Project.MODIFY;
              END;
            TmpProjectPassed."No." := ProjectObject."Project No.";
            TmpProjectPassed.INSERT;

          END;

          IF NOT FirstObjectOfProject THEN
            ProjectObject.SetSkipConfirmDelete(TRUE);

          ProjectObject.DELETE(TRUE);
        UNTIL TmpProjectObject.NEXT = 0;

      IF RepositorySetup."Use Repository" THEN
        IF TmpProjectPassed.FINDSET THEN
          REPEAT
            Project.GET(TmpProjectPassed."No.");
            Project."Repository Busy" := FALSE;
            Project.MODIFY(TRUE);
          UNTIL TmpProjectPassed.NEXT = 0;
    END;

    PROCEDURE MoveObjects@2(VAR ProjectObject@1000 : Record 11102037);
    VAR
      AssignModificationsMgt@1001 : Codeunit 11102036;
    BEGIN
      AssignModificationsMgt.MoveObjectsBetweenProjects(ProjectObject);
    END;

    PROCEDURE ShowModifications@3();
    VAR
      Modification@1000 : Record 11102042;
      Modifications@1001 : Page 11102049;
    BEGIN
      Modification.SETCURRENTKEY("Object Type", "Object No.");
      Modification.SETRANGE("Object Type", "Object Type");
      Modification.SETRANGE("Object No.", "Object No.");
      Modifications.SETTABLEVIEW(Modification);
      Modifications.RUN;
    END;

    PROCEDURE ShowProjects@4();
    VAR
      ProjectObject@1000 : Record 11102037;
      ProjectObjects@1001 : Page 11102042;
    BEGIN
      ProjectObject.SETCURRENTKEY("Object Type", "Object No.");
      ProjectObject.SETRANGE("Object Type", "Object Type");
      ProjectObject.SETRANGE("Object No.", "Object No.");
      ProjectObjects.SETTABLEVIEW(ProjectObject);
      ProjectObjects.RUN;
    END;

    PROCEDURE SetSkipConfirmDelete@5(NewValue@1000 : Boolean);
    BEGIN
      SkipConfirmDelete := NewValue;
    END;

    PROCEDURE RunObject@6();
    VAR
      Object@1000 : Record 2000000001;
      ObjectMgt@1001 : Codeunit 11102046;
    BEGIN
      Object.GET("Object Type", '', "Object No.");
      ObjectMgt.RunObject(Object);
    END;

    PROCEDURE DesignObject@7();
    VAR
      Object@1000 : Record 2000000001;
      ObjectMgt@1001 : Codeunit 11102046;
    BEGIN
      Object.GET("Object Type", '', "Object No.");
      ObjectMgt.DesignObject(Object);
    END;

    PROCEDURE Navigate@16();
    VAR
      Object@1000 : Record 2000000001;
      ObjectMgt@1001 : Codeunit 11102046;
    BEGIN
      Object.GET("Object Type", '', "Object No.");
      ObjectMgt.Navigate(Object);
    END;

    PROCEDURE ShowHistoryVersions@8();
    VAR
      CALHistoryObject@1000 : Record 11102052;
    BEGIN
      CALHistoryObject.SETCURRENTKEY("Object Type", "Object No.");
      CALHistoryObject.SETRANGE("Object Type", "Object Type");
      CALHistoryObject.SETRANGE("Object No.", "Object No.");
      PAGE.RUN(PAGE::"OM - C/AL History Objects", CALHistoryObject);
    END;

    LOCAL PROCEDURE UpdateRepository@9(Trigger@1000 : Integer);
    VAR
      RepositoryMgt@1001 : Codeunit 11102067;
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      RepositoryMgt.UpdateProject(RecRef, "Project No.", Trigger);
      RecRef.SETTABLE(Rec);
    END;

    PROCEDURE ShowDocumentationTags@10();
    VAR
      Setup@1000 : Record 11102035;
      CALHistoryObject@1001 : Record 11102052;
      CALHistoryLine@1002 : Record 11102053;
      Object@1003 : Record 2000000001;
      CheckCALHistory@1004 : Codeunit 11102063;
      CALHistoryLines@1005 : Page 11102073;
      NavgateEditor@1006 : Page 11175819;
      FirstLineNo@1007 : Integer;
      LastLineNo@1008 : Integer;
      StopLoop@1009 : Boolean;
    BEGIN
      Object.GET("Object Type", '', "Object No.");
      Object.SETRECFILTER;
      CheckCALHistory.RunWithObjectRange(Object);

      CALHistoryObject.GoToLastRecord("Object Type", "Object No.");
      CALHistoryLine.SETRANGE("Entry No.", CALHistoryObject."Entry No.");
      IF NOT CALHistoryLine.FINDLAST THEN
        EXIT;

      IF CALHistoryLine.NEXT(-3) <> -3 THEN
        EXIT;

      LastLineNo := CALHistoryLine."Line No.";

      WHILE NOT StopLoop DO BEGIN

        IF CALHistoryLine.Code = '    BEGIN' THEN BEGIN
          FirstLineNo := CALHistoryLine."Line No.";
          StopLoop := TRUE;
        END;

        IF CALHistoryLine.NEXT(-1) = 0 THEN
          StopLoop := TRUE;

      END;

      Setup.CustomGet;
      CASE Setup.Editor OF
        Setup.Editor::"Object Manager" :
          BEGIN
            CALHistoryLine.SETRANGE("Line No.", FirstLineNo, LastLineNo);

            CALHistoryLines.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");
            CALHistoryLines.SetHighLightedLines(CALHistoryLine);

            COMMIT;
            CALHistoryLines.RUN;
          END;

        Setup.Editor::Navgate :
          BEGIN
            NavgateEditor.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");
            NavgateEditor.GotoPosition(LastLineNo, 0);
            COMMIT;
            NavgateEditor.RUN;
          END;

      END;
    END;

    PROCEDURE DrillDownCheckGuidelines@11(ShowType@1000 : 'All,New,Known');
    VAR
      Project@1001 : Record 11102036;
      CheckGuidelinesComment@1002 : Record 11102081;
      CheckGuidelinesComments@1003 : Page 11102117;
    BEGIN
      CheckGuidelinesComment.SETRANGE("Object Type", "Object Type");
      CheckGuidelinesComment.SETRANGE("Object No.", "Object No.");

      CASE ShowType OF
        ShowType::New :
          CheckGuidelinesComment.SETRANGE("Known Comment", FALSE);
        ShowType::Known :
          CheckGuidelinesComment.SETRANGE("Known Comment", TRUE);
      END;

      Project.GET("Project No.");
      CheckGuidelinesComments.SetProject(Project);
      CheckGuidelinesComments.SETTABLEVIEW(CheckGuidelinesComment);
      CheckGuidelinesComments.RUN;
    END;

    PROCEDURE ShowCode@12();
    VAR
      Modification@1000 : Record 11102042;
    BEGIN
      Modification.SETCURRENTKEY("Object Type", "Object No.", "Insert Date", "Insert Time");
      Modification.SETRANGE("Object Type", "Object Type");
      Modification.SETRANGE("Object No.", "Object No.");
      Modification.FINDLAST;

      Modification.ShowCode;
    END;

    PROCEDURE SetRollbackDateTime@13();
    VAR
      Modification@1000 : Record 11102042;
    BEGIN
      Modification.SETCURRENTKEY("Assigned to Project No.", "Object Type", "Object No.", "Object Date", "Object Time");
      Modification.SETRANGE("Assigned to Project No.", "Project No.");
      Modification.SETRANGE("Object Type", "Object Type");
      Modification.SETRANGE("Object No.", "Object No.");
      Modification.SETFILTER("Object Date", '<>%1', 0D);
      IF Modification.FINDFIRST THEN BEGIN
        "Rollback Date" := Modification."Object Date";
        "Rollback Time" := Modification."Object Time";
      END;
    END;

    PROCEDURE WhereUsed@14();
    VAR
      Object@1000 : Record 2000000001;
      WhereUsedMgt@1001 : Codeunit 11102045;
    BEGIN
      Object.GET("Object Type", '', "Object No.");
      WhereUsedMgt.OpenCardWithObject(Object);
    END;

    PROCEDURE LastModifiedBy@15() : Code[50];
    VAR
      Modification@1000 : Record 11102042;
    BEGIN
      Modification.SETCURRENTKEY("Object Type", "Object No.", "Insert Date", "Insert Time");
      Modification.SETRANGE("Object Type", "Object Type");
      Modification.SETRANGE("Object No.", "Object No.");
      IF Modification.FINDLAST THEN
        EXIT(Modification."Inserted By");
    END;

    BEGIN
    END.
  }
}

