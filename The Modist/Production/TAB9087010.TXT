OBJECT Table 9087010 Season Currency
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=11:45:15 AM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Season Code");
               Rec.TESTFIELD("Currency Code");
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    CaptionML=[ENU=Season Currency;
               ENG=Season Currency];
    LookupPageID=Page9087016;
    DrillDownPageID=Page9087016;
  }
  FIELDS
  {
    { 1   ;   ;Season Code         ;Code20        ;TableRelation=Season;
                                                   CaptionML=[ENU=Season Code;
                                                              ENG=Season Code] }
    { 2   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                lCurrencyExchangeRate@1000000001 : Record 330;
                                                                lCurrencyExchangeRateDate@1000000000 : Date;
                                                              BEGIN
                                                                Season.GET(Rec."Season Code");
                                                                IF (Season."Start Date" <> 0D) THEN
                                                                  lCurrencyExchangeRateDate := Season."Start Date"
                                                                ELSE
                                                                  lCurrencyExchangeRateDate := WORKDATE();
                                                                Rec.VALIDATE("Currency Factor", lCurrencyExchangeRate.ExchangeRate(lCurrencyExchangeRateDate, Rec."Currency Code"));
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ENG=Currency Code] }
    { 10  ;   ;Currency Factor     ;Decimal       ;CaptionML=[ENU=Currency Factor;
                                                              ENG=Currency Factor] }
    { 20  ;   ;Buying Sheets       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Buying Sheet Header" WHERE (Season Code=FIELD(Season Code),
                                                                                                  Currency Code=FIELD(Currency Code)));
                                                   CaptionML=[ENU=Buying Sheets;
                                                              ENG=Buying Sheets];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 30  ;   ;Purchase Orders     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Season Code=FIELD(Season Code),
                                                                                              Currency Code=FIELD(Currency Code)));
                                                   CaptionML=[ENU=Purchase Orders;
                                                              ENG=Purchase Orders];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Season Code,Currency Code               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Season@1000000003 : Record 9087009;
      SeasonManagement@1000000002 : Codeunit 9087005;
      TnpToolkit@1000000004 : Codeunit 9062230;

    PROCEDURE Edit@1000000000();
    BEGIN
      Rec.TESTFIELD("Season Code");
      Season.GET(Rec."Season Code");
      PAGE.RUN(PAGE::"Season List", Season);
    END;

    PROCEDURE UpdateCurrencyFactor@1000000003();
    BEGIN
      SeasonManagement.UpdateCurrencyFactor(Rec, TRUE, TRUE);
    END;

    PROCEDURE ShowBuyingSheets@1000000001();
    VAR
      lBuyingSheetHeader@1000000000 : Record 9087020;
    BEGIN
      lBuyingSheetHeader.RESET();
      lBuyingSheetHeader.SETCURRENTKEY("Season Code", "Currency Code");
      lBuyingSheetHeader.SETRANGE("Season Code", Rec."Season Code");
      lBuyingSheetHeader.SETRANGE("Currency Code", Rec."Currency Code");
      PAGE.RUN(0, lBuyingSheetHeader);
    END;

    PROCEDURE ShowPurchaseOrders@1000000002();
    VAR
      lPurchaseHeader@1000000000 : Record 38;
    BEGIN
      lPurchaseHeader.RESET();
      lPurchaseHeader.FILTERGROUP(2);
      lPurchaseHeader.SETRANGE("Document Type", lPurchaseHeader."Document Type"::Order);
      lPurchaseHeader.SETRANGE("Season Code", Rec."Season Code");
      lPurchaseHeader.SETRANGE("Currency Code", Rec."Currency Code");
      lPurchaseHeader.FILTERGROUP(0);
      PAGE.RUN(PAGE::"Purchase Order List", lPurchaseHeader);
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 19/11/2013 - Created
      //DOC NS2016.16 JH 05/09/2016 - nSemble 2016
    }
    END.
  }
}

