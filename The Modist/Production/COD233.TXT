OBJECT Codeunit 233 Gen. Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=26/02/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45243;
  }
  PROPERTIES
  {
    TableNo=232;
    OnRun=BEGIN
            GenJnlBatch.COPY(Rec);
            Code;
            COPY(GenJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?;ENG=Do you want to post the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ENG=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ENG="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ENG=The journals that were not successfully posted are now marked.';
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1005 : Record 232;
      GenJnlLine@1006 : Record 81;
      GenJnlPostBatch@1007 : Codeunit 13;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH GenJnlBatch DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlLine.SETRANGE("Journal Batch Name",Name);
          IF GenJnlLine.FINDFIRST THEN BEGIN
            CLEAR(GenJnlPostBatch);
            IF GenJnlPostBatch.RUN(GenJnlLine) THEN
              MARK(FALSE)
            ELSE BEGIN
              MARK(TRUE);
              JnlWithErrors := TRUE;
            END;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE BEGIN
          MARKEDONLY(TRUE);
          MESSAGE(
            Text002 +
            Text003);
        END;

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

