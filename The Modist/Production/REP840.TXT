OBJECT Report 840 Suggest Worksheet Lines
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 355=rimd,
                TableData 847=rimd;
    CaptionML=[ENU=Suggest Worksheet Lines;
               ENG=Suggest Worksheet Lines];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF CashFlowNo = '' THEN
                    ERROR(Text000);

                  IF NOT SelectionCashFlowForecast.GET(CashFlowNo) THEN
                    ERROR(Text001);

                  IF NoOptionsChosen THEN
                    ERROR(Text002,CashFlowNo);

                  CFSetup.GET;
                  GLSetup.GET;

                  Window.OPEN(
                    Text003 +
                    Text033 +
                    Text034);

                  Window.UPDATE(1,"Cash Flow Forecast"."No.");
                END;

  }
  DATASET
  {
    { 8965;    ;DataItem;                    ;
               DataItemTable=Table840;
               DataItemTableView=SORTING(No.)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               SETRANGE("No.",CashFlowNo);
                               LineNo := 0;
                               NeedsManualPmtUpdate := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                END;

               OnPostDataItem=VAR
                                TempCashFlowForecast@1001 : TEMPORARY Record 840;
                              BEGIN
                                InsertWorksheetLines(TempCashFlowForecast);
                                DeleteEntries(TempCashFlowForecast);
                                IF NeedsManualPmtUpdate THEN
                                  MESSAGE(ManualPmtRevExpNeedsUpdateMsg,TempCashFlowForecast."No.");
                              END;
                               }

    { 6780;1   ;DataItem;                    ;
               DataItemTable=Table841;
               DataItemTableView=SORTING(No.)
                                 ORDER(Ascending)
                                 WHERE(G/L Integration=FILTER(Balance|Both),
                                       G/L Account Filter=FILTER(<>''));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Liquid Funds"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  GLAcc@1000 : Record 15;
                                BEGIN
                                  GLAcc.SETFILTER("No.","G/L Account Filter");
                                  IF GLAcc.FINDSET THEN
                                    REPEAT
                                      GLAcc.CALCFIELDS(Balance);

                                      Window.UPDATE(2,Text004);
                                      Window.UPDATE(3,GLAcc."No.");
                                      InsertCFLineForGLAccount(GLAcc);
                                    UNTIL GLAcc.NEXT = 0;
                                END;
                                 }

    { 8503;1   ;DataItem;                    ;
               DataItemTable=Table21;
               DataItemTableView=SORTING(Open,Due Date)
                                 ORDER(Ascending)
                                 WHERE(Open=CONST(Yes),
                                       Remaining Amount=FILTER(<>0));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::Receivables] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text005);
                                  Window.UPDATE(3,"Entry No.");

                                  IF "Customer No." <> '' THEN
                                    Customer.GET("Customer No.")
                                  ELSE
                                    Customer.INIT;

                                  CALCFIELDS("Remaining Amt. (LCY)","Remaining Amount");

                                  InsertCFLineForCustLedgerEntry;
                                END;
                                 }

    { 4114;1   ;DataItem;                    ;
               DataItemTable=Table25;
               DataItemTableView=SORTING(Open,Due Date)
                                 ORDER(Ascending)
                                 WHERE(Open=CONST(Yes),
                                       Remaining Amount=FILTER(<>0));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::Payables] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text006);
                                  Window.UPDATE(3,"Entry No.");

                                  IF "Vendor No." <> '' THEN
                                    Vendor.GET("Vendor No.")
                                  ELSE
                                    Vendor.INIT;

                                  CALCFIELDS("Remaining Amt. (LCY)","Remaining Amount");

                                  InsertCFLineForVendorLedgEntry;
                                END;
                                 }

    { 6547;1   ;DataItem;                    ;
               DataItemTable=Table39;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.)
                                 ORDER(Ascending)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Purchase Order"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text007);
                                  Window.UPDATE(3,"Document No.");

                                  PurchHeader.GET("Document Type","Document No.");
                                  IF PurchHeader."Buy-from Vendor No." <> '' THEN
                                    Vendor.GET(PurchHeader."Pay-to Vendor No.")
                                  ELSE
                                    Vendor.INIT;

                                  InsertCFLineForPurchaseLine;
                                END;
                                 }

    { 2844;1   ;DataItem;                    ;
               DataItemTable=Table37;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.)
                                 ORDER(Ascending)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Sales Order"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text008);
                                  Window.UPDATE(3,"Document No.");

                                  SalesHeader.GET("Document Type","Document No.");
                                  IF SalesHeader."Sell-to Customer No." <> '' THEN
                                    Customer.GET(SalesHeader."Bill-to Customer No.")
                                  ELSE
                                    Customer.INIT;

                                  InsertCFLineForSalesLine;
                                END;
                                 }

    { 8549;1   ;DataItem;InvestmentFixedAsset;
               DataItemTable=Table5600;
               DataItemTableView=SORTING(No.)
                                 WHERE(Budgeted Asset=CONST(Yes));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Budgeted Fixed Asset"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;

                               FASetup.GET;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF FADeprBook.GET("No.",FASetup."Default Depr. Book") THEN BEGIN
                                    FADeprBook.CALCFIELDS("Acquisition Cost");

                                    Window.UPDATE(2,Text009);
                                    Window.UPDATE(3,"No.");

                                    InsertCFLineForInvestmentFixAs;
                                  END;
                                END;
                                 }

    { 1713;1   ;DataItem;SaleFixedAsset      ;
               DataItemTable=Table5600;
               DataItemTableView=SORTING(No.)
                                 WHERE(Budgeted Asset=CONST(No));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Sale of Fixed Asset"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;

                               FASetup.GET;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF FADeprBook.GET("No.",FASetup."Default Depr. Book") THEN
                                    IF (FADeprBook."Disposal Date" = 0D) AND
                                       (FADeprBook."Projected Disposal Date" <> 0D) AND
                                       (FADeprBook."Projected Proceeds on Disposal" <> 0)
                                    THEN BEGIN
                                      Window.UPDATE(2,Text010);
                                      Window.UPDATE(3,"No.");

                                      InsertCFLineForSaleFixedAsset;
                                    END;
                                END;
                                 }

    { 5017;1   ;DataItem;                    ;
               DataItemTable=Table850;
               DataItemTableView=SORTING(Code)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Cash Flow Manual Expense"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text011);
                                  Window.UPDATE(3,Code);

                                  InsertCFLineForManualExpens;
                                END;
                                 }

    { 4269;1   ;DataItem;                    ;
               DataItemTable=Table849;
               DataItemTableView=SORTING(Code)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Cash Flow Manual Revenue"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text012);
                                  Window.UPDATE(3,Code);

                                  InsertCFLineForManualRevenue;
                                END;
                                 }

    { 5339;1   ;DataItem;CFAccountForBudget  ;
               DataItemTable=Table841;
               DataItemTableView=SORTING(No.)
                                 ORDER(Ascending)
                                 WHERE(G/L Integration=FILTER(Budget|Both),
                                       G/L Account Filter=FILTER(<>''));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"G/L Budget"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  GLAcc@1000 : Record 15;
                                BEGIN
                                  GLAcc.SETFILTER("No.","G/L Account Filter");
                                  IF GLAcc.FINDSET THEN
                                    REPEAT
                                      Window.UPDATE(2,Text031);
                                      Window.UPDATE(3,GLAcc."No.");

                                      GLBudgEntry.SETRANGE("Budget Name",GLBudgName);
                                      GLBudgEntry.SETRANGE("G/L Account No.",GLAcc."No.");
                                      GLBudgEntry.SETRANGE(Date,"Cash Flow Forecast"."G/L Budget From","Cash Flow Forecast"."G/L Budget To");
                                      IF GLBudgEntry.FINDSET THEN
                                        REPEAT
                                          InsertCFLineForGLBudget(GLAcc);
                                        UNTIL GLBudgEntry.NEXT = 0;
                                    UNTIL GLAcc.NEXT = 0;
                                END;
                                 }

    { 6560;1   ;DataItem;                    ;
               DataItemTable=Table5902;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               IF NOT ConsiderSource[SourceType::"Service Orders"] THEN
                                 CurrReport.BREAK;

                               IF NOT READPERMISSION THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,Text032);
                                  Window.UPDATE(3,"Document No.");

                                  ServiceHeader.GET("Document Type","Document No.");
                                  IF ServiceHeader."Bill-to Customer No." <> '' THEN
                                    Customer.GET(ServiceHeader."Bill-to Customer No.")
                                  ELSE
                                    Customer.INIT;

                                  InsertCFLineForServiceLine;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=VAR
               CFManualExpense@1000 : Record 850;
               CFManualRevenue@1003 : Record 849;
               GLBudgetEntry@1001 : Record 96;
               GLAcc@1002 : Record 15;
             BEGIN
               IF ConsiderSource[SourceType::"Liquid Funds"] THEN
                 ConsiderSource[SourceType::"Liquid Funds"] := GLAcc.READPERMISSION;
               IF ConsiderSource[SourceType::Receivables] THEN
                 ConsiderSource[SourceType::Receivables] := "Cust. Ledger Entry".READPERMISSION;
               IF ConsiderSource[SourceType::Payables] THEN
                 ConsiderSource[SourceType::Payables] := "Vendor Ledger Entry".READPERMISSION;
               IF ConsiderSource[SourceType::"Purchase Order"] THEN
                 ConsiderSource[SourceType::"Purchase Order"] := "Purchase Line".READPERMISSION;
               IF ConsiderSource[SourceType::"Sales Order"] THEN
                 ConsiderSource[SourceType::"Sales Order"] := "Sales Line".READPERMISSION;
               IF ConsiderSource[SourceType::"Cash Flow Manual Expense"] THEN
                 ConsiderSource[SourceType::"Cash Flow Manual Expense"] := CFManualExpense.READPERMISSION;
               IF ConsiderSource[SourceType::"Cash Flow Manual Revenue"] THEN
                 ConsiderSource[SourceType::"Cash Flow Manual Revenue"] := CFManualRevenue.READPERMISSION;
               IF ConsiderSource[SourceType::"Budgeted Fixed Asset"] THEN
                 ConsiderSource[SourceType::"Budgeted Fixed Asset"] := InvestmentFixedAsset.READPERMISSION;
               IF ConsiderSource[SourceType::"Sale of Fixed Asset"] THEN
                 ConsiderSource[SourceType::"Sale of Fixed Asset"] := SaleFixedAsset.READPERMISSION;
               IF ConsiderSource[SourceType::"G/L Budget"] THEN
                 ConsiderSource[SourceType::"G/L Budget"] := GLBudgetEntry.READPERMISSION;
               IF ConsiderSource[SourceType::"Service Orders"] THEN
                 ConsiderSource[SourceType::"Service Orders"] := "Service Line".READPERMISSION;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Cash Flow Forecast;
                             ENG=Cash Flow Forecast];
                  SourceExpr=CashFlowNo;
                  TableRelation="Cash Flow Forecast" }

      { 3   ;2   ;Group     ;
                  CaptionML=[ENU=Source Types to Include:;
                             ENG=Source Types to Include:];
                  GroupType=Group }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=Liquid Funds;
                             ENG=Liquid Funds];
                  SourceExpr=ConsiderSource[SourceType::"Liquid Funds"] }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=Receivables;
                             ENG=Receivables];
                  SourceExpr=ConsiderSource[SourceType::Receivables] }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Sales Orders;
                             ENG=Sales Orders];
                  SourceExpr=ConsiderSource[SourceType::"Sales Order"] }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Service Orders;
                             ENG=Service Orders];
                  SourceExpr=ConsiderSource[SourceType::"Service Orders"] }

      { 13  ;3   ;Field     ;
                  CaptionML=[ENU=Fixed Assets Disposal;
                             ENG=Fixed Assets Disposal];
                  SourceExpr=ConsiderSource[SourceType::"Sale of Fixed Asset"] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Cash Flow Manual Revenues;
                             ENG=Cash Flow Manual Revenues];
                  SourceExpr=ConsiderSource[SourceType::"Cash Flow Manual Revenue"] }

      { 6   ;3   ;Field     ;
                  CaptionML=[ENU=Payables;
                             ENG=Payables];
                  SourceExpr=ConsiderSource[SourceType::Payables] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Purchase Orders;
                             ENG=Purchase Orders];
                  SourceExpr=ConsiderSource[SourceType::"Purchase Order"] }

      { 10  ;3   ;Field     ;
                  CaptionML=[ENU=Fixed Assets Budget;
                             ENG=Fixed Assets Budget];
                  SourceExpr=ConsiderSource[SourceType::"Budgeted Fixed Asset"] }

      { 12  ;3   ;Field     ;
                  CaptionML=[ENU=Cash Flow Manual Expenses;
                             ENG=Cash Flow Manual Expenses];
                  SourceExpr=ConsiderSource[SourceType::"Cash Flow Manual Expense"] }

      { 16  ;3   ;Field     ;
                  CaptionML=[ENU=G/L Budget;
                             ENG=G/L Budget];
                  SourceExpr=ConsiderSource[SourceType::"G/L Budget"] }

      { 17  ;3   ;Field     ;
                  CaptionML=[ENU=G/L Budget Name;
                             ENG=G/L Budget Name];
                  SourceExpr=GLBudgName;
                  TableRelation="G/L Budget Name" }

      { 18  ;2   ;Field     ;
                  CaptionML=[ENU=Group by Document Type;
                             ENG=Group by Document Type];
                  SourceExpr=Summarized }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must choose a cash flow forecast.;ENG=You must choose a cash flow forecast.';
      Text001@1001 : TextConst 'ENU=Choose a valid cash flow forecast.;ENG=Choose a valid cash flow forecast.';
      Text002@1002 : TextConst 'ENU=Choose one option for filling the cash flow forecast no. %1.;ENG=Choose one option for filling the cash flow forecast no. %1.';
      Text003@1003 : TextConst 'ENU=Cash Flow Forecast No.      #1##########\\;ENG=Cash Flow Forecast No.      #1##########\\';
      Text004@1004 : TextConst 'ENU=Liquid Funds;ENG=Liquid Funds';
      Text005@1005 : TextConst 'ENU=Receivables;ENG=Receivables';
      Text006@1006 : TextConst 'ENU=Payables;ENG=Payables';
      Text007@1007 : TextConst 'ENU=Purchase Orders;ENG=Purchase Orders';
      Text008@1008 : TextConst 'ENU=Sales Orders;ENG=Sales Orders';
      Text009@1009 : TextConst 'ENU=Fixed Assets Budget;ENG=Fixed Assets Budget';
      Text010@1010 : TextConst 'ENU=Fixed Assets Disposal;ENG=Fixed Assets Disposal';
      Text011@1011 : TextConst 'ENU=Cash Flow Manual Expenses;ENG=Cash Flow Manual Expenses';
      Text012@1012 : TextConst 'ENU=Cash Flow Manual Revenues;ENG=Cash Flow Manual Revenues';
      Text013@1013 : TextConst 'ENU="%1 Balance=%2";ENG="%1 Balance=%2"';
      Text025@1025 : TextConst 'ENU=%1 %2 %3;ENG=%1 %2 %3';
      Text027@1027 : TextConst 'ENU="%1 AC= %2";ENG="%1 AC= %2"';
      Text028@1028 : TextConst 'ENU=Cash Flow Manual Expenses %1;ENG=Cash Flow Manual Expenses %1';
      Text029@1029 : TextConst 'ENU=Cash Flow Manual Revenues %1;ENG=Cash Flow Manual Revenues %1';
      Text030@1030 : TextConst 'ENU="%1 Budget %2 ";ENG="%1 Budget %2 "';
      Text031@1031 : TextConst 'ENU=G/L Budget;ENG=G/L Budget';
      Text032@1032 : TextConst 'ENU=Service Orders;ENG=Service Orders';
      Text033@1033 : TextConst 'ENU=Search for          #2####################\;ENG=Search for                  #2####################\';
      Text034@1034 : TextConst 'ENU=Record found        #3####################;ENG=Record found                #3####################';
      CFSetup@1041 : Record 843;
      SelectionCashFlowForecast@1042 : Record 840;
      TempCFWorksheetLine@1016 : TEMPORARY Record 846;
      CFWorksheetLine2@1044 : Record 846;
      Customer@1045 : Record 18;
      Vendor@1046 : Record 23;
      PurchHeader@1048 : Record 38;
      SalesHeader@1049 : Record 36;
      ServiceHeader@1050 : Record 5900;
      FASetup@1052 : Record 5603;
      FADeprBook@1053 : Record 5612;
      GLBudgEntry@1061 : Record 96;
      GLSetup@1063 : Record 98;
      Currency@1100 : Record 4;
      CurrExchRate@1102 : Record 330;
      Window@1066 : Dialog;
      ConsiderSource@1014 : ARRAY [11] OF Boolean;
      SourceType@1015 : ',Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Budgeted Fixed Asset,Sale of Fixed Asset,Service Orders,G/L Budget';
      CashFlowNo@1082 : Code[20];
      LineNo@1086 : Integer;
      DateLastExecution@1087 : Date;
      ExecutionDate@1088 : Date;
      GLBudgName@1089 : Code[10];
      TotalAmt@1056 : Decimal;
      MultiSalesLines@1037 : Boolean;
      Summarized@1036 : Boolean;
      NeedsManualPmtUpdate@1017 : Boolean;
      ManualPmtRevExpNeedsUpdateMsg@1018 : TextConst 'ENU=There are one or more Cash Flow Manual Revenues/Expenses with a Recurring Frequency.\But the Recurring Frequency cannot be applied because the Manual Payments To date in Cash Flow Forecast %1 is empty.\Fill in this date in order to get multiple lines.;ENG=There are one or more Cash Flow Manual Revenues/Expenses with a Recurring Frequency.\But the Recurring Frequency cannot be applied because the Manual Payments To date in Cash Flow Forecast %1 is empty.\Fill in this date in order to get multiple lines.';

    LOCAL PROCEDURE InsertTempCFWorksheetLine@16(MaxPmtTolerance@1001 : Decimal);
    BEGIN
      WITH TempCFWorksheetLine DO BEGIN
        LineNo := LineNo + 100;
        TRANSFERFIELDS(CFWorksheetLine2);
        "Cash Flow Forecast No." := "Cash Flow Forecast"."No.";
        "Line No." := LineNo;

        CalculateCFAmountAndCFDate;

        IF ABS("Amount (LCY)") < ABS(MaxPmtTolerance) THEN
          "Amount (LCY)" := 0
        ELSE
          "Amount (LCY)" := "Amount (LCY)" - MaxPmtTolerance;

        IF "Amount (LCY)" <> 0 THEN
          INSERT;
      END;
    END;

    LOCAL PROCEDURE InsertWorksheetLines@21(VAR TempCashFlowForecast@1001 : TEMPORARY Record 840);
    VAR
      CFWorksheetLine@1000 : Record 846;
      LastCFForecastNo@1002 : Code[20];
    BEGIN
      CFWorksheetLine.LOCKTABLE;

      CFWorksheetLine.RESET;
      CFWorksheetLine.DELETEALL;

      TempCFWorksheetLine.RESET;
      TempCFWorksheetLine.SETCURRENTKEY("Cash Flow Forecast No.");
      IF TempCFWorksheetLine.FINDSET THEN
        REPEAT
          CFWorksheetLine := TempCFWorksheetLine;
          CFWorksheetLine.INSERT(TRUE);

          IF LastCFForecastNo <> CFWorksheetLine."Cash Flow Forecast No." THEN BEGIN
            TempCashFlowForecast."No." := CFWorksheetLine."Cash Flow Forecast No.";
            TempCashFlowForecast.INSERT;
            LastCFForecastNo := CFWorksheetLine."Cash Flow Forecast No.";
          END;
        UNTIL TempCFWorksheetLine.NEXT = 0;

      TempCFWorksheetLine.DELETEALL;
    END;

    LOCAL PROCEDURE DeleteEntries@22(VAR TempCashFlowForecast@1000 : TEMPORARY Record 840);
    VAR
      CFForecastEntry@1001 : Record 847;
    BEGIN
      TempCashFlowForecast.RESET;
      IF TempCashFlowForecast.FINDSET THEN BEGIN
        CFForecastEntry.LOCKTABLE;
        CFForecastEntry.RESET;
        REPEAT
          CFForecastEntry.SETRANGE("Cash Flow Forecast No.",TempCashFlowForecast."No.");
          CFForecastEntry.DELETEALL;
        UNTIL TempCashFlowForecast.NEXT = 0;
      END;
      TempCashFlowForecast.DELETEALL;
    END;

    LOCAL PROCEDURE InsertCFLineForGLAccount@1(GLAcc@1000 : Record 15);
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::"Liquid Funds";
        "Source No." := GLAcc."No.";
        "Document No." := GLAcc."No.";
        "Cash Flow Account No." := "Cash Flow Account"."No.";
        Description :=
          COPYSTR(
            STRSUBSTNO(Text013,GLAcc.Name,FORMAT(GLAcc.Balance)),
            1,MAXSTRLEN(Description));
        "Cash Flow Date" := WORKDATE;
        "Amount (LCY)" := GLAcc.Balance;
        "Shortcut Dimension 2 Code" := GLAcc."Global Dimension 2 Code";
        "Shortcut Dimension 1 Code" := GLAcc."Global Dimension 1 Code";
        MoveDefualtDimToJnlLineDim(DATABASE::"G/L Account",GLAcc."No.","Dimension Set ID");
        InsertTempCFWorksheetLine(0);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForCustLedgerEntry@3();
    VAR
      MaxPmtTolerance@1000 : Decimal;
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::Receivables;
        "Source No." := "Cust. Ledger Entry"."Document No.";
        "Document Type" := "Cust. Ledger Entry"."Document Type";
        "Document Date" := "Cust. Ledger Entry"."Document Date";
        "Shortcut Dimension 2 Code" := "Cust. Ledger Entry"."Global Dimension 2 Code";
        "Shortcut Dimension 1 Code" := "Cust. Ledger Entry"."Global Dimension 1 Code";
        "Dimension Set ID" := "Cust. Ledger Entry"."Dimension Set ID";
        "Cash Flow Account No." := CFSetup."Receivables CF Account No.";
        Description := COPYSTR(Customer.TABLECAPTION + ' ' + FORMAT("Document Type") + ' ' + '-' + ' ' +
            FORMAT("Cust. Ledger Entry"."Due Date") + ' ' + Customer.Name,1,MAXSTRLEN(Description));
        "Document No." := "Cust. Ledger Entry"."Document No.";
        "Cash Flow Date" := "Cust. Ledger Entry"."Due Date";
        "Amount (LCY)" := "Cust. Ledger Entry"."Remaining Amt. (LCY)";
        "Pmt. Discount Date" := "Cust. Ledger Entry"."Pmt. Discount Date";
        "Pmt. Disc. Tolerance Date" := "Cust. Ledger Entry"."Pmt. Disc. Tolerance Date";

        IF "Cust. Ledger Entry"."Currency Code" <> '' THEN
          Currency.GET("Cust. Ledger Entry"."Currency Code")
        ELSE
          Currency.InitRoundingPrecision;

        "Payment Discount" := ROUND("Cust. Ledger Entry"."Remaining Pmt. Disc. Possible" /
            "Cust. Ledger Entry"."Adjusted Currency Factor",Currency."Amount Rounding Precision");

        IF "Cash Flow Forecast"."Consider Pmt. Tol. Amount" THEN
          MaxPmtTolerance := ROUND("Cust. Ledger Entry"."Max. Payment Tolerance" /
              "Cust. Ledger Entry"."Adjusted Currency Factor",Currency."Amount Rounding Precision")
        ELSE
          MaxPmtTolerance := 0;

        IF "Cash Flow Forecast"."Consider CF Payment Terms" AND (Customer."Cash Flow Payment Terms Code" <> '') THEN
          "Payment Terms Code" := Customer."Cash Flow Payment Terms Code"
        ELSE
          "Payment Terms Code" := '';

        InsertTempCFWorksheetLine(MaxPmtTolerance);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForVendorLedgEntry@4();
    VAR
      MaxPmtTolerance@1000 : Decimal;
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::Payables;
        "Source No." := "Vendor Ledger Entry"."Document No.";
        "Document Type" := "Vendor Ledger Entry"."Document Type";
        "Document Date" := "Vendor Ledger Entry"."Document Date";
        "Shortcut Dimension 2 Code" := "Vendor Ledger Entry"."Global Dimension 2 Code";
        "Shortcut Dimension 1 Code" := "Vendor Ledger Entry"."Global Dimension 1 Code";
        "Dimension Set ID" := "Vendor Ledger Entry"."Dimension Set ID";
        "Cash Flow Account No." := CFSetup."Payables CF Account No.";
        Description := COPYSTR(Vendor.TABLECAPTION + ' ' + FORMAT("Document Type") + ' ' + '-' + ' ' +
            FORMAT("Vendor Ledger Entry"."Due Date") + ' ' + Vendor.Name,1,MAXSTRLEN(Description));
        "Cash Flow Date" := "Vendor Ledger Entry"."Due Date";
        "Document No." := "Vendor Ledger Entry"."Document No.";
        "Amount (LCY)" := "Vendor Ledger Entry"."Remaining Amt. (LCY)";
        "Pmt. Discount Date" := "Vendor Ledger Entry"."Pmt. Discount Date";
        "Pmt. Disc. Tolerance Date" := "Vendor Ledger Entry"."Pmt. Disc. Tolerance Date";

        IF "Vendor Ledger Entry"."Currency Code" <> '' THEN
          Currency.GET("Vendor Ledger Entry"."Currency Code")
        ELSE
          Currency.InitRoundingPrecision;

        "Payment Discount" := ROUND("Vendor Ledger Entry"."Remaining Pmt. Disc. Possible" /
            "Vendor Ledger Entry"."Adjusted Currency Factor",Currency."Amount Rounding Precision");

        IF "Cash Flow Forecast"."Consider Pmt. Tol. Amount" THEN
          MaxPmtTolerance := ROUND("Vendor Ledger Entry"."Max. Payment Tolerance" /
              "Vendor Ledger Entry"."Adjusted Currency Factor",Currency."Amount Rounding Precision")
        ELSE
          MaxPmtTolerance := 0;

        IF "Cash Flow Forecast"."Consider CF Payment Terms" AND (Vendor."Cash Flow Payment Terms Code" <> '') THEN
          "Payment Terms Code" := Vendor."Cash Flow Payment Terms Code"
        ELSE
          "Payment Terms Code" := '';

        InsertTempCFWorksheetLine(MaxPmtTolerance);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForPurchaseLine@5();
    VAR
      PurchLine2@1000 : Record 39;
    BEGIN
      PurchLine2 := "Purchase Line";
      IF Summarized AND (PurchLine2.NEXT <> 0) AND (PurchLine2."Buy-from Vendor No." <> '') AND
         (PurchLine2."Document No." = "Purchase Line"."Document No.")
      THEN BEGIN
        TotalAmt += CalculateLineAmountForPurchaseLine(PurchHeader,"Purchase Line");
        MultiSalesLines := TRUE;
      END ELSE
        WITH CFWorksheetLine2 DO BEGIN
          INIT;
          "Source Type" := "Source Type"::"Purchase Orders";
          "Source No." := "Purchase Line"."Document No.";
          "Document Type" := "Document Type"::Invoice;
          "Document Date" := PurchHeader."Document Date";
          "Shortcut Dimension 1 Code" := PurchHeader."Shortcut Dimension 1 Code";
          "Shortcut Dimension 2 Code" := PurchHeader."Shortcut Dimension 2 Code";
          "Dimension Set ID" := PurchHeader."Dimension Set ID";
          "Cash Flow Account No." := CFSetup."Purch. Order CF Account No.";
          Description :=
            COPYSTR(
              STRSUBSTNO(
                Text025,PurchHeader.TABLECAPTION,PurchHeader."Buy-from Vendor Name",FORMAT(PurchHeader."Order Date")),
              1,MAXSTRLEN(Description));
          "Cash Flow Date" := PurchHeader."Due Date";
          "Document No." := "Purchase Line"."Document No.";

          "Amount (LCY)" := CalculateLineAmountForPurchaseLine(PurchHeader,"Purchase Line");

          IF Summarized AND MultiSalesLines THEN BEGIN
            "Amount (LCY)" := "Amount (LCY)" + TotalAmt;
            MultiSalesLines := FALSE;
            TotalAmt := 0;
          END;

          IF "Cash Flow Forecast"."Consider CF Payment Terms" AND (Vendor."Cash Flow Payment Terms Code" <> '') THEN
            "Payment Terms Code" := Vendor."Cash Flow Payment Terms Code"
          ELSE
            "Payment Terms Code" := PurchHeader."Payment Terms Code";

          InsertTempCFWorksheetLine(0);
        END;
    END;

    LOCAL PROCEDURE InsertCFLineForSalesLine@6();
    VAR
      SalesLine2@1000 : Record 37;
    BEGIN
      SalesLine2 := "Sales Line";
      IF Summarized AND (SalesLine2.NEXT <> 0) AND (SalesLine2."Sell-to Customer No." <> '') AND
         (SalesLine2."Document No." = "Sales Line"."Document No.")
      THEN BEGIN
        TotalAmt += CalculateLineAmountForSalesLine(SalesHeader,"Sales Line");
        MultiSalesLines := TRUE;
      END ELSE
        WITH CFWorksheetLine2 DO BEGIN
          INIT;
          "Document Type" := "Document Type"::Invoice;
          "Document Date" := SalesHeader."Document Date";
          "Source Type" := "Source Type"::"Sales Orders";
          "Source No." := "Sales Line"."Document No.";
          "Shortcut Dimension 1 Code" := SalesHeader."Shortcut Dimension 1 Code";
          "Shortcut Dimension 2 Code" := SalesHeader."Shortcut Dimension 2 Code";
          "Dimension Set ID" := SalesHeader."Dimension Set ID";
          "Cash Flow Account No." := CFSetup."Sales Order CF Account No.";
          Description :=
            COPYSTR(
              STRSUBSTNO(
                Text025,
                SalesHeader.TABLECAPTION,
                SalesHeader."Sell-to Customer Name",
                FORMAT(SalesHeader."Order Date")),
              1,MAXSTRLEN(Description));
          "Cash Flow Date" := SalesHeader."Due Date";
          "Document No." := "Sales Line"."Document No.";
          "Amount (LCY)" := CalculateLineAmountForSalesLine(SalesHeader,"Sales Line");

          IF Summarized AND MultiSalesLines THEN BEGIN
            "Amount (LCY)" := "Amount (LCY)" + TotalAmt;
            MultiSalesLines := FALSE;
            TotalAmt := 0;
          END;

          IF "Cash Flow Forecast"."Consider CF Payment Terms" AND (Customer."Cash Flow Payment Terms Code" <> '') THEN
            "Payment Terms Code" := Customer."Cash Flow Payment Terms Code"
          ELSE
            "Payment Terms Code" := SalesHeader."Payment Terms Code";

          InsertTempCFWorksheetLine(0);
        END;
    END;

    LOCAL PROCEDURE InsertCFLineForInvestmentFixAs@7();
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::"Fixed Assets Budget";
        "Source No." := InvestmentFixedAsset."No.";
        "Document No." := InvestmentFixedAsset."No.";
        "Cash Flow Account No." := CFSetup."FA Budget CF Account No.";
        Description :=
          COPYSTR(
            STRSUBSTNO(
              Text027,InvestmentFixedAsset."No.",FORMAT(-FADeprBook."Acquisition Cost")),
            1,MAXSTRLEN(Description));
        "Cash Flow Date" := FADeprBook."Acquisition Date";
        "Amount (LCY)" := -FADeprBook."Acquisition Cost";
        "Shortcut Dimension 2 Code" := InvestmentFixedAsset."Global Dimension 2 Code";
        "Shortcut Dimension 1 Code" := InvestmentFixedAsset."Global Dimension 1 Code";
        MoveDefualtDimToJnlLineDim(DATABASE::"Fixed Asset",InvestmentFixedAsset."No.","Dimension Set ID");
        InsertTempCFWorksheetLine(0);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForSaleFixedAsset@8();
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::"Fixed Assets Disposal";
        "Source No." := SaleFixedAsset."No.";
        "Document No." := SaleFixedAsset."No.";
        "Cash Flow Account No." := CFSetup."FA Disposal CF Account No.";
        Description :=
          COPYSTR(
            STRSUBSTNO(
              Text027,SaleFixedAsset."No.",FORMAT(FADeprBook."Projected Proceeds on Disposal")),
            1,MAXSTRLEN(Description));
        "Cash Flow Date" := FADeprBook."Projected Disposal Date";
        "Amount (LCY)" := FADeprBook."Projected Proceeds on Disposal" ;
        "Shortcut Dimension 2 Code" := SaleFixedAsset."Global Dimension 2 Code";
        "Shortcut Dimension 1 Code" := SaleFixedAsset."Global Dimension 1 Code";
        MoveDefualtDimToJnlLineDim(DATABASE::"Fixed Asset",SaleFixedAsset."No.","Dimension Set ID");
        InsertTempCFWorksheetLine(0);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForManualExpens@9();
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        "Cash Flow Manual Expense".TESTFIELD("Starting Date");
        INIT;
        "Source Type" := "Source Type"::"Cash Flow Manual Expense";
        "Source No." := "Cash Flow Manual Expense".Code;
        "Document No." := "Cash Flow Manual Expense".Code;
        "Cash Flow Account No." := "Cash Flow Manual Expense"."Cash Flow Account No.";
        "Shortcut Dimension 1 Code" := "Cash Flow Manual Expense"."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := "Cash Flow Manual Expense"."Global Dimension 2 Code";
        MoveDefualtDimToJnlLineDim(DATABASE::"Cash Flow Manual Expense","Cash Flow Manual Expense".Code,"Dimension Set ID");
        Description := COPYSTR(STRSUBSTNO(Text028,"Cash Flow Manual Expense".Description),1,MAXSTRLEN(Description));
        DateLastExecution := "Cash Flow Forecast"."Manual Payments To";
        IF ("Cash Flow Manual Expense"."Ending Date" <> 0D) AND
           ("Cash Flow Manual Expense"."Ending Date" < "Cash Flow Forecast"."Manual Payments To")
        THEN
          DateLastExecution := "Cash Flow Manual Expense"."Ending Date";
        ExecutionDate := "Cash Flow Manual Expense"."Starting Date";
        IF FORMAT("Cash Flow Manual Expense"."Recurring Frequency") <> '' THEN BEGIN
          IF DateLastExecution = 0D THEN BEGIN
            NeedsManualPmtUpdate := TRUE;
            InsertManualData(
              ExecutionDate,"Cash Flow Forecast",-"Cash Flow Manual Expense".Amount);
          END ELSE
            WHILE ExecutionDate <= DateLastExecution DO BEGIN
              InsertManualData(
                ExecutionDate,"Cash Flow Forecast",-"Cash Flow Manual Expense".Amount);
              ExecutionDate := CALCDATE("Cash Flow Manual Expense"."Recurring Frequency",ExecutionDate);
            END;
        END ELSE
          InsertManualData(ExecutionDate,"Cash Flow Forecast",-"Cash Flow Manual Expense".Amount);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForManualRevenue@11();
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        "Cash Flow Manual Revenue".TESTFIELD("Starting Date");
        INIT;
        "Source Type" := "Source Type"::"Cash Flow Manual Revenue";
        "Source No." := "Cash Flow Manual Revenue".Code;
        "Document No." := "Cash Flow Manual Revenue".Code;
        "Cash Flow Account No." := "Cash Flow Manual Revenue"."Cash Flow Account No.";
        "Shortcut Dimension 1 Code" := "Cash Flow Manual Revenue"."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := "Cash Flow Manual Revenue"."Global Dimension 2 Code";
        MoveDefualtDimToJnlLineDim(DATABASE::"Cash Flow Manual Revenue","Cash Flow Manual Revenue".Code,"Dimension Set ID");
        Description := COPYSTR(STRSUBSTNO(Text029,"Cash Flow Manual Revenue".Description),1,MAXSTRLEN(Description));
        DateLastExecution := "Cash Flow Forecast"."Manual Payments To";
        IF ("Cash Flow Manual Revenue"."Ending Date" <> 0D) AND
           ("Cash Flow Manual Revenue"."Ending Date" < "Cash Flow Forecast"."Manual Payments To")
        THEN
          DateLastExecution := "Cash Flow Manual Revenue"."Ending Date";
        ExecutionDate := "Cash Flow Manual Revenue"."Starting Date";
        IF FORMAT("Cash Flow Manual Revenue"."Recurring Frequency") <> '' THEN BEGIN
          IF DateLastExecution = 0D THEN BEGIN
            NeedsManualPmtUpdate := TRUE;
            InsertManualData(
              ExecutionDate,"Cash Flow Forecast","Cash Flow Manual Revenue".Amount);
          END ELSE
            WHILE ExecutionDate <= DateLastExecution DO BEGIN
              InsertManualData(
                ExecutionDate,"Cash Flow Forecast","Cash Flow Manual Revenue".Amount);
              ExecutionDate := CALCDATE("Cash Flow Manual Revenue"."Recurring Frequency",ExecutionDate);
            END;
        END ELSE
          InsertManualData(ExecutionDate,"Cash Flow Forecast","Cash Flow Manual Revenue".Amount);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForGLBudget@10(GLAcc@1000 : Record 15);
    BEGIN
      WITH CFWorksheetLine2 DO BEGIN
        INIT;
        "Source Type" := "Source Type"::"G/L Budget";
        "Source No." := GLAcc."No.";
        "G/L Budget Name" := GLBudgEntry."Budget Name";
        "Document No." := FORMAT(GLBudgEntry."Entry No.");
        "Cash Flow Account No." := CFAccountForBudget."No.";
        Description :=
          COPYSTR(
            STRSUBSTNO(
              Text030,GLAcc.Name,FORMAT(GLBudgEntry.Description)),
            1,MAXSTRLEN(Description));
        "Cash Flow Date" := GLBudgEntry.Date;
        "Amount (LCY)" := -GLBudgEntry.Amount;
        "Shortcut Dimension 1 Code" := GLBudgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := GLBudgEntry."Global Dimension 2 Code";
        "Dimension Set ID" := GLBudgEntry."Dimension Set ID";
        InsertTempCFWorksheetLine(0);
      END;
    END;

    LOCAL PROCEDURE InsertCFLineForServiceLine@12();
    VAR
      ServiceLine2@1000 : Record 5902;
    BEGIN
      ServiceLine2 := "Service Line";
      IF Summarized AND (ServiceLine2.NEXT <> 0) AND (ServiceLine2."Customer No." <> '') AND
         (ServiceLine2."Document No." = "Service Line"."Document No.")
      THEN BEGIN
        TotalAmt += CalculateLineAmountForServiceLine("Service Line");

        MultiSalesLines := TRUE;
      END ELSE
        WITH CFWorksheetLine2 DO BEGIN
          INIT;
          "Source Type" := "Source Type"::"Service Orders";
          "Source No." := "Service Line"."Document No.";
          "Document Type" := "Document Type"::Invoice;
          "Document Date" := ServiceHeader."Document Date";
          "Shortcut Dimension 1 Code" := ServiceHeader."Shortcut Dimension 1 Code";
          "Shortcut Dimension 2 Code" := ServiceHeader."Shortcut Dimension 2 Code";
          "Dimension Set ID" := ServiceHeader."Dimension Set ID";
          "Cash Flow Account No." := CFSetup."Service CF Account No.";
          Description :=
            COPYSTR(
              STRSUBSTNO(
                Text025,
                ServiceHeader.TABLECAPTION,
                ServiceHeader.Name,
                FORMAT(ServiceHeader."Order Date")),
              1,MAXSTRLEN(Description));
          "Cash Flow Date" := ServiceHeader."Due Date";
          "Document No." := "Service Line"."Document No.";
          "Amount (LCY)" := CalculateLineAmountForServiceLine("Service Line");

          IF Summarized AND MultiSalesLines THEN BEGIN
            "Amount (LCY)" := "Amount (LCY)" + TotalAmt;
            MultiSalesLines := FALSE;
            TotalAmt := 0;
          END;

          IF "Cash Flow Forecast"."Consider CF Payment Terms" AND (Customer."Cash Flow Payment Terms Code" <> '') THEN
            "Payment Terms Code" := Customer."Cash Flow Payment Terms Code"
          ELSE
            "Payment Terms Code" := ServiceHeader."Payment Terms Code";

          InsertTempCFWorksheetLine(0);
        END;
    END;

    LOCAL PROCEDURE CalculateLineAmountForPurchaseLine@19(PurchHeader2@1001 : Record 38;PurchaseLine@1002 : Record 39) : Decimal;
    VAR
      PrepmtAmtInvLCY@1000 : Decimal;
    BEGIN
      IF PurchHeader2."Currency Code" <> '' THEN BEGIN
        Currency.GET(PurchHeader2."Currency Code");
        PrepmtAmtInvLCY :=
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
              PurchHeader2."Posting Date",PurchHeader2."Currency Code",
              PurchaseLine."Prepmt. Amt. Inv.",PurchHeader2."Currency Factor"),
            Currency."Amount Rounding Precision");
      END ELSE
        PrepmtAmtInvLCY := PurchaseLine."Prepmt. Amt. Inv.";

      Currency.InitRoundingPrecision;
      IF PurchHeader2."Prices Including VAT" THEN
        EXIT(-(GetPurchaseAmountForCFLine(PurchaseLine) - PrepmtAmtInvLCY));
      EXIT(
        -(GetPurchaseAmountForCFLine(PurchaseLine) -
          (PrepmtAmtInvLCY +
           ROUND(PrepmtAmtInvLCY * PurchaseLine."VAT %" / 100,Currency."Amount Rounding Precision",Currency.VATRoundingDirection))));
    END;

    LOCAL PROCEDURE CalculateLineAmountForSalesLine@14(SalesHeader2@1001 : Record 36;SalesLine@1002 : Record 37) : Decimal;
    VAR
      PrepmtAmtInvLCY@1000 : Decimal;
    BEGIN
      IF SalesHeader2."Currency Code" <> '' THEN BEGIN
        Currency.GET(SalesHeader2."Currency Code");
        PrepmtAmtInvLCY :=
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
              SalesHeader2."Posting Date",SalesHeader2."Currency Code",
              SalesLine."Prepmt. Amt. Inv.",SalesHeader2."Currency Factor"),
            Currency."Amount Rounding Precision");
      END ELSE
        PrepmtAmtInvLCY := SalesLine."Prepmt. Amt. Inv.";

      Currency.InitRoundingPrecision;
      IF SalesHeader2."Prices Including VAT" THEN
        EXIT(GetSalesAmountForCFLine(SalesLine) - PrepmtAmtInvLCY);
      EXIT(
        GetSalesAmountForCFLine(SalesLine) -
        (PrepmtAmtInvLCY +
         ROUND(PrepmtAmtInvLCY * SalesLine."VAT %" / 100,Currency."Amount Rounding Precision",Currency.VATRoundingDirection)));
    END;

    LOCAL PROCEDURE CalculateLineAmountForServiceLine@20(ServiceLine@1002 : Record 5902) : Decimal;
    BEGIN
      EXIT(GetServiceAmountForCFLine(ServiceLine));
    END;

    LOCAL PROCEDURE NoOptionsChosen@15() : Boolean;
    VAR
      SourceType@1000 : Integer;
    BEGIN
      FOR SourceType := 1 TO ARRAYLEN(ConsiderSource) DO
        IF ConsiderSource[SourceType] THEN
          EXIT(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE InitializeRequest@13(NewConsiderSource@1000 : ARRAY [11] OF Boolean;CFNo@1011 : Code[20];NewGLBudgetName@1012 : Code[10];GroupByDocumentType@1001 : Boolean);
    BEGIN
      COPYARRAY(ConsiderSource,NewConsiderSource,1);
      CashFlowNo := CFNo;
      GLBudgName := NewGLBudgetName;
      Summarized := GroupByDocumentType;
    END;

    LOCAL PROCEDURE InsertManualData@2(ExecutionDate@1001 : Date;CashFlowForecast@1002 : Record 840;ManualAmount@1000 : Decimal);
    BEGIN
      IF ((CashFlowForecast."Manual Payments From" <> 0D) AND
          (ExecutionDate < CashFlowForecast."Manual Payments From")) OR
         ((CashFlowForecast."Manual Payments To" <> 0D) AND
          (ExecutionDate > CashFlowForecast."Manual Payments To"))
      THEN
        EXIT;

      WITH CFWorksheetLine2 DO BEGIN
        "Cash Flow Date" := ExecutionDate;
        "Amount (LCY)" := ManualAmount;
        InsertTempCFWorksheetLine(0);
      END;
    END;

    LOCAL PROCEDURE GetPurchaseAmountForCFLine@23(PurchaseLine@1000 : Record 39) : Decimal;
    BEGIN
      EXIT(PurchaseLine."Outstanding Amount (LCY)" + PurchaseLine."Amt. Rcd. Not Invoiced (LCY)");
    END;

    LOCAL PROCEDURE GetSalesAmountForCFLine@24(SalesLine@1000 : Record 37) : Decimal;
    BEGIN
      EXIT(SalesLine."Outstanding Amount (LCY)" + SalesLine."Shipped Not Invoiced (LCY)");
    END;

    LOCAL PROCEDURE GetServiceAmountForCFLine@17(ServiceLine@1000 : Record 5902) : Decimal;
    BEGIN
      EXIT(ServiceLine."Outstanding Amount (LCY)" + ServiceLine."Shipped Not Invoiced (LCY)");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

