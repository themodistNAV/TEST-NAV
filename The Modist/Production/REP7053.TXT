OBJECT Report 7053 Implement Price Change
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Implement Price Change;
               ENG=Implement Price Change];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5995;    ;DataItem;                    ;
               DataItemTable=Table7023;
               DataItemTableView=SORTING(Starting Date,Ending Date,Sales Type,Sales Code,Currency Code,Item No.,Variant Code,Unit of Measure Code,Minimum Quantity);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text000 +
                                 Text007 +
                                 Text008 +
                                 Text009 +
                                 Text010 +
                                 Text011);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"Item No.");
                                  Window.UPDATE(2,"Sales Type");
                                  Window.UPDATE(3,"Sales Code");
                                  Window.UPDATE(4,"Currency Code");
                                  Window.UPDATE(5,"Starting Date");

                                  SalesPrice.INIT;
                                  SalesPrice.VALIDATE("Item No.","Item No.");
                                  SalesPrice.VALIDATE("Sales Type","Sales Type");
                                  SalesPrice.VALIDATE("Sales Code","Sales Code");
                                  SalesPrice.VALIDATE("Unit of Measure Code","Unit of Measure Code");
                                  SalesPrice.VALIDATE("Variant Code","Variant Code");
                                  SalesPrice.VALIDATE("Starting Date","Starting Date");
                                  SalesPrice.VALIDATE("Ending Date","Ending Date");
                                  SalesPrice."Minimum Quantity" := "Minimum Quantity";
                                  SalesPrice."Currency Code" := "Currency Code";
                                  SalesPrice."Unit Price" := "New Unit Price";
                                  SalesPrice."Price Includes VAT" := "Price Includes VAT";
                                  SalesPrice."Allow Line Disc." := "Allow Line Disc.";
                                  SalesPrice."Allow Invoice Disc." := "Allow Invoice Disc.";
                                  SalesPrice."VAT Bus. Posting Gr. (Price)" := "VAT Bus. Posting Gr. (Price)";
                                  IF SalesPrice."Unit Price" <> 0 THEN
                                    IF NOT SalesPrice.INSERT(TRUE) THEN
                                      SalesPrice.MODIFY(TRUE);
                                END;

               OnPostDataItem=BEGIN
                                COMMIT;
                                IF NOT DeleteWhstLine THEN
                                  DeleteWhstLine := CONFIRM(Text005);
                                IF DeleteWhstLine THEN
                                  DELETEALL;
                                COMMIT;
                                IF SalesPrice.FINDFIRST THEN;
                              END;

               ReqFilterFields=Item No.,Sales Type,Sales Code,Unit of Measure Code,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Updating Unit Prices...\\;ENG=Updating Unit Prices...\\';
      Text005@1001 : TextConst 'ENU=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?;ENG=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?';
      Text007@1003 : TextConst 'ENU=Item No.               #1##########\;ENG=Item No.               #1##########\';
      Text008@1004 : TextConst 'ENU=Sales Type             #2##########\;ENG=Sales Type             #2##########\';
      Text009@1005 : TextConst 'ENU=Sales Code             #3##########\;ENG=Sales Code             #3##########\';
      Text010@1006 : TextConst 'ENU=Currency Code          #4##########\;ENG=Currency Code          #4##########\';
      Text011@1007 : TextConst 'ENU=Starting Date          #5######;ENG=Starting Date          #5######';
      SalesPrice@1008 : Record 7002;
      Window@1009 : Dialog;
      DeleteWhstLine@1010 : Boolean;

    PROCEDURE InitializeRequest@1(NewDeleteWhstLine@1000 : Boolean);
    BEGIN
      DeleteWhstLine := NewDeleteWhstLine;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

