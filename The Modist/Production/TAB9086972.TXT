OBJECT Table 9086972 nF. Smart Filter
{
  OBJECT-PROPERTIES
  {
    Date=30/10/16;
    Time=12:09:44 PM;
    Version List=NF2016.17;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(ID);
               Rec.TESTFIELD(Pattern);
             END;

    CaptionML=[ENU=nForcement Smart Filter;
               ENG=nForcement Smart Filter];
    LookupPageID=Page9086975;
    DrillDownPageID=Page9086975;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 11  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 20  ;   ;Pattern             ;Text250       ;CaptionML=[ENU=Pattern;
                                                              ENG=Pattern] }
    { 21  ;   ;Use Regular Expressions;Boolean    ;CaptionML=[ENU=Use Regular Expressions;
                                                              ENG=Use Regular Expressions] }
    { 30  ;   ;Syntax              ;Text250       ;CaptionML=[ENU=Syntax;
                                                              ENG=Syntax] }
    { 40  ;   ;Help                ;BLOB          ;CaptionML=[ENU=Help;
                                                              ENG=Help];
                                                   SubType=Memo }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NfBlobToolkit@1000000000 : Codeunit 9086979;
      LastId@1000000001 : Integer;

    PROCEDURE Add@1000000000(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pHelp@1000000001 : Text) : Integer;
    BEGIN
      ///<summary>Adding an entry</summary>
      ///<param name="pCode">Code</param>
      ///<param name="pDescription">Description</param>
      ///<param name="pHelp">Help</param>
      ///<returns>ID of entry added</returns>

      EXIT(AddInternal(pCode, pDescription, pCode, pCode, pHelp, FALSE));
    END;

    PROCEDURE AddRegEx@1000000001(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pPattern@1000000003 : Text;pSyntax@1000000001 : Text;pHelp@1000000004 : Text) : Integer;
    BEGIN
      ///<summary>Adding regular-expression entry</summary>
      ///<param name="pCode">Code</param>
      ///<param name="pDescription">Description</param>
      ///<param name="pPattern">Pattern</param>
      ///<param name="pSyntax">Syntax</param>
      ///<param name="pHelp">Help</param>
      ///<returns>ID of entry added</returns>

      EXIT(AddInternal(pCode, pDescription, pPattern, pSyntax, pHelp, TRUE));
    END;

    LOCAL PROCEDURE AddInternal@1000000003(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pPattern@1000000003 : Text;pSyntax@1000000005 : Text;pHelp@1000000001 : Text;pUseRegularExpressions@1000000004 : Boolean) : Integer;
    VAR
      lHelpBlob@1000000006 : Record 99008535;
    BEGIN
      ///<summary>Adding an entry (internal version)</summary>
      ///<param name="pCode">Code</param>
      ///<param name="pDescription">Description</param>
      ///<param name="pPattern">Pattern</param>
      ///<param name="pSyntax">Syntax</param>
      ///<param name="pHelp">Help</param>
      ///<param name="pUseRegularExpressions">Flag to use regular expressions</param>
      ///<returns>ID of entry added</returns>

      LastId += 1;

      Rec.INIT();
      Rec.VALIDATE(ID, LastId);
      Rec.VALIDATE(Code, pCode);
      Rec.VALIDATE(Description, COPYSTR(pDescription, 1, MAXSTRLEN(Rec.Description)));
      Rec.VALIDATE(Pattern, pPattern);
      Rec.VALIDATE(Syntax, pSyntax);
      NfBlobToolkit.TextToBlob(pHelp, lHelpBlob);
      Rec.VALIDATE(Help, lHelpBlob.Blob);
      Rec.VALIDATE("Use Regular Expressions", pUseRegularExpressions);
      Rec.INSERT(TRUE);

      EXIT(LastId);
    END;

    BEGIN
    {
      ///<summary>Table used as in-memory buffer for nForcement smart filters</summary>
      ///<change version="NF2016.17" initials="JH" date="2016-10-20">Created</change>
    }
    END.
  }
}

