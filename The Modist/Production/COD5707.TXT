OBJECT Codeunit 5707 TransferOrder-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;ENG=&Ship,&Receive';
      TransHeader@1001 : Record 5740;

    LOCAL PROCEDURE Code@2();
    VAR
      TransLine@1000 : Record 5741;
      DefaultNumber@1001 : Integer;
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      Selection@1004 : ' ,Shipment,Receipt';
    BEGIN
      WITH TransHeader DO BEGIN
        TransLine.SETRANGE("Document No.","No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := Selection::Shipment;
            IF (TransLine."Quantity Received" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := Selection::Receipt;
          UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);

        IF DefaultNumber = 0 THEN
          DefaultNumber := Selection::Shipment;
        Selection := STRMENU(Text000,DefaultNumber);
        CASE Selection OF
          0:
            EXIT;
          Selection::Shipment:
            TransferPostShipment.RUN(TransHeader);
          Selection::Receipt:
            TransferPostReceipt.RUN(TransHeader);
        END;
        PrintReport(TransHeader,Selection);
      END;
    END;

    PROCEDURE PrintReport@1(TransHeaderSource@1001 : Record 5740;Selection@1000 : ' ,Shipment,Receipt');
    BEGIN
      WITH TransHeaderSource DO
        CASE Selection OF
          Selection::Shipment:
            PrintShipment("Last Shipment No.");
          Selection::Receipt:
            PrintReceipt("Last Receipt No.");
        END;
    END;

    LOCAL PROCEDURE PrintShipment@6(DocNo@1000 : Code[20]);
    VAR
      TransShptHeader@1001 : Record 5744;
    BEGIN
      IF TransShptHeader.GET(DocNo) THEN BEGIN
        TransShptHeader.SETRECFILTER;
        TransShptHeader.PrintRecords(FALSE);
      END;
    END;

    LOCAL PROCEDURE PrintReceipt@7(DocNo@1000 : Code[20]);
    VAR
      TransRcptHeader@1001 : Record 5746;
    BEGIN
      IF TransRcptHeader.GET(DocNo) THEN BEGIN
        TransRcptHeader.SETRECFILTER;
        TransRcptHeader.PrintRecords(FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

