OBJECT Table 9087001 Size Group
{
  OBJECT-PROPERTIES
  {
    Date=17/10/18;
    Time=[ 3:46:18 PM];
    Modified=Yes;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(Code);
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CheckOnDeletion();

               Size.RESET();
               Size.SETRANGE("Size Group Code", Rec.Code);
               Size.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Size Group;
               ENG=Size Group];
    LookupPageID=Page9087001;
    DrillDownPageID=Page9087001;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                TnpToolkit.CodeToName(Rec.Code, Rec.Name);
                                                                Rec.VALIDATE(Name);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 10  ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 20  ;   ;Sequence            ;Integer       ;CaptionML=[ENU=Sequence;
                                                              ENG=Sequence];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 30  ;   ;Standard Size Group Code;Code20    ;TableRelation="Size Group";
                                                   OnValidate=BEGIN
                                                                UpdateSizes(Rec.FIELDNO("Standard Size Group Code"));
                                                              END;

                                                   CaptionML=[ENU=Standard Size Group Code;
                                                              ENG=Standard Size Group Code] }
    { 40  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ENG=Blocked] }
    { 50  ;   ;Styles              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Style WHERE (Size Group Code=FIELD(Code)));
                                                   CaptionML=[ENU=Styles;
                                                              ENG=Styles];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 51  ;   ;Items               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Item WHERE (Size Group Code=FIELD(Code)));
                                                   CaptionML=[ENU=Items;
                                                              ENG=Items];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
    { 50000;  ;Hide In BS          ;Boolean        }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Sequence,Code                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Size@1000000001 : Record 9087002;
      TnpToolkit@1000000000 : Codeunit 9062230;

    LOCAL PROCEDURE CheckOnDeletion@1000000001();
    VAR
      lDeletionCheck@1000000000 : Codeunit 9062384;
    BEGIN
      lDeletionCheck.AddTable(DATABASE::Style);
      lDeletionCheck.AddTable(DATABASE::"Style Colour Size");
      lDeletionCheck.AddTable(DATABASE::Item);
      lDeletionCheck.AddTable(DATABASE::"Item Variant");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Summary Line");
      lDeletionCheck.AddTable(DATABASE::"Purchase Line");
      lDeletionCheck.AddTable(DATABASE::"Item Ledger Entry");
      lDeletionCheck.AddTable(DATABASE::"Value Entry");
      lDeletionCheck.AddTable(DATABASE::"Purch. Rcpt. Line");
      lDeletionCheck.AddTable(DATABASE::"Purch. Inv. Line");
      lDeletionCheck.AddTable(DATABASE::"Purch. Cr. Memo Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Arch. Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Arch. Sum. Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Arch. Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Arch. Line");
      lDeletionCheck.CheckMultipleTables(DATABASE::"Size Group", Rec.Code, 'Size Group Code');
    END;

    PROCEDURE UpdateSizes@1000000000(pFieldId@1000000000 : Integer);
    BEGIN
      Size.RESET();
      Size.SETRANGE("Size Group Code", Rec.Code);
      IF (Size.FINDSET(TRUE)) THEN
      REPEAT
        CASE (pFieldId) OF
          Rec.FIELDNO("Standard Size Group Code"):
            Size.VALIDATE("Standard Size Group Code", Rec."Standard Size Group Code");
        END;
        Size.MODIFY(TRUE);
      UNTIL (Size.NEXT() = 0);
    END;

    PROCEDURE ShowStyles@1000000005();
    VAR
      lStyle@1000000000 : Record 9087012;
    BEGIN
      lStyle.RESET();
      lStyle.SETRANGE("Size Group Code", Rec.Code);
      PAGE.RUN(0, lStyle);
    END;

    PROCEDURE ShowItems@1000000006();
    VAR
      lItem@1000000000 : Record 27;
    BEGIN
      lItem.RESET();
      lItem.SETRANGE("Size Group Code", Rec.Code);
      PAGE.RUN(0, lItem);
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 08/01/2014 - Created
      //DOC NS2015.3  JH 30/07/2014 - "Blocked" added
      //DOC NS2016.16 JH 05/09/2016 - nSemble 2016
    }
    END.
  }
}

