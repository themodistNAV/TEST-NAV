OBJECT Table 11102048 OM - Action
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=Action Type,Object No.,Object Name;
    OnInsert=VAR
               Project@1000 : Record 11102036;
             BEGIN
               IF Type = Type::Project THEN
                 IF Project.GET("No.") THEN
                   "Transport No." := Project."Transport No.";

               UpdateRepository(1);
             END;

    OnModify=BEGIN
               UpdateRepository(2);
             END;

    OnDelete=VAR
               ActionField@1000 : Record 11102049;
             BEGIN
               ActionField.SETRANGE(Type, Type);
               ActionField.SETRANGE("No.", "No.");
               ActionField.SETRANGE("Sub Type", "Sub Type");
               ActionField.SETRANGE("Line No.", "Line No.");
               ActionField.DELETEALL;

               UpdateRepository(3);
             END;

    OnRename=BEGIN
               UpdateRepository(4);
             END;

    CaptionML=[DEU=Action;
               ENU=Action;
               ESP=Action;
               FRA=Action;
               NLD=Actie;
               DEA=Action];
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=[DEU=Type;
                                                              ENU=Type;
                                                              ESP=Type;
                                                              FRA=Type;
                                                              NLD=Soort;
                                                              DEA=Type];
                                                   OptionCaptionML=[DEU=Group,Project,Temporary;
                                                                    ENU=Group,Project,Temporary;
                                                                    ESP=Group,Project,Temporary;
                                                                    FRA=Group,Project,Temporary;
                                                                    NLD=Groep,Project,Tijdelijk;
                                                                    DEA=Group,Project,Temporary];
                                                   OptionString=Group,Project,Temporary }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Group)) "OM - Action Group"
                                                                 ELSE IF (Type=CONST(Project)) "OM - Project";
                                                   CaptionML=[DEU=No.;
                                                              ENU=No.;
                                                              ESP=No.;
                                                              FRA=No.;
                                                              NLD=Nr.;
                                                              DEA=No.] }
    { 3   ;   ;Sub Type            ;Option        ;CaptionML=[DEU=Sub Type;
                                                              ENU=Sub Type;
                                                              ESP=Sub Type;
                                                              FRA=Sub Type;
                                                              NLD=Subtype;
                                                              DEA=Sub Type];
                                                   OptionCaptionML=[DEU=" ,Before,After";
                                                                    ENU=" ,Before,After";
                                                                    ESP=" ,Before,After";
                                                                    FRA=" ,Before,After";
                                                                    NLD=" ,Vooraf,Achteraf";
                                                                    DEA=" ,Before,After"];
                                                   OptionString=[ ,Before,After] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Line No.;
                                                              ENU=Line No.;
                                                              ESP=Line No.;
                                                              FRA=Line No.;
                                                              NLD=Regelnr.;
                                                              DEA=Line No.] }
    { 5   ;   ;Action Type         ;Option        ;OnValidate=VAR
                                                                OriginalRec@1000 : Record 11102048;
                                                                Object@1001 : Record 2000000001;
                                                              BEGIN
                                                                OriginalRec := Rec;
                                                                INIT;
                                                                "Action Type" := OriginalRec."Action Type";

                                                                "DELETEALL;" := FALSE;
                                                                "INIT;" := TRUE;
                                                                "IF FIND('=') THEN;" := TRUE;
                                                                "IF INSERT THEN;" := TRUE;
                                                                "IF MODIFY THEN;" := TRUE;

                                                                IF IsTableRecord AND Object.GET(Object.Type::Table, '', OriginalRec."Object No.") THEN
                                                                  VALIDATE("Object No.", OriginalRec."Object No.")
                                                                ELSE
                                                                  VALIDATE("Object No.", 0);

                                                                IF NOT IsTableRecord THEN BEGIN
                                                                  "INIT;" := FALSE;
                                                                  "IF FIND('=') THEN;" := FALSE;
                                                                  "IF INSERT THEN;" := FALSE;
                                                                  "IF MODIFY THEN;" := FALSE;
                                                                END;

                                                                CASE "Action Type" OF

                                                                  "Action Type"::"Transfer Data" :
                                                                    BEGIN
                                                                      VALIDATE("Company Name", COMPANYNAME);
                                                                      VALIDATE("Into Company Name", AllCompaniesText);
                                                                    END;

                                                                  "Action Type"::"Add Record" :
                                                                    BEGIN
                                                                      "IF FIND('=') THEN;" := FALSE;
                                                                      VALIDATE("Into Company Name", AllCompaniesText);
                                                                    END;

                                                                  "Action Type"::"Renumber Object" :
                                                                    BEGIN
                                                                      VALIDATE("Object No.", DATABASE::Object);
                                                                      VALIDATE("Company Name", AllCompaniesText);
                                                                    END;

                                                                  "Action Type"::"Renumber Field" :
                                                                    BEGIN
                                                                      VALIDATE("Object No.", DATABASE::Field);
                                                                      VALIDATE("Company Name", AllCompaniesText);
                                                                    END;

                                                                  ELSE
                                                                    VALIDATE("Company Name", AllCompaniesText);

                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Action Type;
                                                              ENU=Action Type;
                                                              ESP=Action Type;
                                                              FRA=Action Type;
                                                              NLD=Actiesoort;
                                                              DEA=Action Type];
                                                   OptionCaptionML=[DEU=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command;
                                                                    ENU=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command;
                                                                    ESP=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command;
                                                                    FRA=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command;
                                                                    NLD=Data kopi‰ren,Data verwijderen,Data overzetten,Velden vullen,Rapport draaien,Codeunit draaien,,,Record toevoegen,Data hernoemen,Object omnummeren,Veld omnummeren,,,SQL-query uitvoeren,DOS-commando uitvoeren;
                                                                    DEA=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command];
                                                   OptionString=Copy Data,Delete Data,Transfer Data,Fill Fields,Run Report,Run Codeunit,,,Add Record,Rename Data,Renumber Object,Renumber Field,,,Execute SQL Query,Execute DOS Command }
    { 20  ;   ;Object No.          ;Integer       ;TableRelation=IF (Action Type=FILTER(Copy Data|Delete Data|Transfer Data|Fill Fields|Rename Data|Renumber Object|Renumber Field)) AllObj."Object ID" WHERE (Object Type=CONST(Table))
                                                                 ELSE IF (Action Type=FILTER(Run Report)) AllObj."Object ID" WHERE (Object Type=CONST(Report))
                                                                 ELSE IF (Action Type=FILTER(Run Codeunit)) AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                                AllObj@1001 : Record 2000000038;
                                                              BEGIN
                                                                CASE TRUE OF

                                                                  Object.GET(GetObjectType, '', "Object No.") :
                                                                    "Object Name" := Object.Name;

                                                                  AllObj.GET(GetObjectType, "Object No.") :
                                                                    "Object Name" := AllObj."Object Name";

                                                                  ELSE
                                                                    "Object Name" := '';
                                                                END;

                                                                FillActionFields;
                                                                CLEAR(Filter);
                                                              END;

                                                   CaptionML=[DEU=Object No.;
                                                              ENU=Object No.;
                                                              ESP=Object No.;
                                                              FRA=Object No.;
                                                              NLD=Objectnr.;
                                                              DEA=Object No.];
                                                   BlankZero=Yes }
    { 21  ;   ;Object Name         ;Text30        ;CaptionML=[DEU=Object Name;
                                                              ENU=Object Name;
                                                              ESP=Object Name;
                                                              FRA=Object Name;
                                                              NLD=Objectnaam;
                                                              DEA=Object Name];
                                                   Editable=No }
    { 22  ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                Company@1000 : Record 2000000006;
                                                              BEGIN
                                                                IF CurrFieldNo = FIELDNO("Company Name") THEN
                                                                  IF NOT ("Company Name" IN ['', ActiveCompanyText, AllCompaniesText]) THEN
                                                                    Company.GET("Company Name");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Company Name;
                                                              ENU=Company Name;
                                                              ESP=Company Name;
                                                              FRA=Company Name;
                                                              NLD=Bedrijfsnaam;
                                                              DEA=Company Name] }
    { 23  ;   ;Fields              ;Option        ;OnValidate=BEGIN
                                                                IF Fields = Fields::All THEN
                                                                  FillActionFields;
                                                              END;

                                                   CaptionML=[DEU=Fields;
                                                              ENU=Fields;
                                                              ESP=Fields;
                                                              FRA=Fields;
                                                              NLD=Velden;
                                                              DEA=Fields];
                                                   OptionCaptionML=[DEU=All,Selected;
                                                                    ENU=All,Selected;
                                                                    ESP=All,Selected;
                                                                    FRA=All,Selected;
                                                                    NLD=Alle,Geselecteerd;
                                                                    DEA=All,Selected];
                                                   OptionString=All,Selected }
    { 24  ;   ;Into Table No.      ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table),
                                                                                  Company Name=CONST());
                                                   OnValidate=VAR
                                                                ActionField@1000 : Record 11102049;
                                                              BEGIN
                                                                CALCFIELDS("Into Table Name");

                                                                FillDefaultFieldMapActions(ActionField);
                                                              END;

                                                   CaptionML=[DEU=Into Table No.;
                                                              ENU=Into Table No.;
                                                              ESP=Into Table No.;
                                                              FRA=Into Table No.;
                                                              NLD=Doel tabelnr.;
                                                              DEA=Into Table No.];
                                                   BlankZero=Yes }
    { 25  ;   ;Into Table Name     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         Company Name=CONST(),
                                                                                         ID=FIELD(Into Table No.)));
                                                   CaptionML=[DEU=Into Table Name;
                                                              ENU=Into Table Name;
                                                              ESP=Into Table Name;
                                                              FRA=Into Table Name;
                                                              NLD=Doel tabelnaam;
                                                              DEA=Into Table Name];
                                                   Editable=No }
    { 26  ;   ;Into Company Name   ;Text30        ;CaptionML=[DEU=Into Company Name;
                                                              ENU=Into Company Name;
                                                              ESP=Into Company Name;
                                                              FRA=Into Company Name;
                                                              NLD=Doel bedrijfsnaam;
                                                              DEA=Into Company Name] }
    { 27  ;   ;Into Fields         ;Option        ;OnValidate=VAR
                                                                ActionField@1000 : Record 11102049;
                                                              BEGIN
                                                                IF "Into Fields" = "Into Fields"::" " THEN
                                                                  FillDefaultFieldMapActions(ActionField);
                                                              END;

                                                   CaptionML=[DEU=Into Fields;
                                                              ENU=Into Fields;
                                                              ESP=Into Fields;
                                                              FRA=Into Fields;
                                                              NLD=Doelvelden;
                                                              DEA=Into Fields];
                                                   OptionCaptionML=[DEU=" ,Mapped";
                                                                    ENU=" ,Mapped";
                                                                    ESP=" ,Mapped";
                                                                    FRA=" ,Mapped";
                                                                    NLD=" ,Gelinkt";
                                                                    DEA=" ,Mapped"];
                                                   OptionString=[ ,Mapped] }
    { 28  ;   ;Filter              ;TableFilter   ;CaptionML=[DEU=Filter;
                                                              ENU=Filter;
                                                              ESP=Filter;
                                                              FRA=Filter;
                                                              NLD=Filter;
                                                              DEA=Filter] }
    { 29  ;   ;No. of Records      ;Integer       ;CaptionML=[DEU=No. of Records;
                                                              ENU=No. of Records;
                                                              ESP=No. of Records;
                                                              FRA=No. of Records;
                                                              NLD=Aantal records;
                                                              DEA=No. of Records] }
    { 30  ;   ;[DELETEALL;]        ;Boolean       ;CaptionML=[DEU="DELETEALL;";
                                                              ENU="DELETEALL;";
                                                              ESP="DELETEALL;";
                                                              FRA="DELETEALL;";
                                                              NLD="DELETEALL;";
                                                              DEA="DELETEALL;"] }
    { 31  ;   ;[INIT;]             ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU="INIT;";
                                                              ENU="INIT;";
                                                              ESP="INIT;";
                                                              FRA="INIT;";
                                                              NLD="INIT;";
                                                              DEA="INIT;"] }
    { 32  ;   ;[IF FIND('=') THEN;];Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU="IF FIND('=') THEN;";
                                                              ENU="IF FIND('=') THEN;";
                                                              ESP="IF FIND('=') THEN;";
                                                              FRA="IF FIND('=') THEN;";
                                                              NLD="IF FIND('=') THEN;";
                                                              DEA="IF FIND('=') THEN;"] }
    { 33  ;   ;[IF INSERT THEN;]   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU="IF INSERT THEN;";
                                                              ENU="IF INSERT THEN;";
                                                              ESP="IF INSERT THEN;";
                                                              FRA="IF INSERT THEN;";
                                                              NLD="IF INSERT THEN;";
                                                              DEA="IF INSERT THEN;"] }
    { 34  ;   ;[IF MODIFY THEN;]   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU="IF MODIFY THEN;";
                                                              ENU="IF MODIFY THEN;";
                                                              ESP="IF MODIFY THEN;";
                                                              FRA="IF MODIFY THEN;";
                                                              NLD="IF MODIFY THEN;";
                                                              DEA="IF MODIFY THEN;"] }
    { 35  ;   ;Commit Type         ;Option        ;CaptionML=[DEU=Commit Type;
                                                              ENU=Commit Type;
                                                              ESP=Commit Type;
                                                              FRA=Commit Type;
                                                              NLD=Commitsoort;
                                                              DEA=Commit Type];
                                                   OptionCaptionML=[DEU=" ,At the End,After Each Record,After 100 Records";
                                                                    ENU=" ,At the End,After Each Record,After 100 Records";
                                                                    ESP=" ,At the End,After Each Record,After 100 Records";
                                                                    FRA=" ,At the End,After Each Record,After 100 Records";
                                                                    NLD=" ,Einde van actie,Na 100 records,Na elk record";
                                                                    DEA=" ,At the End,After Each Record,After 100 Records"];
                                                   OptionString=[ ,At the End,After Each Record,After 100 Records] }
    { 36  ;   ;Transport No.       ;Code20        ;TableRelation="OM - Transport";
                                                   CaptionML=[DEU=Transport No.;
                                                              ENU=Transport No.;
                                                              ESP=Transport No.;
                                                              FRA=Transport No.;
                                                              NLD=Transportnr.;
                                                              DEA=Transport No.] }
    { 37  ;   ;Key Index           ;Integer       ;CaptionML=[DEU=Key Index;
                                                              ENU=Key Index;
                                                              ESP=Key Index;
                                                              FRA=Key Index;
                                                              NLD=Sleutelnr.;
                                                              DEA=Key Index] }
    { 38  ;   ;SQL Query           ;Text100       ;CaptionML=[DEU=SQL Query;
                                                              ENU=SQL Query;
                                                              ESP=SQL Query;
                                                              FRA=SQL Query;
                                                              NLD=SQL-query;
                                                              DEA=SQL Query] }
    { 39  ;   ;SQL Query Blob      ;BLOB          ;CaptionML=[DEU=SQL Query Blob;
                                                              ENU=SQL Query Blob;
                                                              ESP=SQL Query Blob;
                                                              FRA=SQL Query Blob;
                                                              NLD=SQL-query blob;
                                                              DEA=SQL Query Blob] }
    { 40  ;   ;Export FlowFields   ;Boolean       ;OnValidate=VAR
                                                                ActionField@1000 : Record 11102049;
                                                                Fld@1001 : Record 2000000041;
                                                              BEGIN
                                                                IF Type <> Type::"Temporary" THEN BEGIN

                                                                  Fld.SETRANGE(TableNo, "Object No.");
                                                                  Fld.SETRANGE(Class, Fld.Class::FlowField);
                                                                  IF Fld.FINDSET THEN
                                                                    REPEAT

                                                                      ActionField.SETRANGE(Type, Type);
                                                                      ActionField.SETRANGE("No.", "No.");
                                                                      ActionField.SETRANGE("Sub Type", "Sub Type");
                                                                      ActionField.SETRANGE("Line No.", "Line No.");
                                                                      ActionField.SETRANGE("Field No.", Fld."No.");
                                                                      IF ActionField.ISEMPTY AND "Export FlowFields" THEN BEGIN
                                                                        ActionField.INIT;
                                                                        ActionField.Type := Type;
                                                                        ActionField."No." := "No.";
                                                                        ActionField."Sub Type" := "Sub Type";
                                                                        ActionField."Line No." := "Line No.";
                                                                        ActionField."Table No." := "Object No.";
                                                                        ActionField."Field No." := Fld."No.";
                                                                        ActionField."Field Name" := Fld.FieldName;
                                                                        ActionField."Type Name" := Fld."Type Name";
                                                                        ActionField.INSERT;
                                                                      END;

                                                                      IF NOT ActionField.ISEMPTY AND NOT "Export FlowFields" THEN BEGIN
                                                                        ActionField.FINDFIRST;
                                                                        ActionField.DELETE;
                                                                      END;

                                                                    UNTIL Fld.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Export FlowFields;
                                                              ENU=Export FlowFields;
                                                              ESP=Export FlowFields;
                                                              FRA=Export FlowFields;
                                                              NLD=FlowFields exporteren;
                                                              DEA=Export FlowFields] }
    { 41  ;   ;DOS Command         ;Text100       ;CaptionML=[DEU=DOS Command;
                                                              ENU=DOS Command;
                                                              ESP=DOS Command;
                                                              FRA=DOS Command;
                                                              NLD=DOS-commando;
                                                              DEA=DOS Command] }
  }
  KEYS
  {
    {    ;Type,No.,Sub Type,Line No.              ;Clustered=Yes }
    {    ;Transport No.                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetObjectType@1() : Integer;
    VAR
      Object@1000 : Record 2000000001;
    BEGIN
      CASE "Action Type" OF
        "Action Type"::"Copy Data",
        "Action Type"::"Delete Data",
        "Action Type"::"Transfer Data",
        "Action Type"::"Fill Fields",
        "Action Type"::"Add Record" :
          EXIT(Object.Type::Table);

        "Action Type"::"Run Report" :
          EXIT(Object.Type::Report);

        "Action Type"::"Run Codeunit" :
          EXIT(Object.Type::Codeunit);

        "Action Type"::"6" :
          EXIT(Object.Type::"4");

      END;
    END;

    PROCEDURE FieldAssist@2(VAR ActionField@1000 : Record 11102049;TempRecord@1001 : Boolean);
    VAR
      ActionFields@1002 : Page 11102061;
    BEGIN
      TESTFIELD("Object No.");

      ActionField.RESET;

      ActionField.FILTERGROUP(2);
      ActionField.SETRANGE(Type, Type);
      ActionField.SETRANGE("No.", "No.");
      ActionField.SETRANGE("Sub Type", "Sub Type");
      ActionField.SETRANGE("Line No.", "Line No.");
      ActionField.FILTERGROUP(0);
      IF ActionField.FINDFIRST THEN
        ;

      ActionFields.LOOKUPMODE := TRUE;
      ActionFields.SetAction(Rec);

      IF NOT IsTableRecord THEN
        ActionFields.EDITABLE := FALSE;

      IF TempRecord THEN BEGIN
        ActionFields.EDITABLE := FALSE;
        ActionFields.SetRecords(ActionField);
        ActionFields.RUNMODAL;
      END ELSE BEGIN
        ActionFields.SETTABLEVIEW(ActionField);
        ActionFields.RUNMODAL;
      END;

      IF NOT TempRecord THEN BEGIN
        ActionField.SETRANGE(Selected, TRUE);
        IF ActionField.ISEMPTY THEN BEGIN
          Fields := Fields::All;
          FillActionFields;
        END ELSE
          Fields := Fields::Selected;
      END;
    END;

    PROCEDURE FieldsIntoAssist@3(VAR ActionField@1000 : Record 11102049);
    VAR
      TmpActionField@1001 : TEMPORARY Record 11102049;
      Mapped@1002 : Boolean;
    BEGIN
      TESTFIELD("Into Table No.");

      ActionField.RESET;
      ActionField.FILTERGROUP(2);
      ActionField.SETRANGE(Type, Type);
      ActionField.SETRANGE("No.", "No.");
      ActionField.SETRANGE("Sub Type", "Sub Type");
      ActionField.SETRANGE("Line No.", "Line No.");
      ActionField.FILTERGROUP(0);

      IF Fields = Fields::Selected THEN BEGIN
        ActionField.FILTERGROUP(2);
        ActionField.SETRANGE(Selected, TRUE);
        ActionField.FILTERGROUP(0);
      END;

      IF ActionField.FINDSET THEN
        REPEAT
          TmpActionField := ActionField;
          TmpActionField.INSERT;
        UNTIL ActionField.NEXT = 0;

      IF ActionField.FINDFIRST THEN
        ;

      IF PAGE.RUNMODAL(PAGE::"OM - Action Fields Into", ActionField) = ACTION::LookupOK THEN
        ;

      FillDefaultFieldMapActions(TmpActionField);

      IF TmpActionField.FINDSET THEN
        REPEAT
          IF ActionField.GET(
               TmpActionField.Type,
               TmpActionField."No.",
               TmpActionField."Sub Type",
               TmpActionField."Line No.",
               TmpActionField."Field No.")
          THEN
            IF (TmpActionField."Import Action" <> ActionField."Import Action") OR
               (TmpActionField."Into Field No." <> ActionField."Into Field No.")
            THEN
              Mapped := TRUE;
        UNTIL TmpActionField.NEXT = 0;

      IF Mapped THEN
        "Into Fields" := "Into Fields"::Mapped
      ELSE
        "Into Fields" := "Into Fields"::" ";
    END;

    PROCEDURE FillDefaultFieldMapActions@4(VAR TmpActionField@1000 : TEMPORARY Record 11102049);
    VAR
      Field@1001 : Record 2000000041;
      Field2@1002 : Record 2000000041;
      PreviousFieldNo@1003 : Integer;
    BEGIN
      TmpActionField.RESET;
      TmpActionField.SETRANGE(Type, Type);
      TmpActionField.SETRANGE("No.", "No.");
      TmpActionField.SETRANGE("Sub Type", "Sub Type");
      TmpActionField.SETRANGE("Line No.", "Line No.");
      IF TmpActionField.FINDSET THEN
        REPEAT

          PreviousFieldNo := TmpActionField."Into Field No.";

          TmpActionField."Into Table No." := "Into Table No.";
          Field.SETRANGE(TableNo, "Into Table No.");
          Field.SETRANGE(FieldName, TmpActionField."Field Name");

          CASE TRUE OF

            Field2.GET("Into Table No.", PreviousFieldNo) :
              BEGIN
                TmpActionField."Into Field No." := Field2."No.";
                TmpActionField."Import Action" := TmpActionField."Import Action"::Fill;
              END;

            Field.FINDFIRST :
              BEGIN
                TmpActionField."Into Field No." := Field."No.";
                TmpActionField."Import Action" := TmpActionField."Import Action"::Fill;
              END;

            Field2.GET("Into Table No.", TmpActionField."Field No.") :
              BEGIN
                TmpActionField."Into Field No." := Field2."No.";
                TmpActionField."Import Action" := TmpActionField."Import Action"::Fill;
              END;

            ELSE BEGIN
              TmpActionField."Into Field No." := 0;
              TmpActionField."Import Action" := TmpActionField."Import Action"::Skip;
            END;
          END;

          TmpActionField.MODIFY;
        UNTIL TmpActionField.NEXT = 0;
    END;

    PROCEDURE IsTableRecord@5() : Boolean;
    BEGIN
      EXIT(
        "Action Type" IN [
          "Action Type"::"Copy Data", "Action Type"::"Delete Data",
          "Action Type"::"Transfer Data", "Action Type"::"Fill Fields", "Action Type"::"Add Record",
          "Action Type"::"Rename Data", "Action Type"::"Renumber Object", "Action Type"::"Renumber Field"]);
    END;

    PROCEDURE LookupCompanyName@6(VAR Text@1000 : Text;FromCompany@1001 : Boolean) : Boolean;
    VAR
      Company@1002 : Record 2000000006;
      TmpCompany@1003 : TEMPORARY Record 2000000006;
      ApplicationSettings@1004 : Codeunit 11102101;
      Companies@1005 : Page 11102066;
    BEGIN
      IF Company.FINDSET THEN
        REPEAT
          TmpCompany := Company;
          TmpCompany.INSERT;
        UNTIL Company.NEXT = 0;

      IF NOT TmpCompany.GET(' ' + AllCompaniesText) THEN BEGIN
        TmpCompany.Name := ' ' + AllCompaniesText;
        TmpCompany.INSERT;
      END;

      IF NOT TmpCompany.GET(' ' + ActiveCompanyText) THEN BEGIN
        TmpCompany.Name := ' ' + ActiveCompanyText;
        TmpCompany.INSERT;
      END;

      IF FromCompany THEN
        ApplicationSettings.SetLookupTableNo("Object No.")
      ELSE BEGIN
        IF NOT TmpCompany.GET(' ' + SameCompanyText) THEN BEGIN
          TmpCompany.Name := ' ' + SameCompanyText;
          TmpCompany.INSERT;
        END;
        ApplicationSettings.SetLookupTableNo("Into Table No.");
      END;

      Companies.SetTempRecord(TmpCompany);

      IF TmpCompany.GET(Text) THEN
        Companies.SETRECORD(TmpCompany);

      IF TmpCompany.GET(' ' + Text) THEN
        Companies.SETRECORD(TmpCompany);

      Companies.LOOKUPMODE := TRUE;
      IF Companies.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Companies.GETRECORD(TmpCompany);
        Text := TmpCompany.Name;
        IF Text = ' ' + AllCompaniesText THEN
          Text := AllCompaniesText;
        IF Text = ' ' + ActiveCompanyText THEN
          Text := ActiveCompanyText;
        IF Text = ' ' + SameCompanyText THEN
          Text := SameCompanyText;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE AllCompaniesText@7() : Text;
    VAR
      ctAllCompanies@1000 : TextConst 'DEU=<All companies>;ENU=<All companies>;ESP=<All companies>;FRA=<All companies>;NLD=<All companies>;DEA=<All companies>';
    BEGIN
      EXIT(ctAllCompanies);
    END;

    PROCEDURE SameCompanyText@8() : Text;
    VAR
      ctSameCompany@1000 : TextConst 'DEU=<Same company>;ENU=<Same company>;ESP=<Same company>;FRA=<Same company>;NLD=<Same company>;DEA=<Same company>';
    BEGIN
      EXIT(ctSameCompany);
    END;

    PROCEDURE ActiveCompanyText@9() : Text;
    VAR
      ctActiveCompany@1000 : TextConst 'DEU=<Active company>;ENU=<Active company>;ESP=<Active company>;FRA=<Active company>;NLD=<Active company>;DEA=<Active company>';
    BEGIN
      EXIT(ctActiveCompany);
    END;

    LOCAL PROCEDURE UpdateRepository@10(Trigger@1000 : Integer);
    VAR
      RepositoryMgt@1001 : Codeunit 11102067;
      RecRef@1002 : RecordRef;
    BEGIN
      IF Type = Type::Project THEN BEGIN
        RecRef.GETTABLE(Rec);
        RepositoryMgt.UpdateProject(RecRef, "No.", Trigger);
        RecRef.SETTABLE(Rec);
      END;
    END;

    LOCAL PROCEDURE IsRenameAction@11() : Boolean;
    BEGIN
      EXIT("Action Type" IN ["Action Type"::"Rename Data", "Action Type"::"Renumber Object", "Action Type"::"Renumber Field"]);
    END;

    PROCEDURE IsRenumberAction@12() : Boolean;
    BEGIN
      EXIT("Action Type" IN ["Action Type"::"Renumber Object", "Action Type"::"Renumber Field"]);
    END;

    PROCEDURE Evaluate1FieldFilter@13(Field1No@1000 : Integer;Field1Value@1001 : Text);
    BEGIN
      EVALUATE(Filter, STRSUBSTNO('Table%1: Field%2=''%3''', "Object No.", Field1No, Field1Value));
    END;

    PROCEDURE Evaluate2FieldsFilter@14(Field1No@1000 : Integer;Field1Value@1001 : Text;Field2No@1002 : Integer;Field2Value@1003 : Text);
    BEGIN
      EVALUATE(Filter,
        STRSUBSTNO('Table%1: Field%2=''%3'', Field%4=''%5''', "Object No.", Field1No, Field1Value, Field2No, Field2Value));
    END;

    PROCEDURE Evaluate3FieldsFilter@15(Field1No@1000 : Integer;Field1Value@1001 : Text;Field2No@1002 : Integer;Field2Value@1003 : Text;Field3No@1004 : Integer;Field3Value@1005 : Text);
    BEGIN
      EVALUATE(Filter,
        STRSUBSTNO(
          'Table%1: Field%2=''%3'', Field%4=''%5'', Field%6=''%7''',
          "Object No.", Field1No, Field1Value, Field2No, Field2Value, Field3No, Field3Value));
    END;

    PROCEDURE AssistEditSQLQuery@16();
    VAR
      FileHandler@1000 : Codeunit 11102097;
      EditFile@1001 : Page 11102075;
      oFile@1002 : File;
      InStr@1003 : InStream;
      OutStr@1004 : OutStream;
      ClientFileName@1005 : Text;
      FileLine@1006 : Text[1024];
      ServerFileName@1007 : Text;
      ctEditQuery@1008 : TextConst 'DEU=Edit SQL Query;ENU=Edit SQL Query;ESP=Edit SQL Query;FRA=Edit SQL Query;NLD=SQL-query wijzigen;DEA=Edit SQL Query';
      ctFileName@1009 : TextConst 'DEU=SQLQuery.txt;ENU=SQLQuery.txt;ESP=SQLQuery.txt;FRA=SQLQuery.txt;NLD=SQLQuery.txt;DEA=SQLQuery.txt';
      ctQuestion@1010 : TextConst 'DEU=Modify the SQL Query in the external editor and press OK.;ENU=Modify the SQL Query in the external editor and press OK.;ESP=Modify the SQL Query in the external editor and press OK.;FRA=Modify the SQL Query in the external editor and press OK.;NLD=Wijzig de SQL-query in de externe tekst-editor en kies OK.;DEA=Modify the SQL Query in the external editor and press OK.';
    BEGIN
      TESTFIELD("Action Type", "Action Type"::"Execute SQL Query");

      ServerFileName := FileHandler.FileNameServerPath(ctFileName);

      IF EXISTS(ServerFileName) THEN
        ERASE(ServerFileName);

      oFile.WRITEMODE(TRUE);
      oFile.CREATE(ServerFileName);
      CALCFIELDS("SQL Query Blob");
      IF "SQL Query Blob".HASVALUE THEN BEGIN
        "SQL Query Blob".CREATEINSTREAM(InStr);
        oFile.CREATEOUTSTREAM(OutStr);
        COPYSTREAM(OutStr, InStr);
      END;

      oFile.CLOSE;

      ClientFileName := FileHandler.DownloadToClient(ServerFileName, '', ctFileName);

      HYPERLINK(ClientFileName);

      EditFile.SetCaption(ctEditQuery);
      EditFile.SetQuestion(ctQuestion);
      EditFile.SetFileName(ClientFileName);

      IF EditFile.RUNMODAL IN [ACTION::OK, ACTION::Yes] THEN BEGIN

        ServerFileName := FileHandler.UploadToServer(ClientFileName, ctFileName);

        oFile.TEXTMODE(TRUE);
        oFile.OPEN(ServerFileName);
        oFile.CREATEINSTREAM(InStr);

        CLEAR("SQL Query Blob");
        "SQL Query Blob".CREATEOUTSTREAM(OutStr);
        COPYSTREAM(OutStr, InStr);

        oFile.CLOSE;

        oFile.TEXTMODE(TRUE);
        oFile.OPEN(ServerFileName);
        "SQL Query" := '';
        WHILE oFile.READ(FileLine) > 0 DO
          IF DELCHR(FileLine, '<>') <> '' THEN
            "SQL Query" := DELSTR("SQL Query" + DELCHR(FileLine, '<>'), MAXSTRLEN("SQL Query")) + ' ';

        IF STRLEN("SQL Query") > MAXSTRLEN("SQL Query") - 2 THEN
          "SQL Query" := DELSTR("SQL Query", MAXSTRLEN("SQL Query") - 2) + '...';

        oFile.CLOSE;

      END;
    END;

    LOCAL PROCEDURE FillActionFields@17();
    VAR
      ActionField@1000 : Record 11102049;
      TmpActionField@1001 : TEMPORARY Record 11102049;
      Fld@1002 : Record 2000000041;
      ActionMgt@1003 : Codeunit 11102038;
      FieldsSelected@1004 : Boolean;
    BEGIN
      IF Type <> Type::"Temporary" THEN BEGIN
        ActionField.SETRANGE(Type, Type);
        ActionField.SETRANGE("No.", "No.");
        ActionField.SETRANGE("Sub Type", "Sub Type");
        ActionField.SETRANGE("Line No.", "Line No.");
        IF ActionField.FINDSET THEN
          REPEAT
            TmpActionField := ActionField;
            TmpActionField.INSERT;
          UNTIL ActionField.NEXT = 0;
        ActionField.DELETEALL;

        Fld.SETRANGE(TableNo, "Object No.");
        ActionMgt.SetFieldTypeFilter(Fld);
        IF IsRenameAction THEN
          ActionMgt.SetPrimaryKeyFilter(Fld);

        IF "Export FlowFields" THEN
          Fld.SETFILTER(Class, '<>%1', Fld.Class::FlowFilter)
        ELSE
          Fld.SETRANGE(Class, Fld.Class::Normal);

        IF Fld.FINDSET THEN
          REPEAT
            ActionField.INIT;
            ActionField.Type := Type;
            ActionField."No." := "No.";
            ActionField."Sub Type" := "Sub Type";
            ActionField."Line No." := "Line No.";
            ActionField."Table No." := "Object No.";
            ActionField."Field No." := Fld."No.";
            ActionField."Field Name" := Fld.FieldName;
            ActionField."Type Name" := Fld."Type Name";

            TmpActionField.SETRANGE("Table No.", "Object No.");
            TmpActionField.SETRANGE("Field No.", Fld."No.");
            IF TmpActionField.FINDFIRST THEN BEGIN
              ActionField.Selected := TmpActionField.Selected;
              IF ActionField.Selected THEN
                FieldsSelected := TRUE;

              ActionField."Import Action" := TmpActionField."Import Action";
              ActionField.Value := TmpActionField.Value;
            END;

            ActionField.INSERT;
          UNTIL Fld.NEXT = 0;
      END;

      IF FieldsSelected THEN
        Fields := Fields::Selected
      ELSE
        Fields := Fields::All;
    END;

    BEGIN
    END.
  }
}

