OBJECT Codeunit 9086969 nF. Explain Permission Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=23/10/16;
    Time=[ 8:47:37 PM];
    Version List=NF2016.17;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NfExplainPermissionContext@1000000000 : Record 9086973;
      NfUpdateUsersPermissions@1000000001 : Codeunit 9086957;

    PROCEDURE Update@1000000002(VAR pNfExplainPermissionContext@1000000000 : Record 9086973) : Boolean;
    VAR
      lResult@1000000001 : Boolean;
    BEGIN
      ///<summary>Updating "Explain Permission" information</summary>
      ///<param name="pNfExplainPermissionContext">The current context</param>
      ///<returns>TRUE = Update necessary, FALSE = Nothing changed</returns>

      lResult := FALSE;

      IF ((pNfExplainPermissionContext."nF. User ID" <> NfExplainPermissionContext."nF. User ID") OR
          (pNfExplainPermissionContext."nF. Company ID" <> NfExplainPermissionContext."nF. Company ID") OR
          (pNfExplainPermissionContext."Object Type" <> NfExplainPermissionContext."Object Type") OR
          (pNfExplainPermissionContext."Object ID" <> NfExplainPermissionContext."Object ID")) THEN
      BEGIN
        pNfExplainPermissionContext.ClearResult();
        IF ((pNfExplainPermissionContext."nF. User ID" <> 0) AND
            (pNfExplainPermissionContext."Object Type" <> pNfExplainPermissionContext."Object Type"::" ") AND
            (pNfExplainPermissionContext."Object ID" <> 0)) THEN
        BEGIN
          CLEAR(NfUpdateUsersPermissions);
          NfUpdateUsersPermissions.ExplainPermission(pNfExplainPermissionContext);
        END;
        NfExplainPermissionContext := pNfExplainPermissionContext;
        lResult := TRUE;
      END;

      EXIT(lResult);
    END;

    PROCEDURE StartOver@1000000000(VAR pNfExplainPermissionContext@1000000000 : Record 9086973);
    BEGIN
      ///<summary>Staring over "Explain Permission"</summary>
      ///<param name="pNfExplainPermissionContext">The current context</param>

      pNfExplainPermissionContext.VALIDATE("nF. User ID", 0);
      pNfExplainPermissionContext.VALIDATE("nF. Company ID", 0);
      pNfExplainPermissionContext.VALIDATE("Permission Type", pNfExplainPermissionContext."Permission Type"::" ");
      pNfExplainPermissionContext.VALIDATE("Object Type", pNfExplainPermissionContext."Object Type"::" ");
      pNfExplainPermissionContext.VALIDATE("Object ID", 0);
      pNfExplainPermissionContext.MODIFY(TRUE);

      Update(pNfExplainPermissionContext);
    END;

    BEGIN
    {
      ///<summary>nForcement "Explain Permission" toolkit</summary>
      ///<change version="NF2016.17" initials="JH" date="2016-10-23">Created</change>
    }
    END.
  }
}

