OBJECT Page 11175821 OM - Renumber Objects Wizard
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Renumber Objects Wizard;
               ENU=Renumber Objects Wizard;
               ESP=Renumber Objects Wizard;
               FRA=Renumber Objects Wizard;
               NLD=Objecten omnummeren wizard;
               DEA=Renumber Objects Wizard];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    PageType=NavigatePage;
    OnInit=BEGIN
             NextEnable := TRUE;
           END;

    OnOpenPage=VAR
                 ObjectToRenumber@1000 : Record 11102063;
                 Object@1001 : Record 2000000001;
                 RenumberObjectMgt@1002 : Codeunit 11102048;
                 i@1003 : Integer;
                 TablesPresent@1004 : Boolean;
               BEGIN
                 ObjectToRenumber.SETRANGE("Object Type", Object.Type::Table);
                 TablesPresent := NOT ObjectToRenumber.ISEMPTY;

                 RenumberObjectMgt.GetRecordSets(TmpObject, TmpOriginalObject, TmpObjectToRenumber);

                 AddStep(StepType::"Check Objects");
                 IF TablesPresent THEN BEGIN
                   AddStep(StepType::"Add Tables");
                   AddStep(StepType::"Transfer Data");
                 END;
                 AddStep(StepType::"Rename Old Objects");
                 AddStep(StepType::"Update Objects");
                 AddStep(StepType::"Compile Objects");

                 IF TablesPresent THEN
                   AddStep(StepType::"Delete Data");

                 AddStep(StepType::"Delete Old Objects");

                 IF SetModifyFlag THEN
                   AddStep(StepType::"Set Datetime Objects");

                 AddStep(StepType::"Update OM Data");

                 NoOfPages := NoOfSteps + 2;

                 FOR i := 1 TO 10 DO
                   SetCheckBox(i, i <= NoOfSteps, FALSE);

                 ActivatePage(1);
               END;

    OnQueryClosePage=VAR
                       ctConfirm@1000 : TextConst 'DEU=Renumbering objects is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;ENU=Renumbering objects is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;ESP=Renumbering objects is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;FRA=Renumbering objects is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;NLD=Het omnummeren van objecten is nog niet afgerond.\Het is ten zeerste aan te raden dit proces af te ronden.\Weet u het zeker dat u wilt stoppen?;DEA=Renumbering objects is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?';
                     BEGIN
                       IF ActivePage IN [1, NoOfPages] THEN
                         EXIT(TRUE);

                       EXIT(CONFIRM(ctConfirm, TRUE));
                     END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=&Previous;
                                 ENU=&Previous;
                                 ESP=&Previous;
                                 FRA=&Previous;
                                 NLD=&Vorige;
                                 DEA=&Previous];
                      Enabled=FALSE;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 ActivatePage(ActivePage - 1);
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Next;
                                 ENU=&Next;
                                 ESP=&Next;
                                 FRA=&Next;
                                 NLD=Vo&lgende;
                                 DEA=&Next];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 IF (ActivePage > 1) AND (ActivePage - 1 <= NoOfSteps) THEN
                                   ExecuteStep(ActivePage - 1);

                                 ActivatePage(ActivePage + 1);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=&Finish;
                                 ENU=&Finish;
                                 ESP=&Finish;
                                 FRA=&Finish;
                                 NLD=Voltooie&n;
                                 DEA=&Finish];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=Step1;
                Visible=Step1Visible }

    { 7   ;2   ;Field     ;
                CaptionML=[DEU=This wizard will help you with renumbering objects.;
                           ENU=This wizard will help you with renumbering objects.;
                           ESP=This wizard will help you with renumbering objects.;
                           FRA=This wizard will help you with renumbering objects.;
                           NLD=Deze wizard helpt u bij het omnummeren van objecten.;
                           DEA=This wizard will help you with renumbering objects.];
                MultiLine=Yes }

    { 8   ;2   ;Field     ;
                CaptionML=[DEU=Press next to go to the first step.;
                           ENU=Press next to go to the first step.;
                           ESP=Press next to go to the first step.;
                           FRA=Press next to go to the first step.;
                           NLD=Druk op volgende om naar de eerste stap te gaan.;
                           DEA=Press next to go to the first step.];
                MultiLine=Yes }

    { 9   ;1   ;Group     ;
                Name=Step2;
                Visible=Step2Visible }

    { 10  ;2   ;Field     ;
                CaptionML=[DEU=The following steps have to be executed.;
                           ENU=The following steps have to be executed.;
                           ESP=The following steps have to be executed.;
                           FRA=The following steps have to be executed.;
                           NLD=De volgende stappen moeten worden uitgevoerd.;
                           DEA=The following steps have to be executed.];
                MultiLine=Yes }

    { 11  ;2   ;Field     ;
                SourceExpr=StepExecuted[1];
                CaptionClass=DisplayStepDescription[1];
                Visible=cChecked1Visible;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=StepExecuted[2];
                CaptionClass=DisplayStepDescription[2];
                Visible=cChecked2Visible;
                Editable=FALSE }

    { 13  ;2   ;Field     ;
                SourceExpr=StepExecuted[3];
                CaptionClass=DisplayStepDescription[3];
                Visible=cChecked3Visible;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr=StepExecuted[4];
                CaptionClass=DisplayStepDescription[4];
                Visible=cChecked4Visible;
                Editable=FALSE }

    { 15  ;2   ;Field     ;
                SourceExpr=StepExecuted[5];
                CaptionClass=DisplayStepDescription[5];
                Visible=cChecked5Visible;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr=StepExecuted[6];
                CaptionClass=DisplayStepDescription[6];
                Visible=cChecked6Visible;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                SourceExpr=StepExecuted[7];
                CaptionClass=DisplayStepDescription[7];
                Visible=cChecked7Visible;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr=StepExecuted[8];
                CaptionClass=DisplayStepDescription[8];
                Visible=cChecked8Visible;
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr=StepExecuted[9];
                CaptionClass=DisplayStepDescription[9];
                Visible=cChecked9Visible;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr=StepExecuted[10];
                CaptionClass=DisplayStepDescription[10];
                Visible=cChecked10Visible;
                Editable=FALSE }

    { 21  ;1   ;Group     ;
                Name=Step3;
                Visible=Step3Visible }

    { 22  ;2   ;Field     ;
                CaptionML=[DEU=The objects are renumbered;
                           ENU=The objects are renumbered;
                           ESP=The objects are renumbered;
                           FRA=The objects are renumbered;
                           NLD=De objecten zijn omgenummerd;
                           DEA=The objects are renumbered];
                MultiLine=Yes }

    { 23  ;2   ;Field     ;
                CaptionML=[DEU=Press finish to close the wizard.;
                           ENU=Press finish to close the wizard.;
                           ESP=Press finish to close the wizard.;
                           FRA=Press finish to close the wizard.;
                           NLD=Druk op voltooien om de wizard te sluiten.;
                           DEA=Press finish to close the wizard.];
                MultiLine=Yes }

  }
  CODE
  {
    VAR
      TmpCALHistoryObject@1000 : TEMPORARY Record 11102052;
      TmpObjectToRenumber@1001 : TEMPORARY Record 11102063;
      TmpObject@1002 : TEMPORARY Record 2000000001;
      TmpOriginalObject@1003 : TEMPORARY Record 2000000001;
      DisplayStepDescription@1004 : ARRAY [10] OF Text[1024];
      StepDescription@1005 : ARRAY [10] OF Text[1024];
      ActivePage@1006 : Integer;
      NoOfPages@1007 : Integer;
      NoOfSteps@1008 : Integer;
      Step@1009 : ARRAY [10] OF Integer;
      StepType@1010 : ' ,Check Objects,Add Tables,Transfer Data,Rename Old Objects,Update Objects,Compile Objects,Delete Data,Delete Old Objects,Set Datetime Objects,Update OM Data';
      cChecked10Visible@1011 : Boolean INDATASET;
      cChecked1Visible@1012 : Boolean INDATASET;
      cChecked2Visible@1013 : Boolean INDATASET;
      cChecked3Visible@1014 : Boolean INDATASET;
      cChecked4Visible@1015 : Boolean INDATASET;
      cChecked5Visible@1016 : Boolean INDATASET;
      cChecked6Visible@1017 : Boolean INDATASET;
      cChecked7Visible@1018 : Boolean INDATASET;
      cChecked8Visible@1019 : Boolean INDATASET;
      cChecked9Visible@1020 : Boolean INDATASET;
      FinishEnable@1021 : Boolean INDATASET;
      NextEnable@1022 : Boolean INDATASET;
      SetModifyFlag@1023 : Boolean INDATASET;
      Step1Visible@1024 : Boolean INDATASET;
      Step2Visible@1025 : Boolean INDATASET;
      Step3Visible@1026 : Boolean INDATASET;
      StepExecuted@1027 : ARRAY [12] OF Boolean;
      UpdateReferencingObjects@1028 : Boolean;

    LOCAL PROCEDURE ActivatePage@1(PageNo@1000 : Integer);
    VAR
      ActiveStep@1001 : Integer;
      i@1002 : Integer;
      ScreenNo@1003 : Integer;
    BEGIN
      CASE TRUE OF
        PageNo = 1 :
          ScreenNo := 1;

        PageNo - 1 <= NoOfSteps :
          ScreenNo := 2;

        ELSE
          ScreenNo := 3;
      END;

      Step1Visible := ScreenNo = 1;
      Step2Visible := ScreenNo = 2;
      Step3Visible := ScreenNo = 3;

      ActivePage := PageNo;
      NextEnable := ActivePage < NoOfPages;
      FinishEnable := ActivePage = NoOfPages;

      IF (PageNo > 1) AND (PageNo - 1 <= NoOfSteps) THEN
        ActiveStep := PageNo - 1;

      FOR i := 1 TO 10 DO
        IF i = ActiveStep THEN
          DisplayStepDescription[i] := ' > ' + StepDescription[i]
        ELSE
          IF StepDescription[i] = '' THEN
            DisplayStepDescription[i] := ''
          ELSE
            DisplayStepDescription[i] := '    ' + StepDescription[i];
    END;

    LOCAL PROCEDURE SetCheckBox@2(Number@1000 : Integer;Visible@1001 : Boolean;Checked@1002 : Boolean);
    BEGIN
      CASE Number OF
        1 : cChecked1Visible := Visible;
        2 : cChecked2Visible := Visible;
        3 : cChecked3Visible := Visible;
        4 : cChecked4Visible := Visible;
        5 : cChecked5Visible := Visible;
        6 : cChecked6Visible := Visible;
        7 : cChecked7Visible := Visible;
        8 : cChecked8Visible := Visible;
        9 : cChecked9Visible := Visible;
        10 : cChecked10Visible := Visible;
      END;

      StepExecuted[Number] := Checked;
    END;

    LOCAL PROCEDURE ExecuteStep@3(StepToExecute@1000 : Integer);
    VAR
      ObjectToRenumber@1001 : Record 11102063;
      Object@1002 : Record 2000000001;
      Progressbar@1003 : Codeunit 11102044;
      RenumberObjectMgt@1004 : Codeunit 11102048;
      StartDate@1005 : Date;
      StartTime@1006 : Time;
      MaxBar@1007 : Integer;
      NoOfTables@1008 : Integer;
      ctUpdatingObjects@1009 : TextConst 'DEU=Updating objects...;ENU=Updating objects...;ESP=Updating objects...;FRA=Updating objects...;NLD=Objecten bijwerken...;DEA=Updating objects...';
    BEGIN
      StartDate := TODAY;
      StartTime := TIME;

      ObjectToRenumber.SETRANGE("Object Type", Object.Type::Table);
      NoOfTables := ObjectToRenumber.COUNT;

      CASE Step[StepToExecute] OF
        StepType::"Check Objects" : MaxBar := TmpObject.COUNT * 4;
        StepType::"Add Tables" : MaxBar := NoOfTables;
        StepType::"Transfer Data" : MaxBar := NoOfTables;
        StepType::"Rename Old Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Update Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Compile Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Delete Data" : MaxBar := NoOfTables;
        StepType::"Delete Old Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Set Datetime Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Update OM Data" : MaxBar := TmpObject.COUNT * 3;
      END;

      Progressbar.Open(ctUpdatingObjects, 1);
      Progressbar.BarSize := MaxBar;

      CASE Step[StepToExecute] OF

        StepType::"Check Objects" :
          RenumberObjectMgt.CheckObjects(Progressbar, TmpOriginalObject, TmpCALHistoryObject);

        StepType::"Add Tables" :
          RenumberObjectMgt.AddNewTables(Progressbar);

        StepType::"Transfer Data" :
          RenumberObjectMgt.TransferData(Progressbar);

        StepType::"Rename Old Objects" :
          RenumberObjectMgt.RenameOldObjects(Progressbar);

        StepType::"Update Objects" :
          RenumberObjectMgt.UpdateObjects(Progressbar, TmpCALHistoryObject, TmpObject, TmpObjectToRenumber);

        StepType::"Compile Objects" :
          RenumberObjectMgt.CompileObjects(Progressbar, TmpObject);

        StepType::"Delete Data" :
          RenumberObjectMgt.DeleteData(Progressbar, TmpOriginalObject);

        StepType::"Delete Old Objects" :
          RenumberObjectMgt.DeleteOldObjects(Progressbar, TmpOriginalObject, TmpObject);

        StepType::"Set Datetime Objects" :
          RenumberObjectMgt.SetDatetimeStamp(Progressbar, TmpObject, StartDate, StartTime);

        StepType::"Update OM Data" :
          BEGIN
            RenumberObjectMgt.UpdateOMData(Progressbar, TmpObjectToRenumber);
            RenumberObjectMgt.UpdateCALHistory(Progressbar, TmpObject);
            RenumberObjectMgt.UpdateWhereUsedObjects(Progressbar, TmpObject);
            RenumberObjectMgt.UpdateWhereUsedIn(Progressbar, TmpObject);
          END;

      END;

      StepExecuted[StepToExecute] := TRUE;
    END;

    PROCEDURE Finished@4() : Boolean;
    BEGIN
      EXIT(StepExecuted[StepType::"Update OM Data"]);
    END;

    PROCEDURE SetUpdateReferencingObjects@32(NewValue@1000 : Boolean);
    BEGIN
      UpdateReferencingObjects := NewValue;
    END;

    PROCEDURE SetSetModifyFlag@26(NewValue@1000 : Boolean);
    BEGIN
      SetModifyFlag := NewValue;
    END;

    LOCAL PROCEDURE AddStep@5(StepType2@1000 : Integer);
    VAR
      ctAddTables@1001 : TextConst 'DEU=Add new tables;ENU=Add new tables;ESP=Add new tables;FRA=Add new tables;NLD=Nieuwe tabellen toevoegen;DEA=Add new tables';
      ctCheckingObjects@1002 : TextConst 'DEU=Check existing objects;ENU=Check existing objects;ESP=Check existing objects;FRA=Check existing objects;NLD=Bestaande objecten controleren;DEA=Check existing objects';
      ctCompileObjects@1003 : TextConst 'DEU=Compile objects;ENU=Compile objects;ESP=Compile objects;FRA=Compile objects;NLD=Objecten compileren;DEA=Compile objects';
      ctDeleteData@1004 : TextConst 'DEU=Delete data from old tables;ENU=Delete data from old tables;ESP=Delete data from old tables;FRA=Delete data from old tables;NLD=Data verwijderen uit oude tabellen;DEA=Delete data from old tables';
      ctDeleteOldObjects@1005 : TextConst 'DEU=Delete old objects;ENU=Delete old objects;ESP=Delete old objects;FRA=Delete old objects;NLD=Oude objecten verwijderen;DEA=Delete old objects';
      ctRenameOldObjects@1006 : TextConst 'DEU=Rename old objects;ENU=Rename old objects;ESP=Rename old objects;FRA=Rename old objects;NLD=Oude objecten hernoemen;DEA=Rename old objects';
      ctSetDatetimeObjects@1007 : TextConst 'DEU=Set new datetime stamp on objects;ENU=Set new datetime stamp on objects;ESP=Set new datetime stamp on objects;FRA=Set new datetime stamp on objects;NLD=Nieuwe datumstempel op objecten;DEA=Set new datetime stamp on objects';
      ctTransferData@1008 : TextConst 'DEU=Copy data to new tables;ENU=Copy data to new tables;ESP=Copy data to new tables;FRA=Copy data to new tables;NLD=Data naar nieuwe tabellen kopi‰ren;DEA=Copy data to new tables';
      ctUpdateObjects@1009 : TextConst 'DEU=Update objects;ENU=Update objects;ESP=Update objects;FRA=Update objects;NLD=Objecten bijwerken;DEA=Update objects';
      ctUpdateOMData@1010 : TextConst 'DEU=Update Object Manager data;ENU=Update Object Manager data;ESP=Update Object Manager data;FRA=Update Object Manager data;NLD=Object Manager data bijwerken;DEA=Update Object Manager data';
    BEGIN
      NoOfSteps += 1;
      Step[NoOfSteps] := StepType2;

      CASE StepType2 OF

        StepType::"Check Objects" :
          StepDescription[NoOfSteps] := ctCheckingObjects;

        StepType::"Add Tables" :
          StepDescription[NoOfSteps] := ctAddTables;

        StepType::"Transfer Data" :
          StepDescription[NoOfSteps] := ctTransferData;

        StepType::"Rename Old Objects" :
          StepDescription[NoOfSteps] := ctRenameOldObjects;

        StepType::"Update Objects" :
          StepDescription[NoOfSteps] := ctUpdateObjects;

        StepType::"Compile Objects" :
          StepDescription[NoOfSteps] := ctCompileObjects;

        StepType::"Delete Data" :
          StepDescription[NoOfSteps] := ctDeleteData;

        StepType::"Delete Old Objects" :
          StepDescription[NoOfSteps] := ctDeleteOldObjects;

        StepType::"Set Datetime Objects" :
          StepDescription[NoOfSteps] := ctSetDatetimeObjects;

        StepType::"Update OM Data" :
          StepDescription[NoOfSteps] := ctUpdateOMData;

      END;
    END;

    BEGIN
    END.
  }
}

