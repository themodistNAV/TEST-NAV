OBJECT Table 9021623 Label Attribute
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:01 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LabelAttributeRec.RESET;
               IF LabelAttributeRec.FINDLAST THEN
                 "Entry No." := LabelAttributeRec."Entry No." + 1
               ELSE
                 "Entry No." := 1;

               //DOC EC2015.5.03 -
               {
               IF "Label Code" = '' THEN BEGIN
                 LabelRec.RESET;
                 LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
                 IF LabelRec.COUNT = 1 THEN BEGIN
                   LabelRec.FINDFIRST;
                   "Label Code" := LabelRec.Code;
                 END;
               END;
               }
               IF "Label Code" = '' THEN
                 "Label Code" := GlobalFuncs.GetLabelCode;
               //DOC EC2015.5.03

               LabelAttributeRec.RESET;
               LabelAttributeRec.SETCURRENTKEY("Label Code","Attribute Type",
                                               "Attribute Owner Reference","Type Code");
               LabelAttributeRec.SETFILTER("Entry No.",'<>%1',"Entry No.");
               LabelAttributeRec.SETRANGE("Label Code","Label Code");
               LabelAttributeRec.SETRANGE("Attribute Type","Attribute Type");
               LabelAttributeRec.SETRANGE("Attribute Owner Reference","Attribute Owner Reference");
               LabelAttributeRec.SETRANGE("Type Code","Type Code");
               IF LabelAttributeRec.FINDFIRST THEN
                 ERROR(Text002);


               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021623);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;

               //DOC EC2015.5.03 -
               IF "Attribute Type" = 'PRODUCT' THEN BEGIN
                 TESTFIELD("Label Code");
                 TESTFIELD("Attribute Owner Reference");
                 PageItem.RESET;
                 PageItem.SETCURRENTKEY("Label Code",Type,"No.");
                 PageItem.SETRANGE("Label Code","Label Code");
                 PageItem.SETFILTER(Type,'%1|%2',PageItem.Type::Item,PageItem.Type::"Promo Item");
                 PageItem.SETRANGE("No.","Attribute Owner Reference");
                 IF NOT PageItem.FINDFIRST THEN
                   VALIDATE("Do Not Extract",TRUE);
               END;
               //DOC EC2015.5.03 +
             END;

    OnModify=BEGIN
               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021623);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    OnDelete=BEGIN
               LabAttTransValueRec.RESET;
               LabAttTransValueRec.SETRANGE("Label Code","Label Code");
               LabAttTransValueRec.SETRANGE("Label Attribute Entry No.","Entry No.");
               LabAttTransValueRec.DELETEALL;
               //DOC EC2013.3.80 SyncMgt.DeletionMgt(9021623,FORMAT("Entry No."),'','','','','','','',"Sync Log No.","Sync Version No.");
             END;

    OnRename=BEGIN
               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021623);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    CaptionML=[ENU=Label Attribute;
               ENG=Label Attribute];
    LookupPageID=Page9021633;
    DrillDownPageID=Page9021633;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ENG=Entry No.] }
    { 3   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 10  ;   ;Attribute Type      ;Code10        ;TableRelation="Label Attribute Type";
                                                   OnValidate=BEGIN
                                                                IF "Attribute Type" <> xRec."Attribute Type" THEN
                                                                  "Attribute Owner Reference" := '';
                                                              END;

                                                   CaptionML=[ENU=Attribute Type;
                                                              ENG=Attribute Type];
                                                   NotBlank=Yes }
    { 11  ;   ;Attribute Owner Reference;Code20   ;OnLookup=VAR
                                                              PageGuID@1190000006 : Integer;
                                                              VersionCalls@1000000000 : Codeunit 9021662;
                                                            BEGIN
                                                              CASE "Attribute Type" OF
                                                                'PAGE' :
                                                                  BEGIN
                                                                    ePage.RESET;
                                                                    ePage."Label Code" := "Label Code";
                                                                    IF "Attribute Owner Reference" <> '' THEN BEGIN
                                                                      EVALUATE(PageGuID,"Attribute Owner Reference");
                                                                      ePage."Page GUID" := PageGuID;
                                                                    END;
                                                                    //IF FORM.RUNMODAL(0,ePage,ePage."Page GUID") = ACTION::LookupOK THEN
                                                                    IF VersionCalls.LookupEPage(ePage) THEN //DOC EC2013.4.00
                                                                      "Attribute Owner Reference" := FORMAT(ePage."Page GUID");
                                                                  END;
                                                                'PRODUCT' :
                                                                  BEGIN
                                                                    Item.RESET;
                                                                    IF "Attribute Owner Reference" <> '' THEN
                                                                      Item."No." := "Attribute Owner Reference";
                                                                    //IF FORM.RUNMODAL(0,Item,Item."No.") = ACTION::LookupOK THEN
                                                                    IF VersionCalls.LookupItem(Item) THEN //DOC EC2013.4.00
                                                                      "Attribute Owner Reference" := Item."No.";
                                                                  END;
                                                                'CONTENT' :
                                                                  BEGIN
                                                                    PageItemContent.RESET;
                                                                    IF "Attribute Owner Reference" <> '' THEN
                                                                      PageItemContent."No." := "Attribute Owner Reference";
                                                                    //IF FORM.RUNMODAL(0,PageItemContent,PageItemContent."No.") = ACTION::LookupOK THEN
                                                                    IF VersionCalls.LookupPageItemContent(PageItemContent) THEN   //DOC EC2013.4.00
                                                                      "Attribute Owner Reference" := PageItemContent."No.";
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Attribute Owner Reference;
                                                              ENG=Attribute Owner Reference];
                                                   NotBlank=Yes;
                                                   Description=MQ 18/06/2012 - GW120618 }
    { 16  ;   ;Type Code           ;Code20        ;TableRelation=IF (Attribute Type=CONST(PAGE)) "Label Lookup".Code WHERE (Attribute Type=CONST(PAGE))
                                                                 ELSE IF (Attribute Type=CONST(PRODUCT)) "Label Lookup".Code WHERE (Attribute Type=CONST(PRODUCT))
                                                                 ELSE IF (Attribute Type=CONST(CONTENT)) "Label Lookup".Code WHERE (Attribute Type=CONST(CONTENT));
                                                   OnValidate=BEGIN
                                                                IF "Type Code" = '' THEN BEGIN
                                                                  "Allow Blank Language Code" := FALSE;
                                                                  "Allow HTML" := FALSE;
                                                                END ELSE BEGIN
                                                                  LabelLookupRec.GET("Attribute Type","Type Code");
                                                                  "Allow Blank Language Code" := LabelLookupRec."Allow Blank Language Code";
                                                                  "Allow HTML" := LabelLookupRec."Allow HTML";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type Code;
                                                              ENG=Type Code];
                                                   NotBlank=Yes;
                                                   Description=MQ 03/10/2012 - GW121003 Made Not Blank }
    { 20  ;   ;Allow Blank Language Code;Boolean  ;CaptionML=[ENU=Allow Blank Language Code;
                                                              ENG=Allow Blank Language Code] }
    { 31  ;   ;Allow HTML          ;Boolean       ;CaptionML=[ENU=Allow HTML;
                                                              ENG=Allow HTML] }
    { 34  ;   ;Transfer Template Value;Boolean    ;CaptionML=[ENU=Transfer Template Value;
                                                              ENG=Transfer Template Value] }
    { 100 ;   ;First Saved Value   ;Text250       ;CaptionML=[ENU=First Saved Value;
                                                              ENG=First Saved Value] }
    { 101 ;   ;Language Value (ENG);Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Label Attrib Translation Value".Value WHERE (Label Code=FIELD(Label Code),
                                                                                                                    Label Attribute Entry No.=FIELD(Entry No.),
                                                                                                                    Language Code=FILTER(ENG)));
                                                   CaptionML=[ENU=Language Value (ENG);
                                                              ENG=Language Value (ENG)];
                                                   Editable=No }
    { 102 ;   ;Language Filter     ;Code10        ;OnValidate=BEGIN
                                                                CALCFIELDS("Language Value","HTML Entry No.");
                                                              END;

                                                   CaptionML=[ENU=Language Filter;
                                                              ENG=Language Filter] }
    { 103 ;   ;Language Value      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Label Attrib Translation Value".Value WHERE (Label Code=FIELD(Label Code),
                                                                                                                    Label Attribute Entry No.=FIELD(Entry No.),
                                                                                                                    Language Code=FIELD(Language Filter)));
                                                   CaptionML=[ENU=Language Value;
                                                              ENG=Language Value];
                                                   Editable=No }
    { 110 ;   ;HTML Entry No.      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Label Attrib Translation Value"."HTML Entry No." WHERE (Label Code=FIELD(Label Code),
                                                                                                                               Label Attribute Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=HTML Entry No.;
                                                              ENG=HTML Entry No.];
                                                   Description=3.00 MQ 29/08/2012 - GW120829 changed flowfield formula;
                                                   Editable=No }
    { 112 ;   ;No of Label Att. Trans. Values;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Label Attrib Translation Value" WHERE (Label Code=FIELD(Label Code),
                                                                                                             Label Attribute Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=No of Label Att. Trans. Values;
                                                              ENG=No of Label Att. Trans. Values];
                                                   Description=GW add 131023 flowfield }
    { 200 ;   ;Do Not Extract      ;Boolean       ;OnValidate=VAR
                                                                AttrValLocal@1000000000 : Record 9021624;
                                                              BEGIN
                                                                //DOC EC2015.5.03 new field and code
                                                                AttrValLocal.RESET;
                                                                AttrValLocal.SETRANGE("Label Code","Label Code");
                                                                AttrValLocal.SETRANGE("Label Attribute Entry No.","Entry No.");
                                                                IF AttrValLocal.FIND('-') THEN
                                                                  AttrValLocal.MODIFYALL("Do Not Extract","Do Not Extract");
                                                              END;

                                                   CaptionML=[ENU=Do Not Extract;
                                                              ENG=Do Not Extract];
                                                   Description=GW add 150602 }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Label Code,Attribute Type,Attribute Owner Reference,Type Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LabelAttributeRec@1040000 : Record 9021623;
      LabAttTransValueRec@1040001 : Record 9021624;
      LabelLookupRec@1000000000 : Record 9021622;
      Text001@1000000001 : TextConst 'ENU=No %1 record was found with the %2 ''%3''.\\Could not set attribute value ''%4''.\\Please create the missing %1 and try again.;ENG=No %1 record was found with the %2 ''%3''.\\Could not set attribute value ''%4''.\\Please create the missing %1 and try again.';
      ePage@1190000006 : Record 9021628;
      Item@1190000007 : Record 27;
      PageItemContent@1190000008 : Record 9021636;
      Text002@1000000002 : TextConst 'ENU=You cannot have more than one record with the same Attribute Type, Owner Refernce and Type Code.;ENG=You cannot have more than one record with the same Attribute Type, Owner Refernce and Type Code.';
      LabelRec@1000000003 : Record 9021618;
      PageItem@1000000004 : Record 9021629;
      Text003@1000000006 : TextConst 'ENU=Not allowed as ''%1'' is FALSE.;ENG=Not allowed as ''%1'' is FALSE.';
      Text004@1000000007 : TextConst 'ENU=You cannot create HTML Entries as Label Attribute Translation Values with a Language Code exist.;ENG=You cannot create HTML Entries as Label Attribute Translation Values with a Language Code exist.';
      Text005@1000000008 : TextConst 'ENU=This code only supports Attribute Type of PRODUCT or PAGE.;ENG=This code only supports Attribute Type of PRODUCT or PAGE.';
      GlobalFuncs@1000000005 : Codeunit 9021624;

    PROCEDURE DeleteAttribute@1000000005(LabelCode@1000000003 : Code[10];AttributeType@1000000004 : Code[10];OwnerRef@1000000000 : Code[20];AttributeCode@1000000001 : Code[20]) Success : Boolean;
    VAR
      AttrLocal@1000000005 : Record 9021623;
    BEGIN
      //find the referenced attribute...
      AttrLocal.RESET;
      AttrLocal.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      AttrLocal.SETRANGE("Label Code",LabelCode);
      AttrLocal.SETRANGE("Attribute Type",AttributeType);
      AttrLocal.SETRANGE("Attribute Owner Reference",OwnerRef);
      AttrLocal.SETRANGE("Type Code",AttributeCode);

      //delete it...
      IF AttrLocal.FINDFIRST THEN
        AttrLocal.DELETE(TRUE);  //values are deleted in DELETE trigger code
    END;

    PROCEDURE DeleteAttributes@1000000003(LabelCode@1000000003 : Code[10];AttributeType@1000000004 : Code[10];OwnerRef@1000000000 : Code[20]) Success : Boolean;
    VAR
      AttrLocal@1000000005 : Record 9021623;
    BEGIN
      //find the referenced attributes...
      AttrLocal.RESET;
      AttrLocal.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      AttrLocal.SETRANGE("Label Code",LabelCode);
      AttrLocal.SETRANGE("Attribute Type",AttributeType);
      AttrLocal.SETRANGE("Attribute Owner Reference",OwnerRef);

      //delete em...
      IF AttrLocal.FINDFIRST THEN
        AttrLocal.DELETEALL(TRUE);  //values are deleted in DELETE trigger code
    END;

    PROCEDURE DeleteAttributeValue@1000000000(LabelCode@1000000003 : Code[10];AttributeType@1000000004 : Code[10];OwnerRef@1000000000 : Code[20];AttributeCode@1000000001 : Code[20];LanguageCode@1000000006 : Code[10]) Success : Boolean;
    VAR
      AttrLocal@1000000005 : Record 9021623;
      AttrValLocal@1000000002 : Record 9021624;
      IsLastValueEntry@1000000007 : Boolean;
    BEGIN
      //find the referenced attribute...
      AttrLocal.RESET;
      AttrLocal.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      AttrLocal.SETRANGE("Label Code",LabelCode);
      AttrLocal.SETRANGE("Attribute Type",AttributeType);
      AttrLocal.SETRANGE("Attribute Owner Reference",OwnerRef);
      AttrLocal.SETRANGE("Type Code",AttributeCode);
      IF NOT AttrLocal.FINDFIRST THEN EXIT;

      //find the value...
      //MQ 09/02/2011 - GW110209  I do not understand why we are using SETRANGE - we can use a get
      AttrValLocal.RESET;
      AttrValLocal.SETRANGE("Label Code",LabelCode);
      AttrValLocal.SETRANGE("Label Attribute Entry No.",AttrLocal."Entry No.");
      IF AttrValLocal.COUNT=1 THEN IsLastValueEntry:=TRUE;
      AttrValLocal.SETRANGE("Language Code",LanguageCode);
      IF NOT AttrValLocal.FINDFIRST THEN EXIT;

      IF IsLastValueEntry THEN BEGIN
        //delete the attribute too if this is the last value held against it...
        AttrLocal.DELETE(TRUE);  //values are deleted in DELETE trigger code
      END ELSE BEGIN
        AttrValLocal.DELETE(TRUE);
      END;
    END;

    PROCEDURE GetAttributeValue@1000000001(LabelCode@1000000003 : Code[10];AttributeType@1000000004 : Code[10];OwnerRef@1000000000 : Code[20];AttributeCode@1000000001 : Code[20];LanguageCode@1000000002 : Code[10]) RetVal : Text[250];
    VAR
      AttrLocal@1000000006 : Record 9021623;
      AttrValLocal@1000000005 : Record 9021624;
    BEGIN
      //find the referenced attribute...
      AttrLocal.RESET;
      AttrLocal.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      AttrLocal.SETRANGE("Label Code",LabelCode);
      AttrLocal.SETRANGE("Attribute Type",AttributeType);
      AttrLocal.SETRANGE("Attribute Owner Reference",OwnerRef);
      AttrLocal.SETRANGE("Type Code",AttributeCode);
      IF NOT AttrLocal.FINDFIRST THEN EXIT('');

      //find value of referenced value for the specified language...
      AttrValLocal.RESET;
      IF NOT AttrValLocal.GET(LabelCode,AttrLocal."Entry No.",LanguageCode) THEN
        EXIT('');

      EXIT(AttrValLocal.Value);
    END;

    PROCEDURE SetAttributeValue@1000000002(LabelCode@1000000003 : Code[10];AttributeType@1000000004 : Code[10];OwnerRef@1000000000 : Code[20];TypeCode@1000000001 : Code[20];LanguageCode@1000000002 : Code[10];AttributeValue@1000000005 : Text[250]) ValueSet : Boolean;
    VAR
      AttrLocal@1000000007 : Record 9021623;
      LabelLookup@1000000008 : Record 9021622;
      AttrValLocal@1000000006 : Record 9021624;
    BEGIN
      //find the specified attribute type...
      AttrLocal.RESET;
      LabelLookup.SETRANGE("Attribute Type",AttributeType);
      LabelLookup.SETRANGE(Code,TypeCode);
      IF NOT LabelLookup.FINDFIRST THEN BEGIN
        IF GUIALLOWED THEN
          ERROR(Text001,LabelLookup.TABLENAME,LabelLookup.FIELDNAME(LabelLookup.Code),TypeCode,AttributeValue);
        EXIT(FALSE);
      END;

      //find the referenced attribute...
      AttrLocal.RESET;
      AttrLocal.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      AttrLocal.SETRANGE("Label Code",LabelCode);
      AttrLocal.SETRANGE("Attribute Type",AttributeType);
      AttrLocal.SETRANGE("Attribute Owner Reference",OwnerRef);
      AttrLocal.SETRANGE("Type Code",TypeCode);
      IF NOT AttrLocal.FINDFIRST THEN BEGIN
        //no attribute found; create one...
        AttrLocal.INIT;
        AttrLocal."Label Code":=LabelCode;
        AttrLocal."Attribute Type" := AttributeType;
        AttrLocal."Attribute Owner Reference" := OwnerRef;
        AttrLocal."Type Code":=TypeCode;
        AttrLocal."Allow Blank Language Code":=LabelLookup."Allow Blank Language Code";
        AttrLocal."Allow HTML":=LabelLookup."Allow HTML";
        AttrLocal."First Saved Value":=AttributeValue;
        AttrLocal.INSERT(TRUE);
      END;

      //find value of referenced value for the specified language...
      AttrValLocal.RESET;
      IF NOT AttrValLocal.GET(LabelCode,AttrLocal."Entry No.",LanguageCode) THEN BEGIN
        //no value found; create one...
        AttrValLocal.INIT;
        AttrValLocal."Label Code":=LabelCode;
        AttrValLocal."Label Attribute Entry No.":=AttrLocal."Entry No.";
        AttrValLocal."Language Code":=LanguageCode;
        AttrValLocal.Value:=AttributeValue;
        AttrValLocal."Html Allowed":=AttrLocal."Allow HTML";
        AttrValLocal.INSERT;
      END ELSE BEGIN
        //update existing value...
        AttrValLocal.Value:=AttributeValue;
        AttrValLocal.MODIFY;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE EditHtmlEntry@1000000004();
    BEGIN
      //MQ 03/09/2012 - GW120903
      //Note 1: added this function to improve setting up HTML Entries against Items whether
      //called from Page Item or Items.  This function is called from Label Attribute form.
      //and was built to overcome code that was not working consistently on the Label Atrib
      //Translation Value subform when there was no Translation Value.
      //Note 2: the 4th parameter, Shared HTML is hard coded to FALSE as this code is never
      //called from Page Content Value which is the other place Shared HTML could be TRUE.
      //Note 3: there are no Label Lookups of Attribute Type CONTENT so this code does not
      //support CONTENT
      //Note 4: the function HTMLEntry function in ePage table starts with an Error so this is not
      //supported either so the only time this function will be valid is where the Attribute Type is PRODUCT
      //MQ 09/09/2012 - GW120909 also supports PAGE
      IF ("Attribute Type" <> 'PRODUCT') AND ("Attribute Type" <> 'PAGE') THEN
        ERROR(Text005);

      CALCFIELDS("HTML Entry No.");
      IF "HTML Entry No." = 0 THEN BEGIN
        IF "Allow HTML" = FALSE THEN
          ERROR(Text003,FIELDCAPTION("Allow HTML"));
        LabAttTransValueRec.RESET;
        LabAttTransValueRec.SETCURRENTKEY("Label Code","Label Attribute Entry No.","Language Code");
        LabAttTransValueRec.SETRANGE("Label Code","Label Code");
        LabAttTransValueRec.SETRANGE("Label Attribute Entry No.","Entry No.");
        LabAttTransValueRec.SETFILTER("Language Code",'<>%1','');
        IF LabAttTransValueRec.FINDFIRST THEN
          ERROR(Text004);
        IF LabAttTransValueRec.GET("Label Code","Entry No.",'') THEN BEGIN
          LabelRec.EditHtmlEntry("Allow HTML",LabAttTransValueRec."HTML Entry No.","Label Code",
                                 FALSE,"Attribute Type","Attribute Owner Reference",
                                 "Type Code",GlobalFuncs.GetLanguage);
          LabAttTransValueRec.MODIFY(TRUE);
        END ELSE BEGIN
          LabAttTransValueRec.INIT;
          LabAttTransValueRec."Label Code" := "Label Code";
          LabAttTransValueRec."Label Attribute Entry No." := "Entry No.";
          LabAttTransValueRec."Language Code" := '';
          LabAttTransValueRec."HTML Entry No." := 0;
          LabAttTransValueRec.INSERT(TRUE);
          LabelRec.EditHtmlEntry("Allow HTML",LabAttTransValueRec."HTML Entry No.","Label Code",
                                 FALSE,"Attribute Type","Attribute Owner Reference",
                                 "Type Code",GlobalFuncs.GetLanguage);
          LabAttTransValueRec.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        LabelRec.EditHtmlEntry("Allow HTML","HTML Entry No.","Label Code",FALSE,
                                "Attribute Type","Attribute Owner Reference",
                                "Type Code",GlobalFuncs.GetLanguage);
      END;
    END;

    BEGIN
    {
      //DOC EC2013.3.80 MQ 20/08/2013 - GW130820 Remove fields, key, global and use of Sync Mgt
      //DOC EC2013.3.80 MQ 23/10/2013 - GW131023 Add field 112
      //DOC EC2013.4.00 MQ 04/11/2013 - GW131104 RTC
      //DOC EC2015.5.03 MQ 02/06/2015 - GW150602 Do Not Extract field and code.
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

