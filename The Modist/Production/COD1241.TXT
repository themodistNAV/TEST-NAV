OBJECT Codeunit 1241 Fixed File Import
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=1220;
    Permissions=TableData 1221=rimd;
    OnRun=VAR
            ReadStream@1003 : InStream;
            ReadText@1002 : Text;
            ReadLen@1001 : Integer;
            LineNo@1000 : Integer;
            SkippedLineNo@1004 : Integer;
          BEGIN
            "File Content".CREATEINSTREAM(ReadStream);
            DataExchDef.GET("Data Exch. Def Code");
            LineNo := 1;
            REPEAT
              ReadLen := ReadStream.READTEXT(ReadText);
              IF ReadLen > 0 THEN
                ParseLine(ReadText,Rec,LineNo,SkippedLineNo);
            UNTIL ReadLen = 0;
          END;

  }
  CODE
  {
    VAR
      DataExchDef@1000 : Record 1222;

    LOCAL PROCEDURE ParseLine@2(Line@1005 : Text;DataExch@1000 : Record 1220;VAR LineNo@1007 : Integer;VAR SkippedLineNo@1001 : Integer);
    VAR
      DataExchLineDef@1008 : Record 1227;
      DataExchColumnDef@1009 : Record 1223;
      DataExchField@1010 : Record 1221;
      StartPosition@1011 : Integer;
    BEGIN
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExch."Data Exch. Def Code");
      DataExchLineDef.FINDFIRST;

      IF ((LineNo + SkippedLineNo) <= DataExchDef."Header Lines") OR
         ((DataExchLineDef."Data Line Tag" <> '') AND (STRPOS(Line,DataExchLineDef."Data Line Tag") <> 1))
      THEN BEGIN
        SkippedLineNo += 1;
        EXIT;
      END;

      DataExchColumnDef.SETRANGE("Data Exch. Def Code",DataExch."Data Exch. Def Code");
      DataExchColumnDef.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
      DataExchColumnDef.FINDSET;

      StartPosition := 1;
      REPEAT
        DataExchField.InsertRecXMLField(DataExch."Entry No.",LineNo,DataExchColumnDef."Column No.",'',
          COPYSTR(Line,StartPosition,DataExchColumnDef.Length),DataExchLineDef.Code);
        StartPosition += DataExchColumnDef.Length;
      UNTIL DataExchColumnDef.NEXT = 0;
      LineNo += 1;
    END;

    BEGIN
    END.
  }
}

