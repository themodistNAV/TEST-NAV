OBJECT Table 11102095 OM - Comment
{
  OBJECT-PROPERTIES
  {
    Date=12/10/14;
    Time=12:00:00 PM;
    Version List=OMA9.04;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=Table Name,No.;
    OnInsert=VAR
               Mgt@1000 : Codeunit 11102035;
             BEGIN
               "Comment Date" := TODAY;
               "Comment Time" := TIME;
               From := Mgt.ShortUserId;

               UpdateRepository(1);
             END;

    OnModify=BEGIN
               UpdateRepository(2);
             END;

    OnDelete=VAR
               DocumentLine@1000 : Record 11102046;
               CommentLine@1001 : Record 11102096;
             BEGIN
               CommentLine.SETRANGE("Comment Entry No.", "Entry No.");
               CommentLine.DELETEALL(TRUE);

               DocumentLine.SETCURRENTKEY("Comment Entry No.");
               DocumentLine.SETRANGE("Comment Entry No.", "Entry No.");
               DocumentLine.DELETEALL(TRUE);

               UpdateRepository(3);
             END;

    OnRename=BEGIN
               UpdateRepository(4);
             END;

    CaptionML=[DEU=Comment;
               ENU=Comment;
               ESP=Comment;
               FRA=Comment;
               NLD=Opmerking;
               DEA=Comment];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DEU=Entry No.;
                                                              ENU=Entry No.;
                                                              ESP=Entry No.;
                                                              FRA=Entry No.;
                                                              NLD=Volgnummer;
                                                              DEA=Entry No.] }
    { 2   ;   ;Table Name          ;Option        ;CaptionML=[DEU=Table Name;
                                                              ENU=Table Name;
                                                              ESP=Table Name;
                                                              FRA=Table Name;
                                                              NLD=Tabelnaam;
                                                              DEA=Table Name];
                                                   OptionCaptionML=[DEU=Project,Transport,Branch,Merge;
                                                                    ENU=Project,Transport,Branch,Merge;
                                                                    ESP=Project,Transport,Branch,Merge;
                                                                    FRA=Project,Transport,Branch,Merge;
                                                                    NLD=Project,Transport,Branch,Merge;
                                                                    DEA=Project,Transport,Branch,Merge];
                                                   OptionString=Project,Transport,Branch,Merge }
    { 3   ;   ;No.                 ;Code20        ;TableRelation=IF (Table Name=CONST(Project)) "OM - Project"
                                                                 ELSE IF (Table Name=CONST(Transport)) "OM - Transport"
                                                                 ELSE IF (Table Name=CONST(Branch)) "OM - Branch";
                                                   CaptionML=[DEU=No.;
                                                              ENU=No.;
                                                              ESP=No.;
                                                              FRA=No.;
                                                              NLD=Nr.;
                                                              DEA=No.] }
    { 10  ;   ;Comment Date        ;Date          ;CaptionML=[DEU=Date;
                                                              ENU=Date;
                                                              ESP=Date;
                                                              FRA=Date;
                                                              NLD=Datum;
                                                              DEA=Date] }
    { 11  ;   ;Comment Time        ;Time          ;CaptionML=[DEU=Time;
                                                              ENU=Time;
                                                              ESP=Time;
                                                              FRA=Time;
                                                              NLD=Tijd;
                                                              DEA=Time] }
    { 12  ;   ;From                ;Code50        ;CaptionML=[DEU=From;
                                                              ENU=From;
                                                              ESP=From;
                                                              FRA=From;
                                                              NLD=Aangemaakt door;
                                                              DEA=From] }
    { 13  ;   ;Send To             ;Code50        ;TableRelation="OM - User";
                                                   CaptionML=[DEU=To;
                                                              ENU=To;
                                                              ESP=To;
                                                              FRA=To;
                                                              NLD=Naar;
                                                              DEA=To] }
    { 14  ;   ;Type                ;Code20        ;TableRelation="OM - Comment Type".Code WHERE (Table Name=FIELD(Table Name));
                                                   OnValidate=VAR
                                                                CommentType@1000 : Record 11102044;
                                                              BEGIN
                                                                IF CommentType.GET("Table Name", Type) THEN BEGIN
                                                                  Subject := CommentType.Description;
                                                                  Print := CommentType.Print;
                                                                  Transport := CommentType.Transport;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Type;
                                                              ENU=Type;
                                                              ESP=Type;
                                                              FRA=Type;
                                                              NLD=Soort;
                                                              DEA=Type] }
    { 15  ;   ;Subject             ;Text250       ;CaptionML=[DEU=Subject;
                                                              ENU=Subject;
                                                              ESP=Subject;
                                                              FRA=Subject;
                                                              NLD=Onderwerp;
                                                              DEA=Subject] }
    { 16  ;   ;Print               ;Boolean       ;CaptionML=[DEU=Print;
                                                              ENU=Print;
                                                              ESP=Print;
                                                              FRA=Print;
                                                              NLD=Afdrukken;
                                                              DEA=Print] }
    { 17  ;   ;Transport           ;Boolean       ;CaptionML=[DEU=Transport;
                                                              ENU=Transport;
                                                              ESP=Transport;
                                                              FRA=Transport;
                                                              NLD=Transporteren;
                                                              DEA=Transport] }
    { 18  ;   ;Attachments         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("OM - Document Line" WHERE (Comment Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[DEU=Attachments;
                                                              ENU=Attachments;
                                                              ESP=Attachments;
                                                              FRA=Attachments;
                                                              NLD=Bijlagen;
                                                              DEA=Attachments];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Table Name,No.                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE EmptyComment@1() : Boolean;
    VAR
      DocumentLine@1000 : Record 11102046;
      CommentLine@1001 : Record 11102096;
    BEGIN
      IF ("Send To" <> '') OR (Subject <> '') OR (Type <> '') THEN
        EXIT;

      CommentLine.SETRANGE("Comment Entry No.", "Entry No.");
      IF NOT CommentLine.ISEMPTY THEN
        EXIT;

      DocumentLine.SETCURRENTKEY("Comment Entry No.");
      DocumentLine.SETRANGE("Comment Entry No.", "Entry No.");
      IF NOT CommentLine.ISEMPTY THEN
        EXIT;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE UpdateRepository@2(Trigger@1000 : Integer);
    VAR
      RepositoryMgt@1001 : Codeunit 11102067;
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);

      CASE "Table Name" OF
        "Table Name"::Project :
          RepositoryMgt.UpdateProject(RecRef, "No.", Trigger);

        "Table Name"::Transport :
          RepositoryMgt.UpdateTransport(RecRef, "No.", Trigger);

      END;

      RecRef.SETTABLE(Rec);
    END;

    PROCEDURE SendEmail@3();
    VAR
      DocumentLine@1000 : Record 11102046;
      User@1001 : Record 11102056;
      CommentLine@1002 : Record 11102096;
      MailMgt@1003 : Codeunit 11102084;
      InStr@1004 : InStream;
      CrLf@1005 : Text;
      ctMailSent@1006 : TextConst 'DEU=Mail sent.;ENU=Mail sent.;ESP=Mail sent.;FRA=Mail sent.;NLD=Mail verzonden.;DEA=Mail sent.';
    BEGIN
      TESTFIELD("Send To");
      User.GET("Send To");
      User.TESTFIELD("E-Mail");

      CrLf[1] := 13;
      CrLf[2] := 10;

      MailMgt.ToAddress := User."E-Mail";
      MailMgt.Subject := Subject;

      CommentLine.SETRANGE("Comment Entry No.", "Entry No.");
      IF CommentLine.FINDSET THEN
        REPEAT
          MailMgt.AddToBody(CommentLine.Text + FORMAT(CrLf));
        UNTIL CommentLine.NEXT = 0;

      DocumentLine.SETCURRENTKEY("Comment Entry No.");
      DocumentLine.SETRANGE("Comment Entry No.", "Entry No.");
      IF DocumentLine.FINDSET THEN
        REPEAT
          IF DocumentLine.Document.HASVALUE THEN BEGIN
            DocumentLine.CALCFIELDS(Document);
            DocumentLine.Document.CREATEINSTREAM(InStr);
            MailMgt.AddAttachmentBlob(InStr, DocumentLine."File Name");
          END;
        UNTIL DocumentLine.NEXT = 0;

      IF MailMgt.Send THEN
        MESSAGE(ctMailSent);
    END;

    PROCEDURE EditInEditor@4();
    VAR
      CommentLine@1000 : Record 11102096;
      FileHandler@1001 : Codeunit 11102097;
      EditFile@1002 : Page 11102075;
      oFile@1003 : File;
      OutStr@1004 : OutStream;
      CrLf@1005 : Text;
      FileLine@1006 : Text;
      FileName@1007 : Text;
      ReadChar@1008 : Char;
      LineNo@1009 : Integer;
      ctEditQuery@1010 : TextConst 'DEU=Edit Comment;ENU=Edit Comment;ESP=Edit Comment;FRA=Edit Comment;NLD=Opmerking wijzigen;DEA=Edit Comment';
      ctFileName@1011 : TextConst 'DEU=EditComment.txt;ENU=EditComment.txt;ESP=EditComment.txt;FRA=EditComment.txt;NLD=EditComment.txt;DEA=EditComment.txt';
      ctQuestion@1012 : TextConst 'DEU=Modify your comment in the external editor and press OK.;ENU=Modify your comment in the external editor and press OK.;ESP=Modify your comment in the external editor and press OK.;FRA=Modify your comment in the external editor and press OK.;NLD=Wijzig je opmerking in de externe tekst-editor en kies OK.;DEA=Modify your comment in the external editor and press OK.';
    BEGIN
      FileName := FileHandler.TempDirectory + ctFileName;

      IF EXISTS(FileName) THEN
        ERASE(FileName);

      CrLf[1] := 13;
      CrLf[2] := 10;

      oFile.TEXTMODE(TRUE);
      oFile.WRITEMODE(TRUE);
      oFile.CREATE(FileName);
      oFile.CREATEOUTSTREAM(OutStr);

      CommentLine.SETRANGE("Comment Entry No.", "Entry No.");
      IF CommentLine.FINDSET THEN
        REPEAT
          OutStr.WRITETEXT(CommentLine.Text);
          OutStr.WRITETEXT(CrLf);
        UNTIL CommentLine.NEXT = 0;

      oFile.CLOSE;

      HYPERLINK(FileName);

      EditFile.SetCaption(ctEditQuery);
      EditFile.SetQuestion(ctQuestion);
      EditFile.SetFileName(FileName);

      IF EditFile.RUNMODAL IN [ACTION::OK, ACTION::Yes] THEN BEGIN

        CommentLine.DELETEALL;

        oFile.TEXTMODE(FALSE);
        oFile.WRITEMODE(FALSE);
        oFile.OPEN(FileName);
        WHILE oFile.READ(ReadChar) > 0 DO

          CASE ReadChar OF

            10 :
              ;

            13 :
              SaveLine(FileLine, LineNo);

            ELSE BEGIN
              IF STRLEN(FileLine) = 250 THEN
                SaveLine(FileLine, LineNo);
              FileLine += FORMAT(ReadChar);
            END;

          END;

        SaveLine(FileLine, LineNo);

        oFile.CLOSE;

      END;
    END;

    LOCAL PROCEDURE SaveLine@5(VAR TextLine@1000 : Text;VAR LineNo@1001 : Integer);
    VAR
      CommentLine@1002 : Record 11102096;
    BEGIN
      LineNo += 10000;

      CommentLine."Comment Entry No." := "Entry No.";
      CommentLine."Line No." := LineNo;
      CommentLine.Text := TextLine;
      CommentLine.INSERT;

      TextLine := '';
    END;

    BEGIN
    END.
  }
}

