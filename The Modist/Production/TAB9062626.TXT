OBJECT Table 9062626 Criteria
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CriteriaField.RESET();
               CriteriaField.SETCURRENTKEY("Criteria ID");
               CriteriaField.SETRANGE("Criteria ID", Rec.ID);
               CriteriaField.DELETEALL();
             END;

    CaptionML=[ENU=Criteria;
               ENG=Criteria];
    LookupPageID=Page9062887;
    DrillDownPageID=Page9062887;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;OnValidate=VAR
                                                                tmpInt@1000000000 : Integer;
                                                              BEGIN
                                                              END;

                                                   AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                UpdateCriteriaTemplate(Rec.FIELDNO(Name));
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 11  ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Table ID" <> xRec."Table ID") THEN
                                                                BEGIN
                                                                  UpdateFields(Rec.FIELDNO("Table ID"));
                                                                  UpdateCriteriaTemplate(Rec.FIELDNO("Table ID"));
                                                                END;
                                                                Rec.CALCFIELDS("Table Caption");
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 12  ;   ;Table Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              ENG=Table Caption];
                                                   Editable=No }
    { 13  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 14  ;   ;View                ;Text250       ;CaptionML=[ENU=View;
                                                              ENG=View] }
    { 20  ;   ;Attached-to Table ID;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Attached-to Table ID" <> xRec."Attached-to Table ID") THEN
                                                                  Rec.VALIDATE("Attached-to Field ID", 0);
                                                                UpdateAttachedToDescription();
                                                                Rec.CALCFIELDS("Attached-to Table Caption");
                                                              END;

                                                   CaptionML=[ENU=Attached-to Table ID;
                                                              ENG=Attached-to Table ID] }
    { 21  ;   ;Attached-to Table Caption;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Attached-to Table ID)));
                                                   CaptionML=[ENU=Attached-to Table Caption;
                                                              ENG=Attached-to Table Caption];
                                                   Editable=No }
    { 22  ;   ;Attached-to Position;Text250       ;OnValidate=BEGIN
                                                                UpdateAttachedToDescription();
                                                              END;

                                                   CaptionML=[ENU=Attached-to Position;
                                                              ENG=Attached-to Position] }
    { 23  ;   ;Attached-to Description;Text100    ;CaptionML=[ENU=Attached-to Description;
                                                              ENG=Attached-to Description] }
    { 24  ;   ;Attached-to Field ID;Integer       ;OnValidate=BEGIN
                                                                Rec.CALCFIELDS("Attached-to Field Caption");
                                                              END;

                                                   CaptionML=[ENU=Attached-to Field ID;
                                                              ENG=Attached-to Field ID] }
    { 25  ;   ;Attached-to Field Caption;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Attached-to Table ID),
                                                                                                   No.=FIELD(Attached-to Field ID)));
                                                   CaptionML=[ENU=Attached-to Field Caption;
                                                              ENG=Attached-to Field Caption];
                                                   Editable=No }
    { 30  ;   ;Copied-from Criteria ID;Integer    ;TableRelation=Criteria;
                                                   OnValidate=BEGIN
                                                                Rec.CALCFIELDS("Copied-from Critaria Name");
                                                              END;

                                                   CaptionML=[ENU=Copied-from Criteria ID;
                                                              ENG=Copied-from Criteria ID] }
    { 31  ;   ;Copied-from Critaria Name;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Criteria.Name WHERE (ID=FIELD(Copied-from Criteria ID)));
                                                   CaptionML=[ENU=Copied-from Critaria Name;
                                                              ENG=Copied-from Critaria Name];
                                                   Editable=No }
    { 40  ;   ;Add Table Name to Description;Boolean;
                                                   InitValue=Yes;
                                                   CaptionML=[ENU=Add Table Name to Description;
                                                              ENG=Add Table Name to Description] }
    { 50  ;   ;Field Filter Enabled;Boolean       ;CaptionML=[ENU=Field Filter Enabled;
                                                              ENG=Field Filter Enabled] }
    { 51  ;   ;Record Filter Enabled;Boolean      ;CaptionML=[ENU=Record Filter Enabled;
                                                              ENG=Record Filter Enabled] }
    { 52  ;   ;Filter Source Table Id;Integer     ;CaptionML=[ENU=Filter Source Table Id;
                                                              ENG=Filter Source Table Id] }
    { 53  ;   ;Filter Source Field Id;Integer     ;CaptionML=[ENU=Filter Source Field Id;
                                                              ENG=Filter Source Field Id] }
    { 54  ;   ;Filter Source Caption Field Id;Integer;
                                                   CaptionML=[ENU=Filter Source Caption Field Id;
                                                              ENG=Filter Source Caption Field Id] }
    { 55  ;   ;Filter Source Record Position;Text250;
                                                   CaptionML=[ENU=Filter Source Record Position;
                                                              ENG=Filter Source Record Position] }
    { 56  ;   ;Filter Source Record Page Id;Integer;
                                                   CaptionML=[ENU=Filter Source Record Page Id;
                                                              ENG=Filter Source Record Page Id] }
    { 60  ;   ;Field Class         ;Option        ;CaptionML=[ENU=Field Class;
                                                              ENG=Field Class];
                                                   OptionCaptionML=[ENU=" ,Normal,FlowField,FlowFilter";
                                                                    ENG=" ,Normal,FlowField,FlowFilter"];
                                                   OptionString=[ ,Normal,FlowField,FlowFilter] }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Attached-to Table ID,Attached-to Position }
    {    ;Copied-from Criteria ID                  }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description                         }
  }
  CODE
  {
    VAR
      eShortCodeDup@1000000003 : TextConst 'ENU=The Short Code %1 is already in use on Dataset %2 %3;ENG=The Short Code %1 is already in use on Dataset %2 %3';
      eShortCodeBlank@1000000004 : TextConst 'ENU=The Short Code field cannot be left blank;ENG=The Short Code field cannot be left blank';
      eDatasetNotFound_SC@1000000006 : TextConst 'ENU=The Dataset was not found, Type: %1, Short Code: %2;ENG=The Dataset was not found, Type: %1, Short Code: %2';
      CriteriaField@1000000001 : Record 9062627;
      CriteriaTemplate@1000000005 : Record 9062628;
      CriteriaManagement@1000000008 : Codeunit 9062462;
      TnpToolkit@1000000000 : Codeunit 9062230;
      TnpUtils@1000000007 : Codeunit 9062229;
      SystemActivity@1000000002 : Boolean;

    PROCEDURE SetSystemActivity@1000000002(pSystemActivity@1000000000 : Boolean);
    BEGIN
      //DOC NA2015.3 - Setting system activity flag

      SystemActivity := pSystemActivity;
    END;

    PROCEDURE UpdateCriteriaTemplate@1000000003(pFieldID@1000000000 : Integer);
    BEGIN
      //DOC NA2015.3 - Updating criteria template
      //  -> pFieldID: Field ID

      IF ((SystemActivity) OR (Rec."Attached-to Table ID" <> DATABASE::"Criteria Template") OR (Rec."Attached-to Position" = '')) THEN
        EXIT;

      CriteriaTemplate.RESET();
      CriteriaTemplate.SETPOSITION(Rec."Attached-to Position");
      CriteriaTemplate.SETRECFILTER();
      IF (CriteriaTemplate.FINDFIRST()) THEN
      BEGIN
        CriteriaTemplate.SetSystemActivity(TRUE);
        CASE (pFieldID) OF
          Rec.FIELDNO(Name):
            CriteriaTemplate.VALIDATE(Name, Rec.Name);
          Rec.FIELDNO("Table ID"):
            CriteriaTemplate.VALIDATE("Table ID", Rec."Table ID");
        END;
        CriteriaTemplate.MODIFY(TRUE);
        CriteriaTemplate.SetSystemActivity(FALSE);
      END;
    END;

    PROCEDURE UpdateFields@1000000001(pFieldID@1000000000 : Integer);
    BEGIN
      //DOC NA2015.3 - Updating fields
      //  -> pFieldID: Field ID to update in "Criteria Field" table

      CriteriaField.RESET();
      CriteriaField.SETCURRENTKEY("Criteria ID");
      CriteriaField.SETRANGE("Criteria ID", Rec.ID);
      IF (CriteriaField.FINDSET()) THEN
      REPEAT
        CASE (pFieldID) OF
          Rec.FIELDNO("Table ID"):
            CriteriaField.VALIDATE("Table ID", Rec."Table ID");
        END;
        CriteriaField.MODIFY(TRUE);
      UNTIL (CriteriaField.NEXT() = 0);
    END;

    PROCEDURE UpdateDescriptionAndView@1000000006(pModify@1000000001 : Boolean);
    VAR
      lCriteriaField@1000000000 : Record 9062627;
    BEGIN
      //DOC NA2015.3 - Updating description and view
      //  -> pModify: Modify flag

      UpdateDescriptionAndViewForField(lCriteriaField, FALSE, pModify);
    END;

    PROCEDURE UpdateDescriptionAndViewForField@1000000000(pCriteriaField@1000000000 : Record 9062627;pDeleting@1000000001 : Boolean;pModify@1000000006 : Boolean);
    VAR
      lDescription@1000000002 : Text;
      lView@1000000003 : Text;
      lFieldName@1000000004 : Text[50];
      lAddField@1000000005 : Boolean;
      lValue@1000000007 : Text;
      lDescriptionTemplate@1000000008 : Text;
      l@1000000009 : Integer;
      lFilterFunction@1000000010 : Text;
    BEGIN
      //DOC NA2015.3 - Updating description and view for the given field
      //  -> pCriteriaField: The current field (can be empty)
      //  -> pDeleting: Deleting flag
      //  -> pModify: Modify flag

      IF (SystemActivity) THEN
        EXIT;

      lDescription := '';
      lView := '';

      CriteriaField.RESET();
      CriteriaField.SETCURRENTKEY("Criteria ID", Sequence);
      CriteriaField.SETRANGE("Criteria ID", Rec.ID);
      IF (CriteriaField.FINDSET()) THEN
      REPEAT
        lAddField := FALSE;
        IF (CriteriaField.ID = pCriteriaField.ID) THEN
        BEGIN
          lAddField := ( NOT (pDeleting));
          IF (lAddField) THEN
            CriteriaField := pCriteriaField;
        END
        ELSE
          lAddField := TRUE;

        IF (lAddField) THEN
        BEGIN
          lFieldName := CriteriaField.GetFieldName();

          lValue := DELCHR(CriteriaField.Value, '<=>', ' ');
          IF ((COPYSTR(lValue, 1, 1) IN ['=', '>', '<']) OR (COPYSTR(lValue, 1, 2) IN ['<>', '>=', '<='])) THEN
            lDescriptionTemplate := '%1 %2'
          ELSE
            lDescriptionTemplate := '%1 = %2';

          IF (STRLEN(lDescription) > 0) THEN
            lDescription := lDescription + ', ';
          lDescription := lDescription + STRSUBSTNO(lDescriptionTemplate, lFieldName, CriteriaField.Value);

          IF (CriteriaField."Field Type" <> CriteriaField."Field Type"::"Enhanced Field") THEN
          BEGIN
            IF (STRLEN(lView) > 0) THEN
              lView := lView + ',';

            //DOC NA2015.9 >>
            CASE (CriteriaField."Filter Source") OF
              CriteriaField."Filter Source"::Const:
                lFilterFunction := 'FILTER';              //should be 'CONST'
              CriteriaField."Filter Source"::Filter:
                lFilterFunction := 'FILTER';
              CriteriaField."Filter Source"::Field:
                lFilterFunction := 'FIELD';
              CriteriaField."Filter Source"::"Enhanced Field":
                lFilterFunction := 'ENHFIELD';
              CriteriaField."Filter Source"::Record:
                lFilterFunction := 'RECORD';
              ELSE
                lFilterFunction := 'FILTER';
            END;
            lView := lView + STRSUBSTNO('%1=%2(%3)', lFieldName, lFilterFunction, CriteriaField.Value);
            //DOC NA2015.9 <<
          END;
        END;
      UNTIL (CriteriaField.NEXT() = 0);

      IF ((Rec."Add Table Name to Description") AND (STRLEN(lDescription) > 0)) THEN
      BEGIN
        Rec.CALCFIELDS("Table Caption");
        lDescription := STRSUBSTNO('%1: %2', Rec."Table Caption", lDescription);
      END;

      IF (STRLEN(lView) > 0) THEN
        lView := STRSUBSTNO('WHERE(%1)', lView);

      Rec.VALIDATE(Description, COPYSTR(lDescription, 1, MAXSTRLEN(Rec.Description)));
      Rec.VALIDATE(View, COPYSTR(lView, 1, MAXSTRLEN(Rec.View)));

      IF (pModify) THEN
        Rec.MODIFY(TRUE);
    END;

    PROCEDURE UpdateAttachedToDescription@1000000004();
    VAR
      DescriptionText@1000000000 : Text;
    BEGIN
      //DOC NA2015.4 - Updating "Attached-to Description"

      DescriptionText := TnpToolkit.PositionToDescription(Rec."Attached-to Table ID", Rec."Attached-to Position");
      IF (STRLEN(DescriptionText) > MAXSTRLEN("Attached-to Description")) THEN
        DescriptionText := COPYSTR(DescriptionText, 1, MAXSTRLEN("Attached-to Description"));

      Rec.VALIDATE("Attached-to Description", DescriptionText);
    END;

    PROCEDURE AddFields@1000000005();
    BEGIN
      //DOC NA2015.3 - Adding fields

      Rec.TESTFIELD(ID);
      CriteriaManagement.AddCriteriaFields(Rec.ID);
    END;

    BEGIN
    {
      //DOC NA2015.3  JH 17/09/2014 - Created
      //DOC NA2015.4  AZ 30/10/2014 - Modifications
      //DOC NA2015.7  JH 15/05/2015 - "Add Table Name to Description" added
      //DOC NA2016.9  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.12 JH 19/12/2015 - "Attached-to Field ID" added
      //DOC NA2016.12 JH 22/01/2016 - Field class added
      //DOC NA2016.14 JH 19/02/2016 - Bugfixes
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

