OBJECT Table 9021635 Page Content Value
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:03 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Label.GET("Label Code");
               IF Label."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text001,"Label Code",COMPANYNAME);

               IF Label."Multi-Language Mode" THEN
                 VALIDATE("In Multi Language Mode",TRUE); //DOC EC2013.3.00

               "DateTime Created":=CURRENTDATETIME;
               SetModifiedDateTime();

               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021635);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    OnModify=BEGIN
               Label.GET("Label Code");
               IF Label."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text001,"Label Code",COMPANYNAME);

               SetModifiedDateTime();

               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021635);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    OnDelete=BEGIN
               Label.GET("Label Code");
               IF Label."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text001,"Label Code",COMPANYNAME);

               //DOC EC2013.3.80 SyncMgt.DeletionMgt(9021635,"Label Code",FORMAT("Page GroupGuID"),
               //DOC EC2013.3.80                     PlaceholderID,FORMAT("Content Line No."),Attribute,'','','',"Sync Log No.","Sync Version No.");
             END;

    OnRename=BEGIN
               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021635);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    CaptionML=[ENU=Page Content Value;
               ENG=Page Content Value];
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Page GroupGuID      ;Integer       ;CaptionML=[ENU=Page GroupGuID;
                                                              ENG=Page GroupGuID];
                                                   NotBlank=Yes }
    { 3   ;   ;PlaceholderID       ;Text20        ;TableRelation="Layout Component".PlaceHolderID WHERE (Label Code=FIELD(Label Code),
                                                                                                         Template Page GroupGuID=FIELD(Template Page GroupGuID));
                                                   CaptionML=[ENU=PlaceholderID;
                                                              ENG=PlaceholderID];
                                                   NotBlank=Yes }
    { 5   ;   ;Content Line No.    ;Integer       ;CaptionML=[ENU=Content Line No.;
                                                              ENG=Content Line No.];
                                                   NotBlank=Yes }
    { 10  ;   ;Content Type Code   ;Code20        ;TableRelation="Content Type";
                                                   CaptionML=[ENU=Content Type Code;
                                                              ENG=Content Type Code];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 15  ;   ;Attribute           ;Text100       ;TableRelation="Content Type Attribute".Attribute WHERE (Content Type Code=FIELD(Content Type Code));
                                                   CaptionML=[ENU=Attribute;
                                                              ENG=Attribute];
                                                   NotBlank=Yes }
    { 16  ;   ;Html Allowed        ;Boolean       ;CaptionML=[ENU=Html Allowed;
                                                              ENG=Html Allowed] }
    { 18  ;   ;Required            ;Boolean       ;CaptionML=[ENU=Required;
                                                              ENG=Required] }
    { 19  ;   ;Template Page GroupGuID;Integer    ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code),
                                                                                          System Entry=CONST(Yes),
                                                                                          Template Page Entry=CONST(Yes));
                                                   CaptionML=[ENU=Template Page GroupGuID;
                                                              ENG=Template Page GroupGuID];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Value Type          ;Option        ;CaptionML=[ENU=Value Type;
                                                              ENG=Value Type];
                                                   OptionCaptionML=[ENU=" ,Specific,Dynamic";
                                                                    ENG=" ,Specific,Dynamic"];
                                                   OptionString=[ ,Specific,Dynamic] }
    { 21  ;   ;Shared Html         ;Boolean       ;OnValidate=BEGIN
                                                                IF "Shared Html" = FALSE THEN BEGIN
                                                                  IF CONFIRM(Text005) THEN
                                                                   "Html Entry No." := 0
                                                                  ELSE
                                                                    ERROR(Text007);
                                                                END ELSE
                                                                  MESSAGE(Text006);
                                                              END;

                                                   CaptionML=[ENU=Shared Html;
                                                              ENG=Shared Html] }
    { 22  ;   ;Is Master Data      ;Boolean       ;CaptionML=[ENU=Is Master Data;
                                                              ENG=Is Master Data] }
    { 23  ;   ;Advanced Feature    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Content Type Attribute"."Advanced Feature" WHERE (Content Type Code=FIELD(Content Type Code),
                                                                                                                         Attribute=FIELD(Attribute)));
                                                   OnValidate=BEGIN
                                                                IF NOT "Is Master Data" THEN
                                                                  ERROR(Text008);
                                                              END;

                                                   CaptionML=[ENU=Advanced Feature;
                                                              ENG=Advanced Feature];
                                                   Editable=No }
    { 28  ;   ;Content Description ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Page Content".Description WHERE (Label Code=FIELD(Label Code),
                                                                                                        Page GroupGuid=FIELD(Page GroupGuID),
                                                                                                        PlaceholderID=FIELD(PlaceholderID),
                                                                                                        Line No.=FIELD(Content Line No.)));
                                                   CaptionML=[ENU=Content Description;
                                                              ENG=Content Description];
                                                   Editable=No }
    { 30  ;   ;Value Text          ;Text250       ;OnValidate=VAR
                                                                ContentTypeAttr@1190000005 : Record 9021634;
                                                                testForBoolean@1190000010 : Boolean;
                                                                testForDate@1190000009 : Date;
                                                                testForDecimal@1190000008 : Decimal;
                                                                testForInteger@1190000007 : Integer;
                                                                testForTime@1190000006 : Time;
                                                                TextValue@1190000011 : Text[250];
                                                              BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF ("Value Text" <> '') AND
                                                                  ("Multilanguage Allowed" = TRUE) THEN
                                                                  MESSAGE(Text019);
                                                                //DOC EC2013.3.00 +

                                                                TextValue:="Value Text";

                                                                ContentTypeAttr.GET("Content Type Code",Attribute);

                                                                //confirm a value is present and valid if required...
                                                                IF ContentTypeAttr."Value Is Mandatory" THEN BEGIN
                                                                  IF TextValue='' THEN ERROR(Text015);
                                                                END;

                                                                IF (TextValue<>'') AND (ContentTypeAttr."Validate-As Type"<>ContentTypeAttr."Validate-As Type"::" ") THEN BEGIN
                                                                  CASE ContentTypeAttr."Validate-As Type" OF
                                                                    ContentTypeAttr."Validate-As Type"::Boolean:
                                                                    BEGIN
                                                                      //cannot use: EVALUATE(testForBoolean,TextValue);
                                                                      IF (TextValue<>'True') AND (TextValue<>'False') THEN ERROR(Text016);
                                                                    END;
                                                                    ContentTypeAttr."Validate-As Type"::Date:
                                                                    BEGIN
                                                                      EVALUATE(testForDate,TextValue);
                                                                    END;
                                                                    ContentTypeAttr."Validate-As Type"::Decimal:
                                                                    BEGIN
                                                                      EVALUATE(testForDecimal,TextValue);
                                                                    END;
                                                                    ContentTypeAttr."Validate-As Type"::Integer:
                                                                    BEGIN
                                                                      EVALUATE(testForInteger,TextValue);
                                                                    END;
                                                                    ContentTypeAttr."Validate-As Type"::Time:
                                                                    BEGIN
                                                                      EVALUATE(testForTime,TextValue);
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ValueText@1190000005 : Text[250];
                                                            BEGIN
                                                              //DOC EC2013.4.50 ValueText:=AttributeValueLookup(Rec);
                                                              ValueText := VersionCalls.AttributeValueLookup(Rec); //DOC EC2013.4.50
                                                              IF ValueText <> '' THEN
                                                                "Value Text" := ValueText;
                                                            END;

                                                   CaptionML=[ENU=Value Text;
                                                              ENG=Value Text] }
    { 41  ;   ;Html Entry No.      ;Integer       ;OnValidate=BEGIN
                                                                IF "Shared Html" THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[ENU=Html Entry No.;
                                                              ENG=Html Entry No.] }
    { 50  ;   ;Attribute Description;Text250      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Content Type Attribute".Description WHERE (Content Type Code=FIELD(Content Type Code),
                                                                                                                  Attribute=FIELD(Attribute)));
                                                   CaptionML=[ENU=Attribute Description;
                                                              ENG=Attribute Description];
                                                   Editable=No }
    { 78  ;   ;In Multi Language Mode;Boolean     ;CaptionML=[ENU=In Multi Language Mode;
                                                              ENG=In Multi Language Mode] }
    { 80  ;   ;Multilanguage Key   ;Code80        ;OnValidate=BEGIN
                                                                IF "Multilanguage Allowed" = FALSE THEN
                                                                  ERROR(Text018,FIELDCAPTION("Multilanguage Allowed"));

                                                                IF "In Multi Language Mode" = FALSE THEN BEGIN
                                                                  Label.GET("Label Code");
                                                                  IF Label."Multi-Language Mode" THEN BEGIN
                                                                    "In Multi Language Mode" := TRUE;
                                                                    IF "Multilanguage Key" = '?' THEN
                                                                      "Multilanguage Key" :=
                                                                        MultiLangFunc.CreateKey(9021635,"Label Code",
                                                                        (FORMAT("Page GroupGuID") + '_' + PlaceholderID + '_' +
                                                                        FORMAT("Content Line No.") + '_' + Attribute),0);
                                                                  END;
                                                                END ELSE
                                                                  IF "Multilanguage Key" = '?' THEN
                                                                    "Multilanguage Key" :=
                                                                      MultiLangFunc.CreateKey(9021635,"Label Code",
                                                                      (FORMAT("Page GroupGuID") + '_' + PlaceholderID + '_' +
                                                                      FORMAT("Content Line No.") + '_' + Attribute),0);
                                                                IF ("In Multi Language Mode" = FALSE) AND
                                                                  ("Multilanguage Key" <> '') THEN
                                                                  ERROR(Text017,FIELDCAPTION("Multilanguage Key"));
                                                                IF "Multilanguage Key" <> '' THEN
                                                                  MultiLangFunc.UpdateMultiLang("Multilanguage Key","Label Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              VALIDATE("Multilanguage Key",
                                                                       MultiLangFunc.ReturnResxName("Multilanguage Key","Label Code"));
                                                            END;

                                                   CaptionML=[ENU=Multilanguage Key;
                                                              ENG=Multilanguage Key] }
    { 81  ;   ;Multilanguage Allowed;Boolean      ;CaptionML=[ENU=Multilanguage Allowed;
                                                              ENG=Multilanguage Allowed] }
    { 120 ;   ;DateTime Created    ;DateTime      ;CaptionML=[ENU=DateTime Created;
                                                              ENG=DateTime Created] }
    { 121 ;   ;DateTime Last Modified;DateTime    ;CaptionML=[ENU=DateTime Last Modified;
                                                              ENG=DateTime Last Modified] }
  }
  KEYS
  {
    {    ;Label Code,Page GroupGuID,PlaceholderID,Content Line No.,Attribute;
                                                   Clustered=Yes }
    {    ;Label Code,Content Type Code,Attribute   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1040000 : TextConst 'ENU=You cannot do this as the label %1 is not used in the company %2.  (1);ENG=You cannot do this as the label %1 is not used in the company %2.  (1)';
      Label@1040001 : Record 9021618;
      Text002@1040003 : TextConst 'ENU=You cannot edit html for this entry as html is not allowed. (2);ENG=You cannot edit html for this entry as html is not allowed. (2)';
      Text003@1040002 : TextConst 'ENU=You cannot change the Html Entry No. if the Html is shared.  If you want to change the entry number, untick the Shared Html field. (3);ENG=You cannot change the Html Entry No. if the Html is shared.  If you want to change the entry number, untick the Shared Html field. (3)';
      Text004@1040004 : TextConst 'ENU=This Html is shared.  If you change this you will change it for all other shared entries.\\Are you sure you want to proceed? (4);ENG=This Html is shared.  If you change this you will change it for all other shared entries.\\Are you sure you want to proceed? (4)';
      Text005@1040005 : TextConst 'ENU=Unticking this will loose the link to any already existing HTML.  \\Are you sure you want to continue? (5);ENG=Unticking this will loose the link to any already existing HTML.  \\Are you sure you want to continue? (5)';
      Text006@1040006 : TextConst 'ENU=Ticking this will not create any links.  You need to manually link to a HTML Entry No. first. (6);ENG=Ticking this will not create any links.  You need to manually link to a HTML Entry No. first. (6)';
      Text007@1040007 : TextConst 'ENU=Change Abandoned. (7);ENG=Change Abandoned. (7)';
      Text008@1000000000 : TextConst 'ENU=You cannot change this as the line is not master data. (8);ENG=You cannot change this as the line is not master data. (8)';
      Text009@1000000001 : TextConst 'ENU=You are about to reset the ''%1'' Attribute for the ''%2'' Content Type on %3 records, using the Default Value of ''%4''.\\This operation cannot be undone.\\Are you sure you wish to proceed?;ENG=You are about to reset the ''%1'' Attribute for the ''%2'' Content Type on %3 records, using the Default Value of ''%4''.\\This operation cannot be undone.\\Are you sure you wish to proceed?';
      Text010@1000000002 : TextConst 'ENU=No Attribute called ''%1'' can be found for Content Type ''%2''.\\Operation cancelled.;ENG=No Attribute called ''%1'' can be found for Content Type ''%2''.\\Operation cancelled.';
      Text011@1000000003 : TextConst 'ENU="No Page Content records were found using the Attribute ''%1'' of the Content Type ''%2''.\\Operation complete. ";ENG="No Page Content records were found using the Attribute ''%1'' of the Content Type ''%2''.\\Operation complete. "';
      Text012@1000000004 : TextConst 'ENU=Operation complete.;ENG=Operation complete.';
      Text013@1000000005 : TextConst 'ENU=No Label selected.  No changes made.  Operation abandonned.;ENG=No Label selected.  No changes made.  Operation abandonned.';
      Text014@1190000005 : TextConst 'ENU=Choose a file;ENG=Choose a file';
      Text015@1190000007 : TextConst 'ENU=Blank values are not allowed.  Please provide a value.;ENG=Blank values are not allowed.  Please provide a value.';
      Text016@1190000008 : TextConst 'ENU=A Boolean value is required.  Only ''True'' or ''False'' are valid values.\\Values are case sensitive.;ENG=A Boolean value is required.  Only ''True'' or ''False'' are valid values.\\Values are case sensitive.';
      MultiLangFunc@1190000010 : Codeunit 9021603;
      Text017@1190000011 : TextConst 'ENU=The %1 field has no function as you are not in Multi Language Mode.;ENG=The %1 field has no function as you are not in Multi Language Mode.';
      Text018@1190000012 : TextConst 'ENU=You cannot do this as %1 is not TRUE.;ENG=You cannot do this as %1 is not TRUE.';
      Text019@1000000006 : TextConst 'ENU=This has no effect as Multilanguage is TRUE.;ENG=This has no effect as Multilanguage is TRUE.';
      GlobalFuncs@1000000007 : Codeunit 9021624;
      VersionCalls@1000000008 : Codeunit 9021662;

    PROCEDURE HtmlEntry@1040000();
    VAR
      OwnerId@1190000005 : Code[100];
    BEGIN
      //pass "Html Entry No." to function where it's updated if 0...
      //<MA20110714>
      //Label.EditHtmlEntry("Html Allowed","Html Entry No.","Label Code","Shared Html");
      OwnerId:=FORMAT("Page GroupGuID")+'-'+PlaceholderID+'-'+FORMAT("Content Line No.")+"Content Type Code";
      Label.EditHtmlEntry("Html Allowed","Html Entry No.","Label Code","Shared Html",
                                'CTRL',OwnerId,Attribute,GlobalFuncs.GetLanguage);
      //</MA20110714>

      //save any change to "Html Entry No."
      MODIFY;
    END;

    PROCEDURE ResetAllAttrValuesToDefault@1000000000(ContentType@1000000000 : Code[20];ContentTypeAttribute@1000000001 : Text[100];IsUserRequest@1000000002 : Boolean);
    VAR
      ContentTypeAttr@1000000003 : Record 9021634;
      Label@1000000005 : Record 9021618;
      PageContentValue@1000000004 : Record 9021635;
      TxtMultipleTexts@1000000006 : TextConst 'ENU=This is not supported when there are multiple labels.;ENG=This is not supported when there are multiple labels.';
    BEGIN
      IF NOT ContentTypeAttr.GET(ContentType, ContentTypeAttribute) THEN
        IF IsUserRequest THEN
          ERROR(Text010,ContentTypeAttribute,ContentType)
        ELSE
          EXIT;

      //DOC EC2013.3.10 -
      Label.RESET;
      Label.SETRANGE("Used-in Company",COMPANYNAME);
      IF Label.COUNT = 1 THEN BEGIN
        Label.FINDFIRST;
      END ELSE BEGIN
      //DOC EC2013.3.10 +
        //DOC EC2013.4.00 IF NOT (FORM.RUNMODAL(9021623, Label) = ACTION::LookupOK) THEN
        ERROR(TxtMultipleTexts); //DOC EC2013.4.00
        //MQ 04/11/2013 - GW131104 Noone has multiple labels in the same company at moment so will not try to resolve this.
          IF IsUserRequest THEN
            ERROR(Text013)
          ELSE
            EXIT;
      END; //DOC EC2013.3.10

      PageContentValue.SETCURRENTKEY("Label Code","Content Type Code",Attribute);
      PageContentValue.SETRANGE("Label Code",Label.Code);
      PageContentValue.SETRANGE("Content Type Code",ContentType);
      PageContentValue.SETRANGE(Attribute,ContentTypeAttribute);
      IF NOT PageContentValue.FINDFIRST THEN
        IF IsUserRequest THEN
          ERROR(Text011,ContentTypeAttribute,ContentType);

      IF IsUserRequest THEN
        IF NOT DIALOG.CONFIRM(Text009,FALSE,ContentTypeAttribute,ContentType,
                              PageContentValue.COUNT,ContentTypeAttr."Default Value") THEN
          EXIT;

      PageContentValue.MODIFYALL("Multilanguage Key",
                       ContentTypeAttr."Multilanguage Key",FALSE); //DOC EC2013.3.00
      PageContentValue.MODIFYALL("Multilanguage Allowed",
                       ContentTypeAttr."Multilanguage Allowed",FALSE); //DOC EC2013.3.00
      PageContentValue.MODIFYALL("Value Text",ContentTypeAttr."Default Value",TRUE);
      MESSAGE(Text012);
    END;

    PROCEDURE SetModifiedDateTime@1000000003();
    VAR
      Page@1000000000 : Record 9021628;
    BEGIN
      "DateTime Last Modified":=CURRENTDATETIME;
      IF Page.GET("Label Code","Page GroupGuID") THEN BEGIN
        Page.VALIDATE("DateTime Last Modified",CURRENTDATETIME);
        Page.MODIFY;
      END;
    END;

    BEGIN
    {
      //DOC EC2009.2.64 MQ 01/01/2011 - Sync. Mgt.
      //DOC EC2009.2.67 MA 06/07/2011 - MA20110706 added PageParentGuid (for PAGE-BOX-LIST lookup)
      //DOC EC2009.2.67 MA 14/07/2011 - MA20110714 mods to HtmlEntry function
      //DOC EC2013.3.00 MQ 01/01/2013 - Add Multilanguage functionality
      //DOC EC2013.3.10 MQ 01/01/2013 - Do not ask which Label if only 1 in Company
      //DOC EC2013.3.80 MQ 20/08/2013 - GW130820 Remove fields, key, global and use of Sync Mgt
      //DOC EC2013.4.00 MQ 04/11/2013 - GW131104 RTC
      //DOC EC2013.4.50 MQ 08/01/2014 - GW140108 Removed function AttributeValueLookup and called CU directly from ValueText OnLookup
      //DOC EC2013.4.63 MQ 29/05/2014 - GW140529 Set some primary key fields to NotBlank Yes
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

