OBJECT Codeunit 9062385 Panel Timestamp Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=28/08/15;
    Time=10:17:08 AM;
    Version List=NA2016.1;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      mTilePanelTimestamp@1000000000 : Record 9062489;

    PROCEDURE Get@1000000004(pTilePanelCode@1000000000 : Code[20]) : DateTime;
    BEGIN
      //DOC NA2015.1 - Getting tile panel timestamp or 0DT if panel not found
      //  -> pTilePanelCode: Panel code
      //  <- The timestamp

      IF (mTilePanelTimestamp.GET(pTilePanelCode)) THEN
        EXIT(mTilePanelTimestamp."Date Time")
      ELSE
        EXIT(0DT);
    END;

    PROCEDURE Changed@1000000003(pTilePanelCode@1000000000 : Code[20];VAR pDateTime@1000000001 : DateTime) : Boolean;
    VAR
      lResult@1000000002 : Boolean;
    BEGIN
      //DOC NA2015.1 - Checking if panel timestamp has been changed since the last check
      //  -> pTilePanelCode: Panel code
      //  <> pDateTime: Last remembered date & time of modification
      //  <- TRUE = Timestamp changed, FALSE = Not

      IF (mTilePanelTimestamp.GET(pTilePanelCode)) THEN
        IF (mTilePanelTimestamp."Date Time" <> pDateTime) THEN
        BEGIN
          pDateTime := mTilePanelTimestamp."Date Time";
          lResult := TRUE;
        END;

      EXIT(lResult);
    END;

    PROCEDURE Add@1000000001(pTilePanelCode@1000000000 : Code[20]);
    BEGIN
      //DOC NA2015.1 - Adding panel timestamp
      //  -> pTilePanelCode: Panel code

      IF (pTilePanelCode = '') THEN
        EXIT;

      IF (mTilePanelTimestamp.GET(pTilePanelCode)) THEN
      BEGIN
        mTilePanelTimestamp."Date Time" := CURRENTDATETIME();
        mTilePanelTimestamp.MODIFY();
      END
      ELSE
      BEGIN
        mTilePanelTimestamp."Panel Code" := pTilePanelCode;
        mTilePanelTimestamp."Date Time" := CURRENTDATETIME();
        mTilePanelTimestamp.INSERT();
      END;
    END;

    PROCEDURE Remove@1000000002(pTilePanelCode@1000000000 : Code[20]);
    BEGIN
      //DOC NA2015.1 - Removing panel timestamp
      //  -> pTilePanelCode: Panel code

      IF (pTilePanelCode = '') THEN
        EXIT;

      IF (mTilePanelTimestamp.GET(pTilePanelCode)) THEN
        mTilePanelTimestamp.DELETE();
    END;

    PROCEDURE Rename@1000000000(pOldTilePanelCode@1000000000 : Code[20];pTilePanelCode@1000000001 : Code[20]);
    BEGIN
      //DOC NA2015.1 - Renaming panel timestamp
      //  -> pOldTilePanelCode: Old tile panel code
      //  -> pTilePanelCode: Tile panel code

      IF (pOldTilePanelCode <> pTilePanelCode) THEN
        Remove(pOldTilePanelCode);
      Add(pTilePanelCode);
    END;

    BEGIN
    {
      //DOC NA2015.1 JH 17/02/2014 - Created
      //DOC NA2015.1 JH 15/04.2014 - Tile panels 3
      //DOC NA2016.1 JH 28/08/2015 - Upgraded to 2016
    }
    END.
  }
}

