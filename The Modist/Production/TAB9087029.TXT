OBJECT Table 9087029 Item Matrix Buffer
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=12:28:46 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Matrix Buffer;
               ENG=Item Matrix Buffer];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Style No.           ;Code20        ;TableRelation=Style;
                                                   OnValidate=BEGIN
                                                                UpdateDescription();
                                                              END;

                                                   CaptionML=[ENU=Style No.;
                                                              ENG=Style No.] }
    { 11  ;   ;Style Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Style Description;
                                                              ENG=Style Description];
                                                   Editable=No }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF ( NOT (mItem.GET(Rec."Item No."))) THEN
                                                                  CLEAR(mItem);
                                                                Rec.VALIDATE("Style No.", mItem."Style No.");
                                                                Rec.VALIDATE("Colour Code", mItem."Colour Code");
                                                                UpdateDescription();
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.] }
    { 21  ;   ;Item Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Style.Description WHERE (No.=FIELD(Style No.)));
                                                   CaptionML=[ENU=Item Description;
                                                              ENG=Item Description];
                                                   Editable=No }
    { 30  ;   ;Colour Code         ;Code20        ;TableRelation=Colour;
                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 50  ;   ;No. of Lines        ;Integer       ;CaptionML=[ENU=No. of Lines;
                                                              ENG=No. of Lines] }
    { 60  ;   ;Total Quantity      ;Decimal       ;CaptionML=[ENU=Total Quantity;
                                                              ENG=Total Quantity];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Total Amount        ;Decimal       ;CaptionML=[ENU=Total Amount;
                                                              ENG=Total Amount] }
    { 80  ;   ;Show                ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Show;
                                                              ENG=Show];
                                                   OptionCaptionML=[ENU=" ,Quantity,Unit Price,Line Discount,Quantity to Ship,Quantity to Invoice,Quantity Shipped,Quantity Invoiced,Quantity Allocated,Location,Quantity to Receive,Quantity Received,Unit Cost,Unit Price (LCY),Unit Cost (LCY),Landed Cost (LCY)";
                                                                    ENG=" ,Quantity,Unit Price,Line Discount,Quantity to Ship,Quantity to Invoice,Quantity Shipped,Quantity Invoiced,Quantity Allocated,Location,Quantity to Receive,Quantity Received,Unit Cost,Unit Price (LCY),Unit Cost (LCY),Landed Cost (LCY)"];
                                                   OptionString=[ ,Quantity,Unit Price,Line Discount,Quantity to Ship,Quantity to Invoice,Quantity Shipped,Quantity Invoiced,Quantity Allocated,Location,Quantity to Receive,Quantity Received,Unit Cost,Unit Price (LCY),Unit Cost (LCY),Landed Cost (LCY)] }
    { 90  ;   ;Set Update Mode     ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Set Update Mode;
                                                              ENG=Set Update Mode];
                                                   OptionCaptionML=[ENU=" ,Init,Update,Back,Forward";
                                                                    ENG=" ,Init,Update,Back,Forward"];
                                                   OptionString=[ ,Init,Update,Back,Forward] }
    { 100 ;   ;Data Type           ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Data Type;
                                                              ENG=Data Type];
                                                   OptionCaptionML=[ENU=" ,Decimal,Code";
                                                                    ENG=" ,Decimal,Code"];
                                                   OptionString=[ ,Decimal,Code] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      mItem@1000000000 : Record 27;
      mStyle@1000000001 : Record 9087012;

    PROCEDURE GetMatrixPageCaption@1000000002(pStyleNo@1000000004 : Code[20];pItemNo@1000000005 : Code[20];pShow@1000000000 : Integer) : Text[250];
    VAR
      ePageCaption@1000000002 : TextConst 'ENU=%1 %2 - %3;ENG=%1 %2 - %3';
      lPageCaption@1000000003 : Text[250];
    BEGIN
      //DOC NS2015.1 - Returnign matrix page caption
      //  -> pStyleNo: Style no.
      //  -> pItemNo: Item no.
      //  -> pShow: Show value flag (what is being shown)
      //  <- The caption

      IF (pStyleNo <> '') THEN
        lPageCaption := STRSUBSTNO(ePageCaption, mStyle.TABLECAPTION(), pStyleNo, GetShowText(pShow))
      ELSE
        lPageCaption := STRSUBSTNO(ePageCaption, mItem.TABLECAPTION(), pItemNo, GetShowText(pShow));

      EXIT(lPageCaption);
    END;

    PROCEDURE GetShowText@1000000003(pShow@1000000000 : Integer) : Text[50];
    VAR
      lItemMatrixBuffer@1000000001 : Record 9087029;
    BEGIN
      //DOC NS2015.1 - Returning text of "Show" flag
      //  -> pShow: The flag
      //  <- The text

      lItemMatrixBuffer.Show := pShow;
      EXIT(FORMAT(lItemMatrixBuffer.Show));
    END;

    LOCAL PROCEDURE UpdateDescription@1000000001();
    BEGIN
      //DOC NS2015.1 - Updating description from item / style

      IF (Rec."Style No." <> '') THEN
      BEGIN
        Rec.CALCFIELDS("Style Description");
        Rec.VALIDATE(Description, Rec."Style Description");
      END
      ELSE IF (Rec."Item No." <> '') THEN
      BEGIN
        Rec.CALCFIELDS("Item Description");
        Rec.VALIDATE(Description, Rec."Item Description");
      END
      ELSE
        Rec.VALIDATE(Description, '');
    END;

    PROCEDURE ShowItem@1000000009();
    VAR
      lItem@1000000001 : Record 27;
    BEGIN
      //DOC NS2015.1 - Showing the current item

      Rec.TESTFIELD("Item No.");
      lItem.GET(Rec."Item No.");
      PAGE.RUN(PAGE::"Item Card", lItem);
    END;

    PROCEDURE ShowStyle@1000000000();
    VAR
      lStyle@1000000001 : Record 9087012;
    BEGIN
      //DOC NS2015.1 - Showing the current item

      Rec.TESTFIELD("Style No.");
      lStyle.GET(Rec."Style No.");
      PAGE.RUN(PAGE::"Style Card", lStyle);
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 20/03/2014 - Created
      //DOC NS2016.16 JH 07/09/2016 - nSemble 2016
    }
    END.
  }
}

