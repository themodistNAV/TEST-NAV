OBJECT Codeunit 9062545 Quick Report Maintenance
{
  OBJECT-PROPERTIES
  {
    Date=08/12/15;
    Time=[ 9:57:55 AM];
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Code();
          END;

  }
  CODE
  {
    VAR
      QuickReportHeader@1000000002 : Record 9062697;

    LOCAL PROCEDURE "--- General ---"@1000000002();
    BEGIN
    END;

    LOCAL PROCEDURE Code@1000000000();
    BEGIN
      //DOC NA2016.12 - Code of the codeunit

      DeleteOldReports();
      DeleteOldUserReports();
    END;

    LOCAL PROCEDURE DeleteOldReports@1000000004();
    VAR
      lTimestampFilterDateTime@1000000000 : DateTime;
    BEGIN
      //DOC NA2016.12 - Deleting old reports (for all users; reports having timestamp older than 1 day)

      lTimestampFilterDateTime := CREATEDATETIME(CALCDATE('<-1D>', TODAY()), TIME());

      QuickReportHeader.RESET();
      QuickReportHeader.SETCURRENTKEY("Temporary", "Timestamp Date Time");
      QuickReportHeader.SETRANGE("Temporary", TRUE);
      QuickReportHeader.SETFILTER("Timestamp Date Time", '<%1', lTimestampFilterDateTime);
      QuickReportHeader.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE DeleteOldUserReports@1000000005();
    VAR
      lLoginDateTime@1000000000 : DateTime;
    BEGIN
      //DOC NA2016.12 - Deleting old user reports (for the current user; all reports before login)

      lLoginDateTime := FindLoginDateTime();

      QuickReportHeader.RESET();
      QuickReportHeader.SETCURRENTKEY("Temporary", "User ID", "Timestamp Date Time");
      QuickReportHeader.SETRANGE("Temporary", TRUE);
      QuickReportHeader.SETRANGE("User ID", USERID);
      QuickReportHeader.SETFILTER("Timestamp Date Time", '<%1', lLoginDateTime);
      QuickReportHeader.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE "--- Tools ---"@1000000003();
    BEGIN
    END;

    LOCAL PROCEDURE FindLoginDateTime@1000000001() : DateTime;
    VAR
      lActiveSession@1000000001 : Record 2000000110;
      lLoginDateTime@1000000000 : DateTime;
    BEGIN
      //DOC NA2016.12 - Trying to find user (session) date & time
      //  <- The date & time

      IF ( NOT (lActiveSession.GET(SERVICEINSTANCEID, SESSIONID))) THEN
        CLEAR(lActiveSession);

      IF (lActiveSession."Login Datetime" <> 0DT) THEN
        lLoginDateTime := lActiveSession."Login Datetime"
      ELSE
        lLoginDateTime := CREATEDATETIME(TODAY, 000000T);

      EXIT(lLoginDateTime);
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 08/12/2015 - Created
    }
    END.
  }
}

