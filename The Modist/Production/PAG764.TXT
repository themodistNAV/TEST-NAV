OBJECT Page 764 Acc. Sched. Chart Matrix
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Acc. Sched. Chart Matrix;
               ENG=Acc. Sched. Chart Matrix];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table763;
    PageType=List;
    OnFindRecord=BEGIN
                   EXIT(FINDSET);
                 END;

    OnAfterGetRecord=VAR
                       AccSchedLine@1000 : Record 85;
                     BEGIN
                       IF AccSchedLine.GET("Account Schedule Name","Account Schedule Line No.") THEN BEGIN
                         AccSchedLineRowNo := AccSchedLine."Row No.";
                         AccSchedLineDescription := AccSchedLine.Description;
                         GetChartTypes;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;Action    ;
                      Name=ShowAll;
                      CaptionML=[ENU=Select All;
                                 ENG=Select All];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=AllLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AccSchedChartSetupLine@1000 : Record 763;
                                 AccSchedChartManagement@1001 : Codeunit 762;
                               BEGIN
                                 AccSchedChartSetupLine.COPY(Rec);
                                 AccSchedChartSetupLine.SETRANGE("Column Layout Line No.");
                                 AccSchedChartManagement.SelectAll(AccSchedChartSetupLine,TRUE);
                               END;
                                }
      { 20      ;1   ;Action    ;
                      Name=ShowNone;
                      CaptionML=[ENU=Deselect All;
                                 ENG=Deselect All];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CancelAllLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AccSchedChartSetupLine@1001 : Record 763;
                                 AccSchedChartManagement@1000 : Codeunit 762;
                               BEGIN
                                 AccSchedChartSetupLine.COPY(Rec);
                                 AccSchedChartSetupLine.SETRANGE("Column Layout Line No.");
                                 AccSchedChartManagement.DeselectAll(AccSchedChartSetupLine,TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 5   ;2   ;Field     ;
                Name=<Row No.>;
                CaptionML=[ENU=Row No.;
                           ENG=Row No.];
                SourceExpr=AccSchedLineRowNo;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=<Description>;
                CaptionML=[ENU=Description;
                           ENG=Description];
                SourceExpr=AccSchedLineDescription;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[1];
                CaptionClass='3,' + ColumnCaptions[1];
                OnValidate=BEGIN
                             SetChartType(1);
                           END;
                            }

    { 17  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[2];
                CaptionClass='3,' + ColumnCaptions[2];
                OnValidate=BEGIN
                             SetChartType(2);
                           END;
                            }

    { 16  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[3];
                CaptionClass='3,' + ColumnCaptions[3];
                OnValidate=BEGIN
                             SetChartType(3);
                           END;
                            }

    { 15  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[4];
                CaptionClass='3,' + ColumnCaptions[4];
                OnValidate=BEGIN
                             SetChartType(4);
                           END;
                            }

    { 14  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[5];
                CaptionClass='3,' + ColumnCaptions[5];
                OnValidate=BEGIN
                             SetChartType(5);
                           END;
                            }

    { 13  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[6];
                CaptionClass='3,' + ColumnCaptions[6];
                OnValidate=BEGIN
                             SetChartType(6);
                           END;
                            }

    { 12  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[7];
                CaptionClass='3,' + ColumnCaptions[7];
                OnValidate=BEGIN
                             SetChartType(7);
                           END;
                            }

    { 11  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[8];
                CaptionClass='3,' + ColumnCaptions[8];
                OnValidate=BEGIN
                             SetChartType(8);
                           END;
                            }

    { 10  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[9];
                CaptionClass='3,' + ColumnCaptions[9];
                OnValidate=BEGIN
                             SetChartType(9);
                           END;
                            }

    { 9   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[10];
                CaptionClass='3,' + ColumnCaptions[10];
                OnValidate=BEGIN
                             SetChartType(10);
                           END;
                            }

    { 4   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[11];
                CaptionClass='3,' + ColumnCaptions[11];
                OnValidate=BEGIN
                             SetChartType(11);
                           END;
                            }

    { 3   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=ChartType[12];
                CaptionClass='3,' + ColumnCaptions[12];
                OnValidate=BEGIN
                             SetChartType(12);
                           END;
                            }

  }
  CODE
  {
    VAR
      AccSchedLineRowNo@1004 : Code[10];
      AccSchedLineDescription@1005 : Text[80];
      ChartType@1000 : ARRAY [12] OF ' ,Line,StepLine,Column,StackedColumn';
      ColumnCaptions@1001 : ARRAY [12] OF Text[80];
      ColumnLineNos@1002 : ARRAY [12] OF Integer;
      MaxColumns@1003 : Integer;
      Text001@1006 : TextConst 'ENU=Invalid Column Layout.;ENG=Invalid Column Layout.';

    PROCEDURE SetFilters@1(AccountSchedulesChartSetup@1000 : Record 762);
    BEGIN
      RESET;

      AccountSchedulesChartSetup.SetLinkToLines(Rec);
      CASE AccountSchedulesChartSetup."Base X-Axis on" OF
        AccountSchedulesChartSetup."Base X-Axis on"::Period:
          IF FINDFIRST THEN
            SETRANGE("Column Layout Line No.","Column Layout Line No.");
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Line",
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Column":
          SETRANGE("Column Layout Line No.",0);
      END;
      UpdateColumnCaptions(AccountSchedulesChartSetup);
    END;

    LOCAL PROCEDURE UpdateColumnCaptions@19(AccountSchedulesChartSetup@1003 : Record 762);
    VAR
      ColumnLayout@1002 : Record 334;
      ColumnNo@1000 : Integer;
      i@1001 : Integer;
    BEGIN
      CLEAR(ColumnCaptions);
      AccountSchedulesChartSetup.FilterColumnLayout(ColumnLayout);
      IF ColumnLayout.FINDSET THEN
        REPEAT
          ColumnNo := ColumnNo + 1;
          IF ColumnNo <= ARRAYLEN(ColumnCaptions) THEN BEGIN
            ColumnCaptions[ColumnNo] := ColumnLayout."Column Header";
            ColumnLineNos[ColumnNo] := ColumnLayout."Line No.";
          END;
        UNTIL ColumnLayout.NEXT = 0;
      MaxColumns := ColumnNo;
      // Set unused columns to blank to prevent RTC to display control ID as caption
      FOR i := MaxColumns + 1 TO ARRAYLEN(ColumnCaptions) DO
        ColumnCaptions[i] := ' ';
    END;

    LOCAL PROCEDURE GetChartTypes@3();
    VAR
      AccountSchedulesChartSetup@1003 : Record 762;
      AccSchedChartSetupLine@1000 : Record 763;
      AccSchedChartSetupLine2@1002 : Record 763;
      i@1001 : Integer;
    BEGIN
      AccountSchedulesChartSetup.GET("User ID",Name);
      CASE AccountSchedulesChartSetup."Base X-Axis on" OF
        AccountSchedulesChartSetup."Base X-Axis on"::Period:
          FOR i := 1 TO MaxColumns DO BEGIN
            IF AccSchedChartSetupLine.GET("User ID",Name,"Account Schedule Line No.",ColumnLineNos[i]) THEN
              ChartType[i] := AccSchedChartSetupLine."Chart Type";
          END;
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Line":
          BEGIN
            AccSchedChartSetupLine.GET("User ID",Name,"Account Schedule Line No.",0);
            IF AccSchedChartSetupLine."Chart Type" <> AccSchedChartSetupLine."Chart Type"::" " THEN
              FOR i := 1 TO MaxColumns DO BEGIN
                IF AccSchedChartSetupLine2.GET("User ID",Name,0,ColumnLineNos[i]) THEN
                  ChartType[i] := AccSchedChartSetupLine2."Chart Type"
              END
            ELSE
              FOR i := 1 TO MaxColumns DO
                ChartType[i] := AccSchedChartSetupLine2."Chart Type"::" ";
          END;
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Column":
          BEGIN
            AccSchedChartSetupLine.GET("User ID",Name,"Account Schedule Line No.",0);
            FOR i := 1 TO MaxColumns DO BEGIN
              AccSchedChartSetupLine2.GET("User ID",Name,0,ColumnLineNos[i]);
              IF AccSchedChartSetupLine2."Chart Type" <> AccSchedChartSetupLine2."Chart Type"::" " THEN
                ChartType[i] := AccSchedChartSetupLine."Chart Type"
              ELSE
                ChartType[i] := AccSchedChartSetupLine."Chart Type"::" ";
            END;
          END;
      END;
      FOR i := MaxColumns + 1 TO ARRAYLEN(ColumnCaptions) DO
        ChartType[i] := AccSchedChartSetupLine."Chart Type"::" ";
    END;

    LOCAL PROCEDURE SetChartType@2(ColumnNo@1000 : Integer);
    VAR
      AccSchedChartSetupLine@1001 : Record 763;
      AccountSchedulesChartSetup@1002 : Record 762;
    BEGIN
      IF ColumnNo > MaxColumns THEN
        ERROR(Text001);

      AccountSchedulesChartSetup.GET("User ID",Name);
      CASE AccountSchedulesChartSetup."Base X-Axis on" OF
        AccountSchedulesChartSetup."Base X-Axis on"::Period:
          AccSchedChartSetupLine.GET("User ID",Name,"Account Schedule Line No.",ColumnLineNos[ColumnNo]);
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Line":
          AccSchedChartSetupLine.GET("User ID",Name,0,ColumnLineNos[ColumnNo]);
        AccountSchedulesChartSetup."Base X-Axis on"::"Acc. Sched. Column":
          AccSchedChartSetupLine.GET("User ID",Name,"Account Schedule Line No.",0);
      END;
      AccSchedChartSetupLine.VALIDATE("Chart Type",ChartType[ColumnNo]);
      AccSchedChartSetupLine.MODIFY;
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

