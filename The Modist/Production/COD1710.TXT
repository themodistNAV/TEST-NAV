OBJECT Codeunit 1710 Exp. User Feedback Pos. Pay
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=1220;
    Permissions=TableData 272=rimd,
                TableData 1231=rimd,
                TableData 1232=rimd;
    OnRun=VAR
            CheckLedgerEntry@1000 : Record 272;
            PositivePayEntry@1001 : Record 1231;
            BankAccNo@1003 : Code[20];
            LastUpdateDateTime@1004 : DateTime;
          BEGIN
            // Update the CheckLedgerEntry for the Data Exch. Entry No. as exported successfully
            CheckLedgerEntry.SETRANGE("Data Exch. Entry No.","Entry No.");
            CheckLedgerEntry.SETRANGE("Positive Pay Exported",FALSE);
            CheckLedgerEntry.SETFILTER(
              "Entry Status",'%1|%2|>%3',
              CheckLedgerEntry."Entry Status"::Printed,
              CheckLedgerEntry."Entry Status"::Posted,
              CheckLedgerEntry."Entry Status"::"Test Print");
            IF CheckLedgerEntry.FINDFIRST THEN
              BankAccNo := CheckLedgerEntry."Bank Account No.";
            CheckLedgerEntry.MODIFYALL("Positive Pay Exported",TRUE,TRUE);

            // Update the CheckLedgerEntry for the Data Exch. Voided Entry No. for Checks as exported successfully
            CheckLedgerEntry.SETRANGE("Data Exch. Entry No.");
            CheckLedgerEntry.SETRANGE("Data Exch. Voided Entry No.","Entry No.");
            CheckLedgerEntry.SETFILTER(
              "Entry Status",'%1|%2|%3',
              CheckLedgerEntry."Entry Status"::Voided,
              CheckLedgerEntry."Entry Status"::"Financially Voided",
              CheckLedgerEntry."Entry Status"::"Test Print");
            // Only populate the BankAcct if there were no open checks found in case there are no voids
            IF BankAccNo = '' THEN
              IF CheckLedgerEntry.FINDFIRST THEN
                BankAccNo := CheckLedgerEntry."Bank Account No.";
            CheckLedgerEntry.MODIFYALL("Positive Pay Exported",TRUE,TRUE);

            IF BankAccNo <> '' THEN
              LastUpdateDateTime := GetLastUploadDateTime(BankAccNo);

            // Initialize Pos. Pay Enties
            PositivePayEntry.INIT;
            PositivePayEntry.VALIDATE("Bank Account No.",BankAccNo);
            PositivePayEntry."Last Upload Date" := DT2DATE(LastUpdateDateTime);
            PositivePayEntry."Last Upload Time" := DT2TIME(LastUpdateDateTime);

            // Retrieve the range of exported data and move Pos. Pay Entry Detail tables
            CreatePosPayEntryDetail(PositivePayEntry,"Entry No.",BankAccNo);

            CALCFIELDS("File Content");
            PositivePayEntry."Exported File" := "File Content";
            PositivePayEntry.INSERT;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE GetLastUploadDateTime@4(BankAccNo@1000 : Code[20]) : DateTime;
    VAR
      PositivePayEntry@1001 : Record 1231;
    BEGIN
      // Retrieve the Last Updated Date and Time to set in the new record
      PositivePayEntry.SETRANGE("Bank Account No.",BankAccNo);
      IF PositivePayEntry.FINDLAST THEN
        EXIT(PositivePayEntry."Upload Date-Time");
    END;

    LOCAL PROCEDURE CreatePosPayEntryDetail@1(VAR PositivePayEntry@1009 : Record 1231;EntryNo@1001 : Integer;BankAccNo@1003 : Code[20]);
    VAR
      PositivePayEntryDetail@1002 : Record 1232;
      PositivePayDetail@1004 : Record 1241;
    BEGIN
      PositivePayDetail.SETRANGE("Data Exch. Entry No.",EntryNo);
      IF PositivePayDetail.FINDSET THEN BEGIN
        REPEAT
          PositivePayEntryDetail.INIT;
          PositivePayEntryDetail."Upload Date-Time" := PositivePayEntry."Upload Date-Time";
          PositivePayEntryDetail.CopyFromPosPayEntryDetail(PositivePayDetail,BankAccNo);
          IF PositivePayEntryDetail."Document Type" = PositivePayEntryDetail."Document Type"::CHECK THEN BEGIN
            PositivePayEntry."Number of Checks" += 1;
            PositivePayEntry."Check Amount" += PositivePayEntryDetail.Amount;
          END ELSE BEGIN
            PositivePayEntry."Number of Voids" += 1;
            PositivePayEntry."Void Amount" += PositivePayEntryDetail.Amount;
          END;
          PositivePayEntry."Number of Uploads" += 1;
          PositivePayEntryDetail.INSERT;

        UNTIL PositivePayDetail.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

