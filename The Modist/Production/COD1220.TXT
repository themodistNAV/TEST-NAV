OBJECT Codeunit 1220 SEPA CT-Export File
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 1221=rimd;
    OnRun=VAR
            BankAccount@1001 : Record 270;
            ExpUserFeedbackGenJnl@1000 : Codeunit 1278;
          BEGIN
            LOCKTABLE;
            BankAccount.GET("Bal. Account No.");
            IF Export(Rec,BankAccount.GetPaymentExportXMLPortID) THEN
              ExpUserFeedbackGenJnl.SetExportFlagOnGenJnlLine(Rec);
          END;

  }
  CODE
  {
    VAR
      ExportToServerFile@1000 : Boolean;

    PROCEDURE Export@1(VAR GenJnlLine@1005 : Record 81;XMLPortID@1004 : Integer) : Boolean;
    VAR
      CreditTransferRegister@1003 : Record 1205;
      TempBlob@1002 : Record 99008535;
      FileManagement@1001 : Codeunit 419;
      OutStr@1000 : OutStream;
      UseCommonDialog@1006 : Boolean;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(OutStr);
      XMLPORT.EXPORT(XMLPortID,OutStr,GenJnlLine);

      CreditTransferRegister.FINDLAST;
      UseCommonDialog := NOT ExportToServerFile;
      IF FileManagement.BLOBExport(TempBlob,STRSUBSTNO('%1.XML',CreditTransferRegister.Identifier),UseCommonDialog) <> '' THEN
        SetCreditTransferRegisterToFileCreated(CreditTransferRegister,TempBlob);

      EXIT(CreditTransferRegister.Status = CreditTransferRegister.Status::"File Created");
    END;

    LOCAL PROCEDURE SetCreditTransferRegisterToFileCreated@2(VAR CreditTransferRegister@1000 : Record 1205;VAR TempBlob@1001 : Record 99008535);
    BEGIN
      CreditTransferRegister.Status := CreditTransferRegister.Status::"File Created";
      CreditTransferRegister."Exported File" := TempBlob.Blob;
      CreditTransferRegister.MODIFY;
    END;

    PROCEDURE EnableExportToServerFile@4();
    BEGIN
      ExportToServerFile := TRUE;
    END;

    BEGIN
    END.
  }
}

