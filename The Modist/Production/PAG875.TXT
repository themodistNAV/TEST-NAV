OBJECT Page 875 Social Listening FactBox
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Social Media Insights FactBox;
               ENG=Social Media Insights FactBox];
    SourceTable=Table871;
    PageType=CardPart;
    OnAfterGetCurrRecord=BEGIN
                           IsDataReady := TRUE;
                           UpdateAddIn;
                         END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                Name=SocialListening;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.SocialListening;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      SocialListeningMgt@1005 : Codeunit 871;
      IsDataReady@1000 : Boolean;
      IsAddInReady@1003 : Boolean;

    LOCAL PROCEDURE UpdateAddIn@6();
    VAR
      SocialListeningSetup@1000 : Record 870;
    BEGIN
      IF "Search Topic" = '' THEN
        EXIT;
      IF NOT IsAddInReady THEN
        EXIT;

      IF NOT IsDataReady THEN
        EXIT;

      IF NOT SocialListeningSetup.GET OR
         (SocialListeningSetup."Solution ID" = '')
      THEN
        EXIT;

      CurrPage.SocialListening.DetermineUserAuthentication(SocialListeningMgt.MSLAuthenticationStatusURL);
    END;

    EVENT SocialListening@-2::AddInReady@3();
    BEGIN
      IsAddInReady := TRUE;
      UpdateAddIn;
    END;

    EVENT SocialListening@-2::DetermineUserAuthenticationResult@4(result@1000 : Integer);
    BEGIN
      CASE result OF
        -1: // Error
          CurrPage.SocialListening.ShowMessage(SocialListeningMgt.GetAuthenticationConectionErrorMsg);
        0: // User is not authenticated
          CurrPage.SocialListening.ShowMessage(SocialListeningMgt.GetAuthenticationUserErrorMsg);
        1: // User is authenticated
          CurrPage.SocialListening.ShowWidget(SocialListeningMgt.GetAuthenticationWidget("Search Topic"));
      END;
    END;

    EVENT SocialListening@-2::MessageLinkClick@5(identifier@1000 : Integer);
    BEGIN
      CASE identifier OF
        1: // Refresh
          UpdateAddIn;
      END;
    END;

    BEGIN
    END.
  }
}

