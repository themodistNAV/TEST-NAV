OBJECT Codeunit 5303 Outlook Synch. Deletion Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1004 : TextConst '@@@={Locked};ENU=APP;ENG=APP';
      Text002@1003 : TextConst '@@@={Locked};ENU=TASK;ENG=TASK';
      Text003@1002 : TextConst '@@@={Locked};ENU=SEGMENT;ENG=SEGMENT';
      OSynchOutlookMgt@1008 : Codeunit 5304;
      OSynchProcessLine@1009 : Codeunit 5305;
      Text100@1010 : TextConst 'ENU=A deleted Outlook item cannot be synchronized because a conflict has occurred when processing the %1 entity.;ENG=A deleted Outlook item cannot be synchronised because a conflict has occurred when processing the %1 entity.';
      Text101@1011 : TextConst 'ENU=An Outlook item from the %1 entity cannot be synchronized because the deleting of the %2 collection of this entity is not supported. Try again later and if the problem persists contact your system administrator.;ENG=An Outlook item from the %1 entity cannot be synchronised because the deleting of the %2 collection of this entity is not supported. Try again later and if the problem persists contact your system administrator.';

    PROCEDURE ProcessItem@8(OSynchUserSetup@1000 : Record 5305;EntityRecID@1001 : RecordID;VAR ErrorLogXMLWriter@1004 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter";StartSynchTime@1002 : DateTime);
    VAR
      EntityRecRef@1003 : RecordRef;
      Container@1005 : Text;
    BEGIN
      IF NOT EntityRecRef.GET(EntityRecID) THEN
        EXIT;

      IF NOT OSynchProcessLine.CheckEntityIdentity(EntityRecID,OSynchUserSetup."Synch. Entity Code") THEN BEGIN
        RemoveLink(OSynchUserSetup."User ID",EntityRecID);
        EXIT;
      END;

      IF ConflictDetected(EntityRecRef,OSynchUserSetup,StartSynchTime) THEN BEGIN
        Container := '';
        OSynchOutlookMgt.WriteErrorLog(
          OSynchUserSetup."User ID",
          EntityRecID,
          'Conflict',
          OSynchUserSetup."Synch. Entity Code",
          STRSUBSTNO(Text100,OSynchUserSetup."Synch. Entity Code"),
          ErrorLogXMLWriter,
          Container);

        ERROR('');
      END;

      CASE OSynchUserSetup."Synch. Entity Code" OF
        Text001:
          ProcessAppointment(EntityRecRef);
        Text002:
          ProcessTask(EntityRecRef);
      END;

      RemoveLink(OSynchUserSetup."User ID",EntityRecID);
    END;

    LOCAL PROCEDURE ProcessTask@1(EntityRecRef@1000 : RecordRef);
    VAR
      EntityFieldRef@1002 : FieldRef;
      FieldID@1003 : Integer;
    BEGIN
      FieldID := 17;

      EntityFieldRef := EntityRecRef.FIELD(FieldID);
      EntityFieldRef.VALIDATE(TRUE);
      EntityRecRef.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE ProcessAppointment@2(EntityRecRef@1001 : RecordRef);
    BEGIN
      ProcessTask(EntityRecRef);
    END;

    PROCEDURE ProcessCollectionElements@11(VAR CollectionElementsBuffer@1001 : Record 5302;OSynchEntityElement@1000 : Record 5301;EntityRecID@1004 : RecordID);
    VAR
      EntityRecRef@1005 : RecordRef;
      CollectionElementRecRef@1003 : RecordRef;
      RecID@1002 : RecordID;
    BEGIN
      IF NOT CollectionElementsBuffer.FIND('-') THEN
        EXIT;

      IF NOT EntityRecRef.GET(EntityRecID) THEN
        EXIT;

      IF NOT OSynchProcessLine.CheckEntityIdentity(EntityRecID,OSynchEntityElement."Synch. Entity Code") THEN
        EXIT;

      REPEAT
        EVALUATE(RecID,FORMAT(CollectionElementsBuffer."Record ID"));
        IF CollectionElementRecRef.GET(RecID) THEN
          CASE OSynchEntityElement."Synch. Entity Code" OF
            Text001:
              BEGIN
                IF OSynchEntityElement."Outlook Collection" = 'Links' THEN
                  ProcessAppointmentLinks(CollectionElementRecRef);

                IF OSynchEntityElement."Outlook Collection" = 'Recipients' THEN
                  ProcessAppointmentRecipients(CollectionElementRecRef);
              END;
            Text002:
              IF OSynchEntityElement."Outlook Collection" = 'Links' THEN
                ProcessTaskLinks(CollectionElementRecRef);
            Text003:
              IF OSynchEntityElement."Outlook Collection" = 'Members' THEN
                ProcessDistListMembers(CollectionElementRecRef);
            ELSE
              ERROR(Text101,OSynchEntityElement."Synch. Entity Code",OSynchEntityElement."Outlook Collection");
          END;
      UNTIL CollectionElementsBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessTaskLinks@7(VAR CollectionElementRecRef@1000 : RecordRef);
    VAR
      CollectionElementFieldRef@1002 : FieldRef;
      CodeVar@1003 : Code[20];
      FieldID@1001 : Integer;
    BEGIN
      CodeVar := '';
      FieldID := 5;

      CollectionElementFieldRef := CollectionElementRecRef.FIELD(FieldID);
      CollectionElementFieldRef.VALIDATE(CodeVar);
      CollectionElementRecRef.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE ProcessAppointmentRecipients@10(VAR CollectionElementRecRef@1000 : RecordRef);
    BEGIN
      CollectionElementRecRef.DELETE(TRUE);
    END;

    LOCAL PROCEDURE ProcessAppointmentLinks@9(VAR CollectionElementRecRef@1000 : RecordRef);
    BEGIN
      ProcessAppointmentRecipients(CollectionElementRecRef);
    END;

    LOCAL PROCEDURE ProcessDistListMembers@3(VAR CollectionElementRecRef@1000 : RecordRef);
    BEGIN
      CollectionElementRecRef.DELETE(TRUE);
    END;

    LOCAL PROCEDURE RemoveLink@6(UserID@1001 : Code[50];RecID@1002 : RecordID);
    VAR
      OSynchLink@1000 : Record 5302;
    BEGIN
      IF OSynchLink.GET(UserID,RecID) THEN BEGIN
        OSynchLink."Outlook Entry ID Hash" := '';
        // Item has been deleted so we update the last sync time.
        OSynchLink."Synchronization Date" := CURRENTDATETIME;
        OSynchLink.MODIFY;
      END;
    END;

    LOCAL PROCEDURE ConflictDetected@21(SynchRecRef@1009 : RecordRef;OSynchUserSetup@1006 : Record 5305;StartSynchTime@1002 : DateTime) IsConflict : Boolean;
    VAR
      ChangeLogEntry@1000 : Record 405;
      OSynchField@1001 : Record 5304;
      OSynchLink@1005 : Record 5302;
      RecID@1004 : RecordID;
    BEGIN
      EVALUATE(RecID,FORMAT(SynchRecRef.RECORDID));

      IF NOT OSynchLink.GET(OSynchUserSetup."User ID",RecID) THEN
        EXIT;

      ChangeLogEntry.SETCURRENTKEY("Table No.","Primary Key Field 1 Value");
      OSynchProcessLine.FilterChangeLog(RecID,ChangeLogEntry);
      ChangeLogEntry.SETFILTER("Type of Change",'<>%1',ChangeLogEntry."Type of Change"::Deletion);

      IF OSynchLink."Synchronization Date" >= OSynchUserSetup."Last Synch. Time" THEN BEGIN
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',OSynchUserSetup."Last Synch. Time");
        IF NOT ChangeLogEntry.FINDLAST THEN
          EXIT;

        IF ChangeLogEntry."Date and Time" <= OSynchLink."Synchronization Date" THEN
          EXIT;

        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',OSynchLink."Synchronization Date");
      END ELSE
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1&<=%2',OSynchUserSetup."Last Synch. Time",StartSynchTime);

      IF NOT ChangeLogEntry.FINDFIRST THEN
        EXIT;

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchUserSetup."Synch. Entity Code");
      OSynchField.SETRANGE("Element No.",0);
      OSynchField.SETFILTER("Read-Only Status",'<>%1',OSynchField."Read-Only Status"::"Read-Only in Microsoft Dynamics NAV");
      IsConflict := OSynchProcessLine.SetupFieldsModified(ChangeLogEntry,OSynchField);

      IF IsConflict THEN
        OSynchProcessLine.CheckUserSettingsForConflicts(SynchRecRef,RecID.TABLENO);
    END;

    BEGIN
    END.
  }
}

