OBJECT Table 5717 Item Cross Reference
{
  OBJECT-PROPERTIES
  {
    Date=02/09/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Cross-Reference Type No." <> '') AND
                  ("Cross-Reference Type" = "Cross-Reference Type"::" ")
               THEN
                 ERROR(Text000,FIELDCAPTION("Cross-Reference Type No."));

               Item.GET("Item No.");
               IF "Unit of Measure" = '' THEN
                 VALIDATE("Unit of Measure",Item."Base Unit of Measure");
               CreateItemVendor;
             END;

    OnDelete=BEGIN
               IF "Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                 DeleteItemVendor(Rec)
             END;

    OnRename=BEGIN
               IF ("Cross-Reference Type No." <> '') AND
                  ("Cross-Reference Type" = "Cross-Reference Type"::" ")
               THEN
                 ERROR(Text000,FIELDCAPTION("Cross-Reference Type No."));

               IF ("Cross-Reference Type" = "Cross-Reference Type"::Vendor) AND NOT ItemVendorResetRequired(xRec,Rec) THEN
                 UpdateItemVendorNo(xRec,"Cross-Reference No.")
               ELSE BEGIN
                 IF xRec."Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                   DeleteItemVendor(xRec);
                 IF "Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                   CreateItemVendor;
               END;
             END;

    CaptionML=[ENU=Item Cross Reference;
               ENG=Item Cross Reference];
    LookupPageID=Page5724;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.] }
    { 2   ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ENG=Variant Code] }
    { 3   ;   ;Unit of Measure     ;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              ENG=Unit of Measure] }
    { 4   ;   ;Cross-Reference Type;Option        ;OnValidate=BEGIN
                                                                IF ("Cross-Reference Type" <> xRec."Cross-Reference Type") AND
                                                                   (xRec."Cross-Reference Type" <> xRec."Cross-Reference Type"::" ") OR
                                                                   ("Cross-Reference Type" = "Cross-Reference Type"::"Bar Code")
                                                                THEN
                                                                  "Cross-Reference Type No." := '';
                                                              END;

                                                   CaptionML=[ENU=Cross-Reference Type;
                                                              ENG=Cross-Reference Type];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Bar Code";
                                                                    ENG=" ,Customer,Vendor,Bar Code"];
                                                   OptionString=[ ,Customer,Vendor,Bar Code] }
    { 5   ;   ;Cross-Reference Type No.;Code30    ;TableRelation=IF (Cross-Reference Type=CONST(Customer)) Customer.No.
                                                                 ELSE IF (Cross-Reference Type=CONST(Vendor)) Vendor.No.;
                                                   CaptionML=[ENU=Cross-Reference Type No.;
                                                              ENG=Cross-Reference Type No.] }
    { 6   ;   ;Cross-Reference No. ;Code20        ;CaptionML=[ENU=Cross-Reference No.;
                                                              ENG=Cross-Reference No.];
                                                   NotBlank=Yes }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 8   ;   ;Discontinue Bar Code;Boolean       ;OnValidate=BEGIN
                                                                IF "Discontinue Bar Code" AND
                                                                   ("Cross-Reference Type" <> "Cross-Reference Type"::"Bar Code")
                                                                THEN
                                                                  ERROR(Text001,TABLECAPTION);
                                                              END;

                                                   CaptionML=[ENU=Discontinue Bar Code;
                                                              ENG=Discontinue Bar Code] }
  }
  KEYS
  {
    {    ;Item No.,Variant Code,Unit of Measure,Cross-Reference Type,Cross-Reference Type No.,Cross-Reference No.;
                                                   Clustered=Yes }
    {    ;Cross-Reference No.                      }
    {    ;Cross-Reference No.,Cross-Reference Type,Cross-Reference Type No.,Discontinue Bar Code }
    {    ;Cross-Reference Type,Cross-Reference No. }
    {    ;Item No.,Variant Code,Unit of Measure,Cross-Reference Type,Cross-Reference No.,Discontinue Bar Code }
    {    ;Cross-Reference Type,Cross-Reference Type No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot enter a %1 for a blank Cross-Reference Type.;ENG=You cannot enter a %1 for a blank Cross-Reference Type.';
      Text001@1001 : TextConst 'ENU=This %1 is not a bar code.;ENG=This %1 is not a bar code.';
      Item@1002 : Record 27;
      ItemVend@1003 : Record 99;
      "--- NS.M ---"@1000000001 : Integer;
      SystemActivity@1000000000 : Boolean;

    LOCAL PROCEDURE CreateItemVendor@1();
    BEGIN
      IF ("Cross-Reference Type" = "Cross-Reference Type"::Vendor) AND
         ItemVend.WRITEPERMISSION
      THEN BEGIN
        ItemVend.RESET;
        ItemVend.SETRANGE("Item No.","Item No.");
        ItemVend.SETRANGE("Vendor No.","Cross-Reference Type No.");
        ItemVend.SETRANGE("Variant Code","Variant Code");
        IF ItemVend.ISEMPTY THEN BEGIN
          ItemVend."Item No." := "Item No.";
          ItemVend."Vendor No." := "Cross-Reference Type No.";
          ItemVend.VALIDATE("Vendor No.");
          ItemVend."Variant Code" := "Variant Code";
          ItemVend."Vendor Item No." := "Cross-Reference No.";
          ItemVend.INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteItemVendor@2(ItemCrossReference@1000 : Record 5717);
    BEGIN
      IF NOT MultipleCrossReferencesExist(ItemCrossReference) THEN
        IF ItemVend.GET(ItemCrossReference."Cross-Reference Type No.",ItemCrossReference."Item No.",ItemCrossReference."Variant Code") THEN
          IF UPPERCASE(DELCHR(ItemVend."Vendor Item No.",'<',' ')) = ItemCrossReference."Cross-Reference No." THEN
            ItemVend.DELETE;
    END;

    LOCAL PROCEDURE UpdateItemVendorNo@3(ItemCrossReference@1000 : Record 5717;NewCrossRefNo@1002 : Code[20]);
    BEGIN
      IF NOT MultipleCrossReferencesExist(ItemCrossReference) THEN
        IF ItemVend.GET(ItemCrossReference."Cross-Reference Type No.",ItemCrossReference."Item No.",ItemCrossReference."Variant Code") THEN BEGIN
          ItemVend.VALIDATE("Vendor Item No.",NewCrossRefNo);
          ItemVend.MODIFY;
        END;
    END;

    LOCAL PROCEDURE ItemVendorResetRequired@5(OldItemCrossRef@1000 : Record 5717;NewItemCrossRef@1001 : Record 5717) : Boolean;
    BEGIN
      EXIT(
        (OldItemCrossRef."Item No." <> NewItemCrossRef."Item No.") OR
        (OldItemCrossRef."Variant Code" <> NewItemCrossRef."Variant Code") OR
        (OldItemCrossRef."Cross-Reference Type" <> NewItemCrossRef."Cross-Reference Type") OR
        (OldItemCrossRef."Cross-Reference Type No." <> NewItemCrossRef."Cross-Reference Type No."));
    END;

    LOCAL PROCEDURE MultipleCrossReferencesExist@8(ItemCrossReference@1000 : Record 5717) : Boolean;
    VAR
      ItemCrossReference2@1001 : Record 5717;
    BEGIN
      ItemCrossReference2.SETRANGE("Item No.",ItemCrossReference."Item No.");
      ItemCrossReference2.SETRANGE("Variant Code",ItemCrossReference."Variant Code");
      ItemCrossReference2.SETRANGE("Cross-Reference Type",ItemCrossReference."Cross-Reference Type");
      ItemCrossReference2.SETRANGE("Cross-Reference Type No.",ItemCrossReference."Cross-Reference Type No.");
      ItemCrossReference2.SETRANGE("Cross-Reference No.",ItemCrossReference."Cross-Reference No.");
      ItemCrossReference2.SETFILTER("Unit of Measure",'<>%1',ItemCrossReference."Unit of Measure");

      EXIT(NOT ItemCrossReference2.ISEMPTY);
    END;

    PROCEDURE "--- NS.F ---"@1000000000();
    BEGIN
    END;

    PROCEDURE SetSystemActivity@1000000004(pSystemActivity@1000000000 : Boolean);
    BEGIN
      //DOC NS2015.1 - Setting "System Activity" flag
      //  -> pSystemActivity: The flag value

      SystemActivity := pSystemActivity;
    END;

    BEGIN
    {
      //DOC NS2015.1 JH 13/02/2014 - Connection with item "Bar Code" field
    }
    END.
  }
}

