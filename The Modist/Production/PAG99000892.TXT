OBJECT Page 99000892 Work Center Group Load Lines
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVGB9.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Lines;
               ENG=Lines];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
                 MfgSetup.GET;
                 MfgSetup.TESTFIELD("Show Capacity In");
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,WorkCtrGroupPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,WorkCtrGroupPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       CalculateCapacity(WorkCenterGroup."Capacity (Effective)",WorkCenterGroup."Prod. Order Need (Qty.)");
                       CapacityAvailable := WorkCenterGroup."Capacity (Effective)" - WorkCenterGroup."Prod. Order Need (Qty.)";
                       IF WorkCenterGroup."Capacity (Effective)" <> 0 THEN
                         CapacityEfficiency := ROUND(WorkCenterGroup."Prod. Order Need (Qty.)" / WorkCenterGroup."Capacity (Effective)" * 100,0.1)
                       ELSE
                         CapacityEfficiency := 0;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ENG=Period Start];
                SourceExpr="Period Start" }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ENG=Period Name];
                SourceExpr="Period Name" }

    { 4   ;2   ;Field     ;
                Name=Capacity;
                CaptionML=[ENU=Capacity;
                           ENG=Capacity];
                DecimalPlaces=0:5;
                SourceExpr=WorkCenterGroup."Capacity (Effective)";
                OnDrillDown=VAR
                              CalendarEntry@1001 : Record 99000757;
                            BEGIN
                              CalendarEntry.SETCURRENTKEY("Work Center Group Code");
                              CalendarEntry.SETRANGE("Work Center Group Code",WorkCenterGroup.Code);
                              CalendarEntry.SETFILTER(Date,WorkCenterGroup.GETFILTER("Date Filter"));
                              PAGE.RUN(0,CalendarEntry);
                            END;
                             }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Allocated Qty.;
                           ENG=Allocated Qty.];
                DecimalPlaces=0:5;
                SourceExpr=WorkCenterGroup."Prod. Order Need (Qty.)";
                OnDrillDown=VAR
                              ProdOrderCapNeed@1001 : Record 5410;
                            BEGIN
                              ProdOrderCapNeed.SETCURRENTKEY("Work Center Group Code",Date);
                              ProdOrderCapNeed.SETRANGE("Requested Only",FALSE);
                              ProdOrderCapNeed.SETRANGE("Work Center Group Code",WorkCenterGroup.Code);
                              ProdOrderCapNeed.SETFILTER(Date,WorkCenterGroup.GETFILTER("Date Filter"));
                              PAGE.RUN(0,ProdOrderCapNeed);
                            END;
                             }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Availability After Orders;
                           ENG=Availability After Orders];
                DecimalPlaces=0:5;
                SourceExpr=CapacityAvailable }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Load;
                           ENG=Load];
                DecimalPlaces=0:5;
                SourceExpr=CapacityEfficiency }

  }
  CODE
  {
    VAR
      WorkCenterGroup@1000 : Record 99000756;
      MfgSetup@1007 : Record 99000765;
      PeriodFormMgt@1001 : Codeunit 359;
      CapacityAvailable@1002 : Decimal;
      CapacityEfficiency@1003 : Decimal;
      WorkCtrGroupPeriodLength@1004 : 'Day,Week,Month,Quarter,Year';
      AmountType@1005 : 'Net Change,Balance at Date';
      CapacityUoM@1006 : Code[10];

    PROCEDURE Set@1(VAR NewWorkCenterGroup@1000 : Record 99000756;NewWorkCtrGrPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date';NewCapUoM@1003 : Code[10];VAR CalendarSource@1040000 : ' ,System,AccPeriod');
    BEGIN
      WorkCenterGroup.COPY(NewWorkCenterGroup);
      WorkCtrGroupPeriodLength := NewWorkCtrGrPeriodLength;
      AmountType := NewAmountType;
      CapacityUoM := NewCapUoM;
      PeriodFormMgt.SetCalendarSource(CalendarSource);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        WorkCenterGroup.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        WorkCenterGroup.SETRANGE("Date Filter",0D,"Period End");
    END;

    LOCAL PROCEDURE CalculateCapacity@2(VAR CapacityEffective@1000 : Decimal;VAR ProdOrderNeed@1001 : Decimal);
    VAR
      WorkCenter@1003 : Record 99000754;
      CalendarMgt@1004 : Codeunit 99000755;
      Capacity@1005 : Decimal;
      PONeed@1006 : Decimal;
    BEGIN
      IF CapacityUoM = '' THEN
        CapacityUoM := MfgSetup."Show Capacity In";
      WorkCenter.SETCURRENTKEY("Work Center Group Code");
      WorkCenter.SETRANGE("Work Center Group Code",WorkCenterGroup.Code);
      IF WorkCenter.FINDSET THEN
        REPEAT
          WorkCenterGroup.COPYFILTER("Date Filter",WorkCenter."Date Filter");
          WorkCenter.CALCFIELDS("Capacity (Effective)","Prod. Order Need (Qty.)");
          Capacity :=
            Capacity +
            WorkCenter."Capacity (Effective)" *
            CalendarMgt.TimeFactor(WorkCenter."Unit of Measure Code") /
            CalendarMgt.TimeFactor(CapacityUoM);

          PONeed :=
            PONeed +
            WorkCenter."Prod. Order Need (Qty.)" *
            CalendarMgt.TimeFactor(WorkCenter."Unit of Measure Code") /
            CalendarMgt.TimeFactor(CapacityUoM);
        UNTIL WorkCenter.NEXT = 0;

      CapacityEffective := Capacity;
      ProdOrderNeed := PONeed;
    END;

    BEGIN
    END.
  }
}

