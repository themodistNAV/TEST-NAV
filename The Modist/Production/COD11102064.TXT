OBJECT Codeunit 11102064 OM - Check Settings
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Setup@1000 : Record 11102035;
            CheckSettings@1001 : Codeunit 11102064;
            ErrorMessage@1002 : ARRAY [10] OF Text;
            Result@1003 : Text;
            i@1004 : Integer;
            ctDatabaseSettings@1005 : TextConst 'DEU=Database Settings;ENU=Database Settings;ESP=Database Settings;FRA=Database Settings;NLD=Database-instellingen;DEA=Database Settings';
            ctDotNet@1006 : TextConst 'DEU=DotNet;ENU=DotNet;ESP=DotNet;FRA=DotNet;NLD=DotNet;DEA=DotNet';
            ctFileSettings@1007 : TextConst 'DEU=File Settings;ENU=File Settings;ESP=File Settings;FRA=File Settings;NLD=Bestandsinstellingen;DEA=File Settings';
            ctGeneralSettings@1008 : TextConst 'DEU=General Settings;ENU=General Settings;ESP=General Settings;FRA=General Settings;NLD=Algemene instellingen;DEA=General Settings';
            ctObjectManagementSettings@1009 : TextConst 'DEU=Object Management Settings;ENU=Object Management Settings;ESP=Object Management Settings;FRA=Object Management Settings;NLD=Objectmanagementinstellingen;DEA=Object Management Settings';
            ctTriggers@1010 : TextConst 'DEU=Triggers;ENU=Triggers;ESP=Triggers;FRA=Triggers;NLD=Triggers;DEA=Triggers';
          BEGIN
            CASE RunType OF

              RunType::" " :
                BEGIN
                  FOR i := 1 TO 6 DO BEGIN

                    CLEARLASTERROR;
                    CLEAR(CheckSettings);
                    CheckSettings.SetRunType(i, ErrorOccured);
                    IF CheckSettings.RUN THEN
                      IF CheckSettings.GetSkippedText = '' THEN
                        Result := Result + CheckSettings.GetCurrentAction + ': ' + 'OK\'
                      ELSE BEGIN
                        Result := Result + CheckSettings.GetCurrentAction + ': ' + 'Skipped\\';
                        Result := Result + '     Cause: ' + CheckSettings.GetSkippedText + '\\';
                      END
                    ELSE BEGIN
                      ErrorMessage[i] := GETLASTERRORTEXT;
                      Result := Result + CheckSettings.GetCurrentAction + ': ' + 'Failed\\';
                      Result := Result + '     Error: %' + FORMAT(i) + '\';
                      Result := Result + '     Version: ' + Setup.OMAVersionText + '\';
                      Result := Result + '     Call Stack: \\' + GETLASTERRORCALLSTACK + '\';
                      ErrorOccured := TRUE;
                    END;
                  END;
                  MESSAGE(Result, ErrorMessage[1], ErrorMessage[2], ErrorMessage[3], ErrorMessage[4], ErrorMessage[5]);
                END;

              RunType::General :
                BEGIN
                  CurrentAction := ctGeneralSettings;
                  TestGeneralSettings;
                END;

              RunType::File :
                BEGIN
                  CurrentAction := ctFileSettings;
                  TestFileSettings;
                END;

              RunType::Database :
                BEGIN
                  CurrentAction := ctDatabaseSettings;
                  TestDatabaseSettings;
                END;

              RunType::Objects :
                BEGIN
                  CurrentAction := ctObjectManagementSettings;
                  TestObjectManagementSettings;
                END;

              RunType::Triggers :
                BEGIN
                  CurrentAction := ctTriggers;
                  TestTriggers;
                END;

              RunType::DotNet :
                BEGIN
                  CurrentAction := ctDotNet;
                  TestDotNet;
                END;

            END;
          END;

  }
  CODE
  {
    VAR
      CurrentAction@1000 : Text;
      SkippedText@1001 : Text;
      RunType@1002 : ' ,General,File,Database,Objects,Triggers,DotNet';
      ErrorOccured@1003 : Boolean;

    PROCEDURE SetRunType@8(NewRunType@1000 : ' ,File,Database,Objects,Triggers';NewErrorOccured@1001 : Boolean);
    BEGIN
      RunType := NewRunType;
      ErrorOccured := NewErrorOccured;
    END;

    PROCEDURE GetCurrentAction@9() : Text;
    BEGIN
      EXIT(CurrentAction);
    END;

    PROCEDURE GetSkippedText@14() : Text;
    BEGIN
      EXIT(SkippedText);
    END;

    LOCAL PROCEDURE TestGeneralSettings@11();
    VAR
      Setup@1000 : Record 11102035;
    BEGIN
      Setup.CustomGet;

      Setup.TESTFIELD("Compiler (Finsql on Server)");

      TestExecutableExist(Setup.FIELDCAPTION("Compare Tool"), Setup."Compare Tool", TRUE);
      TestExecutableExist(Setup.FIELDCAPTION("C/AL Editor"), Setup."C/AL Editor", TRUE);
      TestExecutableExist(Setup.FIELDCAPTION("Designer (Finsql on Client)"), Setup."Designer (Finsql on Client)", TRUE);
      TestExecutableExist(Setup.FIELDCAPTION("Compiler (Finsql on Server)"), Setup."Compiler (Finsql on Server)", FALSE);
    END;

    LOCAL PROCEDURE TestExecutableExist@7(FieldName2@1000 : Text;FileName@1001 : Text;OnClient@1002 : Boolean);
    VAR
      FileExists@1003 : Boolean;
      ctNotFound@1004 : TextConst 'DEU=%1 not found: %2;ENU=%1 not found: %2;ESP=%1 not found: %2;FRA=%1 not found: %2;NLD=%1 niet gevonden: %2;DEA=%1 not found: %2';
      IOFileClient@1005 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File" RUNONCLIENT;
      IOFileServer@1006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
    BEGIN
      IF FileName = '' THEN
        EXIT;

      FileName := DeleteAfter(FileName, '.exe');
      FileName := DeleteAfter(FileName, '.com');
      FileName := DeleteAfter(FileName, '.bat');

      IF OnClient THEN
        FileExists := IOFileClient.Exists(FileName)
      ELSE
        FileExists := IOFileServer.Exists(FileName);

      IF NOT FileExists THEN
        ERROR(ctNotFound, FieldName2, FileName);
    END;

    LOCAL PROCEDURE DeleteAfter@10(FileName@1000 : Text;String1@1001 : Text) ExitValue : Text;
    VAR
      Pos@1002 : Integer;
    BEGIN
      ExitValue := FileName;
      Pos := STRPOS(LOWERCASE(ExitValue), LOWERCASE(String1));
      IF Pos > 0 THEN
        ExitValue := COPYSTR(ExitValue, 1, Pos + STRLEN(String1) - 1);
    END;

    LOCAL PROCEDURE TestFileSettings@1();
    BEGIN
      TestSharedPath;
      TestWorkingDirectory;
    END;

    LOCAL PROCEDURE TestSharedPath@6();
    VAR
      Setup@1000 : Record 11102035;
      ImExportMgt@1001 : Codeunit 11102043;
      FileHandler@1002 : Codeunit 11102097;
      oFile@1003 : File;
      ServerFileName@1004 : Text;
      TestString@1005 : Text[1024];
      WorkingDirectory@1006 : Text;
      ctCheckSettings@1007 : TextConst 'DEU=CheckSettings%1%2.txt;ENU=CheckSettings%1%2.txt;ESP=CheckSettings%1%2.txt;FRA=CheckSettings%1%2.txt;NLD=CheckSettings%1%2.txt;DEA=CheckSettings%1%2.txt';
      ctServerFileNotSeenOnClient@1008 : TextConst 'DEU=A file created on the service tier is not seen on the client. Please disable "Shared Path".;ENU=A file created on the service tier is not seen on the client. Please disable "Shared Path".;ESP=A file created on the service tier is not seen on the client. Please disable "Shared Path".;FRA=A file created on the service tier is not seen on the client. Please disable "Shared Path".;NLD=Een bestand dat is aangemaakt op de service tier wordt niet gezien op de client. U kunt "Gedeeld pad" niet gebruiken.;DEA=A file created on the service tier is not seen on the client. Please disable "Shared Path".';
    BEGIN
      Setup.CustomGet;

      IF NOT Setup."Shared Path" THEN
        EXIT;

      WorkingDirectory := FileHandler.WorkingDirectory;
      ServerFileName := WorkingDirectory + ImExportMgt.RemoveSpecialCharacters(STRSUBSTNO(ctCheckSettings, TODAY, TIME));

      TestString := STRSUBSTNO('%1 %2', TODAY, TIME);

      IF EXISTS(ServerFileName) THEN
        ERASE(ServerFileName);

      oFile.WRITEMODE := TRUE;
      oFile.TEXTMODE := TRUE;
      oFile.CREATE(ServerFileName);
      oFile.WRITE(TestString);
      oFile.CLOSE;

      IF NOT FileHandler.ClientFileNameExists(ServerFileName) THEN
        ERROR(ctServerFileNotSeenOnClient);

      ERASE(ServerFileName);
    END;

    LOCAL PROCEDURE TestWorkingDirectory@4();
    VAR
      Setup@1000 : Record 11102035;
      FileHandler@1001 : Codeunit 11102097;
      oFile@1002 : File;
      ClientFileName@1003 : Text;
      ServerFileName@1004 : Text;
      TempDirectory@1005 : Text;
      TestString@1006 : Text[1024];
      WorkingDirectory@1007 : Text;
      ctCheckSettings@1008 : TextConst 'DEU=CheckSettings.txt;ENU=CheckSettings.txt;ESP=CheckSettings.txt;FRA=CheckSettings.txt;NLD=CheckSettings.txt;DEA=CheckSettings.txt';
      ctClientFileNoFound@1009 : TextConst 'DEU=Client file not found.;ENU=Client file not found.;ESP=Client file not found.;FRA=Client file not found.;NLD=Clientbestand niet gevonden.;DEA=Client file not found.';
      IOFileClient@1010 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File" RUNONCLIENT;
    BEGIN
      Setup.CustomGet;

      TempDirectory := FileHandler.TempDirectory;
      ServerFileName := TempDirectory + ctCheckSettings;

      WorkingDirectory := FileHandler.WorkingDirectory;
      ClientFileName := WorkingDirectory + ctCheckSettings;

      TestString := STRSUBSTNO('%1 %2', TODAY, TIME);

      IF EXISTS(ServerFileName) THEN
        ERASE(ServerFileName);

      IF IOFileClient.Exists(ClientFileName) THEN
        IOFileClient.Delete(ClientFileName);

      oFile.WRITEMODE := TRUE;
      oFile.TEXTMODE := TRUE;
      oFile.CREATE(ServerFileName);
      oFile.WRITE(TestString);
      oFile.CLOSE;

      FileHandler.DownloadToClient(ServerFileName, ClientFileName, ClientFileName);

      ERASE(ServerFileName);

      IF ServerFileName <> ClientFileName THEN BEGIN
        IF NOT IOFileClient.Exists(ClientFileName) THEN
          ERROR(ctClientFileNoFound);

        IOFileClient.Delete(ClientFileName);
      END;
    END;

    LOCAL PROCEDURE TestDatabaseSettings@3();
    VAR
      Setup@1000 : Record 11102035;
      Object@1001 : Record 2000000001;
      ObjectMgt@1002 : Codeunit 11102046;
      SqlMgt@1003 : Codeunit 11102075;
      ApplicationSettings@1004 : Codeunit 11102101;
      SqlString@1005 : Text;
      LoopCounter@1006 : Integer;
      ObjectUpdated@1007 : Boolean;
      ctCheckSettings@1008 : TextConst 'DEU=CheckSettings;ENU=CheckSettings;ESP=CheckSettings;FRA=CheckSettings;NLD=CheckSettings;DEA=CheckSettings';
      ctUpdatingObjectFailed@1009 : TextConst 'DEU=A modification made to an object on SQL level is not seen on the service tier. Please check your database settings. Server Name: %1, Database Name: %2;ENU=A modification made to an object on SQL level is not seen on the service tier. Please check your database settings. Server Name: %1, Database Name: %2;ESP=A modification made to an object on SQL level is not seen on the service tier. Please check your database settings. Server Name: %1, Database Name: %2;FRA=A modification made to an object on SQL level is not seen on the service tier. Please check your database settings. Server Name: %1, Database Name: %2;NLD=Een wijziging aan een object op SQL niveau is niet gezien op de service tier. Controleer uw databaseinstellingen servernaam: %1, databasenaam: %2;DEA=A modification made to an object on SQL level is not seen on the service tier. Please check your database settings. Server Name: %1, Database Name: %2';
    BEGIN
      Setup.CustomGet;
      IF Setup."Database Locked" THEN
        Setup.FIELDERROR("Database Locked");

      SqlString := 'UPDATE Object SET Locked = 1, [Locked By] = ''' + ctCheckSettings + ''' WHERE Type = 5 AND ID = 11102035';

      COMMIT;
      ObjectMgt.AddExpectedModification(Object.Type::Codeunit, CODEUNIT::"OM - Management");
      COMMIT;
      SqlMgt.ExecuteNonQuery(SqlString);
      COMMIT;
      ObjectMgt.DeleteExpectedModifications;

      REPEAT
        SLEEP(50);

        SELECTLATESTVERSION;
        Object.GET(Object.Type::Codeunit, '', CODEUNIT::"OM - Management");
        ObjectUpdated := Object."Locked By" = ctCheckSettings;

        LoopCounter += 1;

      UNTIL ObjectUpdated OR (LoopCounter = 100);

      SqlString := 'UPDATE Object SET Locked = 0, [Locked By] = '''' WHERE Type = 5 AND ID = 11102035';

      COMMIT;
      ObjectMgt.AddExpectedModification(Object.Type::Codeunit, CODEUNIT::"OM - Management");
      COMMIT;
      SqlMgt.ExecuteNonQuery(SqlString);
      COMMIT;
      ObjectMgt.DeleteExpectedModifications;

      IF NOT ObjectUpdated THEN
        ERROR(ctUpdatingObjectFailed, ApplicationSettings.ServerInstanceName, ApplicationSettings.DatabaseName);
    END;

    LOCAL PROCEDURE TestObjectManagementSettings@2();
    VAR
      Object@1000 : Record 2000000001;
      TmpObject@1001 : TEMPORARY Record 2000000001;
      ObjectMgt@1002 : Codeunit 11102046;
      FileHandler@1003 : Codeunit 11102097;
      oFile@1004 : File;
      ExportFileName@1005 : Text;
      ImportFileName@1006 : Text;
      TableNo@1007 : Integer;
      ctDateFormat@1008 : TextConst 'DEU=The date format that the service tier uses is different then the date format of the compiler. %1 <> %2;ENU=The date format that the service tier uses is different then the date format of the compiler. %1 <> %2;ESP=The date format that the service tier uses is different then the date format of the compiler. %1 <> %2;FRA=The date format that the service tier uses is different then the date format of the compiler. %1 <> %2;NLD=Het datumformaat dat de service tier gebruikt is anders dan het datumformaar dat de compiler gebruikt.\%1 <> %2>;DEA=The date format that the service tier uses is different then the date format of the compiler. %1 <> %2';
      ctErrorOccured@1009 : TextConst 'DEU=There was an error in a preceding check.;ENU=There was an error in a preceding check.;ESP=There was an error in a preceding check.;FRA=There was an error in a preceding check.;NLD=Er heeft een fout plaatsgevonden in een voorgaande controle.;DEA=There was an error in a preceding check.';
      ctExportFileName@1010 : TextConst 'DEU=ExportObject.txt;ENU=ExportObject.txt;ESP=ExportObject.txt;FRA=ExportObject.txt;NLD=ExportObject.txt;DEA=ExportObject.txt';
      ctExportFileNotFound@1011 : TextConst 'DEU=The exported object is not found.;ENU=The exported object is not found.;ESP=The exported object is not found.;FRA=The exported object is not found.;NLD=Het ge‰xporteerde object is niet gevonden.;DEA=The exported object is not found.';
      ctImportFileName@1012 : TextConst 'DEU=ImportObject.txt;ENU=ImportObject.txt;ESP=ImportObject.txt;FRA=ImportObject.txt;NLD=ImportObject.txt;DEA=ImportObject.txt';
      ctTestSkipped@1013 : TextConst 'DEU=No free codeunit number found to test with.;ENU=No free codeunit number found to test with.;ESP=No free codeunit number found to test with.;FRA=No free codeunit number found to test with.;NLD=Geen beschikbaar codeunitnummer gevonden om mee te testen.;DEA=No free codeunit number found to test with.';
    BEGIN
      IF ErrorOccured THEN BEGIN
        SkippedText := ctErrorOccured;
        EXIT;
      END;

      DeleteTestObject;

      TableNo := FreeTableNo;
      IF TableNo = 0 THEN BEGIN
        SkippedText := ctTestSkipped;
        EXIT;
      END;

      IF NOT CheckPermissions THEN
        EXIT;

      FillTestObject(TmpObject, TableNo);

      ImportFileName := FileHandler.TempDirectory + ctImportFileName;
      IF EXISTS(ImportFileName) THEN
        ERASE(ImportFileName);

      ExportFileName := FileHandler.TempDirectory + ctExportFileName;
      IF EXISTS(ExportFileName) THEN
        ERASE(ExportFileName);

      oFile.WRITEMODE := TRUE;
      oFile.TEXTMODE := TRUE;
      oFile.CREATE(ImportFileName);
      oFile.WRITE(ObjectText(TmpObject));
      oFile.CLOSE;

      ObjectMgt.SetErrorHandling(FALSE);
      ObjectMgt.ImportObject(ImportFileName);
      ObjectMgt.CompileObject(TmpObject);
      ObjectMgt.ExportObject(TmpObject, ExportFileName);

      Object.GET(TmpObject.Type, '', TmpObject.ID);
      IF Object.Date <> TmpObject.Date THEN

        ERROR(
          ctDateFormat,
          FORMAT(TmpObject.Date, 0, '<Day> <Month Text> <Year4>'),
          FORMAT(Object.Date, 0, '<Day> <Month Text> <Year4>'));

      IF EXISTS(ImportFileName) THEN
        ERASE(ImportFileName);

      IF NOT EXISTS(ExportFileName) THEN
        ERROR(ctExportFileNotFound);

      ERASE(ExportFileName);

      DeleteTestObject;
    END;

    LOCAL PROCEDURE DeleteTestObject@15();
    VAR
      Object@1000 : Record 2000000001;
      SqlMgt@1001 : Codeunit 11102075;
    BEGIN
      Object.SETRANGE(Type, Object.Type::Table);
      Object.SETRANGE(Name, TestObjectName);
      IF NOT Object.ISEMPTY THEN BEGIN
        COMMIT;
        SqlMgt.ExecuteNonQuery('DELETE FROM Object WHERE Name = ''' + TestObjectName + '''');
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE FreeTableNo@12() : Integer;
    VAR
      Object@1000 : Record 2000000001;
      PermissionRange@1001 : Record 2000000044;
      i@1002 : Integer;
    BEGIN
      PermissionRange.SETRANGE("Object Type", PermissionRange."Object Type"::Table);
      PermissionRange.SETRANGE("Insert Permission", PermissionRange."Insert Permission"::Yes);
      IF PermissionRange.FINDSET THEN
        REPEAT
          FOR i := PermissionRange.From TO PermissionRange."To" DO BEGIN
            Object.SETRANGE(Type, Object.Type::Table);
            Object.SETRANGE("Company Name", '');
            Object.SETRANGE(ID, i);
            IF Object.ISEMPTY THEN
              EXIT(i);
          END;
        UNTIL PermissionRange.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckPermissions@18() : Boolean;
    VAR
      PermissionName@1000 : Text;
      ctNoPermission@1001 : TextConst 'DEU=No permission to %1.;ENU=No permission to %1.;ESP=No permission to %1.;FRA=No permission to %1.;NLD=Niet de juiste rechten voor %1.;DEA=No permission to %1.';
    BEGIN
      IF NOT HasGotPermission(1320, PermissionName) THEN BEGIN
        SkippedText := STRSUBSTNO(ctNoPermission, PermissionName);
        EXIT;
      END;

      IF NOT HasGotPermission(1340, PermissionName) THEN BEGIN
        SkippedText := STRSUBSTNO(ctNoPermission, PermissionName);
        EXIT;
      END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE HasGotPermission@17(PermissionNo@1000 : Integer;VAR PermissionName@1001 : Text[1024]) : Boolean;
    VAR
      AllObj@1002 : Record 2000000038;
      LicensePermission@1003 : Record 2000000043;
    BEGIN
      AllObj.GET(AllObj."Object Type"::System, PermissionNo);
      PermissionName := AllObj."Object Name";

      LicensePermission.GET(AllObj."Object Type", AllObj."Object ID");
      EXIT(LicensePermission."Execute Permission" = LicensePermission."Execute Permission"::Yes);
    END;

    LOCAL PROCEDURE FillTestObject@21(VAR TmpObject@1000 : TEMPORARY Record 2000000001;ObjectNo@1001 : Integer);
    BEGIN
      TmpObject.Type := TmpObject.Type::Table;
      TmpObject.ID := ObjectNo;
      TmpObject.Name := TestObjectName;
      TmpObject.Date := 010203D;
      TmpObject.Time := 120000T;
      TmpObject.INSERT;
    END;

    LOCAL PROCEDURE ObjectText@13(VAR Object@1000 : Record 2000000001) : Text[1024];
    VAR
      CrLf@1001 : Text[2];
    BEGIN
      CrLf[1] := 13;
      CrLf[2] := 10;

      EXIT(
        'OBJECT Table ' + FORMAT(Object.ID) + ' ' + Object.Name + CrLf +
        '{' + CrLf +
        '  OBJECT-PROPERTIES' + CrLf +
        '  {' + CrLf +
        '    Date=' + FORMAT(Object.Date) + ';' + CrLf +
        '    Time=' + FORMAT(Object.Time) + ';' + CrLf +
        '    Modified=Yes;' + CrLf +
        '    Version List=! CHECK OBJECT VALID !;' + CrLf +
        '  }' + CrLf +
        '  PROPERTIES' + CrLf +
        '  {' + CrLf +
        '  }' + CrLf +
        '  FIELDS' + CrLf +
        '  {' + CrLf +
        '    { 1   ;   ;Code                ;Code10         }' + CrLf +
        '  }' + CrLf +
        '  KEYS' + CrLf +
        '  {' + CrLf +
        '    {    ;Code                                    ;Clustered=Yes }' + CrLf +
        '  }' + CrLf +
        '  FIELDGROUPS' + CrLf +
        '  {' + CrLf +
        '  }' + CrLf +
        '  CODE' + CrLf +
        '  {' + CrLf +
        '' + CrLf +
        '    BEGIN' + CrLf +
        '    END.' + CrLf +
        '  }' + CrLf +
        '}');
    END;

    LOCAL PROCEDURE TestObjectName@16() : Text;
    VAR
      ctTestObjectName@1000 : TextConst 'DEU=OM - Temporary Check Table;ENU=OM - Temporary Check Table;ESP=OM - Temporary Check Table;FRA=OM - Temporary Check Table;NLD=OM - Temporary Check Table;DEA=OM - Temporary Check Table';
    BEGIN
      EXIT(ctTestObjectName);
    END;

    LOCAL PROCEDURE TestTriggers@5();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctAskSQLStatus@1001 : TextConst 'DEU=An old ask-sql-trigger is installed.\You can install a newer version in the administration tasks.;ENU=An old ask-sql-trigger is installed.\You can install a newer version in the administration tasks.;ESP=An old ask-sql-trigger is installed.\You can install a newer version in the administration tasks.;FRA=An old ask-sql-trigger is installed.\You can install a newer version in the administration tasks.;NLD=Een oude ask-sql-trigger is ge‹nstalleerd.;DEA=An old ask-sql-trigger is installed.\You can install a newer version in the administration tasks.';
      ctDbConvStatus@1002 : TextConst 'DEU=An old block database conversion trigger is installed.\You can install a newer version in the administration tasks.;ENU=An old block database conversion trigger is installed.\You can install a newer version in the administration tasks.;ESP=An old block database conversion trigger is installed.\You can install a newer version in the administration tasks.;FRA=An old block database conversion trigger is installed.\You can install a newer version in the administration tasks.;NLD=Een oude blokkeer-database-conversie-trigger is ge‹nstalleerd.;DEA=An old block database conversion trigger is installed.\You can install a newer version in the administration tasks.';
      ctErrorOccured@1003 : TextConst 'DEU=There was an error in a preceding check.;ENU=There was an error in a preceding check.;ESP=There was an error in a preceding check.;FRA=There was an error in a preceding check.;NLD=Er heeft een fout plaatsgevonden in een voorgaande controle.;DEA=There was an error in a preceding check.';
      ctFailedToRunSQLManagement@1004 : TextConst 'DEU=Failed to run codeunit SQL Management.;ENU=Failed to run codeunit SQL Management.;ESP=Failed to run codeunit SQL Management.;FRA=Failed to run codeunit SQL Management.;NLD=Niet gelukt om codeunit SQL Management te draaien.;DEA=Failed to run codeunit SQL Management.';
      ctOldTriggerInstalled@1005 : TextConst 'DEU=An old trace trigger is installed.\You can install a newer version in the administration tasks.;ENU=An old trace trigger is installed.\You can install a newer version in the administration tasks.;ESP=An old trace trigger is installed.\You can install a newer version in the administration tasks.;FRA=An old trace trigger is installed.\You can install a newer version in the administration tasks.;NLD=Een oude traceertrigger is ge‹nstalleerd.;DEA=An old trace trigger is installed.\You can install a newer version in the administration tasks.';
    BEGIN
      IF ErrorOccured THEN BEGIN
        SkippedText := ctErrorOccured;
        EXIT;
      END;

      IF NOT SQLMgt.RUN THEN
        ERROR(ctFailedToRunSQLManagement);

      IF SQLMgt.GetTraceTriggerStatus = 2 THEN
        ERROR(ctOldTriggerInstalled);

      IF SQLMgt.GetAskSqlTriggerStatus = 2 THEN
        ERROR(ctAskSQLStatus);

      IF SQLMgt.GetDbConvTriggerStatus = 2 THEN
        ERROR(ctDbConvStatus);
    END;

    LOCAL PROCEDURE TestDotNet@19();
    VAR
      ActiveSession@1000 : Record 2000000110;
      ctActiveSessionIs@1001 : TextConst 'DEU=The active session is ''%1''.;ENU=The active session is ''%1''.;ESP=The active session is ''%1''.;FRA=The active session is ''%1''.;NLD=De actieve sessie is ''%1''.;DEA=The active session is ''%1''.';
      ctActiveSessionNotFound@1002 : TextConst 'DEU=The active session cannot be found.;ENU=The active session cannot be found.;ESP=The active session cannot be found.;FRA=The active session cannot be found.;NLD=De actieve sessie kan niet worden gevonden.;DEA=The active session cannot be found.';
    BEGIN
      IF NOT ActiveSession.GET(SERVICEINSTANCEID, SESSIONID) THEN
        ERROR(ctActiveSessionNotFound);

      IF ActiveSession."Client Type" <> ActiveSession."Client Type"::"Windows Client" THEN
        ERROR(ctActiveSessionIs, ActiveSession."Client Type");
    END;

    BEGIN
    END.
  }
}

