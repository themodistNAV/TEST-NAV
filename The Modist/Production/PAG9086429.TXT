OBJECT Page 9086429 nHanced Search Results
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=nHanced Search Results;
               ENG=nHanced Search Results];
    SaveValues=No;
    MultipleNewLines=No;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table9062817;
    DelayedInsert=No;
    SourceTableView=SORTING(EntryNo);
    SourceTableTemporary=Yes;
    ShowFilter=No;
    OnOpenPage=BEGIN

                 IF GETFILTER("Search Header") <> '' THEN
                 BEGIN
                     ShowHeader:=FALSE;
                     SearchHeader2.GET(GETFILTER("Search Header"));
                     SetHeader(SearchHeader2);
                 END ELSE BEGIN
                     ShowHeader:=TRUE;
                     SearchHeader2.RESET;
                     SearchHeader2.SETRANGE("Default Search",TRUE);
                     IF SearchHeader2.FINDFIRST THEN BEGIN
                       SetHeader(SearchHeader2);
                     END;
                 END;

                 IF NOT FINDFIRST THEN BEGIN
                   Rec.INIT;
                   Rec.INSERT(TRUE);
                 END;

                 LookFor:='';
               END;

    OnAfterGetRecord=BEGIN
                       Tables.SETRANGE(Type,Tables.Type::Table);
                       Tables.SETRANGE(ID,"Table No");
                       IF Tables.FINDFIRST THEN;
                     END;

    ActionList=ACTIONS
    {
      { 1000000007;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000012;1 ;Action    ;
                      Name=FindRecords;
                      CaptionML=[ENU=Search;
                                 ENG=Search];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Find;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FindInLuceneWithHeader(LookFor,GnHancedSearchHeader);
                               END;
                                }
      { 1000000002;1 ;Action    ;
                      Name=ShowRecord;
                      CaptionML=[ENU=View Details;
                                 ENG=View Details];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewDetails;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowRecord;
                               END;
                                }
      { 1000000005;1 ;Action    ;
                      Name=Index Now;
                      CaptionML=[ENU=Index Now;
                                 ENG=Index Now];
                      Promoted=No;
                      PromotedIsBig=No;
                      Image=Calculate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 eConfirmation@1000000000 : TextConst 'ENU=This will rebuild the indexes are you sure;ENG=This will rebuild the indexes are you sure';
                                 eComplete@1000000001 : TextConst 'ENU=Rebuild complete;ENG=Rebuild complete';
                               BEGIN

                                 IF NOT GnHancedSearchHeader.FINDFIRST THEN EXIT;

                                 IF CONFIRM(eConfirmation) THEN
                                   BEGIN
                                 LuceneFuncs.ProcessIndexHeader(GnHancedSearchHeader);
                                     MESSAGE(eComplete);
                                     END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=General;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 1000000006;2;Field  ;
                Name=LookFor;
                CaptionML=[ENU=Find This;
                           ENG=Find This];
                SourceExpr=LookFor;
                OnValidate=BEGIN
                             FindInLuceneWithHeader(LookFor,GnHancedSearchHeader);
                           END;

                QuickEntry=True }

    { 1000000008;2;Field  ;
                CaptionML=[ENU=Search Type;
                           ENG=Search Type];
                SourceExpr=SearchType;
                OnValidate=BEGIN
                             FindInLuceneWithHeader(LookFor,GnHancedSearchHeader);
                           END;

                QuickEntry=False }

    { 1000000014;2;Field  ;
                CaptionML=[ENU=No Of Records;
                           ENG=No Of Records];
                SourceExpr=ResultCount;
                Enabled=False;
                Editable=False;
                QuickEntry=False }

    { 1000000013;2;Field  ;
                DrillDown=Yes;
                CaptionML=[ENU=Search Name;
                           ENG=Search Name];
                SourceExpr=SearchHeaderName;
                Visible=ShowHeader;
                Editable=false;
                OnLookup=VAR
                           nHancedSearchList@1000000000 : Page 9086432;
                         BEGIN
                           nHancedSearchList.LOOKUPMODE(TRUE);
                           IF nHancedSearchList.RUNMODAL=ACTION::LookupOK THEN BEGIN
                             nHancedSearchList.GETRECORD(SearchHeader);
                             SearchHeader.SETRECFILTER();
                             SearchHeaderName:=SearchHeader.Description;
                             SetHeader(SearchHeader);
                           END;
                         END;

                QuickEntry=False }

    { 1000000003;1;Group  ;
                GroupType=Repeater }

    { 1000000004;2;Field  ;
                Name=Table Name;
                CaptionML=[ENU=Where Found;
                           ENG=Where Found];
                SourceExpr=Tables.Name;
                Editable=False;
                QuickEntry=False }

    { 1000000010;2;Field  ;
                Width=50;
                CaptionML=[ENU=Details;
                           ENG=Details];
                SourceExpr="Text 1";
                Editable=False;
                OnAssistEdit=BEGIN
                               ShowRecord();
                             END;

                QuickEntry=False }

  }
  CODE
  {
    VAR
      LookFor@1000000000 : Text;
      LuceneFuncs@1000000001 : Codeunit 9062684;
      Results@1000000002 : TEMPORARY Record 9062817;
      MyRecordRef@1000000004 : RecordRef;
      VarRecRef@1000000005 : Variant;
      Myaction@1000000006 : Action;
      Tables@1000000003 : Record 2000000001;
      ResultCount@1000000010 : Integer;
      GnHancedSearchHeader@1000000007 : Record 9062815;
      SearchType@1000000008 : 'OR,AND';
      TNPUtils@1000000009 : Codeunit 9062229;
      SearchHeader@1000000011 : Record 9062815;
      SearchHeaderName@1000000012 : Text;
      SearchHeader2@1000000013 : Record 9062815;
      ShowHeader@1000000014 : Boolean;

    LOCAL PROCEDURE ShowRecord@1000000003();
    BEGIN

      IF "Table No" >0 THEN
        TNPUtils.RunCardPage("Table No",PositionText,FALSE);
    END;

    PROCEDURE FindInLuceneWithHeader@1000000004(FindWhat@1000000000 : Text;nHancedSearchHeader@1000000001 : Record 9062815);
    BEGIN

      IF FindWhat='' THEN
        EXIT;

      LookFor := FindWhat;

      Results.DELETEALL;
      DELETEALL;

      LuceneFuncs.FindRecordsWithHeadeAndOr(FindWhat,Results,nHancedSearchHeader,SearchType);

      IF Results.FINDSET THEN BEGIN

       IF  Results.FINDFIRST THEN;

        REPEAT
          Rec.INIT;
          Rec.TRANSFERFIELDS(Results);
          Rec.INSERT;

        UNTIL Results.NEXT = 0;

      END;

      ResultCount := Results.COUNT;

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetHeader@1000000005(VAR nHancedSearchHeaderIn@1000000000 : Record 9062815);
    BEGIN
      GnHancedSearchHeader:=nHancedSearchHeaderIn;
      SearchType:=GnHancedSearchHeader."Default Search Type";
      SearchHeaderName:=GnHancedSearchHeader.Description;
    END;

    BEGIN
    {
      //DOC NA2016.12 BM 02/02/2016 - Created
      //DOC NA2016.15 JH 25/04/2016 - CfMD amendments
    }
    END.
  }
}

