OBJECT Report 88 VAT- VIES Declaration Disk
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 254=imd;
    CaptionML=[ENU=VAT- VIES Declaration Disk;
               ENG=VAT- VIES Declaration Disk];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  FileName := FileManagement.ServerTempFileName('txt');
                END;

    OnPostReport=BEGIN
                   IF NOT HideFileDialog THEN BEGIN
                     FileManagement.DownloadHandler(FileName ,'','',FileManagement.GetToFilterText('',FileName),Text007);
                     FileManagement.DeleteServerFile(FileName);
                   END
                 END;

  }
  DATASET
  {
    { 7612;    ;DataItem;                    ;
               DataItemTable=Table254;
               DataItemTableView=SORTING(Type,Country/Region Code,VAT Registration No.,VAT Bus. Posting Group,VAT Prod. Posting Group,Posting Date)
                                 WHERE(Type=CONST(Sale));
               OnPreDataItem=BEGIN
                               CLEAR(VATFile);
                               VATFile.TEXTMODE := TRUE;
                               VATFile.WRITEMODE := TRUE;
                               VATFile.CREATE(FileName);

                               CompanyInfo.GET;
                               VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');
                               VATFile.WRITE(FORMAT('00' + FORMAT(VATRegNo,8) + Text002,80));
                               VATFile.WRITE(FORMAT('0100001',80));

                               NoOfGrTotal := 0;
                               Period := GETRANGEMAX("Posting Date");
                               InternalReferenceNo := FORMAT(Period,4,2) + '000000';
                             END;

               OnAfterGetRecord=VAR
                                  VATEntry@1001 : Record 254;
                                  TotalValueOfItemSupplies@1003 : Decimal;
                                  TotalValueOfServiceSupplies@1002 : Decimal;
                                  GroupTotal@1000 : Boolean;
                                BEGIN
                                  IF "EU Service" THEN BEGIN
                                    IF UseAmtsInAddCurr THEN
                                      TotalValueOfServiceSupplies := "Additional-Currency Base"
                                    ELSE
                                      TotalValueOfServiceSupplies := Base
                                  END ELSE
                                    IF UseAmtsInAddCurr THEN
                                      TotalValueOfItemSupplies := "Additional-Currency Base"
                                    ELSE
                                      TotalValueOfItemSupplies := Base;

                                  IF "EU 3-Party Trade" THEN BEGIN
                                    EU3PartyItemTradeAmt := EU3PartyItemTradeAmt + TotalValueOfItemSupplies;
                                    EU3PartyServiceTradeAmt := EU3PartyServiceTradeAmt + TotalValueOfServiceSupplies;
                                  END;
                                  TotalValueofItemSuppliesTotal += TotalValueOfItemSupplies;
                                  TotalValueofServiceSuppliesTot += TotalValueOfServiceSupplies;

                                  VATEntry.COPY("VAT Entry");
                                  IF VATEntry.NEXT = 1 THEN BEGIN
                                    IF (VATEntry."Country/Region Code" <> "Country/Region Code") OR
                                       (VATEntry."VAT Registration No." <> "VAT Registration No.")
                                    THEN
                                      GroupTotal := TRUE;
                                  END ELSE
                                    GroupTotal := TRUE;

                                  IF GroupTotal THEN BEGIN
                                    WriteGrTotalsToFile(TotalValueofServiceSuppliesTot,TotalValueofItemSuppliesTotal,
                                      EU3PartyServiceTradeAmt,EU3PartyItemTradeAmt);
                                    EU3PartyItemTradeTotalAmt += EU3PartyItemTradeAmt;
                                    EU3PartyServiceTradeTotalAmt += EU3PartyServiceTradeAmt;

                                    TotalValueofItemSuppliesTotal := 0;
                                    TotalValueofServiceSuppliesTot := 0;

                                    EU3PartyItemTradeAmt := 0;
                                    EU3PartyServiceTradeAmt := 0;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                VATFile.WRITE(
                                  FORMAT(
                                    '10' + DecimalNumeralZeroFormat(NoOfGrTotal,9) +
                                    DecimalNumeralZeroFormat(EU3PartyItemTradeTotalAmt,15) +
                                    DecimalNumeralSign(-EU3PartyItemTradeTotalAmt) +
                                    DecimalNumeralZeroFormat(EU3PartyServiceTradeTotalAmt,15) +
                                    DecimalNumeralSign(-EU3PartyServiceTradeTotalAmt),
                                    80));
                                VATFile.CLOSE;
                              END;

               ReqFilterFields=VAT Bus. Posting Group,VAT Prod. Posting Group,Posting Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Show Amounts in Add. Reporting Currency;
                             ENG=Show Amounts in Add. Reporting Currency];
                  SourceExpr=UseAmtsInAddCurr;
                  MultiLine=Yes }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=WwWw;ENG=WwWw';
      Text002@1002 : TextConst 'ENU=LIST;ENG=LIST';
      Text003@1003 : TextConst 'ENU="%1 was not filled in for all VAT entries in which %2 = %3.";ENG="%1 was not filled in for all VAT entries in which %2 = %3."';
      Text004@1004 : TextConst 'ENU=It is not possible to display %1 in a field with a length of %2.;ENG=It is not possible to display %1 in a field with a length of %2.';
      CompanyInfo@1005 : Record 79;
      Country@1006 : Record 9;
      Cust@1007 : Record 18;
      FileManagement@1000 : Codeunit 419;
      VATFile@1008 : File;
      TotalValueofServiceSuppliesTot@1021 : Decimal;
      TotalValueofItemSuppliesTotal@1013 : Decimal;
      EU3PartyServiceTradeAmt@1022 : Decimal;
      EU3PartyItemTradeAmt@1014 : Decimal;
      EU3PartyItemTradeTotalAmt@1016 : Decimal;
      EU3PartyServiceTradeTotalAmt@1023 : Decimal;
      NoOfGrTotal@1020 : Integer;
      FileName@1009 : Text;
      VATRegNo@1010 : Code[20];
      InternalReferenceNo@1011 : Text[10];
      Period@1012 : Date;
      UseAmtsInAddCurr@1015 : Boolean;
      Text007@1018 : TextConst 'ENU=Default.txt;ENG=Default.txt';
      HideFileDialog@1024 : Boolean;

    LOCAL PROCEDURE DecimalNumeralSign@1(DecimalNumeral@1000 : Decimal) : Text[1];
    BEGIN
      IF DecimalNumeral >= 0 THEN
        EXIT('+');
      EXIT('-');
    END;

    LOCAL PROCEDURE DecimalNumeralZeroFormat@2(DecimalNumeral@1000 : Decimal;Length@1001 : Integer) : Text[250];
    BEGIN
      EXIT(TextZeroFormat(DELCHR(FORMAT(ROUND(ABS(DecimalNumeral),1,'<'),0,1)),Length));
    END;

    LOCAL PROCEDURE TextZeroFormat@3(Text@1000 : Text[250];Length@1001 : Integer) : Text[250];
    BEGIN
      IF STRLEN(Text) > Length THEN
        ERROR(
          Text004,
          Text,Length);
      EXIT(PADSTR('',Length - STRLEN(Text),'0') + Text);
    END;

    LOCAL PROCEDURE WriteGrTotalsToFile@4(TotalValueofServiceSupplies@1003 : Decimal;TotalValueofItemSupplies@1002 : Decimal;EU3PartyServiceTradeAmt@1001 : Decimal;EU3PartyItemTradeAmt@1000 : Decimal);
    BEGIN
      IF (ROUND(ABS(TotalValueofItemSupplies),1,'<') <> 0) OR (ROUND(ABS(TotalValueofServiceSupplies),1,'<') <> 0) OR
         (ROUND(ABS(EU3PartyItemTradeAmt),1,'<') <> 0) OR (ROUND(ABS(EU3PartyServiceTradeAmt),1,'<') <> 0)
      THEN
        WITH "VAT Entry" DO BEGIN
          IF "VAT Registration No." = '' THEN BEGIN
            Type := Type::Sale;
            ERROR(
              Text003,
              FIELDCAPTION("VAT Registration No."),FIELDCAPTION(Type),Type);
          END;

          Cust.GET("Bill-to/Pay-to No.");
          Cust.TESTFIELD("Country/Region Code");
          Country.GET(Cust."Country/Region Code");
          Cust.TESTFIELD("VAT Registration No.");
          Country.GET("Country/Region Code");
          Country.TESTFIELD("EU Country/Region Code");
          NoOfGrTotal := NoOfGrTotal + 1;

          InternalReferenceNo := INCSTR(InternalReferenceNo);
          SETRANGE("Country/Region Code","Country/Region Code");
          SETRANGE("Bill-to/Pay-to No.","Bill-to/Pay-to No.");
          MODIFYALL("Internal Ref. No.",InternalReferenceNo);
          SETRANGE("Country/Region Code");
          SETRANGE("Bill-to/Pay-to No.");

          VATFile.WRITE(
            FORMAT(
              '02' + FORMAT(InternalReferenceNo,10) +
              DecimalNumeralZeroFormat(DATE2DMY(Period,3) MOD 100,2) +
              DecimalNumeralZeroFormat(DATE2DMY(Period,2),2) +
              DecimalNumeralZeroFormat(DATE2DMY(Period,1),2) +
              FORMAT(VATRegNo,8) + FORMAT(Country."EU Country/Region Code",2) + FORMAT("VAT Registration No.",12) +
              DecimalNumeralZeroFormat(TotalValueofItemSupplies,15) + DecimalNumeralSign(-TotalValueofItemSupplies) + '0' +
              DecimalNumeralZeroFormat(TotalValueofServiceSupplies,15) + DecimalNumeralSign(-TotalValueofServiceSupplies) + '0' +
              DecimalNumeralZeroFormat(EU3PartyItemTradeAmt,15) + DecimalNumeralSign(-EU3PartyItemTradeAmt) + '0' +
              DecimalNumeralZeroFormat(EU3PartyServiceTradeAmt,15) + DecimalNumeralSign(-EU3PartyServiceTradeAmt),
              120));
        END;
    END;

    PROCEDURE GetFileName@6() : Text[1024];
    BEGIN
      EXIT(FileName);
    END;

    PROCEDURE InitializeRequest@5(NewHideFileDialog@1000 : Boolean);
    BEGIN
      HideFileDialog := NewHideFileDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

