OBJECT Table 90 BOM Component
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    OnModify=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    OnRename=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    CaptionML=[ENU=BOM Component;
               ENG=BOM Component];
    LookupPageID=Page36;
    DrillDownPageID=Page36;
  }
  FIELDS
  {
    { 1   ;   ;Parent Item No.     ;Code20        ;TableRelation=Item WHERE (Type=CONST(Inventory));
                                                   CaptionML=[ENU=Parent Item No.;
                                                              ENG=Parent Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "No." := '';
                                                                "Variant Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=" ,Item,Resource";
                                                                    ENG=" ,Item,Resource"];
                                                   OptionString=[ ,Item,Resource] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item WHERE (Type=CONST(Inventory))
                                                                 ELSE IF (Type=CONST(Resource)) Resource;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                                "Variant Code" := '';
                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      ValidateAgainstRecursion("No.");
                                                                      Item.CALCFIELDS("Assembly BOM");
                                                                      "Assembly BOM" := Item."Assembly BOM";
                                                                      Description := Item.Description;
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                      ParentItem.GET("Parent Item No.");
                                                                      CalcLowLevelCode.SetRecursiveLevelsOnItem(Item,ParentItem."Low-Level Code" + 1,TRUE);
                                                                      Item.FIND;
                                                                      ParentItem.FIND;
                                                                      IF ParentItem."Low-Level Code" >= Item."Low-Level Code" THEN
                                                                        ERROR(Text001,"No.");
                                                                    END;
                                                                  Type::Resource:
                                                                    BEGIN
                                                                      Res.GET("No.");
                                                                      "Assembly BOM" := FALSE;
                                                                      Description := Res.Name;
                                                                      "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ENG=No.] }
    { 5   ;   ;Assembly BOM        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("BOM Component" WHERE (Type=CONST(Item),
                                                                                            Parent Item No.=FIELD(No.)));
                                                   CaptionML=[ENU=Assembly BOM;
                                                              ENG=Assembly BOM];
                                                   Editable=No }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 7   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Resource)) "Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ENG=Unit of Measure Code] }
    { 8   ;   ;Quantity per        ;Decimal       ;CaptionML=[ENU=Quantity per;
                                                              ENG=Quantity per];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 9   ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              ENG=Position] }
    { 10  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              ENG=Position 2] }
    { 11  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              ENG=Position 3] }
    { 12  ;   ;Machine No.         ;Code10        ;CaptionML=[ENU=Machine No.;
                                                              ENG=Machine No.] }
    { 13  ;   ;Lead-Time Offset    ;DateFormula   ;CaptionML=[ENU=Lead-Time Offset;
                                                              ENG=Lead-Time Offset] }
    { 14  ;   ;BOM Description     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Parent Item No.)));
                                                   CaptionML=[ENU=BOM Description;
                                                              ENG=BOM Description];
                                                   Editable=No }
    { 20  ;   ;Resource Usage Type ;Option        ;OnValidate=BEGIN
                                                                IF "Resource Usage Type" = xRec."Resource Usage Type" THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::Resource);
                                                              END;

                                                   CaptionML=[ENU=Resource Usage Type;
                                                              ENG=Resource Usage Type];
                                                   OptionCaptionML=[ENU=Direct,Fixed;
                                                                    ENG=Direct,Fixed];
                                                   OptionString=Direct,Fixed }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ENG=Variant Code] }
    { 5900;   ;Installed in Line No.;Integer      ;OnValidate=BEGIN
                                                                IF "Installed in Line No." <> 0 THEN BEGIN
                                                                  IF "Installed in Line No." = "Line No." THEN
                                                                    ERROR(Text000,FIELDCAPTION("Installed in Line No."));
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("Line No.","Installed in Line No.");
                                                                  BOMComp.FINDFIRST;
                                                                  BOMComp.TESTFIELD("Quantity per",1);
                                                                  "Installed in Item No." := BOMComp."No.";
                                                                END ELSE
                                                                  "Installed in Item No." := '';
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(AssemblyBOM);
                                                              AssemblyBOM.SETTABLEVIEW(BOMComp);
                                                              AssemblyBOM.EDITABLE(FALSE);
                                                              AssemblyBOM.LOOKUPMODE(TRUE);
                                                              IF AssemblyBOM.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                AssemblyBOM.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Installed in Line No.;
                                                              ENG=Installed in Line No.] }
    { 5901;   ;Installed in Item No.;Code20       ;TableRelation=IF (Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                IF "Installed in Item No." <> '' THEN BEGIN
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("No.","Installed in Item No.");
                                                                  BOMComp.FINDFIRST;
                                                                END;

                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp."No." := "Installed in Item No.";
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(AssemblyBOM);
                                                              AssemblyBOM.SETTABLEVIEW(BOMComp);
                                                              AssemblyBOM.EDITABLE(FALSE);
                                                              AssemblyBOM.LOOKUPMODE(TRUE);
                                                              IF AssemblyBOM.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                AssemblyBOM.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Installed in Item No.;
                                                              ENG=Installed in Item No.] }
  }
  KEYS
  {
    {    ;Parent Item No.,Line No.                ;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 cannot be component of itself.;ENG=%1 cannot be component of itself.';
      Text001@1006 : TextConst 'ENU=You cannot insert item %1 as an assembly component of itself.;ENG=You cannot insert item %1 as an assembly component of itself.';
      Item@1001 : Record 27;
      ParentItem@1007 : Record 27;
      Res@1002 : Record 156;
      ItemVariant@1003 : Record 5401;
      BOMComp@1004 : Record 90;
      CalcLowLevelCode@1008 : Codeunit 99000793;
      AssemblyBOM@1005 : Page 36;

    PROCEDURE ValidateAgainstRecursion@3(ItemNo@1003 : Code[20]);
    VAR
      BOMComp@1001 : Record 90;
    BEGIN
      IF "Parent Item No." = ItemNo THEN
        ERROR(Text001,ItemNo);
      IF Type = Type::Item THEN BEGIN
        BOMComp.SETCURRENTKEY(Type,"No.");
        BOMComp.SETRANGE(Type,Type::Item);
        BOMComp.SETRANGE("No.","Parent Item No.");
        IF BOMComp.FINDSET THEN
          REPEAT
            BOMComp.ValidateAgainstRecursion(ItemNo);
          UNTIL BOMComp.NEXT = 0
      END
    END;

    BEGIN
    END.
  }
}

