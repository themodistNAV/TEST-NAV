OBJECT Table 1006 Job WIP Method
{
  OBJECT-PROPERTIES
  {
    Date=26/11/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.43897;
  }
  PROPERTIES
  {
    OnDelete=VAR
               JobWIPEntry@1000 : Record 1004;
               JobWIPGLEntry@1001 : Record 1005;
               JobsSetup@1002 : Record 315;
             BEGIN
               IF "System Defined" THEN
                 ERROR(Text001,FIELDCAPTION("System Defined"));

               JobWIPEntry.SETRANGE("WIP Method Used",Code);
               JobWIPGLEntry.SETRANGE("WIP Method Used",Code);
               IF JobWIPEntry.FINDFIRST OR JobWIPGLEntry.FINDFIRST THEN
                 ERROR(Text004,JobWIPEntry.TABLECAPTION,JobWIPGLEntry.TABLECAPTION);

               JobsSetup.SETRANGE("Default WIP Method",Code);
               IF JobsSetup.FINDFIRST THEN
                 ERROR(Text006);
             END;

    CaptionML=[ENU=Job WIP Method;
               ENG=Job WIP Method];
    LookupPageID=Page1010;
    DrillDownPageID=Page1010;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                ValidateModification;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                ValidateModification;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 3   ;   ;WIP Cost            ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                ValidateModification;
                                                                IF "Recognized Costs" <> "Recognized Costs"::"Usage (Total Cost)" THEN
                                                                  ERROR(Text003,FIELDCAPTION("Recognized Costs"),"Recognized Costs");
                                                              END;

                                                   CaptionML=[ENU=WIP Cost;
                                                              ENG=WIP Cost] }
    { 4   ;   ;WIP Sales           ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                ValidateModification;
                                                                IF "Recognized Sales" <> "Recognized Sales"::"Contract (Invoiced Price)" THEN
                                                                  ERROR(Text003,FIELDCAPTION("Recognized Sales"),"Recognized Sales");
                                                              END;

                                                   CaptionML=[ENU=WIP Sales;
                                                              ENG=WIP Sales] }
    { 5   ;   ;Recognized Costs    ;Option        ;OnValidate=BEGIN
                                                                ValidateModification;
                                                                IF "Recognized Costs" <> "Recognized Costs"::"Usage (Total Cost)" THEN
                                                                  "WIP Cost" := TRUE;
                                                              END;

                                                   CaptionML=[ENU=Recognized Costs;
                                                              ENG=Recognised Costs];
                                                   OptionCaptionML=[ENU=At Completion,Cost of Sales,Cost Value,Contract (Invoiced Cost),Usage (Total Cost);
                                                                    ENG=At Completion,Cost of Sales,Cost Value,Contract (Invoiced Cost),Usage (Total Cost)];
                                                   OptionString=At Completion,Cost of Sales,Cost Value,Contract (Invoiced Cost),Usage (Total Cost) }
    { 6   ;   ;Recognized Sales    ;Option        ;OnValidate=BEGIN
                                                                ValidateModification;
                                                                IF "Recognized Sales" <> "Recognized Sales"::"Contract (Invoiced Price)" THEN
                                                                  "WIP Sales" := TRUE;
                                                              END;

                                                   CaptionML=[ENU=Recognized Sales;
                                                              ENG=Recognised Sales];
                                                   OptionCaptionML=[ENU=At Completion,Contract (Invoiced Price),Usage (Total Cost),Usage (Total Price),Percentage of Completion,Sales Value;
                                                                    ENG=At Completion,Contract (Invoiced Price),Usage (Total Cost),Usage (Total Price),Percentage of Completion,Sales Value];
                                                   OptionString=At Completion,Contract (Invoiced Price),Usage (Total Cost),Usage (Total Price),Percentage of Completion,Sales Value }
    { 7   ;   ;Valid               ;Boolean       ;InitValue=Yes;
                                                   OnValidate=VAR
                                                                JobsSetup@1000 : Record 315;
                                                              BEGIN
                                                                JobsSetup.SETRANGE("Default WIP Method",Code);
                                                                IF JobsSetup.FINDFIRST THEN
                                                                  ERROR(Text007,JobsSetup.FIELDCAPTION("Default WIP Method"));
                                                              END;

                                                   CaptionML=[ENU=Valid;
                                                              ENG=Valid] }
    { 8   ;   ;System Defined      ;Boolean       ;InitValue=No;
                                                   CaptionML=[ENU=System Defined;
                                                              ENG=System Defined];
                                                   Editable=No }
    { 9   ;   ;System-Defined Index;Integer       ;CaptionML=[ENU=System-Defined Index;
                                                              ENG=System-Defined Index];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Valid                                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You cannot delete methods that are %1.;ENG=You cannot delete methods that are %1.';
      Text002@1001 : TextConst 'ENU=You cannot modify methods that are %1.;ENG=You cannot modify methods that are %1.';
      Text003@1002 : TextConst 'ENU=You cannot modify this field when %1 is %2.;ENG=You cannot modify this field when %1 is %2.';
      Text004@1003 : TextConst 'ENU=You cannot delete methods that have entries in %1 or %2.;ENG=You cannot delete methods that have entries in %1 or %2.';
      Text005@1004 : TextConst 'ENU=You cannot modify methods that have entries in %1 or %2.;ENG=You cannot modify methods that have entries in %1 or %2.';
      Text006@1005 : TextConst 'ENU=You cannot delete the default method.;ENG=You cannot delete the default method.';
      Text007@1006 : TextConst 'ENU=This method must be valid because it is defined as the %1.;ENG=This method must be valid because it is defined as the %1.';

    LOCAL PROCEDURE ValidateModification@3();
    VAR
      JobWIPEntry@1001 : Record 1004;
      JobWIPGLEntry@1000 : Record 1005;
    BEGIN
      IF "System Defined" THEN
        ERROR(Text002,FIELDCAPTION("System Defined"));
      JobWIPEntry.SETRANGE("WIP Method Used",Code);
      JobWIPGLEntry.SETRANGE("WIP Method Used",Code);
      IF JobWIPEntry.FINDFIRST OR JobWIPGLEntry.FINDFIRST THEN
        ERROR(Text005,JobWIPEntry.TABLECAPTION,JobWIPGLEntry.TABLECAPTION);
    END;

    BEGIN
    END.
  }
}

