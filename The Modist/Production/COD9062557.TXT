OBJECT Codeunit 9062557 Quest. Create New Vendor
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    TableNo=9062704;
    OnRun=VAR
            QuestLine@1000000000 : Record 9062705;
            Vendor@1000000001 : Record 23;
            Answer_Name@1000000002 : Text;
            Answer_Address1@1000000003 : Text;
            Answer_City@1000000004 : Text;
            Answer_PhoneNo@1000000005 : Text;
            Answer_EMail@1000000006 : Text;
            Answer_GenBusPostingGroup@1000000007 : Text;
            Answer_VATBusPostingGroup@1000000008 : Text;
          BEGIN
            IF GetAnswer(QuestLine,tQuestion_Name)               THEN Answer_Name               := QuestLine.Answer ELSE ERROR(eAnswerNotGiven,tQuestion_Name);
            IF GetAnswer(QuestLine,tQuestion_Address1)           THEN Answer_Address1           := QuestLine.Answer ELSE ERROR(eAnswerNotGiven,tQuestion_Address1);
            IF GetAnswer(QuestLine,tQuestion_City)               THEN Answer_City               := QuestLine.Answer ELSE ERROR(eAnswerNotGiven,tQuestion_City);
            IF GetAnswer(QuestLine,tQuestion_PhoneNo)            THEN Answer_PhoneNo            := QuestLine.Answer;
            IF GetAnswer(QuestLine,tQuestion_Email)              THEN Answer_EMail              := QuestLine.Answer;
            IF GetAnswer(QuestLine,tQuestion_GenBusPostingGroup) THEN Answer_GenBusPostingGroup := QuestLine.Answer;
            IF GetAnswer(QuestLine,tQuestion_VATBusPostingGroup) THEN Answer_VATBusPostingGroup := QuestLine.Answer;

            //Create Vendor
            WITH Vendor DO BEGIN
              INIT;

              //Required Fields
              VALIDATE(Name,COPYSTR(Answer_Name,1,MAXSTRLEN(Name)));
              VALIDATE(Address,COPYSTR(Answer_Address1,MAXSTRLEN(Address)));
              VALIDATE(City,COPYSTR(Answer_City,1,MAXSTRLEN(City)));

              //Optional Fields
              IF Answer_PhoneNo <> '' THEN VALIDATE("Phone No.",COPYSTR(Answer_PhoneNo,1,MAXSTRLEN("Phone No.")));
              IF Answer_EMail <> '' THEN VALIDATE("E-Mail",COPYSTR(Answer_EMail,1,MAXSTRLEN("E-Mail")));
              IF Answer_GenBusPostingGroup <> '' THEN VALIDATE("Gen. Bus. Posting Group",COPYSTR(Answer_GenBusPostingGroup,1,MAXSTRLEN("Gen. Bus. Posting Group")));
              IF Answer_VATBusPostingGroup <> '' THEN VALIDATE("VAT Bus. Posting Group",COPYSTR(Answer_VATBusPostingGroup,1,MAXSTRLEN("VAT Bus. Posting Group")));

              INSERT(TRUE);
            END;

            //Update Instance with result
            VALIDATE("On Finish - Status","On Finish - Status"::Success);
            VALIDATE("On Finish - Status Text",STRSUBSTNO(tSuccess,Vendor."No.",Vendor.Name));
            VALIDATE("On Finish - Table No.",DATABASE::Vendor);
            VALIDATE("On Finish - Record Position",Vendor.GETPOSITION);

            //Relate this Quest. Instance to the Record we just created
            VALIDATE("Related Table No.",DATABASE::Vendor);
            VALIDATE("Related Record Position",Vendor.GETPOSITION);
          END;

  }
  CODE
  {
    VAR
      eAnswerNotGiven@1000000007 : TextConst 'ENU=No answer was given to the question "%1";ENG=No answer was given to the question "%1"';
      tSuccess@1000000008 : TextConst 'ENU=Vendor %1 (%2) was created;ENG=Vendor %1 (%2) was created';
      tQuestion_Name@1000000000 : TextConst 'ENU=Name;ENG=Name';
      tQuestion_Address1@1000000001 : TextConst 'ENU=Address 1;ENG=Address 1';
      tQuestion_City@1000000002 : TextConst 'ENU=City;ENG=City';
      tQuestion_PhoneNo@1000000003 : TextConst 'ENU=Phone Number;ENG=Phone Number';
      tQuestion_Email@1000000004 : TextConst 'ENU=E-Mail;ENG=E-Mail';
      tQuestion_GenBusPostingGroup@1000000005 : TextConst 'ENU=Gen. Bus. Posting Group;ENG=Gen. Bus. Posting Group';
      tQuestion_VATBusPostingGroup@1000000006 : TextConst 'ENU=VAT Bus. Posting Group;ENG=VAT Bus. Posting Group';

    BEGIN
    {
      //DOC NA2015.7  PC 05/06/2015 - Created
      //DOC NA2016.7  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 09/05/2016 - CfMD modifications
    }
    END.
  }
}

