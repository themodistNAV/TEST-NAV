OBJECT Codeunit 9062455 AutoNAV Task Queue Maintenance
{
  OBJECT-PROPERTIES
  {
    Date=17/12/15;
    Time=[ 3:01:46 PM];
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    TableNo=9062734;
    OnRun=BEGIN
            Code();
          END;

  }
  CODE
  {
    VAR
      AutoNavTask@1000000000 : Record 9062734;
      AutoNavTaskLogEntry@1000000003 : Record 9062736;
      SystemBehaviour@1000000002 : Record 9062230;
      AutoNavTaskLogToolkit@1000000001 : Codeunit 9062602;

    PROCEDURE Code@1000000002();
    VAR
      lProcessingTime@1000000000 : Integer;
      lMaxProcessingTime@1000000001 : Integer;
    BEGIN
      //DOC NA2015.2 - Running the codeunit code

      lMaxProcessingTime := SystemBehaviour.GetInteger('MAX_JOB_Q_PROC_TIME');

      AutoNavTask.RESET();
      AutoNavTask.SETCURRENTKEY(Maintained);
      AutoNavTask.SETRANGE(Maintained, TRUE);
      IF (AutoNavTask.FINDSET(TRUE)) THEN
      REPEAT
        CASE (AutoNavTask.Status) OF
          AutoNavTask.Status::"In Process":
            IF ((lMaxProcessingTime > 0) AND (AutoNavTask."Last Ready State" <> 0DT)) THEN
            BEGIN
              lProcessingTime := (CURRENTDATETIME() - AutoNavTask."Last Ready State") / 60 / 1000;
              IF (lProcessingTime > lMaxProcessingTime) THEN
                RestartAutoNavTask();
            END;
          AutoNavTask.Status::Error:
            RestartAutoNavTask();
        END;
      UNTIL (AutoNavTask.NEXT() = 0);
    END;

    LOCAL PROCEDURE RestartAutoNavTask@1000000007();
    VAR
      eRestarted@9062229 : TextConst 'ENU=The task has been restarted by maintenance routine.;ENG=The task has been restarted by maintenance routine.';
    BEGIN
      //DOC NA2016.12 - Restarting AutoNAV task

      AutoNavTaskLogToolkit.AddEntryForTask(AutoNavTask, eRestarted, AutoNavTaskLogEntry.Status::Information);

      AutoNavTask.SetStatus(AutoNavTask.Status::Ready);
      AutoNavTask.VALIDATE("No. of Failures", 0);
      AutoNavTask.MODIFY(TRUE);
    END;

    BEGIN
    {
      //DOC NA2015.2  JH 02/07/2014 - Created
      //DOC NA2015.4  JH 28/12/2014 - System behaviours renamed
      //DOC NA2016.10 JH 14/09/2015 - Upgraded to 2016, switched to "AutoNAV Task" table
      //DOC NA2016.12 JH 17/12/2015 - Bugfixes
    }
    END.
  }
}

