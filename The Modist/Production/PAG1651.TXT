OBJECT Page 1651 Curr. Exch. Rate Service Card
{
  OBJECT-PROPERTIES
  {
    Date=23/10/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Currency Exch. Rate Service;
               ENG=Currency Exch. Rate Service];
    SourceTable=Table1650;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Setup;
                                ENG=New,Process,Report,Setup];
    OnOpenPage=BEGIN
                 UpdateBasedOnEnable;
               END;

    OnInsertRecord=VAR
                     TempField@1002 : TEMPORARY Record 2000000041;
                     MapCurrencyExchangeRate@1001 : Codeunit 1280;
                   BEGIN
                     MapCurrencyExchangeRate.GetSuggestedFields(TempField);
                     CurrPage.SimpleDataExchSetup.PAGE.SetSuggestedField(TempField);
                     UpdateSimpleMappingsPart;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           GetWebServiceURL(WebServiceURL);
                           IF WebServiceURL <> '' THEN
                             GenerateXMLStructure;

                           UpdateSimpleMappingsPart;
                           UpdateBasedOnEnable;
                         END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      Name=Process;
                      CaptionML=[ENU=Process;
                                 ENG=Process];
                      ActionContainerType=ActionItems }
      { 10      ;1   ;Action    ;
                      Name=Preview;
                      CaptionML=[ENU=Preview;
                                 ENG=Preview];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReviewWorksheet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempCurrencyExchangeRate@1001 : TEMPORARY Record 330;
                                 UpdateCurrencyExchangeRates@1000 : Codeunit 1281;
                               BEGIN
                                 TESTFIELD(Code);
                                 VerifyServiceURL;
                                 VerifyDataExchangeLineDefinition;
                                 UpdateCurrencyExchangeRates.GenerateTempDataFromService(TempCurrencyExchangeRate,Rec);
                                 PAGE.RUN(PAGE::"Currency Exchange Rates",TempCurrencyExchangeRate);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=JobQueueEntry;
                      CaptionML=[ENU=Job Queue Entry;
                                 ENG=Job Queue Entry];
                      Promoted=Yes;
                      Enabled=Enabled;
                      Image=JobListSetup;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowJobQueueEntry;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 5   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General];
                GroupType=Group }

    { 2   ;2   ;Field     ;
                SourceExpr=Code;
                Editable=EditableByNotEnabled }

    { 3   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=EditableByNotEnabled }

    { 14  ;2   ;Field     ;
                SourceExpr=ShowEnableWarning;
                Editable=false;
                OnDrillDown=BEGIN
                              DrilldownCode;
                            END;
                             }

    { 7   ;2   ;Field     ;
                SourceExpr=Enabled;
                OnValidate=BEGIN
                             EditableByNotEnabled := NOT Enabled;
                             CurrPage.UPDATE;
                           END;
                            }

    { 4   ;1   ;Group     ;
                CaptionML=[ENU=Service;
                           ENG=Service];
                GroupType=Group }

    { 11  ;2   ;Field     ;
                Name=ServiceURL;
                CaptionML=[ENU=Service URL;
                           ENG=Service URL];
                SourceExpr=WebServiceURL;
                Editable=EditableByNotEnabled;
                MultiLine=Yes;
                OnValidate=BEGIN
                             SetWebServiceURL(WebServiceURL);
                             GenerateXMLStructure;
                           END;
                            }

    { 8   ;2   ;Field     ;
                SourceExpr="Service Provider";
                Editable=EditableByNotEnabled }

    { 12  ;2   ;Field     ;
                SourceExpr="Terms of Service";
                Editable=EditableByNotEnabled }

    { 6   ;1   ;Part      ;
                Name=SimpleDataExchSetup;
                PagePartID=Page1265;
                PartType=Page }

  }
  CODE
  {
    VAR
      TempXMLBuffer@1001 : TEMPORARY Record 1235;
      WebServiceURL@1000 : Text;
      EditableByNotEnabled@1002 : Boolean;
      EnabledWarningTok@1004 : TextConst 'ENU=You must disable the service before you can make changes.;ENG=You must disable the service before you can make changes.';
      DisableEnableQst@1003 : TextConst 'ENU=Do you want to disable currency exchange rate service?;ENG=Do you want to disable currency exchange rate service?';
      ShowEnableWarning@1005 : Text;

    LOCAL PROCEDURE UpdateSimpleMappingsPart@2();
    BEGIN
      CurrPage.SimpleDataExchSetup.PAGE.SetDataExchDefCode("Data Exch. Def Code");
      CurrPage.SimpleDataExchSetup.PAGE.UpdateData;
      CurrPage.SimpleDataExchSetup.PAGE.UPDATE(FALSE);
      CurrPage.SimpleDataExchSetup.PAGE.SetSourceToBeMandatory("Web Service URL".HASVALUE);
    END;

    LOCAL PROCEDURE GenerateXMLStructure@1();
    BEGIN
      TempXMLBuffer.RESET;
      TempXMLBuffer.DELETEALL;
      GetXMLStructure(TempXMLBuffer,WebServiceURL);
      TempXMLBuffer.RESET;
      CurrPage.SimpleDataExchSetup.PAGE.SetXMLDefinition(TempXMLBuffer);
    END;

    LOCAL PROCEDURE UpdateBasedOnEnable@4();
    BEGIN
      EditableByNotEnabled := NOT Enabled;
      ShowEnableWarning := '';
      IF CurrPage.EDITABLE AND Enabled THEN
        ShowEnableWarning := EnabledWarningTok;
    END;

    LOCAL PROCEDURE DrilldownCode@3();
    BEGIN
      IF CONFIRM(DisableEnableQst,TRUE) THEN BEGIN
        Enabled := FALSE;
        UpdateBasedOnEnable;
        CurrPage.UPDATE;
      END;
    END;

    BEGIN
    END.
  }
}

