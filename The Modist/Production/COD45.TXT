OBJECT Codeunit 45 AutoFormatManagement
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Currency@1001 : Record 4;
      GLSetup@1000 : Record 98;
      GLSetupRead@1002 : Boolean;
      Text012@1003 : TextConst 'ENU=<Precision,%1><Standard Format,0>;ENG=<Precision,%1><Standard Format,0>';

    PROCEDURE AutoFormatTranslate@12(AutoFormatType@1000 : Integer;AutoFormatExpr@1001 : Text[80]) : Text[80];
    BEGIN
      IF AutoFormatType = 0 THEN
        EXIT('');

      IF NOT GetGLSetup THEN
        EXIT('');

      CASE AutoFormatType OF
        1: // Amount
          BEGIN
            IF AutoFormatExpr = '' THEN
              EXIT(STRSUBSTNO(Text012,GLSetup."Amount Decimal Places"));
            IF GetCurrency(COPYSTR(AutoFormatExpr,1,10)) AND
               (Currency."Amount Decimal Places" <> '')
            THEN
              EXIT(STRSUBSTNO(Text012,Currency."Amount Decimal Places"));
            EXIT(STRSUBSTNO(Text012,GLSetup."Amount Decimal Places"));
          END;
        2: // Unit Amount
          BEGIN
            IF AutoFormatExpr = '' THEN
              EXIT(STRSUBSTNO(Text012,GLSetup."Unit-Amount Decimal Places"));
            IF GetCurrency(COPYSTR(AutoFormatExpr,1,10)) AND
               (Currency."Unit-Amount Decimal Places" <> '')
            THEN
              EXIT(STRSUBSTNO(Text012,Currency."Unit-Amount Decimal Places"));
            EXIT(STRSUBSTNO(Text012,GLSetup."Unit-Amount Decimal Places"));
          END;
        10:
          EXIT('<Custom,' + AutoFormatExpr + '>');
      END;
    END;

    PROCEDURE ReadRounding@14() : Decimal;
    BEGIN
      GetGLSetup;
      EXIT(GLSetup."Amount Rounding Precision");
    END;

    LOCAL PROCEDURE GetGLSetup@15() : Boolean;
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetupRead := GLSetup.GET;
      EXIT(GLSetupRead);
    END;

    LOCAL PROCEDURE GetCurrency@16(CurrencyCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF CurrencyCode = Currency.Code THEN
        EXIT(TRUE);
      IF CurrencyCode = '' THEN BEGIN
        CLEAR(Currency);
        Currency.InitRoundingPrecision;
        EXIT(TRUE);
      END;
      EXIT(Currency.GET(CurrencyCode));
    END;

    BEGIN
    END.
  }
}

