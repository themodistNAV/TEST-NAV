OBJECT Codeunit 1204 Map Data Exch. To RapidStart
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempDataExchRapidStartBuffer@1000 : TEMPORARY Record 8650;
      TargetTableFieldDefinitionMustBeSpecifiedErr@1001 : TextConst 'ENU=Column definition must have a target table specified.;ENG=Column definition must have a target table specified.';

    PROCEDURE ProcessAllLinesColumnMapping@12(DataExch@1001 : Record 1220;TargetRapidstartPackageCode@1002 : Code[20]);
    VAR
      DataExchLineDef@1000 : Record 1227;
    BEGIN
      TempDataExchRapidStartBuffer.RESET;
      TempDataExchRapidStartBuffer.DELETEALL;

      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExch."Data Exch. Def Code");
      DataExchLineDef.SETRANGE("Parent Code",'');
      IF DataExchLineDef.FINDSET THEN
        REPEAT
          ProcessColumnMapping(DataExch,DataExchLineDef,TargetRapidstartPackageCode);
        UNTIL DataExchLineDef.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessColumnMapping@13(DataExch@1004 : Record 1220;DataExchLineDef@1001 : Record 1227;TargetRapidstartPackageCode@1008 : Code[20]);
    VAR
      DataExchField@1009 : Record 1221;
      NewConfigPackageRecord@1000 : Record 8614;
      ChildDataExchLineDef@1012 : Record 1227;
      CurrentLineNo@1011 : Integer;
    BEGIN
      DataExchField.SETRANGE("Data Exch. No.",DataExch."Entry No.");
      DataExchField.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);

      IF NOT DataExchField.FINDSET THEN
        EXIT;

      CurrentLineNo := -1;

      REPEAT
        InsertRecordDefinition(DataExchField,DataExchLineDef,NewConfigPackageRecord,CurrentLineNo,TargetRapidstartPackageCode);
        InsertDataValues(DataExchField,DataExchLineDef,TargetRapidstartPackageCode,NewConfigPackageRecord);
      UNTIL DataExchField.NEXT = 0;

      // Process Child Line Definitions
      ChildDataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchLineDef."Data Exch. Def Code");
      ChildDataExchLineDef.SETRANGE("Parent Code",DataExchLineDef.Code);

      IF NOT ChildDataExchLineDef.FINDSET THEN
        EXIT;

      REPEAT
        ProcessColumnMapping(DataExch,ChildDataExchLineDef,TargetRapidstartPackageCode);
      UNTIL ChildDataExchLineDef.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertRecordDefinition@3(DataExchField@1000 : Record 1221;DataExchLineDef@1003 : Record 1227;VAR NewConfigPackageRecord@1012 : Record 8614;VAR CurrentLineNo@1002 : Integer;TargetRapidstartPackageCode@1005 : Code[20]);
    VAR
      DataExchFieldMapping@1001 : Record 1225;
      PreviousConfigPackageRecord@1004 : Record 8614;
    BEGIN
      // Check if definition is already inserted
      IF CurrentLineNo = DataExchField."Line No." THEN
        EXIT;

      // Find the table definition we need to write to.
      DataExchFieldMapping.SETRANGE("Data Exch. Def Code",DataExchLineDef."Data Exch. Def Code");
      DataExchFieldMapping.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
      DataExchFieldMapping.SETRANGE("Table ID",DATABASE::"Config. Package Data");
      DataExchFieldMapping.SETFILTER("Column No.",'>0');
      IF NOT DataExchFieldMapping.FINDFIRST THEN
        ERROR(TargetTableFieldDefinitionMustBeSpecifiedErr);

      CurrentLineNo := DataExchField."Line No.";

      // Initialize new record
      CLEAR(NewConfigPackageRecord);
      NewConfigPackageRecord.INIT;
      NewConfigPackageRecord.VALIDATE("Package Code",TargetRapidstartPackageCode);
      NewConfigPackageRecord.VALIDATE("Table ID",DataExchFieldMapping."Target Table ID");

      // Get last used No.
      PreviousConfigPackageRecord.INIT;
      PreviousConfigPackageRecord.SETRANGE("Table ID",DataExchFieldMapping."Target Table ID");
      PreviousConfigPackageRecord.SETRANGE("Package Code",TargetRapidstartPackageCode);
      IF PreviousConfigPackageRecord.FINDLAST THEN
        NewConfigPackageRecord.VALIDATE("No.",PreviousConfigPackageRecord."No." + 1)
      ELSE
        NewConfigPackageRecord.VALIDATE("No.",1);

      IF DataExchField."Parent Node ID" <> '' THEN BEGIN
        TempDataExchRapidStartBuffer.SETRANGE("Node ID",DataExchField."Parent Node ID");
        TempDataExchRapidStartBuffer.FINDFIRST;
        NewConfigPackageRecord.VALIDATE("Parent Record No.",TempDataExchRapidStartBuffer."RapidStart No.");
      END;

      NewConfigPackageRecord.INSERT;

      // Update buffer with new line
      TempDataExchRapidStartBuffer.INIT;
      TempDataExchRapidStartBuffer."Node ID" := DataExchField."Node ID";
      TempDataExchRapidStartBuffer."RapidStart No." := NewConfigPackageRecord."No.";
      TempDataExchRapidStartBuffer.INSERT;
    END;

    LOCAL PROCEDURE InsertDataValues@6(DataExchField@1003 : Record 1221;DataExchLineDef@1002 : Record 1227;TargetRapidstartPackageCode@1004 : Code[20];ConfigPackageRecord@1005 : Record 8614);
    VAR
      DataExchFieldMapping@1000 : Record 1225;
      ConfigPackageData@1001 : Record 8615;
    BEGIN
      IF DataExchField."Column No." < 1 THEN
        EXIT;

      DataExchFieldMapping.GET(
        DataExchLineDef."Data Exch. Def Code",DataExchLineDef.Code,DATABASE::"Config. Package Data",DataExchField."Column No.");

      ConfigPackageData.INIT;
      ConfigPackageData.VALIDATE("Package Code",TargetRapidstartPackageCode);
      ConfigPackageData.VALIDATE("Table ID",DataExchFieldMapping."Target Table ID");
      ConfigPackageData.VALIDATE("No.",ConfigPackageRecord."No.");
      ConfigPackageData.VALIDATE("Field ID",DataExchFieldMapping."Target Field ID");
      ConfigPackageData.VALIDATE(Value,DataExchField.Value);
      ConfigPackageData.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

