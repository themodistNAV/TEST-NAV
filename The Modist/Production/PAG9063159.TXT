OBJECT Page 9063159 Customise Role Centre Side
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Customise Role Centre Side;
               ENG=Customise Role Centre Side];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table9062776;
    SourceTableView=SORTING(Page ID,Visible,Side,Sequence);
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 CLEAR(RoleCentreControlInfo);
               END;

    OnAfterGetRecord=BEGIN
                       UpdateGlobals();
                     END;

    OnAfterGetCurrRecord=BEGIN
                           RoleCentreControlInfo := Rec;
                           UpdateGlobals();
                         END;

  }
  CONTROLS
  {
    { 9062230;0;Container ;
                ContainerType=ContentArea }

    { 9062231;1;Group     ;
                Name=Group;
                CaptionML=[ENU=Group;
                           ENG=Group];
                GroupType=Repeater }

    { 9062232;2;Field     ;
                CaptionML=[ENU=Part;
                           ENG=Part];
                SourceExpr=Caption;
                Editable=FALSE;
                StyleExpr=Style;
                OnDrillDown=BEGIN
                              SelectPanel();
                            END;
                             }

  }
  CODE
  {
    VAR
      RoleCentreControlInfo@9062230 : Record 9062776;
      SequenceToolkit@9062231 : Codeunit 9062519;
      tNoPanel@9062232 : TextConst 'ENU=Part "%1" is not a panel you can set up.;ENG=Part "%1" is not a panel you can set up.';
      Style@9062233 : Text;
      Changed@9062234 : Boolean;

    PROCEDURE SetRecords@9062231(VAR pRoleCentreControlInfo@9062230 : Record 9062776);
    BEGIN
      //DOC NA2016.11 - Setting records (adding passed recordset to Rec)
      //  <> pRoleCentreControlInfo: The records

      Rec.RESET();
      Rec.DELETEALL();

      IF (pRoleCentreControlInfo.FINDSET()) THEN
      REPEAT
        Rec := pRoleCentreControlInfo;
        Rec.INSERT();
      UNTIL (pRoleCentreControlInfo.NEXT() =  0);

      IF (Rec.FINDFIRST()) THEN;

      Changed := FALSE;
    END;

    PROCEDURE GetRecords@9062239(VAR pRoleCentreControlInfo@9062230 : Record 9062776);
    BEGIN
      //DOC NA2016.11 - Returning records (merging Rec and passed recordset)
      //  <> pRoleCentreControlInfo: The records

      Rec.RESET();
      IF (Rec.FINDSET()) THEN
      REPEAT
        IF (pRoleCentreControlInfo.GET(Rec."Page ID", Rec."Control GUID")) THEN
        BEGIN
          pRoleCentreControlInfo.TRANSFERFIELDS(Rec, FALSE);
          pRoleCentreControlInfo.MODIFY()
        END
        ELSE
        BEGIN
          pRoleCentreControlInfo := Rec;
          pRoleCentreControlInfo.INSERT();
        END;
      UNTIL (Rec.NEXT() = 0);
    END;

    PROCEDURE GetCurrentRecord@9062230(VAR pRoleCentreControlInfo@9062230 : Record 9062776);
    BEGIN
      //DOC NA2016.11 - Returning current record (currently selected)
      //  <> pRoleCentreControlInfo: Target record

      pRoleCentreControlInfo := RoleCentreControlInfo;
    END;

    PROCEDURE AddRecord@9062233(pRoleCentreControlInfo@9062230 : Record 9062776);
    BEGIN
      //DOC NA2016.11 - Adding record to Rec
      //  -> pRoleCentreControlInfo: The record

      Rec := pRoleCentreControlInfo;
      Rec.INSERT();
    END;

    PROCEDURE DeleteRecord@9062234(pRoleCentreControlInfo@9062230 : Record 9062776);
    BEGIN
      //DOC NA2016.11 - Deleting record from Rec
      //  -> pRoleCentreControlInfo: The record

      Rec.GET(pRoleCentreControlInfo."Page ID", pRoleCentreControlInfo."Control GUID");
      Rec.DELETE();
    END;

    PROCEDURE Move@9062232(pDirection@9062230 : Integer);
    VAR
      lRecordVariant@9062231 : Variant;
    BEGIN
      //DOC NA2016.11 - Moving the current record up / down
      //  -> pDirection: Direction (see SequenceToolkit.Up() and SequenceToolkit.Down() for values)

      lRecordVariant := Rec;
      SequenceToolkit.SequenceUpDown(lRecordVariant, DATABASE::"Role Centre Control Info", Rec.FIELDNO(Sequence), pDirection);
      Rec := lRecordVariant;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SelectPanel@9062235() : Boolean;
    VAR
      lPanel@9062230 : Record 9062363;
      lResult@9062231 : Boolean;
    BEGIN
      //DOC NA2016.11 - Selecting panel code for panel part
      //  <- TRUE = Panel changed, FALSE = Not

      CLEAR(lResult);
      IF (Rec."Panel Index" = 0) THEN
        ERROR(tNoPanel, Rec.Caption);
      lPanel.RESET();
      IF (Rec."Panel Code" <> '') THEN
        IF (lPanel.GET(Rec."Panel Code")) THEN;
      IF (PAGE.RUNMODAL(0, lPanel) = ACTION::LookupOK) THEN
      BEGIN
        Rec.VALIDATE("Panel Code", lPanel.Code);
        Rec.MODIFY();
        Changed := TRUE;
        lResult := TRUE;
      END;
      EXIT(lResult);
    END;

    LOCAL PROCEDURE UpdateGlobals@9062236();
    BEGIN
      //DOC NA2016.11 - Updating page globals

      Style := Rec.GetStyle();
    END;

    PROCEDURE GetChanged@9062238() : Boolean;
    BEGIN
      //DOC NA2016.11 - Returning "Changed" flag
      //  <- The flag

      EXIT(Changed);
    END;

    PROCEDURE ClearChanged@9062237();
    BEGIN
      //DOC NA2016.11 - Clearing "Changed" flag

      Changed := FALSE;
    END;

    BEGIN
    {
      //DOC NA2016.11 JH 31/10/2015 - Created
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

