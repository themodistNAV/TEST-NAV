OBJECT Table 9651 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Report ID");
               IF "Company Name" = '' THEN
                 "Company Name" := COMPANYNAME;
             END;

    CaptionML=[ENU=Report Layout Selection;
               ENG=Report Layout Selection];
  }
  FIELDS
  {
    { 1   ;   ;Report ID           ;Integer       ;CaptionML=[ENU=Report ID;
                                                              ENG=Report ID] }
    { 2   ;   ;Report Name         ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=[ENU=Report Name;
                                                              ENG=Report Name];
                                                   Editable=No }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company Name;
                                                              ENG=Company Name] }
    { 4   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Report ID");
                                                                CALCFIELDS("Report Name");
                                                                CASE Type OF
                                                                  Type::"RDLC (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasRdlcLayout("Report ID") THEN
                                                                        ERROR(NoRdlcLayoutErr,"Report Name");
                                                                      "Custom Report Layout ID" := 0;
                                                                    END;
                                                                  Type::"Word (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasWordLayout("Report ID") THEN
                                                                        ERROR(NoWordLayoutErr,"Report Name");
                                                                      "Custom Report Layout ID" := 0;
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=RDLC (built-in),Word (built-in),Custom Layout;
                                                                    ENG=RDLC (built-in),Word (built-in),Custom Layout];
                                                   OptionString=RDLC (built-in),Word (built-in),Custom Layout }
    { 6   ;   ;Custom Report Layout ID;Integer    ;TableRelation="Custom Report Layout" WHERE (Report ID=FIELD(Report ID));
                                                   OnValidate=BEGIN
                                                                IF "Custom Report Layout ID" = 0 THEN
                                                                  Type := GetDefaultType("Report ID")
                                                                ELSE
                                                                  Type := Type::"Custom Layout";
                                                              END;

                                                   CaptionML=[ENU=Custom Report Layout ID;
                                                              ENG=Custom Report Layout ID] }
    { 7   ;   ;Report Layout Description;Text80   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (ID=FIELD(Custom Report Layout ID)));
                                                   CaptionML=[ENU=Report Layout Description;
                                                              ENG=Report Layout Description] }
  }
  KEYS
  {
    {    ;Report ID,Company Name                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoRdlcLayoutErr@1000 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in RDLC layout.;ENG=Report ''%1'' has no built-in RDLC layout.';
      NoWordLayoutErr@1001 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in Word layout.;ENG=Report ''%1'' has no built-in Word layout.';

    PROCEDURE GetDefaultType@2(ReportID@1000 : Integer) : Integer;
    BEGIN
      IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::Word THEN
        EXIT(Type::"Word (built-in)");
      EXIT(Type::"RDLC (built-in)");
    END;

    PROCEDURE IsProcessingOnly@20(ReportID@1000 : Integer) : Boolean;
    BEGIN
      EXIT(REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::None);
    END;

    LOCAL PROCEDURE HasRdlcLayout@21(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.RDLCLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    PROCEDURE HasWordLayout@23(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.WORDLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    PROCEDURE HasCustomLayout@1(ReportID@1000 : Integer) : Integer;
    VAR
      CustomReportLayout@1001 : Record 9650;
    BEGIN
      // Temporarily selected layout for Design-time report execution?
      IF GetTempLayoutSelected <> 0 THEN
        IF CustomReportLayout.GET(GetTempLayoutSelected) THEN BEGIN
          IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
            EXIT(1);
          EXIT(2);
        END;

      // Normal selection
      IF NOT GET(ReportID,COMPANYNAME) THEN
        EXIT(0);
      CASE Type OF
        Type::"RDLC (built-in)":
          BEGIN
            IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::RDLC THEN
              EXIT(0);
            EXIT(1);
          END;
        Type::"Word (built-in)":
          EXIT(2);
        Type::"Custom Layout":
          BEGIN
            IF NOT CustomReportLayout.GET("Custom Report Layout ID") THEN
              EXIT(0);
            IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
              EXIT(1);
            EXIT(2);
          END;
      END;
    END;

    PROCEDURE GetTempLayoutSelected@3() : Integer;
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      EXIT(DesignTimeReportSelection.GetSelectedCustomLayout);
    END;

    PROCEDURE SetTempLayoutSelected@4(NewTempSelectedLayoutID@1001 : Integer);
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      DesignTimeReportSelection.SetSelectedCustomLayout(NewTempSelectedLayoutID);
    END;

    BEGIN
    END.
  }
}

