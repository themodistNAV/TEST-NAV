OBJECT Table 9062787 POS Button Group
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    OnDelete=VAR
               Button@9062230 : Record 9062788;
             BEGIN
               Button.RESET();
               Button.SETRANGE("Button Group Code", Code);
               Button.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=POS Button Group;
               ENG=POS Button Group];
    LookupPageID=Page9063171;
    DrillDownPageID=Page9063171;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 3   ;   ;No. of Columns      ;Integer       ;CaptionML=[ENU=No. of Columns;
                                                              ENG=No. of Columns] }
    { 4   ;   ;No. of Rows         ;Integer       ;CaptionML=[ENU=No. of Rows;
                                                              ENG=No. of Rows] }
    { 16  ;   ;Default Background Color Code;Code20;
                                                   TableRelation="System Colour";
                                                   OnValidate=VAR
                                                                SystemColor@9062231 : Record 9062336;
                                                              BEGIN
                                                                CLEAR("Default Background Color Image");
                                                                IF ("Default Background Color Code" <> '') THEN BEGIN
                                                                  SystemColor.GET("Default Background Color Code");
                                                                  SystemColor.CALCFIELDS(Bitmap);
                                                                  "Default Background Color Image" := SystemColor.Bitmap;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupBackColor();
                                                            END;

                                                   CaptionML=[ENU=Default Background Color Code;
                                                              ENG=Default Background Color Code] }
    { 17  ;   ;Default Color Code  ;Code20        ;TableRelation="System Colour";
                                                   OnValidate=VAR
                                                                SystemColor@9062230 : Record 9062336;
                                                              BEGIN
                                                                CLEAR("Default Color Image");
                                                                IF ("Default Color Code" <> '') THEN BEGIN
                                                                  SystemColor.GET("Default Color Code");
                                                                  SystemColor.CALCFIELDS(Bitmap);
                                                                  "Default Color Image" := SystemColor.Bitmap;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupTextColor();
                                                            END;

                                                   CaptionML=[ENU=Default Color Code;
                                                              ENG=Default Color Code] }
    { 20  ;   ;Default Background Color Image;BLOB;CaptionML=[ENU=Default Background Color Image;
                                                              ENG=Default Background Color Image];
                                                   SubType=Bitmap }
    { 21  ;   ;Default Color Image ;BLOB          ;CaptionML=[ENU=Default Color Image;
                                                              ENG=Default Color Image];
                                                   SubType=Bitmap }
    { 100 ;   ;For UI Part Code    ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=For UI Part Code;
                                                              ENG=For UI Part Code] }
    { 101 ;   ;For Design Mode     ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=For Design Mode;
                                                              ENG=For Design Mode] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateButtons@9062230();
    VAR
      Button@9062230 : Record 9062788;
      RowNo@9062231 : Integer;
      ColumnNo@9062232 : Integer;
      ButtonNo@9062233 : Integer;
    BEGIN
      Button.RESET();
      Button.SETRANGE("Button Group Code", Code);
      Button.DELETEALL();
      ButtonNo := 0;
      FOR RowNo := 1 TO Rec."No. of Rows" DO BEGIN
        FOR ColumnNo := 1 TO Rec."No. of Columns" DO BEGIN
          ButtonNo += 1;
          Button.INIT();
          Button."Button Group Code" := Code;
          Button."No." := ButtonNo;
          Button."Row No." := RowNo;
          Button."Column No." := ColumnNo;
          Button.INSERT();
        END;
      END;
    END;

    LOCAL PROCEDURE SelectColor@1000000000(VAR ColorCode@9062230 : Code[20]) : Boolean;
    VAR
      SystemColor@9062231 : Record 9062336;
    BEGIN
      SystemColor.RESET();
      IF (ColorCode <> '') THEN
        IF (SystemColor.GET(ColorCode)) THEN;
      IF (PAGE.RUNMODAL(PAGE::"System Colour List", SystemColor) = ACTION::LookupOK) THEN BEGIN
        ColorCode := SystemColor.Code;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE LookupTextColor@9062234();
    BEGIN
      IF (SelectColor("Default Color Code")) THEN
        VALIDATE("Default Color Code");
    END;

    PROCEDURE LookupBackColor@9062235();
    BEGIN
      IF (SelectColor("Default Background Color Code")) THEN
        VALIDATE("Default Background Color Code");
    END;

    BEGIN
    {
      //DOC NH2016.12 BM 11/02/2016 - Created
      //DOC NH2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

