OBJECT Codeunit 9062306 Planning Board Menu Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=10/11/15;
    Time=11:29:29 PM;
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PlanningBoardHeader@1000000002 : Record 9062337;
      PlanningBoardMenuItem@1000000000 : Record 9062343;
      PlanningBoardAction@9062230 : Record 9062344;
      TableType@1000000001 : Integer;

    PROCEDURE ProposeMenuItems@1000000000(pPlanningBoardHeader@1000000000 : Record 9062337);
    BEGIN
      //DOC NA2016.12 - Proposing menu items for given planning board
      //  -> pPlanningBoardHeader: The planning board

      PlanningBoardHeader := pPlanningBoardHeader;

      TableType := PlanningBoardMenuItem."Planning Board Table Type"::" ";
      AddItem(PlanningBoardAction."Action Type"::"New Entry");
      AddSeparator();
      AddItem(PlanningBoardAction."Action Type"::Refresh);

      TableType := PlanningBoardMenuItem."Planning Board Table Type"::Resource;
      AddItem(PlanningBoardAction."Action Type"::"View Resource");
      AddItem(PlanningBoardAction."Action Type"::"Edit Resource");
      AddSeparator();
      AddItem(PlanningBoardAction."Action Type"::Refresh);

      TableType := PlanningBoardMenuItem."Planning Board Table Type"::Entry;
      AddItem(PlanningBoardAction."Action Type"::"View Entry");
      AddItem(PlanningBoardAction."Action Type"::"Edit Entry");
      AddItem(PlanningBoardAction."Action Type"::"Delete Entry");
      AddSeparator();
      AddItem(PlanningBoardAction."Action Type"::Refresh);
    END;

    LOCAL PROCEDURE AddItem@1000000002(pActivity@1000000002 : Integer);
    BEGIN
      //DOC NA2016.12 - Adding (menu) item
      //  -> pActivity: Activity

      AddMenuItem(PlanningBoardMenuItem.Type::Item, pActivity);
    END;

    LOCAL PROCEDURE AddSeparator@1000000005();
    BEGIN
      //DOC NA2016.12 - Adding a separator

      AddMenuItem(PlanningBoardMenuItem.Type::Separator, PlanningBoardAction."Action Type"::" ");
    END;

    LOCAL PROCEDURE AddMenuItem@1000000006(pType@1000000003 : Integer;pActionType@9062230 : Integer);
    BEGIN
      //DOC NA2016.12 - Proposing one menu item
      //  -> pType: Menu item type (item / separator)
      //  -> pActionType: Action type

      PlanningBoardAction."Action Type" := pActionType;

      CLEAR(PlanningBoardMenuItem);
      PlanningBoardMenuItem.INIT();
      PlanningBoardMenuItem."Planning Board Code" := PlanningBoardHeader.Code;
      PlanningBoardMenuItem.VALIDATE("Planning Board Table Type", TableType);
      PlanningBoardMenuItem.VALIDATE(Type, pType);
      PlanningBoardMenuItem.VALIDATE(Caption, FORMAT(PlanningBoardAction."Action Type"));
      PlanningBoardMenuItem.INSERT(TRUE);

      CLEAR(PlanningBoardAction);
      PlanningBoardAction.INIT();
      PlanningBoardAction."Planning Board Code" := PlanningBoardHeader.Code;
      PlanningBoardAction.VALIDATE("Parent Table", PlanningBoardAction."Parent Table"::"Planning Board Menu Item");
      PlanningBoardAction.VALIDATE("Parent Table Type", PlanningBoardMenuItem."Planning Board Table Type");
      PlanningBoardAction.VALIDATE("Parent ID", PlanningBoardMenuItem.ID);
      PlanningBoardAction.VALIDATE("Action Type", pActionType);
      PlanningBoardAction.INSERT(TRUE);
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 11/10/2015 - Created
    }
    END.
  }
}

