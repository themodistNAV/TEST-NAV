OBJECT Report 1130 Delete Cost Entries
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm;
    CaptionML=[ENU=Delete Cost Entries;
               ENG=Delete Cost Entries];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  CostRegister@1000 : Record 1105;
                BEGIN
                  IF CostRegister2."No." > CostRegister3."No." THEN
                    ERROR(Text000);

                  IF NOT CONFIRM(Text001,FALSE,CostRegister2."No.",CostRegister3."No.") THEN
                    ERROR('');

                  IF NOT CONFIRM(Text004) THEN
                    ERROR('');

                  CostRegister.FINDLAST;
                  IF CostRegister."No." > CostRegister3."No." THEN
                    ERROR(STRSUBSTNO(CostRegisterHasBeenModifiedErr,CostRegister."No."));

                  Window.OPEN(Text005 +
                    Text006 );
                END;

  }
  DATASET
  {
    { 5327;    ;DataItem;                    ;
               DataItemTable=Table1105;
               DataItemTableView=SORTING(No.)
                                 ORDER(Descending);
               OnPreDataItem=BEGIN
                               SETRANGE("No.",CostRegister2."No.",CostRegister3."No.");
                             END;

               OnAfterGetRecord=VAR
                                  CostEntry@1000 : Record 1104;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  IF Closed THEN
                                    ERROR(Text007,"No.");

                                  IF Source = Source::Allocation THEN BEGIN
                                    CostEntry.SETCURRENTKEY("Allocated with Journal No.");
                                    CostEntry.SETRANGE("Allocated with Journal No.","No.");
                                    CostEntry.MODIFYALL(Allocated,FALSE);
                                    CostEntry.MODIFYALL("Allocated with Journal No.",0);
                                  END;

                                  CostEntry.RESET;
                                  CostEntry.SETRANGE("Entry No.","From Cost Entry No.","To Cost Entry No.");
                                  CostEntry.DELETEALL;
                                END;

               OnPostDataItem=VAR
                                CostEntry@1000 : Record 1104;
                              BEGIN
                                DELETEALL;
                                RESET;
                                SETRANGE(Source,Source::Allocation);
                                IF FINDLAST THEN BEGIN
                                  CostEntry.GET("To Cost Entry No.");
                                  CostAccSetup.GET;
                                  CostAccSetup."Last Allocation Doc. No." := CostEntry."Document No.";
                                  CostAccSetup.MODIFY;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CostRegister2.FINDLAST;
                   CostRegister3.FINDLAST;
                 END;

    }
    CONTROLS
    {
      { 1   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 3   ;2   ;Field     ;
                  Name=FromRegisterNo;
                  Lookup=Yes;
                  CaptionML=[ENU=From Register No.;
                             ENG=From Register No.];
                  SourceExpr=CostRegister2."No.";
                  TableRelation="Cost Register" WHERE (Closed=CONST(No)) }

      { 4   ;2   ;Field     ;
                  Name=ToRegisterNo;
                  CaptionML=[ENU=To Register No.;
                             ENG=To Register No.];
                  SourceExpr=CostRegister3."No.";
                  TableRelation="Cost Register" WHERE (Closed=CONST(No));
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=From Register No. must not be higher than To Register No..;ENG=From Register No. must not be higher than To Register No..';
      Text001@1001 : TextConst 'ENU=All corresponding cost entries and register entries will be deleted. Do you want to delete cost register %1 to %2?;ENG=All corresponding cost entries and register entries will be deleted. Do you want to delete cost register %1 to %2?';
      Text004@1002 : TextConst 'ENU=Are you sure?;ENG=Are you sure?';
      Text005@1003 : TextConst 'ENU=Delete cost register\;ENG=Delete cost register\';
      Text006@1004 : TextConst 'ENU=Register  no.      #1######;ENG=Register  no.      #1######';
      Text007@1005 : TextConst 'ENU=Register %1 can no longer be deleted because it is marked as closed.;ENG=Register %1 can no longer be deleted because it is marked as closed.';
      CostRegister2@1006 : Record 1105;
      CostRegister3@1007 : Record 1105;
      CostAccSetup@1010 : Record 1108;
      Window@1011 : Dialog;
      CostRegisterHasBeenModifiedErr@1008 : TextConst 'ENU=Another user has modified the cost register. The To Register No. field must be equal to %1.\Run the Delete Cost Entries batch job again.;ENG=Another user has modified the cost register. The To Register No. field must be equal to %1.\Run the Delete Cost Entries batch job again.';

    PROCEDURE InitializeRequest@1(NewFromRegisterNo@1000 : Integer;NewToRegisterNo@1001 : Integer);
    BEGIN
      CostRegister2."No." := NewFromRegisterNo;
      CostRegister3."No." := NewToRegisterNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

