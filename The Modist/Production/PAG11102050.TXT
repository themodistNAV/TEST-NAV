OBJECT Page 11102050 OM - Assign Modifications
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Assign Modifications;
               ENU=Assign Modifications;
               ESP=Assign Modifications;
               FRA=Assign Modifications;
               NLD=Wijzigingen toewijzen;
               DEA=Assign Modifications];
    SaveValues=Yes;
    SourceTable=Table11102042;
    SourceTableView=SORTING(Status,Object Type,Object No.,Insert Date,Insert Time)
                    WHERE(Status=CONST(Inserted));
    PageType=Worksheet;
    ShowFilter=Yes;
    OnOpenPage=VAR
                 Mgt@1000 : Codeunit 11102035;
                 AssignModificationsMgt@1001 : Codeunit 11102036;
                 ProcessNewModifications@1002 : Codeunit 11102039;
                 UserIdFilter@1003 : Text;
               BEGIN
                 AssignModificationsMgt.RUN;
                 ProcessNewModifications.OnOpenPage2;

                 GetSettings;

                 IF PassedActiveProject <> '' THEN
                   ProjectNo := PassedActiveProject;

                 IF AssignType = AssignType::"Active Project" THEN
                   ProjectNo := ActiveProjectNo;

                 IF NOT ShowAll THEN BEGIN
                   UserIdFilter := Mgt.CapitalizedUserId;
                   IF STRPOS(UserIdFilter, '\') > 0 THEN
                     UserIdFilter := COPYSTR(UserIdFilter, STRPOS(UserIdFilter, '\') + 1);
                   SETRANGE("Inserted By", UserIdFilter);
                 END;
               END;

    OnAfterGetCurrRecord=BEGIN
                           GetSettings;
                         END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 2       ;1   ;ActionGroup;
                      CaptionML=[DEU=&Object;
                                 ENU=&Object;
                                 ESP=&Object;
                                 FRA=&Object;
                                 NLD=&Object;
                                 DEA=&Object] }
      { 3       ;2   ;Action    ;
                      ShortCutKey=Ctrl+T;
                      CaptionML=[DEU=Projects;
                                 ENU=Projects;
                                 ESP=Projects;
                                 FRA=Projects;
                                 NLD=Projecten;
                                 DEA=Projects];
                      RunObject=Page 11102042;
                      RunPageView=SORTING(Object Type,Object No.);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Image=Journals }
      { 4       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+O;
                      CaptionML=[DEU=Modifications;
                                 ENU=Modifications;
                                 ESP=Modifications;
                                 FRA=Modifications;
                                 NLD=Wijzigingen;
                                 DEA=Modifications];
                      RunObject=Page 11102049;
                      RunPageView=SORTING(Object Type,Object No.,Insert Date,Insert Time);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditList;
                      PromotedCategory=Process }
      { 5       ;2   ;Separator  }
      { 6       ;2   ;Action    ;
                      ShortCutKey=Ctrl+Alt+C;
                      CaptionML=[DEU=Last Changes;
                                 ENU=Last Changes;
                                 ESP=Last Changes;
                                 FRA=Last Changes;
                                 NLD=Laatste wijzigingen;
                                 DEA=Last Changes];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ShowMatrix;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 RightCALHistoryObject@1001 : Record 11102052;
                                 SourceControl@1002 : Codeunit 11102050;
                                 ctError@1003 : TextConst 'DEU=There is only one version of the C/AL code present.;ENU=There is only one version of the C/AL code present.;ESP=There is only one version of the C/AL code present.;FRA=There is only one version of the C/AL code present.;NLD=Er is maar ‚‚n versie van de C/AL code aanwezig.;DEA=There is only one version of the C/AL code present.';
                               BEGIN
                                 CALHistoryObject.GoToLastRecord("Object Type", "Object No.");
                                 RightCALHistoryObject := CALHistoryObject;

                                 IF CALHistoryObject.NEXT(-1) = 0 THEN
                                   ERROR(ctError);

                                 SourceControl.Compare(CALHistoryObject, RightCALHistoryObject);
                               END;
                                }
      { 7       ;2   ;Separator  }
      { 8       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F2;
                      CaptionML=[DEU=Design;
                                 ENU=Design;
                                 ESP=Design;
                                 FRA=Design;
                                 NLD=Design;
                                 DEA=Design];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Design;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.DesignObject(Object);
                               END;
                                }
      { 9       ;2   ;Action    ;
                      ShortCutKey=Ctrl+R;
                      CaptionML=[DEU=Run;
                                 ENU=Run;
                                 ESP=Run;
                                 FRA=Run;
                                 NLD=Run;
                                 DEA=Run];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.RunObject(Object);
                               END;
                                }
      { 43      ;2   ;Action    ;
                      CaptionML=[DEU=Navigate;
                                 ENU=Navigate;
                                 ESP=Navigate;
                                 FRA=Navigate;
                                 NLD=Navigeren;
                                 DEA=Navigate];
                      Image=Navigate;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.Navigate(Object);
                               END;
                                }
      { 10      ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+H;
                      CaptionML=[DEU=C/AL &History;
                                 ENU=C/AL &History;
                                 ESP=C/AL &History;
                                 FRA=C/AL &History;
                                 NLD=C/AL Historie;
                                 DEA=C/AL &History];
                      RunObject=Page 11102072;
                      RunPageView=SORTING(Object Type,Object No.);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=History;
                      PromotedCategory=Process }
      { 11      ;2   ;Separator  }
      { 12      ;2   ;ActionGroup;
                      CaptionML=[DEU=Mark &Objects;
                                 ENU=Mark &Objects;
                                 ESP=Mark &Objects;
                                 FRA=Mark &Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark &Objects];
                      Image=EditFilter }
      { 13      ;3   ;Action    ;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter;
                      OnAction=VAR
                                 Modification@1000 : Record 11102042;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.CheckMarkLock;

                                 CurrPage.SETSELECTIONFILTER(Modification);
                                 IF Modification.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(Modification."Object Type", '', Modification."Object No.") THEN
                                       VersionControl.MarkObject(Object, TRUE);
                                   UNTIL Modification.NEXT = 0;
                               END;
                                }
      { 14      ;3   ;Action    ;
                      CaptionML=[DEU=Remove Marks;
                                 ENU=Remove Marks;
                                 ESP=Remove Marks;
                                 FRA=Remove Marks;
                                 NLD=Verwijder markeringen;
                                 DEA=Remove Marks];
                      Image=RemoveFilterLines;
                      OnAction=VAR
                                 Modification@1000 : Record 11102042;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Modification);
                                 IF Modification.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(Modification."Object Type", '', Modification."Object No.") THEN
                                       VersionControl.RemoveObjectMark(Object);
                                   UNTIL Modification.NEXT = 0;

                                 VersionControl.RemoveMarkLock(TRUE);
                               END;
                                }
      { 15      ;3   ;Action    ;
                      ShortCutKey=Shift+Ctrl+M;
                      CaptionML=[DEU=Remove &all Marks;
                                 ENU=Remove &all Marks;
                                 ESP=Remove &all Marks;
                                 FRA=Remove &all Marks;
                                 NLD=Verwijder &alle markeringen;
                                 DEA=Remove &all Marks];
                      Image=ClearFilter;
                      OnAction=VAR
                                 VersionControl@1000 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.RemoveObjectMarks(TRUE);
                               END;
                                }
      { 16      ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 17      ;1   ;Action    ;
                      CaptionML=[DEU=&Assign;
                                 ENU=&Assign;
                                 ESP=&Assign;
                                 FRA=&Assign;
                                 NLD=&Toewijzen;
                                 DEA=&Assign];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Trace;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Assign;
                               END;
                                }
      { 18      ;1   ;Action    ;
                      ShortCutKey=Ctrl+I;
                      CaptionML=[DEU=&Ignore;
                                 ENU=&Ignore;
                                 ESP=&Ignore;
                                 FRA=&Ignore;
                                 NLD=&Negeren;
                                 DEA=&Ignore];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Ignore;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 19  ;0   ;Container ;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                CaptionML=[DEU=Open modifications;
                           ENU=Open modifications;
                           ESP=Open modifications;
                           FRA=Open modifications;
                           NLD=Openstaande wijzigingen;
                           DEA=Open modifications];
                Editable=FALSE;
                GroupType=Repeater }

    { 21  ;2   ;Field     ;
                SourceExpr="Inserted By" }

    { 22  ;2   ;Field     ;
                SourceExpr="SQL Trigger" }

    { 23  ;2   ;Field     ;
                SourceExpr="Object Type" }

    { 24  ;2   ;Field     ;
                SourceExpr="Object No.";
                LookupPageID=OM - Object List }

    { 25  ;2   ;Field     ;
                SourceExpr="Object Name" }

    { 26  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Object Caption";
                Visible=FALSE }

    { 27  ;2   ;Field     ;
                SourceExpr="Object Date" }

    { 28  ;2   ;Field     ;
                SourceExpr="Object Time" }

    { 29  ;1   ;Group     ;
                CaptionML=[DEU=Assign;
                           ENU=Assign;
                           ESP=Assign;
                           FRA=Assign;
                           NLD=Toewijzen;
                           DEA=Assign];
                GroupType=Group }

    { 30  ;2   ;Group     ;
                GroupType=Group }

    { 31  ;3   ;Field     ;
                CaptionML=[DEU=Add Project Tag;
                           ENU=Add Project Tag;
                           ESP=Add Project Tag;
                           FRA=Add Project Tag;
                           NLD=Projectkenmerk toevoegen;
                           DEA=Add Project Tag];
                SourceExpr=AddProjectTag }

    { 32  ;3   ;Field     ;
                CaptionML=[DEU=Description;
                           ENU=Description;
                           ESP=Description;
                           FRA=Description;
                           NLD=Omschrijving;
                           DEA=Description];
                SourceExpr=ProjectTagDescription;
                Enabled=AddProjectTag }

    { 33  ;3   ;Field     ;
                CaptionML=[DEU=Preview;
                           ENU=Preview;
                           ESP=Preview;
                           FRA=Preview;
                           NLD=Voorbeeld;
                           DEA=Preview];
                SourceExpr=FullProjectTag;
                Enabled=AddProjectTag;
                QuickEntry=FALSE }

    { 34  ;2   ;Group     ;
                GroupType=Group }

    { 35  ;3   ;Field     ;
                CaptionML=[DEU=Assign to Project No.;
                           ENU=Assign to Project No.;
                           ESP=Assign to Project No.;
                           FRA=Assign to Project No.;
                           NLD=Toewijzen aan projectnr.;
                           DEA=Assign to Project No.];
                SourceExpr=ProjectNo;
                TableRelation="OM - Project";
                OnValidate=BEGIN
                             IF AssignType = AssignType::"Active Project" THEN BEGIN
                               ActiveProjectNo := ProjectNo;
                               SetSettings;
                             END;

                             ValidateProjectNo;

                             CurrPage.UPDATE(TRUE);
                           END;

                OnLookup=VAR
                           Project@1000 : Record 11102036;
                         BEGIN
                           //Project.SETRANGE("Status Type", Project."Status Type"::Developing);
                           IF PAGE.RUNMODAL(PAGE::"OM - Projects to Assign", Project) = ACTION::LookupOK THEN BEGIN
                             Text := Project."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 36  ;3   ;Field     ;
                CaptionML=[DEU=Active Project;
                           ENU=Active Project;
                           ESP=Active Project;
                           FRA=Active Project;
                           NLD=Actief project;
                           DEA=Active Project];
                SourceExpr=AssignTypeActiveProject;
                OnValidate=VAR
                             Project@1000 : Record 11102036;
                           BEGIN
                             IF AssignTypeActiveProject THEN BEGIN
                               IF ProjectNo = '' THEN
                                 Project.TESTFIELD("No.");
                               ActiveProjectNo := ProjectNo;
                               AssignType := AssignType::"Active Project";
                             END ELSE
                               AssignType := AssignType::Save;
                             SetSettings;

                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 37  ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 38  ;1   ;Part      ;
                PagePartID=Page11175795;
                PartType=Page }

    { 39  ;1   ;Part      ;
                SubPageLink=Object Type=FIELD(Object Type),
                            Object No.=FIELD(Object No.);
                PagePartID=Page11175794;
                PartType=Page }

    { 40  ;1   ;Part      ;
                SubPageLink=No.=FIELD(Project No. Filter);
                PagePartID=Page11175796;
                PartType=Page }

    { 41  ;1   ;Part      ;
                PartType=System;
                SystemPartID=RecordLinks }

    { 42  ;1   ;Part      ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      TmpObjectLock@1000 : TEMPORARY Record 11102054;
      ProjectTagDescription@1001 : Text;
      ActiveProjectNo@1002 : Code[20];
      PassedActiveProject@1003 : Code[20];
      ProjectNo@1004 : Code[20];
      AssignType@1005 : 'Save,,Active Project';
      AddProjectTag@1006 : Boolean;
      AssignTypeActiveProject@1007 : Boolean;
      LockRangeSet@1008 : Boolean;
      ShowAll@1009 : Boolean;

    LOCAL PROCEDURE Assign@1();
    VAR
      Project@1000 : Record 11102036;
      Modification@1001 : Record 11102042;
      TmpObject@1002 : TEMPORARY Record 2000000001;
      AssignModificationsMgt@1003 : Codeunit 11102036;
      VersionControl@1004 : Codeunit 11102040;
      ctNoProjectTag@1005 : TextConst 'DEU=Please enter a project tag.;ENU=Please enter a project tag.;ESP=Please enter a project tag.;FRA=Please enter a project tag.;NLD=Geen projectkenmerk opgegeven.;DEA=Please enter a project tag.';
    BEGIN
      MODIFY;
      CurrPage.SETSELECTIONFILTER(Modification);

      IF AddProjectTag THEN
        IF ProjectTagDescription = '' THEN
          ERROR(ctNoProjectTag);

      IF ProjectNo = '' THEN
        Project.TESTFIELD("No.");

      Project.GET(ProjectNo);

      IF Project.AssignCancelled THEN
        EXIT;

      IF AssignModificationsMgt.CheckObjectsCompiled(Modification) THEN BEGIN

        IF Modification.FINDSET THEN
          REPEAT

            IF AddProjectTag THEN
              IF NOT TmpObject.GET(Modification."Object Type", '', Modification."Object No.") THEN BEGIN
                TmpObject.Type := Modification."Object Type";
                TmpObject.ID := Modification."Object No.";
                TmpObject.INSERT;
              END;

          UNTIL Modification.NEXT = 0;

        AssignModificationsMgt.AssignModificationsToProject(Modification, Project."No.");

      END;

      IF AddProjectTag THEN
        IF TmpObject.FINDSET THEN
          REPEAT
            VersionControl.AddProjectTag(TmpObject, Project, FullProjectTag);
          UNTIL TmpObject.NEXT = 0;

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Ignore@2();
    VAR
      Setup@1000 : Record 11102035;
      Modification@1001 : Record 11102042;
      Modification2@1002 : Record 11102042;
      Object@1003 : Record 2000000001;
      ObjectMgt@1004 : Codeunit 11102046;
    BEGIN
      CurrPage.SETSELECTIONFILTER(Modification);

      Setup.CustomGet;
      IF Modification.FINDSET THEN
        REPEAT

          Modification2 := Modification;

          IF Modification2.Status = Modification2.Status::Inserted THEN BEGIN
            Modification2.VALIDATE(Status, Status::Ignored);
            Modification2.MODIFY(TRUE);
          END;

          IF Setup."Set Modify Flag to False" THEN
            IF Object.GET(Modification2."Object Type", '', Modification2."Object No.") THEN
              IF Object.Modified THEN BEGIN
                Object.Modified := FALSE;
                ObjectMgt.UpdateObject(Object, FALSE);
              END;

        UNTIL Modification.NEXT = 0;

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetActiveProject@3(NewValue@1000 : Code[20]);
    BEGIN
      PassedActiveProject := NewValue;
    END;

    PROCEDURE SetLockRange@4(VAR ObjectLock@1000 : Record 11102054);
    BEGIN
      LockRangeSet := TRUE;
      IF ObjectLock.FINDSET THEN
        REPEAT
          TmpObjectLock := ObjectLock;
          TmpObjectLock.INSERT;
        UNTIL ObjectLock.NEXT = 0;
    END;

    LOCAL PROCEDURE GetSettings@5();
    VAR
      AssignModificationsMgt@1000 : Codeunit 11102036;
    BEGIN
      AssignModificationsMgt.GetSettings(AssignType, ActiveProjectNo);
      AssignTypeActiveProject := AssignType = AssignType::"Active Project";

      IF AssignType = AssignType::"Active Project" THEN
        ProjectNo := ActiveProjectNo;
    END;

    LOCAL PROCEDURE SetSettings@6();
    VAR
      AssignModificationsMgt@1000 : Codeunit 11102036;
    BEGIN
      AssignModificationsMgt.SetSettings(AssignType, ActiveProjectNo, TRUE);
    END;

    LOCAL PROCEDURE FullProjectTag@7() : Text;
    VAR
      Project@1000 : Record 11102036;
      VersionControl@1001 : Codeunit 11102040;
    BEGIN
      IF Project.GET(ProjectNo) THEN
        EXIT(VersionControl.ProjectTagPreview(Project, ProjectTagDescription));
    END;

    LOCAL PROCEDURE ValidateProjectNo@8();
    BEGIN
      SETRANGE("Project No. Filter", ProjectNo);
    END;

    PROCEDURE SetShowAll@9(NewValue@1000 : Boolean);
    BEGIN
      ShowAll := NewValue;
    END;

    BEGIN
    END.
  }
}

