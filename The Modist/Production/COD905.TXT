OBJECT Codeunit 905 Assembly Line Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 901=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1002 : TextConst 'ENU=Do you want to update the %1 on the lines?;ENG=Do you want to update the %1 on the lines?';
      Text002@1003 : TextConst 'ENU=Do you want to update the Dimensions on the lines?;ENG=Do you want to update the Dimensions on the lines?';
      Text003@1001 : TextConst 'ENU=Changing %1 will change all the lines. Do you want to change the %1 from %2 to %3?;ENG=Changing %1 will change all the lines. Do you want to change the %1 from %2 to %3?';
      WarningModeOff@1000 : Boolean;
      Text004@1004 : TextConst 'ENU=This assembly order may have customized lines. Are you sure that you want to reset the lines according to the assembly BOM?;ENG=This assembly order may have customised lines. Are you sure that you want to reset the lines according to the assembly BOM?';
      Text005@1005 : TextConst 'ENU=Due Date %1 is before work date %2 in one or more of the assembly lines.;ENG=Due Date %1 is before work date %2 in one or more of the assembly lines.';
      Text006@1006 : TextConst 'ENU=Item %1 is not a BOM.;ENG=Item %1 is not a BOM.';
      Text007@1007 : TextConst 'ENU=There is not enough space to explode the BOM.;ENG=There is not enough space to explode the BOM.';

    LOCAL PROCEDURE LinesExist@3(AsmHeader@1001 : Record 900) : Boolean;
    VAR
      AssemblyLine@1000 : Record 901;
    BEGIN
      SetLinkToLines(AsmHeader,AssemblyLine);
      EXIT(NOT AssemblyLine.ISEMPTY);
    END;

    LOCAL PROCEDURE SetLinkToLines@29(AsmHeader@1001 : Record 900;VAR AssemblyLine@1000 : Record 901);
    BEGIN
      AssemblyLine.SETRANGE("Document Type",AsmHeader."Document Type");
      AssemblyLine.SETRANGE("Document No.",AsmHeader."No.");
    END;

    LOCAL PROCEDURE SetLinkToItemLines@12(AsmHeader@1001 : Record 900;VAR AssemblyLine@1000 : Record 901);
    BEGIN
      SetLinkToLines(AsmHeader,AssemblyLine);
      AssemblyLine.SETRANGE(Type,AssemblyLine.Type::Item);
    END;

    LOCAL PROCEDURE SetLinkToBOM@32(AsmHeader@1001 : Record 900;VAR BOMComponent@1000 : Record 90);
    BEGIN
      BOMComponent.SETRANGE("Parent Item No.",AsmHeader."Item No.");
    END;

    PROCEDURE GetNextAsmLineNo@6(VAR AsmLine@1000 : Record 901;AsmLineRecordIsTemporary@1002 : Boolean) : Integer;
    VAR
      TempAssemblyLine2@1001 : TEMPORARY Record 901;
      AssemblyLine2@1003 : Record 901;
    BEGIN
      IF AsmLineRecordIsTemporary THEN BEGIN
        TempAssemblyLine2.COPY(AsmLine,TRUE);
        TempAssemblyLine2.SETRANGE("Document Type",AsmLine."Document Type");
        TempAssemblyLine2.SETRANGE("Document No.",AsmLine."Document No.");
        IF TempAssemblyLine2.FINDLAST THEN
          EXIT(TempAssemblyLine2."Line No." + 10000);
      END ELSE BEGIN
        AssemblyLine2.SETRANGE("Document Type",AsmLine."Document Type");
        AssemblyLine2.SETRANGE("Document No.",AsmLine."Document No.");
        IF AssemblyLine2.FINDLAST THEN
          EXIT(AssemblyLine2."Line No." + 10000);
      END;
      EXIT(10000);
    END;

    LOCAL PROCEDURE InsertAsmLine@11(AsmHeader@1002 : Record 900;VAR AssemblyLine@1001 : Record 901;AsmLineRecordIsTemporary@1000 : Boolean);
    BEGIN
      WITH AsmHeader DO BEGIN
        AssemblyLine.INIT;
        AssemblyLine."Document Type" := "Document Type";
        AssemblyLine."Document No." := "No.";
        AssemblyLine."Line No." := GetNextAsmLineNo(AssemblyLine,AsmLineRecordIsTemporary);
        AssemblyLine.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE AddBOMLine2@35(AsmHeader@1004 : Record 900;VAR AssemblyLine@1000 : Record 901;AsmLineRecordIsTemporary@1002 : Boolean;BomComponent@1001 : Record 90;ShowDueDateBeforeWorkDateMessage@1003 : Boolean);
    VAR
      DueDateBeforeWorkDateMsgShown@1005 : Boolean;
      SkipVerificationsThatChangeDatabase@1006 : Boolean;
    BEGIN
      WITH AsmHeader DO BEGIN
        SkipVerificationsThatChangeDatabase := AsmLineRecordIsTemporary;
        AssemblyLine.SetSkipVerificationsThatChangeDatabase(SkipVerificationsThatChangeDatabase);
        AssemblyLine.VALIDATE(Type,BomComponent.Type);
        AssemblyLine.VALIDATE("No.",BomComponent."No.");
        IF AssemblyLine.Type = AssemblyLine.Type::Resource THEN
          CASE BomComponent."Resource Usage Type" OF
            BomComponent."Resource Usage Type"::Direct:
              AssemblyLine.VALIDATE("Resource Usage Type",AssemblyLine."Resource Usage Type"::Direct);
            BomComponent."Resource Usage Type"::Fixed:
              AssemblyLine.VALIDATE("Resource Usage Type",AssemblyLine."Resource Usage Type"::Fixed);
          END;
        AssemblyLine.VALIDATE("Unit of Measure Code",BomComponent."Unit of Measure Code");
        IF AssemblyLine.Type <> AssemblyLine.Type::" " THEN
          AssemblyLine.VALIDATE(
            "Quantity per",
            AssemblyLine.CalcQuantityFromBOM(
              BomComponent.Type,BomComponent."Quantity per",1,"Qty. per Unit of Measure",AssemblyLine."Resource Usage Type"));
        AssemblyLine.VALIDATE(
          Quantity,
          AssemblyLine.CalcQuantityFromBOM(
            BomComponent.Type,BomComponent."Quantity per",Quantity,"Qty. per Unit of Measure",AssemblyLine."Resource Usage Type"));
        AssemblyLine.VALIDATE(
          "Quantity to Consume",
          AssemblyLine.CalcQuantityFromBOM(
            BomComponent.Type,BomComponent."Quantity per","Quantity to Assemble","Qty. per Unit of Measure",
            AssemblyLine."Resource Usage Type"));
        AssemblyLine.ValidateDueDate(AsmHeader,"Starting Date",ShowDueDateBeforeWorkDateMessage);
        DueDateBeforeWorkDateMsgShown := (AssemblyLine."Due Date" < WORKDATE) AND ShowDueDateBeforeWorkDateMessage;
        AssemblyLine.ValidateLeadTimeOffset(
          AsmHeader,BomComponent."Lead-Time Offset",NOT DueDateBeforeWorkDateMsgShown AND ShowDueDateBeforeWorkDateMessage);
        AssemblyLine.Description := BomComponent.Description;
        AssemblyLine."Description 2" := "Description 2";
        IF AssemblyLine.Type = AssemblyLine.Type::Item THEN
          AssemblyLine.VALIDATE("Variant Code",BomComponent."Variant Code");
        AssemblyLine.Position := BomComponent.Position;
        AssemblyLine."Position 2" := BomComponent."Position 2";
        AssemblyLine."Position 3" := BomComponent."Position 3";
        IF "Location Code" <> '' THEN
          IF AssemblyLine.Type = AssemblyLine.Type::Item THEN
            AssemblyLine.VALIDATE("Location Code","Location Code");
        AssemblyLine.MODIFY(TRUE);
      END;
    END;

    PROCEDURE AddBOMLine@10(AsmHeader@1005 : Record 900;VAR AssemblyLine@1004 : Record 901;BomComponent@1002 : Record 90);
    BEGIN
      InsertAsmLine(AsmHeader,AssemblyLine,FALSE);
      AddBOMLine2(AsmHeader,AssemblyLine,FALSE,BomComponent,GetWarningMode);
    END;

    PROCEDURE ExplodeAsmList@13(VAR AsmLine@1000 : Record 901);
    VAR
      AssemblyHeader@1001 : Record 900;
      FromBOMComp@1002 : Record 90;
      ToAssemblyLine@1004 : Record 901;
      TempAssemblyLine@1008 : TEMPORARY Record 901;
      NoOfBOMCompLines@1003 : Integer;
      LineSpacing@1005 : Integer;
      NextLineNo@1006 : Integer;
      DueDateBeforeWorkDate@1009 : Boolean;
      NewLineDueDate@1010 : Date;
    BEGIN
      WITH AsmLine DO BEGIN
        TESTFIELD(Type,Type::Item);
        TESTFIELD("Consumed Quantity",0);
        CALCFIELDS("Reserved Qty. (Base)");
        TESTFIELD("Reserved Qty. (Base)",0);

        AssemblyHeader.GET("Document Type","Document No.");
        FromBOMComp.SETRANGE("Parent Item No.","No.");
        NoOfBOMCompLines := FromBOMComp.COUNT;
        IF NoOfBOMCompLines = 0 THEN
          ERROR(Text006,"No.");

        ToAssemblyLine.RESET;
        ToAssemblyLine.SETRANGE("Document Type","Document Type");
        ToAssemblyLine.SETRANGE("Document No.","Document No.");
        ToAssemblyLine := AsmLine;
        LineSpacing := 10000;
        IF ToAssemblyLine.FIND('>') THEN BEGIN
          LineSpacing := (ToAssemblyLine."Line No." - "Line No.") DIV (1 + NoOfBOMCompLines);
          IF LineSpacing = 0 THEN
            ERROR(Text007);
        END;

        TempAssemblyLine := AsmLine;
        TempAssemblyLine.INIT;
        TempAssemblyLine.Description := Description;
        TempAssemblyLine."Description 2" := "Description 2";
        TempAssemblyLine."No." := "No.";
        TempAssemblyLine.INSERT;

        NextLineNo := "Line No.";
        FromBOMComp.FINDSET;
        REPEAT
          TempAssemblyLine.INIT;
          TempAssemblyLine."Document Type" := "Document Type";
          TempAssemblyLine."Document No." := "Document No.";
          NextLineNo := NextLineNo + LineSpacing;
          TempAssemblyLine."Line No." := NextLineNo;
          TempAssemblyLine.INSERT(TRUE);
          AddBOMLine2(AssemblyHeader,TempAssemblyLine,TRUE,FromBOMComp,FALSE);
          TempAssemblyLine.Quantity := TempAssemblyLine.Quantity * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Quantity (Base)" := TempAssemblyLine."Quantity (Base)" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Quantity per" := TempAssemblyLine."Quantity per" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Remaining Quantity" := TempAssemblyLine."Remaining Quantity" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Remaining Quantity (Base)" :=
            TempAssemblyLine."Remaining Quantity (Base)" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Quantity to Consume" :=
            TempAssemblyLine."Quantity to Consume" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Quantity to Consume (Base)" :=
            TempAssemblyLine."Quantity to Consume (Base)" * "Quantity per" * "Qty. per Unit of Measure";
          TempAssemblyLine."Cost Amount" := TempAssemblyLine."Unit Cost" * TempAssemblyLine.Quantity;
          TempAssemblyLine."Dimension Set ID" := "Dimension Set ID";
          TempAssemblyLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
          TempAssemblyLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
          TempAssemblyLine.MODIFY(TRUE);
        UNTIL FromBOMComp.NEXT = 0;

        TempAssemblyLine.RESET;
        TempAssemblyLine.FINDSET;
        ToAssemblyLine := TempAssemblyLine;
        ToAssemblyLine.MODIFY;
        WHILE TempAssemblyLine.NEXT <> 0 DO BEGIN
          ToAssemblyLine := TempAssemblyLine;
          ToAssemblyLine.INSERT;
          IF ToAssemblyLine."Due Date" < WORKDATE THEN BEGIN
            DueDateBeforeWorkDate := TRUE;
            NewLineDueDate := ToAssemblyLine."Due Date";
          END;
        END;
      END;

      IF DueDateBeforeWorkDate THEN
        ShowDueDateBeforeWorkDateMsg(NewLineDueDate);
    END;

    PROCEDURE DeleteLines@24(AsmHeader@1001 : Record 900);
    VAR
      AssemblyLine@1000 : Record 901;
    BEGIN
      WITH AsmHeader DO BEGIN
        SetLinkToLines(AsmHeader,AssemblyLine);
        IF AssemblyLine.FIND('-') THEN BEGIN
          HandleItemTrackingDeletion;
          REPEAT
            AssemblyLine.SuspendStatusCheck(TRUE);
            AssemblyLine.DELETE(TRUE);
          UNTIL AssemblyLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE UpdateWarningOnLines@2(AsmHeader@1000 : Record 900);
    VAR
      AssemblyLine@1001 : Record 901;
    BEGIN
      SetLinkToLines(AsmHeader,AssemblyLine);
      IF AssemblyLine.FINDSET THEN
        REPEAT
          AssemblyLine.UpdateAvailWarning;
          AssemblyLine.MODIFY;
        UNTIL AssemblyLine.NEXT = 0;
    END;

    PROCEDURE UpdateAssemblyLines@62(VAR AsmHeader@1007 : Record 900;OldAsmHeader@1014 : Record 900;FieldNum@1001 : Integer;ReplaceLinesFromBOM@1012 : Boolean;CurrFieldNo@1004 : Integer;CurrentFieldNum@1015 : Integer);
    VAR
      AssemblyLine@1000 : Record 901;
      TempAssemblyHeader@1006 : TEMPORARY Record 900;
      TempAssemblyLine@1002 : TEMPORARY Record 901;
      BomComponent@1011 : Record 90;
      TempCurrAsmLine@1018 : TEMPORARY Record 901;
      ItemCheckAvail@1010 : Codeunit 311;
      NoOfLinesFound@1013 : Integer;
      UpdateDueDate@1005 : Boolean;
      UpdateLocation@1009 : Boolean;
      UpdateQuantity@1003 : Boolean;
      UpdateUOM@1017 : Boolean;
      UpdateQtyToConsume@1008 : Boolean;
      UpdateDimension@1016 : Boolean;
      DueDateBeforeWorkDate@1019 : Boolean;
      NewLineDueDate@1020 : Date;
    BEGIN
      IF (FieldNum <> CurrentFieldNum) OR // Update has been called from OnValidate of another field than was originally intended.
         ((NOT (FieldNum IN [AsmHeader.FIELDNO("Item No."),
                             AsmHeader.FIELDNO("Variant Code"),
                             AsmHeader.FIELDNO("Location Code"),
                             AsmHeader.FIELDNO("Starting Date"),
                             AsmHeader.FIELDNO(Quantity),
                             AsmHeader.FIELDNO("Unit of Measure Code"),
                             AsmHeader.FIELDNO("Quantity to Assemble"),
                             AsmHeader.FIELDNO("Dimension Set ID")])) AND (NOT ReplaceLinesFromBOM))
      THEN
        EXIT;

      NoOfLinesFound := CopyAssemblyData(AsmHeader,TempAssemblyHeader,TempAssemblyLine);
      IF ReplaceLinesFromBOM THEN BEGIN
        TempAssemblyLine.DELETEALL;
        IF NOT ((AsmHeader."Quantity (Base)" = 0) OR (AsmHeader."Item No." = '')) THEN BEGIN  // condition to replace asm lines
          SetLinkToBOM(AsmHeader,BomComponent);

          IF BomComponent.FINDSET THEN
            REPEAT
              InsertAsmLine(AsmHeader,TempAssemblyLine,TRUE);
              AddBOMLine2(AsmHeader,TempAssemblyLine,TRUE,BomComponent,FALSE);
            UNTIL BomComponent.NEXT <= 0;
        END;
      END ELSE
        IF NoOfLinesFound = 0 THEN
          EXIT; // MODIFY condition but no lines to modify

      // make pre-checks OR ask user to confirm
      IF PreCheckAndConfirmUpdate(AsmHeader,OldAsmHeader,FieldNum,ReplaceLinesFromBOM,TempAssemblyLine,
           UpdateDueDate,UpdateLocation,UpdateQuantity,UpdateUOM,UpdateQtyToConsume,UpdateDimension)
      THEN
        EXIT;

      IF NOT ReplaceLinesFromBOM THEN
        IF TempAssemblyLine.FIND('-') THEN
          REPEAT
            TempCurrAsmLine := TempAssemblyLine;
            TempCurrAsmLine.INSERT;
            TempAssemblyLine.SetSkipVerificationsThatChangeDatabase(TRUE);
            UpdateExistingLine(AsmHeader,OldAsmHeader,CurrFieldNo,TempAssemblyLine,
              UpdateDueDate,UpdateLocation,UpdateQuantity,UpdateUOM,UpdateQtyToConsume,UpdateDimension);
          UNTIL TempAssemblyLine.NEXT = 0;

      IF NOT (FieldNum IN [AsmHeader.FIELDNO("Quantity to Assemble"),AsmHeader.FIELDNO("Dimension Set ID")]) THEN
        IF ShowAvailability(FALSE,TempAssemblyHeader,TempAssemblyLine) THEN
          ItemCheckAvail.RaiseUpdateInterruptedError;

      DoVerificationsSkippedEarlier(
        ReplaceLinesFromBOM,TempAssemblyLine,TempCurrAsmLine,UpdateDimension,AsmHeader."Dimension Set ID",
        OldAsmHeader."Dimension Set ID");

      AssemblyLine.RESET;
      IF ReplaceLinesFromBOM THEN BEGIN
        DeleteLines(AsmHeader);
        TempAssemblyLine.RESET;
      END;

      IF TempAssemblyLine.FIND('-') THEN
        REPEAT
          IF NOT ReplaceLinesFromBOM THEN
            AssemblyLine.GET(TempAssemblyLine."Document Type",TempAssemblyLine."Document No.",TempAssemblyLine."Line No.");
          AssemblyLine := TempAssemblyLine;
          IF ReplaceLinesFromBOM THEN
            AssemblyLine.INSERT(TRUE)
          ELSE
            AssemblyLine.MODIFY(TRUE);
          AsmHeader.AutoReserveAsmLine(AssemblyLine);
          IF AssemblyLine."Due Date" < WORKDATE THEN BEGIN
            DueDateBeforeWorkDate := TRUE;
            NewLineDueDate := AssemblyLine."Due Date";
          END;
        UNTIL TempAssemblyLine.NEXT = 0;

      IF ReplaceLinesFromBOM OR UpdateDueDate THEN
        IF DueDateBeforeWorkDate THEN
          ShowDueDateBeforeWorkDateMsg(NewLineDueDate);
    END;

    LOCAL PROCEDURE PreCheckAndConfirmUpdate@34(AsmHeader@1008 : Record 900;OldAsmHeader@1007 : Record 900;FieldNum@1000 : Integer;VAR ReplaceLinesFromBOM@1011 : Boolean;VAR TempAssemblyLine@1001 : TEMPORARY Record 901;VAR UpdateDueDate@1009 : Boolean;VAR UpdateLocation@1005 : Boolean;VAR UpdateQuantity@1003 : Boolean;VAR UpdateUOM@1004 : Boolean;VAR UpdateQtyToConsume@1002 : Boolean;VAR UpdateDimension@1012 : Boolean) : Boolean;
    BEGIN
      UpdateDueDate := FALSE;
      UpdateLocation := FALSE;
      UpdateQuantity := FALSE;
      UpdateUOM := FALSE;
      UpdateQtyToConsume := FALSE;
      UpdateDimension := FALSE;

      WITH AsmHeader DO
        CASE FieldNum OF
          FIELDNO("Item No."):
            BEGIN
              IF "Item No." <> OldAsmHeader."Item No." THEN
                IF LinesExist(AsmHeader) THEN
                  IF GUIALLOWED THEN
                    IF NOT CONFIRM(STRSUBSTNO(Text003,FIELDCAPTION("Item No."),OldAsmHeader."Item No.","Item No."),TRUE) THEN
                      ERROR('');
            END;
          FIELDNO("Variant Code"):
            UpdateDueDate := TRUE;
          FIELDNO("Location Code"):
            BEGIN
              UpdateDueDate := TRUE;
              IF "Location Code" <> OldAsmHeader."Location Code" THEN BEGIN
                TempAssemblyLine.SETRANGE(Type,TempAssemblyLine.Type::Item);
                TempAssemblyLine.SETFILTER("Location Code",'<>%1',"Location Code");
                IF NOT TempAssemblyLine.ISEMPTY THEN
                  IF GUIALLOWED THEN
                    IF CONFIRM(STRSUBSTNO(Text001,TempAssemblyLine.FIELDCAPTION("Location Code")),FALSE) THEN
                      UpdateLocation := TRUE;
                TempAssemblyLine.SETRANGE("Location Code");
                TempAssemblyLine.SETRANGE(Type);
              END;
            END;
          FIELDNO("Starting Date"):
            UpdateDueDate := TRUE;
          FIELDNO(Quantity):
            IF Quantity <> OldAsmHeader.Quantity THEN BEGIN
              UpdateQuantity := TRUE;
              UpdateQtyToConsume := TRUE;
            END;
          FIELDNO("Unit of Measure Code"):
            IF "Unit of Measure Code" <> OldAsmHeader."Unit of Measure Code" THEN
              UpdateUOM := TRUE;
          FIELDNO("Quantity to Assemble"):
            UpdateQtyToConsume := TRUE;
          FIELDNO("Dimension Set ID"):
            IF "Dimension Set ID" <> OldAsmHeader."Dimension Set ID" THEN BEGIN
              IF LinesExist(AsmHeader) THEN
                IF GUIALLOWED AND CONFIRM(STRSUBSTNO(Text002)) THEN
                  UpdateDimension := TRUE;
            END;
          ELSE
            IF CalledFromRefreshBOM(ReplaceLinesFromBOM,FieldNum) THEN
              IF LinesExist(AsmHeader) THEN
                IF GUIALLOWED THEN
                  IF NOT CONFIRM(Text004,FALSE) THEN
                    ReplaceLinesFromBOM := FALSE;
        END;

      IF NOT (UpdateDueDate OR UpdateLocation OR UpdateQuantity OR UpdateUOM OR UpdateQtyToConsume OR UpdateDimension) AND
         // nothing to update
         NOT ReplaceLinesFromBOM
      THEN
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE UpdateExistingLine@26(VAR AsmHeader@1006 : Record 900;OldAsmHeader@1012 : Record 900;CurrFieldNo@1001 : Integer;VAR AssemblyLine@1000 : Record 901;UpdateDueDate@1007 : Boolean;UpdateLocation@1008 : Boolean;UpdateQuantity@1003 : Boolean;UpdateUOM@1005 : Boolean;UpdateQtyToConsume@1009 : Boolean;UpdateDimension@1002 : Boolean);
    VAR
      QtyRatio@1004 : Decimal;
      QtyToConsume@1010 : Decimal;
    BEGIN
      WITH AsmHeader DO BEGIN
        IF IsStatusCheckSuspended THEN
          AssemblyLine.SuspendStatusCheck(TRUE);

        IF UpdateLocation THEN BEGIN
          IF AssemblyLine.Type = AssemblyLine.Type::Item THEN
            AssemblyLine.VALIDATE("Location Code","Location Code");
        END;

        IF UpdateDueDate THEN BEGIN
          AssemblyLine.SetTestReservationDateConflict(CurrFieldNo <> 0);
          AssemblyLine.ValidateLeadTimeOffset(AsmHeader,AssemblyLine."Lead-Time Offset",FALSE);
        END;

        IF UpdateQuantity THEN BEGIN
          QtyRatio := Quantity / OldAsmHeader.Quantity;
          IF AssemblyLine.FixedUsage THEN
            AssemblyLine.VALIDATE(Quantity)
          ELSE
            AssemblyLine.VALIDATE(Quantity,AssemblyLine.Quantity * QtyRatio);
          AssemblyLine.InitQtyToConsume;
        END;

        IF UpdateUOM THEN BEGIN
          QtyRatio := "Qty. per Unit of Measure" / OldAsmHeader."Qty. per Unit of Measure";
          IF AssemblyLine.FixedUsage THEN
            AssemblyLine.VALIDATE("Quantity per")
          ELSE
            AssemblyLine.VALIDATE("Quantity per",AssemblyLine."Quantity per" * QtyRatio);
          AssemblyLine.InitQtyToConsume;
        END;

        IF UpdateQtyToConsume THEN
          IF NOT AssemblyLine.FixedUsage THEN BEGIN
            AssemblyLine.InitQtyToConsume;
            QtyToConsume := AssemblyLine.Quantity * "Quantity to Assemble" / Quantity;
            RoundQty(QtyToConsume);
            IF QtyToConsume <= AssemblyLine.MaxQtyToConsume THEN
              AssemblyLine.VALIDATE("Quantity to Consume",QtyToConsume);
          END;

        IF UpdateDimension THEN
          AssemblyLine.UpdateDim("Dimension Set ID",OldAsmHeader."Dimension Set ID");

        AssemblyLine.MODIFY(TRUE);
      END;
    END;

    PROCEDURE ShowDueDateBeforeWorkDateMsg@7(ActualLineDueDate@1001 : Date);
    BEGIN
      IF GUIALLOWED THEN
        IF GetWarningMode THEN
          MESSAGE(Text005,ActualLineDueDate,WORKDATE);
    END;

    PROCEDURE CopyAssemblyData@71(FromAssemblyHeader@1003 : Record 900;VAR ToAssemblyHeader@1000 : Record 900;VAR ToAssemblyLine@1001 : Record 901) NoOfLinesInserted : Integer;
    VAR
      AssemblyLine@1002 : Record 901;
    BEGIN
      ToAssemblyHeader := FromAssemblyHeader;
      ToAssemblyHeader.INSERT;

      SetLinkToLines(FromAssemblyHeader,AssemblyLine);
      AssemblyLine.SETFILTER(Type,'%1|%2',AssemblyLine.Type::Item,AssemblyLine.Type::Resource);
      ToAssemblyLine.RESET;
      ToAssemblyLine.DELETEALL;
      IF AssemblyLine.FIND('-') THEN
        REPEAT
          ToAssemblyLine := AssemblyLine;
          ToAssemblyLine.INSERT;
          NoOfLinesInserted += 1;
        UNTIL AssemblyLine.NEXT = 0;
    END;

    PROCEDURE ShowAvailability@37(ShowPageEvenIfEnoughComponentsAvailable@1004 : Boolean;VAR TmpAssemblyHeader@1000 : Record 900;VAR TempAssemblyLine@1001 : TEMPORARY Record 901) Rollback : Boolean;
    VAR
      Item@1003 : Record 27;
      TempAssemblyLine2@1012 : TEMPORARY Record 901;
      AssemblySetup@1013 : Record 905;
      ItemCheckAvail@1005 : Codeunit 311;
      AssemblyAvailability@1002 : Page 908;
      Inventory@1011 : Decimal;
      GrossRequirement@1010 : Decimal;
      ReservedRequirement@1014 : Decimal;
      ScheduledReceipts@1009 : Decimal;
      ReservedReceipts@1015 : Decimal;
      EarliestAvailableDateX@1008 : Date;
      QtyAvailToMake@1007 : Decimal;
      QtyAvailTooLow@1006 : Boolean;
    BEGIN
      AssemblySetup.GET;
      IF NOT GUIALLOWED OR
         TempAssemblyLine.ISEMPTY OR
         (NOT AssemblySetup."Stockout Warning" AND NOT ShowPageEvenIfEnoughComponentsAvailable) OR
         NOT GetWarningMode
      THEN
        EXIT(FALSE);
      TmpAssemblyHeader.TESTFIELD("Item No.");
      Item.GET(TmpAssemblyHeader."Item No.");

      ItemCheckAvail.AsmOrderCalculate(TmpAssemblyHeader,Inventory,
        GrossRequirement,ReservedRequirement,ScheduledReceipts,ReservedReceipts);
      TempAssemblyLine2.COPY(TempAssemblyLine,TRUE);
      AvailToPromise(TmpAssemblyHeader,TempAssemblyLine2,QtyAvailToMake,EarliestAvailableDateX);
      QtyAvailTooLow := QtyAvailToMake < TmpAssemblyHeader."Remaining Quantity";

      IF ShowPageEvenIfEnoughComponentsAvailable OR QtyAvailTooLow THEN BEGIN
        AssemblyAvailability.SetData(TmpAssemblyHeader,TempAssemblyLine);
        AssemblyAvailability.SetHeaderInventoryData(
          Inventory,GrossRequirement,ReservedRequirement,ScheduledReceipts,ReservedReceipts,
          EarliestAvailableDateX,QtyAvailToMake,QtyAvailTooLow);
        Rollback := NOT (AssemblyAvailability.RUNMODAL = ACTION::Yes);
      END;
    END;

    LOCAL PROCEDURE DoVerificationsSkippedEarlier@9(ReplaceLinesFromBOM@1000 : Boolean;VAR TempNewAsmLine@1001 : TEMPORARY Record 901;VAR TempOldAsmLine@1002 : TEMPORARY Record 901;UpdateDimension@1003 : Boolean;NewHeaderSetID@1005 : Integer;OldHeaderSetID@1006 : Integer);
    BEGIN
      IF TempNewAsmLine.FIND('-') THEN
        REPEAT
          TempNewAsmLine.SetSkipVerificationsThatChangeDatabase(FALSE);
          IF NOT ReplaceLinesFromBOM THEN
            TempOldAsmLine.GET(TempNewAsmLine."Document Type",TempNewAsmLine."Document No.",TempNewAsmLine."Line No.");
          TempNewAsmLine.VerifyReservationQuantity(TempNewAsmLine,TempOldAsmLine);
          TempNewAsmLine.VerifyReservationChange(TempNewAsmLine,TempOldAsmLine);
          TempNewAsmLine.VerifyReservationDateConflict(TempNewAsmLine);

          IF ReplaceLinesFromBOM THEN
            CASE TempNewAsmLine.Type OF
              TempNewAsmLine.Type::Item:
                TempNewAsmLine.CreateDim(DATABASE::Item,TempNewAsmLine."No.",NewHeaderSetID);
              TempNewAsmLine.Type::Resource:
                TempNewAsmLine.CreateDim(DATABASE::Resource,TempNewAsmLine."No.",NewHeaderSetID);
            END
          ELSE BEGIN
            IF UpdateDimension THEN
              TempNewAsmLine.UpdateDim(NewHeaderSetID,OldHeaderSetID);
          END;

          TempNewAsmLine.MODIFY;
        UNTIL TempNewAsmLine.NEXT = 0;
    END;

    LOCAL PROCEDURE AvailToPromise@1(AsmHeader@1000 : Record 900;VAR AssemblyLine@1001 : Record 901;VAR OrderAbleToAssemble@1002 : Decimal;VAR EarliestDueDate@1003 : Date);
    VAR
      LineAvailabilityDate@1004 : Date;
      LineStartingDate@1005 : Date;
      EarliestStartingDate@1018 : Date;
      LineAbleToAssemble@1007 : Decimal;
    BEGIN
      SetLinkToItemLines(AsmHeader,AssemblyLine);
      AssemblyLine.SETFILTER("No.",'<>%1','');
      AssemblyLine.SETFILTER("Quantity per",'<>%1',0);
      OrderAbleToAssemble := AsmHeader."Remaining Quantity";
      IF AssemblyLine.FINDSET THEN
        REPEAT
          LineAbleToAssemble := CalcAvailToAssemble(AssemblyLine,AsmHeader,LineAvailabilityDate);

          IF LineAbleToAssemble < OrderAbleToAssemble THEN
            OrderAbleToAssemble := LineAbleToAssemble;

          IF LineAvailabilityDate > 0D THEN BEGIN
            LineStartingDate := CALCDATE(AssemblyLine."Lead-Time Offset",LineAvailabilityDate);
            IF LineStartingDate > EarliestStartingDate THEN
              EarliestStartingDate := LineStartingDate; // latest of all line starting dates
          END;
        UNTIL AssemblyLine.NEXT = 0;

      EarliestDueDate := CalcEarliestDueDate(AsmHeader,EarliestStartingDate);
    END;

    LOCAL PROCEDURE CalcAvailToAssemble@14(AssemblyLine@1002 : Record 901;AsmHeader@1000 : Record 900;VAR LineAvailabilityDate@1001 : Date) LineAbleToAssemble : Decimal;
    VAR
      Item@1007 : Record 27;
      GrossRequirement@1006 : Decimal;
      ScheduledRcpt@1005 : Decimal;
      ExpectedInventory@1004 : Decimal;
      LineInventory@1003 : Decimal;
    BEGIN
      AssemblyLine.CalcAvailToAssemble(
        AsmHeader,Item,GrossRequirement,ScheduledRcpt,ExpectedInventory,LineInventory,
        LineAvailabilityDate,LineAbleToAssemble);
    END;

    LOCAL PROCEDURE CalcEarliestDueDate@18(AsmHeader@1000 : Record 900;EarliestStartingDate@1003 : Date) EarliestDueDate : Date;
    VAR
      ReqLine@1002 : Record 246;
      LeadTimeMgt@1001 : Codeunit 5404;
      EarliestEndingDate@1004 : Date;
    BEGIN
      WITH AsmHeader DO BEGIN
        EarliestDueDate := 0D;
        IF EarliestStartingDate > 0D THEN BEGIN
          EarliestEndingDate := // earliest starting date + lead time calculation
            LeadTimeMgt.PlannedEndingDate2("Item No.","Location Code","Variant Code",
              '',LeadTimeMgt.ManufacturingLeadTime("Item No.","Location Code","Variant Code"),
              ReqLine."Ref. Order Type"::Assembly,EarliestStartingDate);
          EarliestDueDate := // earliest ending date + (default) safety lead time
            LeadTimeMgt.PlannedDueDate("Item No.","Location Code","Variant Code",
              EarliestEndingDate,'',ReqLine."Ref. Order Type"::Assembly);
        END;
      END;
    END;

    PROCEDURE CompletelyPicked@54(AsmHeader@1001 : Record 900) : Boolean;
    VAR
      AssemblyLine@1000 : Record 901;
    BEGIN
      SetLinkToItemLines(AsmHeader,AssemblyLine);
      IF AssemblyLine.FIND('-') THEN
        REPEAT
          IF NOT AssemblyLine.CompletelyPicked THEN
            EXIT(FALSE);
        UNTIL AssemblyLine.NEXT = 0;
      EXIT(TRUE);
    END;

    PROCEDURE SetWarningsOff@4();
    BEGIN
      WarningModeOff := TRUE;
    END;

    LOCAL PROCEDURE GetWarningMode@8() : Boolean;
    BEGIN
      EXIT(NOT WarningModeOff);
    END;

    LOCAL PROCEDURE CalledFromRefreshBOM@5(ReplaceLinesFromBOM@1000 : Boolean;FieldNum@1001 : Integer) : Boolean;
    BEGIN
      EXIT(ReplaceLinesFromBOM AND (FieldNum = 0));
    END;

    PROCEDURE CreateWhseItemTrkgForAsmLines@22(AsmHeader@1005 : Record 900);
    VAR
      AssemblyLine@1000 : Record 901;
      WhseWkshLine@1001 : Record 7326;
      ItemTrackingMgt@1002 : Codeunit 6500;
      WhseSNRequired@1003 : Boolean;
      WhseLNRequired@1004 : Boolean;
    BEGIN
      WITH AssemblyLine DO BEGIN
        SetLinkToItemLines(AsmHeader,AssemblyLine);
        IF FINDSET THEN
          REPEAT
            ItemTrackingMgt.CheckWhseItemTrkgSetup("No.",WhseSNRequired,WhseLNRequired,FALSE);
            IF WhseSNRequired OR WhseLNRequired THEN
              ItemTrackingMgt.InitItemTrkgForTempWkshLine(
                WhseWkshLine."Whse. Document Type"::Assembly,
                "Document No.",
                "Line No.",
                DATABASE::"Assembly Line",
                "Document Type",
                "Document No.",
                "Line No.",
                0);
          UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

