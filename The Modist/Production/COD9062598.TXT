OBJECT Codeunit 9062598 No. Series Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=15/06/16;
    Time=[ 3:42:51 PM];
    Version List=NA2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NoSeries@1000000000 : Record 308;
      NoSeriesLine@1000000001 : Record 309;
      TnpToolkit@1000000002 : Codeunit 9062230;

    PROCEDURE CreateNoSeries@1000000002(pCode@1000000000 : Code[10];pStartingNo@1000000001 : Code[20]) : Boolean;
    BEGIN
      //DOC NA2016.10 - Creating no. series
      //  -> pCode: No. series code
      //  -> pStartingNo: Starting number
      //  <- TRUE = Created, FALSE = Already exists

      EXIT(CreateNoSeriesAdv(pCode, '', TRUE, FALSE, pStartingNo));
    END;

    PROCEDURE CreateNoSeriesAdv@1000000004(pCode@1000000000 : Code[10];pDescription@1000000001 : Text;pDefaultNos@1000000002 : Boolean;pManualNos@1000000003 : Boolean;pStartingNo@1000000004 : Code[20]) : Boolean;
    BEGIN
      //DOC NA2016.10 - Creating no. series (advanced version)
      //  -> pCode: No. series code
      //  -> pDescription: Description
      //  -> pDefaultNos: Default no. series flag
      //  -> pManualNos: Manual no. series flag
      //  -> pStartingNo: Starting number
      //  <- TRUE = Created, FALSE = Already exists

      IF (NoSeries.GET(pCode)) THEN
        EXIT(FALSE);

      IF (pDescription = '') THEN
        TnpToolkit.CodeToName(pCode, pDescription);
      IF (pStartingNo = '') THEN
        pStartingNo := COPYSTR(pCode, 1, 3) + '000001';

      CLEAR(NoSeries);
      NoSeries.INIT();
      NoSeries.VALIDATE(Code, pCode);
      NoSeries.VALIDATE(Description, COPYSTR(pDescription, 1, MAXSTRLEN(NoSeries.Description)));
      NoSeries.VALIDATE("Default Nos.", pDefaultNos);
      NoSeries.VALIDATE("Manual Nos.", pManualNos);
      NoSeries.INSERT(TRUE);

      CLEAR(NoSeriesLine);
      NoSeriesLine.VALIDATE("Series Code", NoSeries.Code);
      NoSeriesLine.VALIDATE("Line No.", 10000);
      NoSeriesLine.VALIDATE("Starting No.", pStartingNo);
      NoSeriesLine.INSERT(TRUE);

      EXIT(TRUE);
    END;

    PROCEDURE GetNoSeries@1000000000(pRecord@1000000000 : Variant;pFieldId@1000000001 : Integer;pCode@1000000003 : Code[10];pStartingNo@1000000002 : Code[20]) : Boolean;
    BEGIN
      //DOC NA2016.16 - Getting no. series from given record
      //  <> pRecord: Record to get the field from / to
      //  -> pFieldId: Field ID
      //  -> pCode: No. series code
      //  -> pStartingNo: Starting number
      //  <- TRUE = Field amended, FALSE = Field was already there

      EXIT(GetNoSeriesAdv(pRecord, pFieldId, pCode, '', TRUE, FALSE, pStartingNo));
    END;

    PROCEDURE GetNoSeriesAdv@1000000001(VAR pRecord@1000000001 : Variant;pFieldId@1000000000 : Integer;pCode@1000000006 : Code[10];pDescription@1000000005 : Text;pDefaultNos@1000000004 : Boolean;pManualNos@1000000003 : Boolean;pStartingNo@1000000002 : Code[20]) : Boolean;
    VAR
      lRecordRef@1000000007 : RecordRef;
      lFieldRef@1000000008 : FieldRef;
      lFieldValue@1000000010 : Text;
      lResult@1000000009 : Boolean;
    BEGIN
      //DOC NA2016.16 - Getting no. series from given record (advanced version)
      //  <> pRecord: Record to get the field from / to
      //  -> pFieldId: Field ID
      //  -> pCode: No. series code
      //  -> pDescription: Description
      //  -> pDefaultNos: Default no. series flag
      //  -> pManualNos: Manual no. series flag
      //  -> pStartingNo: Starting number
      //  <- TRUE = Field amended, FALSE = Field was already there

      lResult := FALSE;
      lRecordRef.GETTABLE(pRecord);
      lFieldRef := lRecordRef.FIELD(pFieldId);
      lFieldValue := lFieldRef.VALUE();
      IF (lFieldValue = '') THEN
      BEGIN
        CreateNoSeriesAdv(pCode, pDescription, pDefaultNos, pManualNos, pStartingNo);
        lFieldRef.VALIDATE(pCode);
        lResult := TRUE;
      END;
      lRecordRef.SETTABLE(pRecord);
      EXIT(lResult);
    END;

    BEGIN
    {
      //DOC NA2016.10 JH 02/09/2015 - Created
      //DOC NA2016.16 JH 15/06/2016 - "Get..." functions added
    }
    END.
  }
}

