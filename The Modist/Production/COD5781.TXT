OBJECT Codeunit 5781 Whse. Validate Source Header
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE SalesHeaderVerifyChange@1(VAR NewSalesHeader@1000 : Record 36;VAR OldSalesHeader@1001 : Record 36);
    VAR
      SalesLine@1002 : Record 37;
    BEGIN
      WITH NewSalesHeader DO BEGIN
        IF "Shipping Advice" = OldSalesHeader."Shipping Advice" THEN
          EXIT;

        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type",OldSalesHeader."Document Type");
        SalesLine.SETRANGE("Document No.",OldSalesHeader."No.");
        IF SalesLine.FINDSET THEN
          REPEAT
            ChangeWhseLines(
              DATABASE::"Sales Line",
              SalesLine."Document Type",
              SalesLine."Document No.",
              SalesLine."Line No.",
              0,
              "Shipping Advice");
          UNTIL SalesLine.NEXT = 0;
      END;
    END;

    PROCEDURE ServiceHeaderVerifyChange@3(VAR NewServiceHeader@1000 : Record 5900;VAR OldServiceHeader@1001 : Record 5900);
    VAR
      ServiceLine@1002 : Record 5902;
    BEGIN
      WITH NewServiceHeader DO BEGIN
        IF "Shipping Advice" = OldServiceHeader."Shipping Advice" THEN
          EXIT;

        ServiceLine.RESET;
        ServiceLine.SETRANGE("Document Type",OldServiceHeader."Document Type");
        ServiceLine.SETRANGE("Document No.",OldServiceHeader."No.");
        IF ServiceLine.FIND('-') THEN
          REPEAT
            ChangeWhseLines(
              DATABASE::"Service Line",
              ServiceLine."Document Type",
              ServiceLine."Document No.",
              ServiceLine."Line No.",
              0,
              "Shipping Advice");
          UNTIL ServiceLine.NEXT = 0;
      END;
    END;

    PROCEDURE TransHeaderVerifyChange@2(VAR NewTransHeader@1000 : Record 5740;VAR OldTransHeader@1001 : Record 5740);
    VAR
      TransLine@1002 : Record 5741;
    BEGIN
      WITH NewTransHeader DO BEGIN
        IF "Shipping Advice" = OldTransHeader."Shipping Advice" THEN
          EXIT;

        TransLine.RESET;
        TransLine.SETRANGE("Document No.",OldTransHeader."No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            ChangeWhseLines(
              DATABASE::"Transfer Line",
              0,// Outbound Transfer
              TransLine."Document No.",
              TransLine."Line No.",
              0,
              "Shipping Advice");
          UNTIL TransLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ChangeWhseLines@6(SourceType@1000 : Integer;SourceSubType@1001 : Option;SourceNo@1002 : Code[20];SourceLineNo@1003 : Integer;SourceSublineNo@1004 : Integer;ShipAdvice@1005 : Integer);
    VAR
      WhseActivLine@1010 : Record 5767;
      WhseShptLine@1008 : Record 7321;
      WhseWkshLine@1007 : Record 7326;
    BEGIN
      WhseShptLine.RESET;
      WhseShptLine.SETRANGE("Source Type",SourceType);
      WhseShptLine.SETRANGE("Source Subtype",SourceSubType);
      WhseShptLine.SETRANGE("Source No.",SourceNo);
      WhseShptLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseShptLine.MODIFYALL("Shipping Advice",ShipAdvice);

      WhseActivLine.RESET;
      WhseActivLine.SETRANGE("Source Type",SourceType);
      WhseActivLine.SETRANGE("Source Subtype",SourceSubType);
      WhseActivLine.SETRANGE("Source No.",SourceNo);
      WhseActivLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseActivLine.SETRANGE("Source Subline No.",SourceSublineNo);
      WhseActivLine.MODIFYALL("Shipping Advice",ShipAdvice);

      WhseWkshLine.RESET;
      WhseWkshLine.SETRANGE("Source Type",SourceType);
      WhseWkshLine.SETRANGE("Source Subtype",SourceSubType);
      WhseWkshLine.SETRANGE("Source No.",SourceNo);
      WhseWkshLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseWkshLine.MODIFYALL("Shipping Advice",ShipAdvice);
    END;

    BEGIN
    END.
  }
}

