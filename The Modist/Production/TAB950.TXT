OBJECT Table 950 Time Sheet Header
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnDelete=VAR
               TimeSheetCommentLine@1000 : Record 953;
             BEGIN
               TimeSheetLine.SETRANGE("Time Sheet No.","No.");
               TimeSheetLine.DELETEALL(TRUE);

               TimeSheetCommentLine.SETRANGE("No.","No.");
               TimeSheetCommentLine.SETRANGE("Time Sheet Line No.",0);
               TimeSheetCommentLine.DELETEALL;
             END;

    CaptionML=[ENU=Time Sheet Header;
               ENG=Time Sheet Header];
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ENG=No.] }
    { 3   ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ENG=Starting Date] }
    { 4   ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              ENG=Ending Date] }
    { 5   ;   ;Resource No.        ;Code20        ;TableRelation=Resource;
                                                   OnValidate=BEGIN
                                                                ResourcesSetup.GET;
                                                                IF "Resource No." <> '' THEN BEGIN
                                                                  Resource.GET("Resource No.");
                                                                  Resource.TESTFIELD("Time Sheet Owner User ID");
                                                                  Resource.TESTFIELD("Time Sheet Approver User ID");
                                                                  "Owner User ID" := Resource."Time Sheet Owner User ID";
                                                                  "Approver User ID" := Resource."Time Sheet Approver User ID";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              ENG=Resource No.] }
    { 7   ;   ;Owner User ID       ;Code50        ;TableRelation="User Setup";
                                                   CaptionML=[ENU=Owner User ID;
                                                              ENG=Owner User ID] }
    { 8   ;   ;Approver User ID    ;Code50        ;TableRelation="User Setup";
                                                   CaptionML=[ENU=Approver User ID;
                                                              ENG=Approver User ID] }
    { 12  ;   ;Open Exists         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Line" WHERE (Time Sheet No.=FIELD(No.),
                                                                                              Status=CONST(Open)));
                                                   CaptionML=[ENU=Open Exists;
                                                              ENG=Open Exists];
                                                   Editable=No }
    { 13  ;   ;Submitted Exists    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Line" WHERE (Time Sheet No.=FIELD(No.),
                                                                                              Status=CONST(Submitted)));
                                                   CaptionML=[ENU=Submitted Exists;
                                                              ENG=Submitted Exists];
                                                   Editable=No }
    { 14  ;   ;Rejected Exists     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Line" WHERE (Time Sheet No.=FIELD(No.),
                                                                                              Status=CONST(Rejected)));
                                                   CaptionML=[ENU=Rejected Exists;
                                                              ENG=Rejected Exists];
                                                   Editable=No }
    { 15  ;   ;Approved Exists     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Line" WHERE (Time Sheet No.=FIELD(No.),
                                                                                              Status=CONST(Approved)));
                                                   CaptionML=[ENU=Approved Exists;
                                                              ENG=Approved Exists];
                                                   Editable=No }
    { 16  ;   ;Posted Exists       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Posting Entry" WHERE (Time Sheet No.=FIELD(No.)));
                                                   CaptionML=[ENU=Posted Exists;
                                                              ENG=Posted Exists];
                                                   Editable=No }
    { 20  ;   ;Quantity            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Time Sheet Detail".Quantity WHERE (Time Sheet No.=FIELD(No.),
                                                                                                       Status=FIELD(Status Filter),
                                                                                                       Job No.=FIELD(Job No. Filter),
                                                                                                       Job Task No.=FIELD(Job Task No. Filter),
                                                                                                       Date=FIELD(Date Filter),
                                                                                                       Posted=FIELD(Posted Filter),
                                                                                                       Type=FIELD(Type Filter)));
                                                   CaptionML=[ENU=Quantity;
                                                              ENG=Quantity] }
    { 21  ;   ;Posted Quantity     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Time Sheet Posting Entry".Quantity WHERE (Time Sheet No.=FIELD(No.)));
                                                   CaptionML=[ENU=Posted Quantity;
                                                              ENG=Posted Quantity] }
    { 26  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Comment Line" WHERE (No.=FIELD(No.),
                                                                                                      Time Sheet Line No.=CONST(0)));
                                                   CaptionML=[ENU=Comment;
                                                              ENG=Comment];
                                                   Editable=No }
    { 30  ;   ;Status Filter       ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Status Filter;
                                                              ENG=Status Filter];
                                                   OptionCaptionML=[ENU=Open,Submitted,Rejected,Approved;
                                                                    ENG=Open,Submitted,Rejected,Approved];
                                                   OptionString=Open,Submitted,Rejected,Approved }
    { 31  ;   ;Job No. Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Job No. Filter;
                                                              ENG=Job No. Filter] }
    { 32  ;   ;Job Task No. Filter ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Job Task No. Filter;
                                                              ENG=Job Task No. Filter] }
    { 33  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ENG=Date Filter] }
    { 34  ;   ;Posted Filter       ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Posted Filter;
                                                              ENG=Posted Filter] }
    { 35  ;   ;Type Filter         ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Type Filter;
                                                              ENG=Type Filter];
                                                   OptionCaptionML=[ENU=" ,Resource,Job,Service,Absence,Assembly Order";
                                                                    ENG=" ,Resource,Job,Service,Absence,Assembly Order"];
                                                   OptionString=[ ,Resource,Job,Service,Absence,Assembly Order] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Resource No.,Starting Date               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Starting Date,Ending Date,Resource No. }
  }
  CODE
  {
    VAR
      Resource@1001 : Record 156;
      ResourcesSetup@1003 : Record 314;
      TimeSheetLine@1000 : Record 951;
      Text001@1004 : TextConst 'ENU=%1 does not contain lines.;ENG=%1 does not contain lines.';
      TimeSheetMgt@1005 : Codeunit 950;
      Text002@1006 : TextConst 'ENU=No time sheets are available. The time sheet administrator must create time sheets before you can access them in this window.;ENG=No time sheets are available. The time sheet administrator must create time sheets before you can access them in this window.';

    PROCEDURE CalcQtyWithStatus@1(Status@1000 : 'Open,Submitted,Rejected,Approved') : Decimal;
    BEGIN
      SETRANGE("Status Filter",Status);
      CALCFIELDS(Quantity);
      EXIT(Quantity);
    END;

    PROCEDURE Check@2();
    BEGIN
      TimeSheetLine.SETRANGE("Time Sheet No.","No.");
      IF TimeSheetLine.FINDSET THEN BEGIN
        REPEAT
          TimeSheetLine.TESTFIELD(Status,TimeSheetLine.Status::Approved);
          TimeSheetLine.TESTFIELD(Posted,TRUE);
        UNTIL TimeSheetLine.NEXT = 0;
      END ELSE
        ERROR(Text001,"No.");
    END;

    PROCEDURE GetLastLineNo@5() : Integer;
    BEGIN
      TimeSheetLine.RESET;
      TimeSheetLine.SETRANGE("Time Sheet No.","No.");
      IF TimeSheetLine.FINDLAST THEN;
      EXIT(TimeSheetLine."Line No.");
    END;

    PROCEDURE FindLastTimeSheetNo@25(FilterFieldNo@1002 : Integer) : Code[20];
    BEGIN
      RESET;
      SETCURRENTKEY("Resource No.","Starting Date");

      TimeSheetMgt.FilterTimeSheets(Rec,FilterFieldNo);
      SETFILTER("Starting Date",'%1..',WORKDATE);
      IF NOT FINDFIRST THEN BEGIN
        SETRANGE("Starting Date");
        SETRANGE("Ending Date");
        IF NOT FINDLAST THEN
          ERROR(Text002);
      END;
      EXIT("No.");
    END;

    BEGIN
    END.
  }
}

