OBJECT Table 11102068 OM - Translation Tool Line
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=Object Type,Object No.,Name;
    OnDelete=VAR
               Line@1000 : Record 11102068;
             BEGIN
               IF "Caption Entry No." <> 0 THEN BEGIN
                 Line.SETCURRENTKEY("Caption Entry No.");
                 Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
                 Line.SETFILTER("Entry No.", '<>%1', "Entry No.");
                 Line.DELETEALL;
               END;
             END;

    CaptionML=[DEU=Translation Tool Line;
               ENU=Translation Tool Line;
               ESP=Translation Tool Line;
               FRA=Translation Tool Line;
               NLD=Vertaaltoolregel;
               DEA=Translation Tool Line];
    LookupPageID=Page11102099;
    DrillDownPageID=Page11102099;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DEU=Entry No.;
                                                              ENU=Entry No.;
                                                              ESP=Entry No.;
                                                              FRA=Entry No.;
                                                              NLD=Volgnummer;
                                                              DEA=Entry No.] }
    { 2   ;   ;Translate File Line ;Text100       ;OnValidate=VAR
                                                                TranslationMgt@1000 : Codeunit 11102051;
                                                                Identifier@1001 : ARRAY [10] OF Text[1024];
                                                                FieldCounter@1002 : Integer;
                                                                i@1003 : Integer;
                                                                PropertyPosition@1004 : Integer;
                                                              BEGIN
                                                                FieldCounter := 1;
                                                                FOR i := 1 TO STRLEN("Translate File Line") - 1 DO
                                                                  CASE "Translate File Line"[i] OF
                                                                    '-' : FieldCounter += 1;
                                                                    ELSE
                                                                      Identifier[FieldCounter] += FORMAT("Translate File Line"[i]);
                                                                  END;

                                                                IF Identifier[1] IN ['', ' '] THEN
                                                                  EXIT;

                                                                "Object Type" := TranslationMgt.ObjectTypeOfCharacter(DELSTR(Identifier[1], 2));

                                                                EVALUATE("Object No.", COPYSTR(Identifier[1], 2));

                                                                FOR i := 2 TO FieldCounter DO BEGIN
                                                                  CASE DELSTR(Identifier[i], 2) OF
                                                                    'A' : EVALUATE("Language Id", COPYSTR(Identifier[i], 2));
                                                                    'C' : Type := Type::Control;
                                                                    'D' : Type := Type::"Data Item";
                                                                    'E' : Type := Type::"Event";
                                                                    'F' :
                                                                      CASE "Object Type" OF
                                                                        "Object Type"::Table :
                                                                          BEGIN
                                                                            Type := Type::Field;
                                                                            IF EVALUATE(Id, COPYSTR(Identifier[i], 2)) THEN
                                                                              ;

                                                                            IF STRLEN(Identifier[i]) > 1 THEN
                                                                              IF FORMAT(Identifier[i])[2] = 'G' THEN BEGIN
                                                                                Type := Type::"Field Group";
                                                                                IF EVALUATE(Id, COPYSTR(Identifier[i], 3)) THEN
                                                                                  ;
                                                                              END;

                                                                          END;

                                                                        "Object Type"::Report, "Object Type"::"4" :
                                                                          Type := Type::"Request Form";
                                                                      END;

                                                                    'G' : Type := Type::MenuItem;
                                                                    'I' : Type := Type::MenuItem;
                                                                    'K' : Type := Type::"Key Group";
                                                                    'L' : EVALUATE("Max. Length", COPYSTR(Identifier[i], 2));
                                                                    'M' : Type := Type::MenuItem;
                                                                    'N' : Type := Type::"Request Page";

                                                                    'P' :
                                                                      BEGIN
                                                                        PropertyPosition := i;
                                                                        EVALUATE("Property Id", COPYSTR(Identifier[i], 2));
                                                                        VALIDATE("Property Id");
                                                                      END;

                                                                    'Q' : Type := Type::"Text Constante";
                                                                    'T' : Type := Type::Label;
                                                                    'U' : Type := Type::"Function";
                                                                    'V' : Type := Type::Variable;

                                                                    'X' :
                                                                      BEGIN
                                                                        Type := Type::Text;
                                                                        Id := 0;
                                                                      END;

                                                                  END;
                                                                  IF DELSTR(Identifier[i], 2) IN ['C', 'D', 'G', 'I', 'M', 'Q', 'U', 'V'] THEN
                                                                    IF EVALUATE(Id, COPYSTR(Identifier[i], 2)) THEN
                                                                      ;
                                                                END;

                                                                FOR i := 1 TO 10 DO
                                                                  IF Identifier[i] <> '' THEN
                                                                    IF i < PropertyPosition THEN BEGIN
                                                                      "Character Identifiers" += DELSTR(Identifier[i], 2);

                                                                      IF i = 2 THEN
                                                                        IF Type = Type::"Field Group" THEN
                                                                          "Character Identifiers" += 'G';

                                                                      "Translate File Line Part 1" += '-' + Identifier[i];
                                                                    END;

                                                                "Translate File Line Part 1" := COPYSTR("Translate File Line Part 1", 2);

                                                                IF IsCaption THEN BEGIN
                                                                  Captions := TRUE;
                                                                  "Caption Property Id" := "Property Id";
                                                                  "Property Id" := RelatedNamePropertyId;
                                                                END ELSE
                                                                  IF HasCaption THEN BEGIN
                                                                    Captions := TRUE;
                                                                    "Caption Property Id" := RelatedCaptionPropertyId;
                                                                  END;

                                                                IF (STRLEN("Character Identifiers") = 1) AND ("Property Id" = 2818) THEN BEGIN
                                                                  Type := Type::Object;
                                                                  Id := "Object No.";
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Translate File Line;
                                                              ENU=Translate File Line;
                                                              ESP=Translate File Line;
                                                              FRA=Translate File Line;
                                                              NLD=Vertaalregel;
                                                              DEA=Translate File Line] }
    { 3   ;   ;Translate File Line Part 1;Text100 ;CaptionML=[DEU=Translate File Line Part 1;
                                                              ENU=Translate File Line Part 1;
                                                              ESP=Translate File Line Part 1;
                                                              FRA=Translate File Line Part 1;
                                                              NLD=Vertaalregel deel 1;
                                                              DEA=Translate File Line Part 1] }
    { 4   ;   ;Character Identifiers;Text30       ;CaptionML=[DEU=Character Identifiers;
                                                              ENU=Character Identifiers;
                                                              ESP=Character Identifiers;
                                                              FRA=Character Identifiers;
                                                              NLD=Identificatie karakters;
                                                              DEA=Character Identifiers] }
    { 5   ;   ;Property Id         ;Integer       ;OnValidate=BEGIN
                                                                CASE "Property Id" OF
                                                                  8630 : Type := Type::"Tab Names";
                                                                  8631 : Type := Type::"Tool Tip";
                                                                  8632 : Type := Type::Option;
                                                                  10340 : Type := Type::Option;
                                                                  15386 : Type := Type::" ";
                                                                  18490 : Type := Type::ReqFilterHeading;
                                                                  18536 : Type := Type::" ";
                                                                  20058 : Type := Type::"Source Expr.";
                                                                  21115 : Type := Type::" ";
                                                                  55119 : Type := Type::"Instructional Text";
                                                                  57601 : Type := Type::Label;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Property Id;
                                                              ENU=Property Id;
                                                              ESP=Property Id;
                                                              FRA=Property Id;
                                                              NLD=Eigenschap id;
                                                              DEA=Property Id] }
    { 6   ;   ;Caption Property Id ;Integer       ;CaptionML=[DEU=Caption Property Id;
                                                              ENU=Caption Property Id;
                                                              ESP=Caption Property Id;
                                                              FRA=Caption Property Id;
                                                              NLD=Caption eigenschap id;
                                                              DEA=Caption Property Id];
                                                   BlankZero=Yes }
    { 7   ;   ;Max. Length         ;Integer       ;CaptionML=[DEU=Max. Length;
                                                              ENU=Max. Length;
                                                              ESP=Max. Length;
                                                              FRA=Max. Length;
                                                              NLD=Maximale lengte;
                                                              DEA=Max. Length] }
    { 8   ;   ;Object Type         ;Option        ;CaptionML=[DEU=Object Type;
                                                              ENU=Object Type;
                                                              ESP=Object Type;
                                                              FRA=Object Type;
                                                              NLD=Objecttype;
                                                              DEA=Object Type];
                                                   OptionCaptionML=[DEU=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    ENU=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    ESP=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    FRA=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    NLD=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    DEA=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query];
                                                   OptionString=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;Object No.          ;Integer       ;TableRelation=Object.ID WHERE (Type=FIELD(Object Type),
                                                                                  Company Name=FILTER(''));
                                                   CaptionML=[DEU=Object No.;
                                                              ENU=Object No.;
                                                              ESP=Object No.;
                                                              FRA=Object No.;
                                                              NLD=Objectnr.;
                                                              DEA=Object No.];
                                                   Editable=No }
    { 10  ;   ;Object Name         ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=FIELD(Object Type),
                                                                                         Company Name=FILTER(''),
                                                                                         ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Object Name;
                                                              ENU=Object Name;
                                                              ESP=Object Name;
                                                              FRA=Object Name;
                                                              NLD=Objectnaam;
                                                              DEA=Object Name];
                                                   Editable=No }
    { 11  ;   ;Type                ;Option        ;CaptionML=[DEU=Type;
                                                              ENU=Type;
                                                              ESP=Type;
                                                              FRA=Type;
                                                              NLD=Soort;
                                                              DEA=Type];
                                                   OptionCaptionML=[DEU=" ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group";
                                                                    ENU=" ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group";
                                                                    ESP=" ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group";
                                                                    FRA=" ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group";
                                                                    NLD=" ,Object,Veld,Variabele,Optie,Textconstante,Tabnamen,Functie,Label,Tooltip,,Tekst,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructietekst,Field Group,Key Group";
                                                                    DEA=" ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group"];
                                                   OptionString=[ ,Object,Field,Variable,Option,Text Constante,Tab Names,Function,Label,Tool Tip,,Text,Control,MenuItem,Request Form,Data Item,Source Expr.,Request Page,ReqFilterHeading,Event,Instructional Text,Field Group,Key Group];
                                                   Editable=No }
    { 12  ;   ;Name                ;Text150       ;OnValidate=VAR
                                                                Line@1000 : Record 11102068;
                                                                TranslationMgt@1001 : Codeunit 11102051;
                                                                RecRef@1002 : RecordRef;
                                                              BEGIN
                                                                VALIDATE("Name Changed", TRUE);

                                                                IF "Caption Entry No." <> 0 THEN BEGIN

                                                                  TranslationMgt.ParkView(Rec, Line, RecRef);

                                                                  SETCURRENTKEY("Caption Entry No.");
                                                                  SETRANGE("Caption Entry No.", Line."Caption Entry No.");
                                                                  SETFILTER("Entry No.", '<>%1', Line."Entry No.");
                                                                  IF FINDSET THEN
                                                                    REPEAT
                                                                      Name := Line.Name;
                                                                      VALIDATE("Name Changed", TRUE);
                                                                      MODIFY;
                                                                    UNTIL NEXT = 0;

                                                                  TranslationMgt.GetParkedView(Rec, RecRef);

                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              ESP=Name;
                                                              FRA=Name;
                                                              NLD=Naam;
                                                              DEA=Name] }
    { 13  ;   ;Captions            ;Boolean       ;CaptionML=[DEU=Captions;
                                                              ENU=Captions;
                                                              ESP=Captions;
                                                              FRA=Captions;
                                                              NLD=Captions;
                                                              DEA=Captions];
                                                   Editable=No }
    { 14  ;   ;Language Id         ;Integer       ;TableRelation="Windows Language";
                                                   CaptionML=[DEU=Language Id;
                                                              ENU=Language Id;
                                                              ESP=Language Id;
                                                              FRA=Language Id;
                                                              NLD=Taal id;
                                                              DEA=Language Id] }
    { 15  ;   ;Missing Caption     ;Boolean       ;CaptionML=[DEU=Missing Caption;
                                                              ENU=Missing Caption;
                                                              ESP=Missing Caption;
                                                              FRA=Missing Caption;
                                                              NLD=Ontbrekende caption;
                                                              DEA=Missing Caption];
                                                   Editable=No }
    { 16  ;   ;Caption 1           ;Text250       ;CaptionML=[DEU=Caption;
                                                              ENU=Caption;
                                                              ESP=Caption;
                                                              FRA=Caption;
                                                              NLD=Caption;
                                                              DEA=Caption] }
    { 17  ;   ;Caption 2           ;Text250       ;CaptionML=[DEU=Caption 2;
                                                              ENU=Caption 2;
                                                              ESP=Caption 2;
                                                              FRA=Caption 2;
                                                              NLD=Caption 2;
                                                              DEA=Caption 2] }
    { 18  ;   ;Caption 3           ;Text250       ;CaptionML=[DEU=Caption 3;
                                                              ENU=Caption 3;
                                                              ESP=Caption 3;
                                                              FRA=Caption 3;
                                                              NLD=Caption 3;
                                                              DEA=Caption 3] }
    { 19  ;   ;Caption 4           ;Text250       ;CaptionML=[DEU=Caption 4;
                                                              ENU=Caption 4;
                                                              ESP=Caption 4;
                                                              FRA=Caption 4;
                                                              NLD=Caption 4;
                                                              DEA=Caption 4] }
    { 21  ;   ;Calculated Caption 1;Text250       ;CaptionML=[DEU=Calculated Caption;
                                                              ENU=Calculated Caption;
                                                              ESP=Calculated Caption;
                                                              FRA=Calculated Caption;
                                                              NLD=Berekende caption;
                                                              DEA=Calculated Caption];
                                                   Editable=No }
    { 22  ;   ;Calculated Caption 2;Text250       ;CaptionML=[DEU=Calculated Caption 2;
                                                              ENU=Calculated Caption 2;
                                                              ESP=Calculated Caption 2;
                                                              FRA=Calculated Caption 2;
                                                              NLD=Berekende caption 2;
                                                              DEA=Calculated Caption 2];
                                                   Editable=No }
    { 23  ;   ;Calculated Caption 3;Text250       ;CaptionML=[DEU=Calculated Caption 3;
                                                              ENU=Calculated Caption 3;
                                                              ESP=Calculated Caption 3;
                                                              FRA=Calculated Caption 3;
                                                              NLD=Berekende caption 3;
                                                              DEA=Calculated Caption 3];
                                                   Editable=No }
    { 24  ;   ;Calculated Caption 4;Text250       ;CaptionML=[DEU=Calculated Caption 4;
                                                              ENU=Calculated Caption 4;
                                                              ESP=Calculated Caption 4;
                                                              FRA=Calculated Caption 4;
                                                              NLD=Berekende caption 4;
                                                              DEA=Calculated Caption 4];
                                                   Editable=No }
    { 25  ;   ;Calculated Caption Differs;Boolean ;CaptionML=[DEU=Calculated Caption Differs;
                                                              ENU=Calculated Caption Differs;
                                                              ESP=Calculated Caption Differs;
                                                              FRA=Calculated Caption Differs;
                                                              NLD=Afwijkende berekende caption;
                                                              DEA=Calculated Caption Differs];
                                                   Editable=No }
    { 26  ;   ;Changed             ;Boolean       ;CaptionML=[DEU=Changed;
                                                              ENU=Changed;
                                                              ESP=Changed;
                                                              FRA=Changed;
                                                              NLD=Gewijzigd;
                                                              DEA=Changed] }
    { 27  ;   ;Id                  ;Integer       ;CaptionML=[DEU=Id;
                                                              ENU=Id;
                                                              ESP=Id;
                                                              FRA=Id;
                                                              NLD=Id;
                                                              DEA=Id];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 28  ;   ;Caption Entry No.   ;Integer       ;CaptionML=[DEU=Caption Entry No.;
                                                              ENU=Caption Entry No.;
                                                              ESP=Caption Entry No.;
                                                              FRA=Caption Entry No.;
                                                              NLD=Captionvolgnr.;
                                                              DEA=Caption Entry No.] }
    { 30  ;   ;Name Changed        ;Boolean       ;OnValidate=BEGIN
                                                                Changed := "Name Changed" OR "Caption Changed";
                                                              END;

                                                   CaptionML=[DEU=Name Changed;
                                                              ENU=Name Changed;
                                                              ESP=Name Changed;
                                                              FRA=Name Changed;
                                                              NLD=Naam gewijzigd;
                                                              DEA=Name Changed] }
    { 31  ;   ;Caption Changed     ;Boolean       ;OnValidate=BEGIN
                                                                Changed := "Name Changed" OR "Caption Changed";
                                                              END;

                                                   CaptionML=[DEU=Caption Changed;
                                                              ENU=Caption Changed;
                                                              ESP=Caption Changed;
                                                              FRA=Caption Changed;
                                                              NLD=Caption gewijzigd;
                                                              DEA=Caption Changed] }
    { 32  ;   ;C/AL Line No.       ;Integer       ;CaptionML=[DEU=C/AL Line No.;
                                                              ENU=C/AL Line No.;
                                                              ESP=C/AL Line No.;
                                                              FRA=C/AL Line No.;
                                                              NLD=C/AL Regelnr.;
                                                              DEA=C/AL Line No.] }
    { 33  ;   ;Added by File       ;Boolean       ;CaptionML=[DEU=Added by File;
                                                              ENU=Added by File;
                                                              ESP=Added by File;
                                                              FRA=Added by File;
                                                              NLD=Toegevoegd met bestand;
                                                              DEA=Added by File] }
    { 34  ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Object Caption;
                                                              ENU=Object Caption;
                                                              ESP=Object Caption;
                                                              FRA=Object Caption;
                                                              NLD=Object caption;
                                                              DEA=Object Caption];
                                                   Editable=No }
    { 35  ;   ;No. of Times        ;Integer       ;CaptionML=[DEU=No. of Times;
                                                              ENU=No. of Times;
                                                              ESP=No. of Times;
                                                              FRA=No. of Times;
                                                              NLD=Aantal keer;
                                                              DEA=No. of Times] }
    { 36  ;   ;Translate Later     ;Boolean       ;CaptionML=[DEU=Translate Later;
                                                              ENU=Translate Later;
                                                              ESP=Translate Later;
                                                              FRA=Translate Later;
                                                              NLD=Later vertalen;
                                                              DEA=Translate Later] }
    { 37  ;   ;Comment             ;Text250       ;CaptionML=[DEU=Comment;
                                                              ENU=Comment;
                                                              ESP=Comment;
                                                              FRA=Comment;
                                                              NLD=Opmerking;
                                                              DEA=Comment] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Translate File Line Part 1               }
    {    ;Changed                                  }
    {    ;Language Id                              }
    {    ;Language Id,Missing Caption              }
    {    ;Language Id,Calculated Caption Differs   }
    {    ;Caption Entry No.,Language Id            }
    {    ;Translate File Line,Language Id          }
    {    ;Language Id,Type,Name                    }
    {    ;Object Type,Object No.                   }
    {    ;Object Type,Object No.,C/AL Line No.    ;MaintainSQLIndex=No }
    {    ;Object Type,Object No.,Caption Entry No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ValidateTranslationFileLine@1(TranslationLine@1000 : Text;VAR LineToMerge@1001 : Record 11102068);
    VAR
      Caption@1002 : Text;
      SplitPos@1003 : Integer;
    BEGIN
      SplitPos := STRPOS(TranslationLine, ':');

      Caption := COPYSTR(TranslationLine, SplitPos + 1);
      VALIDATE("Translate File Line", COPYSTR(TranslationLine, 1, SplitPos));

      IF "Max. Length" IN [30, 128, 132, 256] THEN
        Name := COPYSTR(Caption, 1, MAXSTRLEN(Name))
      ELSE
        IF (Type = Type::Option) AND ("Language Id" = 0) THEN
          Name := COPYSTR(Caption, 1, MAXSTRLEN(Name))
        ELSE
          FillCaption(Caption);

      MergeOverwriteExisting(LineToMerge);
    END;

    PROCEDURE TranslateFileLineCaption@2() : Text;
    VAR
      PropertyId@1000 : Integer;
    BEGIN
      IF "Caption Property Id" = 0 THEN
        PropertyId := "Property Id"
      ELSE
        PropertyId := "Caption Property Id";

      IF "Language Id" = 0 THEN
        EXIT("Translate File Line Part 1" + '-P' + FORMAT("Property Id") + '-L999:')
      ELSE
        EXIT("Translate File Line Part 1" + '-P' + FORMAT(PropertyId) + '-A' + FORMAT("Language Id") + '-L999:');
    END;

    PROCEDURE NameOrNameOfParent@3() ExitValue : Text;
    VAR
      Line@1000 : Record 11102068;
      TranslationMgt@1001 : Codeunit 11102051;
      RecRef@1002 : RecordRef;
      i@1003 : Integer;
    BEGIN
      TranslationMgt.ParkView(Rec, Line, RecRef);

      FOR i := 1 TO STRLEN(Line."Translate File Line Part 1") DO
        IF Line."Translate File Line Part 1"[i] = '-' THEN BEGIN
          SETCURRENTKEY("Translate File Line Part 1");
          SETRANGE("Translate File Line Part 1", DELSTR(Line."Translate File Line Part 1", i));
          SETFILTER(Name, '<>%1', '');
          IF FINDFIRST THEN
            ExitValue += ' :: ' + Name;
        END;

      SETCURRENTKEY("Translate File Line Part 1");
      SETRANGE("Translate File Line Part 1", Line."Translate File Line Part 1");
      SETFILTER(Name, '<>%1', '');
      IF FINDFIRST THEN
        ExitValue += ' :: ' + Name;

      IF ExitValue <> '' THEN
        ExitValue := COPYSTR(ExitValue, 5);

      TranslationMgt.GetParkedView(Rec, RecRef);
    END;

    PROCEDURE ValidateCaption@4(Text@1000 : Text);
    VAR
      Line@1001 : Record 11102068;
      TranslationMgt@1002 : Codeunit 11102051;
      RecRef@1003 : RecordRef;
    BEGIN
      FillCaption(Text);
      VALIDATE("Caption Changed", TRUE);
      MODIFY;

      TranslationMgt.ParkView(Rec, Line, RecRef);
      SETCURRENTKEY("Caption Entry No.");
      SETRANGE("Caption Entry No.", Line."Caption Entry No.");
      IF FINDSET THEN
        REPEAT
          Changed := TRUE;
          UpdateCalculatedCaption;
          MODIFY;
        UNTIL NEXT = 0;
      TranslationMgt.GetParkedView(Rec, RecRef);

      IF GET("Entry No.") THEN
        ;

      CALCFIELDS("Object Name");
    END;

    PROCEDURE DefaultMissingCaption@5() ExitValue : Text;
    VAR
      Line@1000 : Record 11102068;
      TranslationMgt@1001 : Codeunit 11102051;
      RecRef@1002 : RecordRef;
    BEGIN
      ExitValue := Name;

      TranslationMgt.ParkView(Rec, Line, RecRef);

      IF NOT GetByCaptionEntryLanguage(Line."Caption Entry No.", 0) THEN
        IF NOT GetByCaptionEntryLanguage(Line."Caption Entry No.", TranslationMgt.BaseLanguage) THEN BEGIN
          RESET;
          SETRANGE("Caption Entry No.", Line."Caption Entry No.");
          IF FINDFIRST THEN
            ;
        END;

      IF "Entry No." <> 0 THEN
        IF TotalCaption <> '' THEN
          ExitValue := TotalCaption;

      TranslationMgt.GetParkedView(Rec, RecRef);
    END;

    PROCEDURE ShowExistingTranslations@6();
    VAR
      TranslationMgt@1000 : Codeunit 11102051;
    BEGIN
      TranslationMgt.ShowExistingTranslations(Rec);
    END;

    PROCEDURE FillCaption@7(NewCaption@1000 : Text);
    BEGIN
      "Caption 1" := COPYSTR(NewCaption, 1, 250);
      "Caption 2" := COPYSTR(NewCaption, 251, 250);
      "Caption 3" := COPYSTR(NewCaption, 501, 250);
      "Caption 4" := COPYSTR(NewCaption, 751, 250);
    END;

    LOCAL PROCEDURE FillCalculatedCaption@8(NewCaption@1000 : Text);
    BEGIN
      "Calculated Caption 1" := COPYSTR(NewCaption, 1, 250);
      "Calculated Caption 2" := COPYSTR(NewCaption, 251, 250);
      "Calculated Caption 3" := COPYSTR(NewCaption, 501, 250);
      "Calculated Caption 4" := COPYSTR(NewCaption, 751, 250);
    END;

    PROCEDURE TotalCaption@9() : Text;
    BEGIN
      EXIT("Caption 1" + "Caption 2" + "Caption 3" + "Caption 4");
    END;

    PROCEDURE TotalCalculatedCaption@10() : Text;
    BEGIN
      EXIT("Calculated Caption 1" + "Calculated Caption 2" + "Calculated Caption 3" + "Calculated Caption 4");
    END;

    PROCEDURE GetByCaptionEntryLanguage@11(CaptionEntryNo@1000 : Integer;LanguageId@1001 : Integer) : Boolean;
    BEGIN
      SETCURRENTKEY("Caption Entry No.", "Language Id");
      SETRANGE("Caption Entry No.", CaptionEntryNo);
      SETRANGE("Language Id", LanguageId);
      EXIT(FINDFIRST);
    END;

    LOCAL PROCEDURE IsCaption@12() : Boolean;
    BEGIN
      EXIT("Property Id" IN [8629, 8630, 8631, 8632, 18490, 26171, 55119]);
    END;

    LOCAL PROCEDURE HasCaption@13() : Boolean;
    BEGIN
      IF "Caption Property Id" <> 0 THEN
        EXIT(TRUE);

      CASE "Property Id" OF
        2818 :
          BEGIN

            IF Type IN [Type::Variable, Type::"Function", Type::Control, Type::"Event"] THEN
              EXIT(FALSE);

            IF "Character Identifiers" IN ['C', 'M', 'RF', 'DF'] THEN
              EXIT(FALSE);

            EXIT(TRUE);
          END;

        10340 :
          IF "Character Identifiers" = 'TF' THEN
            EXIT(TRUE);

        57601 :
          EXIT(TRUE);

      END;
    END;

    LOCAL PROCEDURE RelatedNamePropertyId@14() : Integer;
    BEGIN
      CASE "Property Id" OF
        8629 :
          IF "Character Identifiers" = 'RT' THEN
            EXIT(57601)
          ELSE
            EXIT(2818);
        8632 : EXIT(10340);
        10340 : EXIT(2818);
        26171 : EXIT(2818);
      END;
    END;

    LOCAL PROCEDURE RelatedCaptionPropertyId@15() : Integer;
    BEGIN
      CASE "Property Id" OF
        2818 :
          CASE Type OF
            Type::Option :
              EXIT(8629);

            Type::"Text Constante" :
              EXIT(26171)

            ELSE
              EXIT(8629);
          END;

        10340 :
          EXIT(8632);

        57601 :
          EXIT(8629);

      END;
    END;

    PROCEDURE UpdateCalculatedCaption@16();
    VAR
      TranslationMgt@1000 : Codeunit 11102051;
      CalculatedCaption@1001 : Text;
    BEGIN
      IF Captions THEN BEGIN
        CalculatedCaption := TranslationMgt.CaptionAfterRulesApplied(Rec);
        FillCalculatedCaption(CalculatedCaption);
        "Calculated Caption Differs" :=
          (CalculatedCaption <> '') AND
          (DELCHR(CalculatedCaption, '=', '&') <> DELCHR(TotalCaption, '=', '&'));
        "Missing Caption" := "Caption 1" = '';
      END ELSE BEGIN
        FillCalculatedCaption('');
        "Calculated Caption Differs" := FALSE;
        "Missing Caption" := FALSE;
      END;
    END;

    PROCEDURE ShowCode@17();
    VAR
      Setup@1000 : Record 11102035;
      CALHistoryObject@1001 : Record 11102052;
      CALHistoryLine@1002 : Record 11102053;
      TmpHighLightedLine@1003 : TEMPORARY Record 11102053;
      CALHistoryLines@1004 : Page 11102073;
      NavgateEditor@1005 : Page 11175819;
    BEGIN
      Setup.CustomGet;

      CASE Setup.Editor OF
        Setup.Editor::"Object Manager" :
          BEGIN
            CALHistoryObject.GoToLastRecord("Object Type", "Object No.");

            CALHistoryLines.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");

            IF CALHistoryLine.GET(CALHistoryObject."Entry No.", "C/AL Line No.", 1) THEN BEGIN
              TmpHighLightedLine := CALHistoryLine;
              IF TmpHighLightedLine.INSERT THEN
                ;
              CALHistoryLines.SetHighLightedLines(TmpHighLightedLine);
            END;

            CALHistoryLines.RUN;
          END;

        Setup.Editor::Navgate :
          BEGIN
            NavgateEditor.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");
            NavgateEditor.GotoPosition("C/AL Line No.", 0);
            NavgateEditor.RUN;
          END;

      END;
    END;

    LOCAL PROCEDURE MergeOverwriteExisting@18(VAR Line@1000 : Record 11102068);
    VAR
      TempLine@1001 : TEMPORARY Record 11102068;
      CurrentLanguageId@1002 : Integer;
    BEGIN
      Line.RESET;
      Line.SETCURRENTKEY("Translate File Line Part 1");
      Line.SETRANGE("Translate File Line Part 1", "Translate File Line Part 1");
      Line.SETRANGE("Property Id", "Property Id");
      Line.SETRANGE("Caption Property Id", "Caption Property Id");
      IF Line.ISEMPTY THEN
        EXIT;

      CurrentLanguageId := "Language Id";

      IF Line.FINDSET THEN
        REPEAT

          "Caption Entry No." := Line."Caption Entry No.";

          IF CurrentLanguageId = 0 THEN BEGIN

            Line.Name := Name;
            Line."Max. Length" := "Max. Length";
            Line."Translate File Line" := "Translate File Line";
            Line."Character Identifiers" := "Character Identifiers";
            Line.VALIDATE("Name Changed", FALSE);
            Line.MODIFY;

            Rec := Line;

          END ELSE BEGIN

            Name := Line.Name;
            "Max. Length" := Line."Max. Length";
            "Translate File Line" := Line."Translate File Line";
            "Character Identifiers" := Line."Character Identifiers";

            IF Line."Language Id" = 0 THEN BEGIN
              TempLine := Rec;
              Rec := Line;

              "Language Id" := TempLine."Language Id";
              "Caption 1" := TempLine."Caption 1";
              "Caption 2" := TempLine."Caption 2";
              "Caption 3" := TempLine."Caption 3";
              "Caption 4" := TempLine."Caption 4";
              VALIDATE("Caption Changed", FALSE);

            END;

            IF "Language Id" = Line."Language Id" THEN BEGIN
              TempLine := Rec;
              Rec := Line;

              "Caption 1" := TempLine."Caption 1";
              "Caption 2" := TempLine."Caption 2";
              "Caption 3" := TempLine."Caption 3";
              "Caption 4" := TempLine."Caption 4";
              VALIDATE("Caption Changed", FALSE);

            END;
          END;

        UNTIL Line.NEXT = 0;
    END;

    PROCEDURE DrillDownNoOfCaptions@19();
    VAR
      Line@1000 : Record 11102068;
      TranslationToolCaptions@1001 : Page 11102100;
    BEGIN
      Line.SETCURRENTKEY("Caption Entry No.", "Language Id");
      Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
      Line.SETFILTER("Language Id", '<>1');
      Line.SETRANGE(Captions, TRUE);

      TranslationToolCaptions.SETTABLEVIEW(Line);
      TranslationToolCaptions.RUNMODAL;
    END;

    PROCEDURE NoOfCaptions@20() : Integer;
    VAR
      Line@1000 : Record 11102068;
    BEGIN
      IF "Caption Entry No." <> 0 THEN BEGIN
        Line.SETCURRENTKEY("Caption Entry No.", "Language Id");
        Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
        Line.SETRANGE(Captions, TRUE);
        Line.SETFILTER("Language Id", '<>1');
        Line.SETFILTER("Caption 1", '<>%1', '');
        EXIT(Line.COUNT);
      END;
    END;

    PROCEDURE DatabaseComment@21() : Text;
    VAR
      Line@1000 : Record 11102068;
    BEGIN
      Line.SETCURRENTKEY("Caption Entry No.", "Language Id");
      Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
      Line.SETRANGE("Language Id", 1);
      IF Line.FINDFIRST THEN
        EXIT(Line.TotalCaption);
    END;

    PROCEDURE ValidateComment@22(NewComment@1000 : Text);
    VAR
      Line@1001 : Record 11102068;
    BEGIN
      Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
      Line.SETRANGE("Language Id", 1);
      IF Line.FINDFIRST THEN BEGIN
        Line.FillCaption(NewComment);
        Line.MODIFY;
      END ELSE BEGIN
        Line := Rec;
        Line."Entry No." := 0;
        Line."Language Id" := 1;
        Line.FillCaption(NewComment);
        Line.INSERT(TRUE);
      END;
    END;

    PROCEDURE CompareTranslateFileLine@23() : Text;
    VAR
      LengthPos@1000 : Integer;
    BEGIN
      LengthPos := STRPOS("Translate File Line", '-L');
      IF LengthPos > 0 THEN
        EXIT(COPYSTR("Translate File Line", 1, LengthPos - 1))
      ELSE
        EXIT("Translate File Line");
    END;

    BEGIN
    END.
  }
}

