OBJECT Codeunit 1312 Mini Vendor Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVGB9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NewVendQst@1001 : TextConst 'ENU=This vendor is not registered. Do you want to create a new vendor card for vendor %1?;ENG=This vendor is not registered. Do you want to create a new vendor card for vendor %1?';
      SelectVendorErr@1000 : TextConst 'ENU=You must select an existing vendor.;ENG=You must select an existing vendor.';
      VendorFieldType@1002 : 'BuyFrom,PayTo';

    PROCEDURE GetVendorNoByName@56(VendorName@1000 : Text[50]) : Code[20];
    VAR
      Vendor@1001 : Record 23;
      VendorNo@1002 : Code[20];
    BEGIN
      Vendor.SETRANGE(Name,VendorName);
      IF Vendor.COUNT = 1 THEN BEGIN
        Vendor.FINDFIRST;
        EXIT(Vendor."No.");
      END;

      CLEAR(Vendor);
      Vendor.SETFILTER(Name,'''@*' + VendorName + '*''');
      CASE Vendor.COUNT OF
        0:
          BEGIN
            IF CONFIRM(NewVendQst,TRUE,VendorName) THEN
              EXIT(CreateNewVendor(VendorName));
            ERROR(SelectVendorErr);
          END;
        1:
          BEGIN
            Vendor.FINDFIRST;
            EXIT(Vendor."No.");
          END;
        ELSE BEGIN
          VendorNo := PickVendor(Vendor);
          IF VendorNo = '' THEN
            ERROR(SelectVendorErr);
          EXIT(VendorNo);
        END;
      END;
    END;

    LOCAL PROCEDURE CreateNewVendor@59(VendorName@1000 : Text[50]) : Code[20];
    VAR
      Vendor@1005 : Record 23;
      MiniVendorTemplate@1006 : Record 1303;
      MiniVendorCard@1002 : Page 1330;
      NewVendorCode@1007 : Code[20];
    BEGIN
      NewVendorCode := MiniVendorTemplate.NewVendorFromTemplate;
      IF NewVendorCode = '' THEN
        ERROR(SelectVendorErr);

      Vendor.GET(NewVendorCode);
      Vendor.Name := VendorName;
      Vendor.MODIFY(TRUE);
      COMMIT;
      MiniVendorCard.SETRECORD(Vendor);
      IF NOT (MiniVendorCard.RUNMODAL = ACTION::OK) THEN
        ERROR(SelectVendorErr);

      MiniVendorCard.GETRECORD(Vendor);

      EXIT(Vendor."No.");
    END;

    LOCAL PROCEDURE PickVendor@58(VAR Vendor@1000 : Record 23) : Code[20];
    VAR
      MiniVendorList@1001 : Page 1331;
    BEGIN
      MiniVendorList.SETTABLEVIEW(Vendor);
      MiniVendorList.SETRECORD(Vendor);
      MiniVendorList.LOOKUPMODE := TRUE;
      IF MiniVendorList.RUNMODAL = ACTION::LookupOK THEN
        MiniVendorList.GETRECORD(Vendor);
      EXIT(Vendor."No.");
    END;

    PROCEDURE LookupBuyFromVendor@13(VAR PurchaseHeader@1002 : Record 38;VAR OldPurchaseHeader@1003 : Record 38) : Boolean;
    BEGIN
      EXIT(LookupVendor(PurchaseHeader,OldPurchaseHeader,VendorFieldType::BuyFrom));
    END;

    PROCEDURE LookupPayToVendor@61(VAR PurchaseHeader@1003 : Record 38;VAR OldPurchaseHeader@1002 : Record 38) : Boolean;
    BEGIN
      EXIT(LookupVendor(PurchaseHeader,OldPurchaseHeader,VendorFieldType::PayTo));
    END;

    PROCEDURE ValidateBuyFromVendor@60(VAR PurchaseHeader@1003 : Record 38;VAR OldPurchaseHeader@1002 : Record 38) : Boolean;
    VAR
      NewVendorNo@1001 : Code[20];
    BEGIN
      IF PurchaseHeader."Buy-from Vendor Name" <> OldPurchaseHeader."Buy-from Vendor Name" THEN BEGIN
        NewVendorNo := GetVendorNoByName(PurchaseHeader."Buy-from Vendor Name");
        UpdateVendor(PurchaseHeader,NewVendorNo,VendorFieldType::BuyFrom);
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE ValidatePayToVendor@62(VAR PurchaseHeader@1003 : Record 38;VAR OldPurchaseHeader@1002 : Record 38) : Boolean;
    VAR
      NewVendorNo@1000 : Code[20];
    BEGIN
      IF PurchaseHeader."Pay-to Name" <> OldPurchaseHeader."Pay-to Name" THEN BEGIN
        NewVendorNo := GetVendorNoByName(PurchaseHeader."Pay-to Name");
        UpdateVendor(PurchaseHeader,NewVendorNo,VendorFieldType::PayTo);
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE LookupVendor@66(VAR PurchaseHeader@1002 : Record 38;VAR OldPurchaseHeader@1000 : Record 38;VendorField@1003 : 'BuyFrom,PayTo') : Boolean;
    VAR
      Vendor@1005 : Record 23;
      NewVendorNo@1001 : Code[20];
      CurrVendorNo@1006 : Code[20];
      OldVendorNo@1007 : Code[20];
    BEGIN
      IF VendorField = VendorFieldType::BuyFrom THEN BEGIN
        CurrVendorNo := PurchaseHeader."Buy-from Vendor No.";
        OldVendorNo := OldPurchaseHeader."Buy-from Vendor No.";
      END ELSE BEGIN
        CurrVendorNo := PurchaseHeader."Pay-to Vendor No.";
        OldVendorNo := OldPurchaseHeader."Pay-to Vendor No.";
      END;

      IF CurrVendorNo <> '' THEN
        Vendor.GET(CurrVendorNo);

      NewVendorNo := PickVendor(Vendor);

      IF NewVendorNo = '' THEN
        EXIT(FALSE);

      IF NewVendorNo <> OldVendorNo THEN BEGIN
        UpdateVendor(PurchaseHeader,NewVendorNo,VendorField);
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE UpdateVendor@63(VAR PurchaseHeader@1004 : Record 38;NewVendorNo@1001 : Code[20];VendorField@1000 : 'BuyFrom,PayTo');
    VAR
      MiniPurchCalcDiscByType@1002 : Codeunit 1310;
    BEGIN
      WITH PurchaseHeader DO
        IF VendorField = VendorFieldType::BuyFrom THEN BEGIN
          IF "No." = '' THEN
            InitRecord;

          VALIDATE("Buy-from Vendor No.",NewVendorNo)
        END ELSE
          VALIDATE("Pay-to Vendor No.",NewVendorNo);

      MiniPurchCalcDiscByType.ApplyDefaultInvoiceDiscount(0,PurchaseHeader);
    END;

    PROCEDURE CalcAmountsOnPostedInvoices@1(VendNo@1000 : Code[20];VAR RecCount@1001 : Integer) : Decimal;
    VAR
      VendorLedgerEntry@1002 : Record 25;
    BEGIN
      EXIT(CalcAmountsOnPostedDocs(VendNo,RecCount,VendorLedgerEntry."Document Type"::Invoice));
    END;

    PROCEDURE CalcAmountsOnPostedCrMemos@2(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer) : Decimal;
    VAR
      VendorLedgerEntry@1002 : Record 25;
    BEGIN
      EXIT(CalcAmountsOnPostedDocs(VendNo,RecCount,VendorLedgerEntry."Document Type"::"Credit Memo"));
    END;

    PROCEDURE CalcAmountsOnPostedOrders@3(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer) : Decimal;
    VAR
      VendorLedgerEntry@1002 : Record 25;
    BEGIN
      EXIT(CalcAmountsOnPostedDocs(VendNo,RecCount,VendorLedgerEntry."Document Type"::" "));
    END;

    PROCEDURE CalcAmountsOnUnpostedInvoices@4(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer) : Decimal;
    VAR
      VendorLedgerEntry@1002 : Record 25;
    BEGIN
      EXIT(CalcAmountsOnUnpostedDocs(VendNo,RecCount,VendorLedgerEntry."Document Type"::Invoice));
    END;

    PROCEDURE CalcAmountsOnUnpostedCrMemos@5(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer) : Decimal;
    VAR
      VendorLedgerEntry@1002 : Record 25;
    BEGIN
      EXIT(CalcAmountsOnUnpostedDocs(VendNo,RecCount,VendorLedgerEntry."Document Type"::"Credit Memo"));
    END;

    PROCEDURE DrillDownOnPostedInvoices@20(VendNo@1001 : Code[20]);
    VAR
      PurchInvHeader@1000 : Record 122;
    BEGIN
      WITH PurchInvHeader DO BEGIN
        SETRANGE("Buy-from Vendor No.",VendNo);
        SETFILTER("Posting Date",GetCurrentYearFilter);

        PAGE.RUN(PAGE::"Mini Posted Purchase Invoices",PurchInvHeader);
      END;
    END;

    PROCEDURE DrillDownOnPostedCrMemo@8(VendNo@1001 : Code[20]);
    VAR
      PurchCrMemoHdr@1000 : Record 124;
    BEGIN
      WITH PurchCrMemoHdr DO BEGIN
        SETRANGE("Buy-from Vendor No.",VendNo);
        SETFILTER("Posting Date",GetCurrentYearFilter);

        PAGE.RUN(PAGE::"Mini Posted Purch. Cr. Memos",PurchCrMemoHdr);
      END;
    END;

    PROCEDURE DrillDownOnPostedOrders@19(VendNo@1001 : Code[20]);
    VAR
      PurchaseLine@1000 : Record 39;
    BEGIN
      WITH PurchaseLine DO BEGIN
        SETRANGE("Buy-from Vendor No.",VendNo);
        SETRANGE("Document Type","Document Type"::Order);
        SETFILTER("Order Date",GetCurrentYearFilter);

        PAGE.RUN(PAGE::"Purchase Orders",PurchaseLine);
      END;
    END;

    PROCEDURE DrillDownOnOutstandingInvoices@15(VendNo@1001 : Code[20]);
    VAR
      PurchaseHeader@1000 : Record 38;
      PurchaseLine@1002 : Record 39;
    BEGIN
      SetFilterForUnpostedLines(PurchaseLine,VendNo,PurchaseHeader."Document Type"::Invoice);
      PAGE.RUN(PAGE::"Mini Purchase Invoice List",PurchaseHeader);
    END;

    PROCEDURE DrillDownOnOutstandingCrMemos@14(VendNo@1001 : Code[20]);
    VAR
      PurchaseHeader@1000 : Record 38;
      PurchaseLine@1002 : Record 39;
    BEGIN
      SetFilterForUnpostedLines(PurchaseLine,VendNo,PurchaseHeader."Document Type"::"Credit Memo");
      PAGE.RUN(PAGE::"Mini Purchase Credit Memos",PurchaseHeader);
    END;

    LOCAL PROCEDURE CalcAmountsOnPostedDocs@6(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer;DocType@1002 : Integer) : Decimal;
    VAR
      VendLedgEntry@1003 : Record 25;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        SetFilterForPostedDocs(VendLedgEntry,VendNo,DocType);
        RecCount := COUNT;
        CALCSUMS("Purchase (LCY)");
        EXIT("Purchase (LCY)");
      END;
    END;

    LOCAL PROCEDURE CalcAmountsOnUnpostedDocs@7(VendNo@1001 : Code[20];VAR RecCount@1000 : Integer;DocType@1002 : Integer) : Decimal;
    VAR
      PurchaseLine@1007 : Record 39;
      Result@1006 : Decimal;
      VAT@1005 : Decimal;
      OutstandingAmount@1004 : Decimal;
      OldDocumentNo@1003 : Code[20];
    BEGIN
      RecCount := 0;
      Result := 0;

      SetFilterForUnpostedLines(PurchaseLine,VendNo,DocType);
      WITH PurchaseLine DO BEGIN
        IF FINDSET THEN
          REPEAT
            CASE "Document Type" OF
              "Document Type"::Invoice:
                OutstandingAmount := "Outstanding Amount (LCY)";
              "Document Type"::"Credit Memo":
                OutstandingAmount := -"Outstanding Amount (LCY)";
            END;
            VAT := 100 + "VAT %";
            Result += OutstandingAmount * 100 / VAT;

            IF OldDocumentNo <> "Document No." THEN BEGIN
              OldDocumentNo := "Document No.";
              RecCount += 1;
            END;
          UNTIL NEXT = 0;
      END;
      EXIT(ROUND(Result));
    END;

    LOCAL PROCEDURE SetFilterForUnpostedLines@9(VAR PurchaseLine@1003 : Record 39;VendNo@1002 : Code[20];DocumentType@1000 : Integer);
    BEGIN
      WITH PurchaseLine DO BEGIN
        SETRANGE("Buy-from Vendor No.",VendNo);

        IF DocumentType = -1 THEN
          SETFILTER("Document Type",'%1|%2',"Document Type"::Invoice,"Document Type"::"Credit Memo")
        ELSE
          SETRANGE("Document Type",DocumentType);
      END;
    END;

    LOCAL PROCEDURE SetFilterForPostedDocs@27(VAR VendLedgEntry@1002 : Record 25;VendNo@1001 : Code[20];DocumentType@1000 : Integer);
    BEGIN
      WITH VendLedgEntry DO BEGIN
        SETRANGE("Buy-from Vendor No.",VendNo);
        SETFILTER("Posting Date",GetCurrentYearFilter);
        SETRANGE("Document Type",DocumentType);
      END;
    END;

    PROCEDURE GetCurrentYearFilter@10() : Text[30];
    VAR
      Calendar@1040002 : Record 2000000007;
      PeriodFormMgt@1040001 : Codeunit 359;
      PeriodType@1040000 : 'Day,Week,Month,Quarter,Year';
      CustDateFilter@1001 : Text[30];
      CustDateName@1000 : Text[30];
    BEGIN
      PeriodFormMgt.FindDate('',Calendar,PeriodType::Year);
      CustDateFilter := PeriodFormMgt.BuildDateFilter(Calendar);

      EXIT(CustDateFilter);
    END;

    BEGIN
    END.
  }
}

