OBJECT Codeunit 9062341 Panel Cache Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=27/08/15;
    Time=12:57:16 PM;
    Version List=NA2016.1;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      mTilePanelCacheTmp@1000000000 : TEMPORARY Record 9062488;

    PROCEDURE Exists@1000000000(pTilePanelCode@1000000000 : Code[20]) : Boolean;
    BEGIN
      //DOC NA2015.1 - Returning if tile panel with given code exists in the cache
      //  -> pTilePanelCode: Tile panel code to check
      //  <- Response

      EXIT(mTilePanelCacheTmp.GET(pTilePanelCode));
    END;

    PROCEDURE Add@1000000001(pTilePanelCode@1000000000 : Code[20];pDateTime@1000000001 : DateTime;pBlob@1000000002 : Record 99008535);
    BEGIN
      //DOC NA2015.1 - Adding tile panel to the cache
      //  -> pTilePanelCode: Panel code
      //  -> pDateTime: Date & time
      //  -> pBlob: BLOB

      IF (mTilePanelCacheTmp.GET(pTilePanelCode)) THEN
      BEGIN
        mTilePanelCacheTmp."Date Time" := pDateTime;
        mTilePanelCacheTmp.BLOB := pBlob.Blob;
        mTilePanelCacheTmp.MODIFY();
      END
      ELSE
      BEGIN
        mTilePanelCacheTmp."Panel Code" := pTilePanelCode;
        mTilePanelCacheTmp."Date Time" := pDateTime;
        mTilePanelCacheTmp.BLOB := pBlob.Blob;
        mTilePanelCacheTmp.INSERT();
      END;
    END;

    PROCEDURE Remove@1000000004(pTilePanelCode@1000000000 : Code[20]);
    BEGIN
      //DOC NA2015.1 - Removing tile panel from the cache
      //  -> pTilePanelCode: Tile panel code

      IF (mTilePanelCacheTmp.GET(pTilePanelCode)) THEN
        mTilePanelCacheTmp.DELETE();
    END;

    PROCEDURE Restore@1000000002(pTilePanelCode@1000000001 : Code[20];VAR pBlob@1000000000 : Record 99008535) : Boolean;
    VAR
      lResult@1000000002 : Boolean;
    BEGIN
      //DOC NA2015.1 - Restoring tile panel from the cache
      //  -> pTilePanelCode: Tile panel code
      //  <> pBlob: Target BLOB
      //  <- TRUE = Panel found, FALSE = Not

      IF (mTilePanelCacheTmp.GET(pTilePanelCode)) THEN
      BEGIN
        mTilePanelCacheTmp.CALCFIELDS(BLOB);
        pBlob.Blob := mTilePanelCacheTmp.BLOB;
        lResult := TRUE;
      END;

      EXIT(lResult);
    END;

    PROCEDURE Clear@1000000003();
    BEGIN
      //DOC NA2015.1 - Clearing the cache

      mTilePanelCacheTmp.RESET();
      mTilePanelCacheTmp.DELETEALL();
    END;

    BEGIN
    {
      //DOC NA2015.1 JH 22/09/2013 - Created
      //DOC NA2015.1 JH 17/02/2014 - Switched to BLOBs
      //DOC NA2015.1 JH 15/04/2014 - Tile panels 3
      //DOC NA2016.1 JH 27/08/2015 - Upgraded to 2016
    }
    END.
  }
}

