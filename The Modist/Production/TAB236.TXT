OBJECT Table 236 Res. Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               ResJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               ResJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               ResJnlLine.SETRANGE("Journal Batch Name",Name);
               ResJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ResJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               ResJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE ResJnlLine.FINDFIRST DO
                 ResJnlLine.RENAME("Journal Template Name",Name,ResJnlLine."Line No.");
             END;

    CaptionML=[ENU=Res. Journal Batch;
               ENG=Res. Journal Batch];
    LookupPageID=Page272;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Res. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ENG=Journal Template Name] }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ENG=Name];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  ResJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  ResJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  ResJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              ENG=Reason Code] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  ResJnlTemplate.GET("Journal Template Name");
                                                                  IF ResJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ENG=No. Series] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                ResJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                ResJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                ResJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ENG=Posting No. Series] }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Res. Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              ENG=Recurring];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ENG=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1;ENG=must not be %1';
      ResJnlTemplate@1002 : Record 206;
      ResJnlLine@1003 : Record 207;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      ResJnlTemplate.GET("Journal Template Name");
      "No. Series" := ResJnlTemplate."No. Series";
      "Posting No. Series" := ResJnlTemplate."Posting No. Series";
      "Reason Code" := ResJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}

