OBJECT Page 9063124 Expense Entry Card
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Expense Entry Card;
               ENG=Expense Entry Card];
    SourceTable=Table9062760;
    DataCaptionExpr=GetCaption();
    PopulateAllFields=Yes;
    PageType=Card;
    OnOpenPage=BEGIN
                 CameraAvailable := CameraProvider.IsAvailable;
                 IF CameraAvailable THEN
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetRecord=BEGIN
                       CurrPage.FileUploadPart.PAGE.SetDestBLOB(Rec.RECORDID, Rec.FIELDNO(Picture));
                     END;

    OnNewRecord=BEGIN
                  Rec.InitRecord();
                END;

    ActionList=ACTIONS
    {
      { 1000000008;  ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
      { 1000000009;1 ;Action    ;
                      CaptionML=[ENU=Take Picture;
                                 ENG=Take Picture];
                      Promoted=Yes;
                      Visible=CameraAvailable;
                      PromotedIsBig=Yes;
                      Image=Camera;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                               BEGIN
                                 IF NOT CameraAvailable THEN
                                   EXIT;
                                 CameraOptions := CameraOptions.CameraOptions;
                                 CameraOptions.Quality := 100;
                                 CameraProvider.RequestPictureAsync(CameraOptions);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=General;
                CaptionML=[ENU=General;
                           ENG=General];
                GroupType=Group }

    { 1000000002;2;Field  ;
                SourceExpr=Day }

    { 1000000004;2;Field  ;
                SourceExpr="Expense Date" }

    { 1000000003;2;Field  ;
                SourceExpr="Catalog Code" }

    { 1000000012;2;Field  ;
                SourceExpr=Description }

    { 1000000011;2;Field  ;
                SourceExpr="Currency Code";
                Importance=Additional }

    { 1000000010;2;Field  ;
                SourceExpr="Currency Factor";
                Importance=Additional }

    { 1000000005;2;Field  ;
                SourceExpr=Amount }

    { 1000000006;2;Field  ;
                SourceExpr="Amount LCY";
                Importance=Additional }

    { 1000000007;2;Field  ;
                SourceExpr=Picture }

    { 1000000013;2;Part   ;
                Name=FileUploadPart;
                CaptionML=[ENU=Picture;
                           ENG=Picture];
                PagePartID=Page9062894;
                PartType=Page }

  }
  CODE
  {
    VAR
      CameraProvider@1000000001 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1000000000 : Boolean;
      txtPageCaption@1000000002 : TextConst 'ENU=%1 %2;ENG=%1 %2';

    LOCAL PROCEDURE OnPictureAvailable@1000000001(PictureName@1000000000 : Text;PictureFilePath@1000000001 : Text);
    VAR
      File@1000000004 : File;
      OutStream@1000000003 : OutStream;
      Instream@1000000002 : InStream;
    BEGIN
      IF (PictureName = '') OR (PictureFilePath = '') THEN
        EXIT;
      File.OPEN(PictureFilePath);
      File.CREATEINSTREAM(Instream);

      Picture.CREATEOUTSTREAM(OutStream);
      COPYSTREAM(OutStream,Instream);
      MODIFY(TRUE);

      File.CLOSE;
      IF ERASE(PictureFilePath) THEN;
    END;

    LOCAL PROCEDURE GetCaption@1000000002() : Text;
    BEGIN
      EXIT(STRSUBSTNO(txtPageCaption, "Expense Date", Description));
    END;

    EVENT CameraProvider@1000000001::PictureAvailable@10(PictureName@1000000001 : Text;PictureFilePath@1000000000 : Text);
    BEGIN
      OnPictureAvailable(PictureName, PictureFilePath);
    END;

    BEGIN
    {
      //DOC NA2016.14 AZ 03/02/2016 - File Upload Add-In Control Added
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

