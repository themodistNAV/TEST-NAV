OBJECT Report 1497 Trans. Bank Rec. to Gen. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Trans. Bank Rec. to Gen. Jnl.;
               ENG=Trans. Bank Rec. to Gen. Jnl.];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   GenJnlManagement.TemplateSelectionFromBatch(GenJnlBatch);
                 END;

  }
  DATASET
  {
    { 3510;    ;DataItem;                    ;
               DataItemTable=Table273;
               DataItemTableView=SORTING(Bank Account No.,Statement No.)
                                 WHERE(Statement Type=CONST(Bank Reconciliation));
               OnPreDataItem=BEGIN
                               SETRANGE("Statement Type",BankAccRecon."Statement Type");
                               SETRANGE("Bank Account No.",BankAccRecon."Bank Account No.");
                               SETRANGE("Statement No.",BankAccRecon."Statement No.");
                             END;
                              }

    { 1210;1   ;DataItem;                    ;
               DataItemTable=Table274;
               DataItemTableView=SORTING(Bank Account No.,Statement No.,Statement Line No.);
               OnPreDataItem=BEGIN
                               GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                               GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                               GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                               IF GenJnlBatch.Name <> '' THEN
                                 GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name)
                               ELSE
                                 GenJnlLine.SETRANGE("Journal Batch Name",'');

                               GenJnlLine.LOCKTABLE;
                               IF GenJnlLine.FINDLAST THEN;
                             END;

               OnAfterGetRecord=VAR
                                  NoSeriesMgt@1000 : Codeunit 396;
                                  SourceCodeSetup@1001 : Record 242;
                                BEGIN
                                  IF (Difference = 0) OR (Type > Type::"Bank Account Ledger Entry") THEN
                                    CurrReport.SKIP;

                                  GenJnlLine.INIT;
                                  GenJnlLine."Line No." := GenJnlLine."Line No." + 10000;
                                  GenJnlLine.VALIDATE("Posting Date","Transaction Date");
                                  SourceCodeSetup.GET;
                                  GenJnlLine."Source Code" := SourceCodeSetup."Trans. Bank Rec. to Gen. Jnl.";
                                  IF "Document No." <> '' THEN
                                    GenJnlLine."Document No." := "Document No."
                                  ELSE
                                    IF GenJnlBatch."No. Series" <> '' THEN
                                      GenJnlLine."Document No." := NoSeriesMgt.GetNextNo(
                                          GenJnlBatch."No. Series","Transaction Date",FALSE);
                                  GenJnlLine."Posting No. Series" := GenJnlBatch."Posting No. Series";

                                  IF GenJnlBatch."Bal. Account No." <> '' THEN BEGIN
                                    GenJnlLine.VALIDATE("Bal. Account Type",GenJnlBatch."Bal. Account Type");
                                    GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                    GenJnlLine.VALIDATE(Amount,Difference);
                                  END ELSE BEGIN
                                    GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Account Type"::"Bank Account");
                                    GenJnlLine.VALIDATE("Bal. Account No.","Bank Acc. Reconciliation"."Bank Account No.");
                                    GenJnlLine.VALIDATE(Amount,-Difference);
                                  END;

                                  GenJnlLine.Description := Description;
                                  GenJnlLine.INSERT;
                                END;

               DataItemLink=Bank Account No.=FIELD(Bank Account No.),
                            Statement No.=FIELD(Statement No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Gen. Journal Template;
                             ENG=Gen. Journal Template];
                  NotBlank=Yes;
                  SourceExpr=GenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template" }

      { 3   ;2   ;Field     ;
                  Lookup=Yes;
                  CaptionML=[ENU=Gen. Journal Batch;
                             ENG=Gen. Journal Batch];
                  NotBlank=Yes;
                  SourceExpr=GenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               GenJnlLine.TESTFIELD("Journal Template Name");
                               GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                             END;

                  OnLookup=BEGIN
                             GenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(2);
                             GenJnlBatch.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(0);
                             GenJnlBatch.Name := GenJnlLine."Journal Batch Name";
                             IF GenJnlBatch.FIND('=><') THEN;
                             IF PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               Text := GenJnlBatch.Name;
                               EXIT(TRUE);
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenJnlTemplate@1000 : Record 80;
      GenJnlBatch@1001 : Record 232;
      GenJnlLine@1002 : Record 81;
      BankAccRecon@1003 : Record 273;
      GenJnlManagement@1004 : Codeunit 230;

    PROCEDURE SetBankAccRecon@1(VAR UseBankAccRecon@1000 : Record 273);
    BEGIN
      BankAccRecon := UseBankAccRecon;
    END;

    PROCEDURE InitializeRequest@2(GenJnlTemplateName@1000 : Code[10];GenJnlBatchName@1001 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := GenJnlTemplateName;
      GenJnlLine."Journal Batch Name" := GenJnlBatchName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

