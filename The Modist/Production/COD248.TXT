OBJECT Codeunit 248 VAT Lookup Ext. Data Hndl
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=249;
    Permissions=TableData 249=rimd;
    OnRun=BEGIN
            VATRegistrationLog := Rec;

            LookupVatRegistrationFromWebService(TRUE);

            Rec := VATRegistrationLog;
          END;

  }
  CODE
  {
    VAR
      NamespaceTxt@1003 : TextConst '@@@={Locked};ENU=urn:ec.europa.eu:taxud:vies:services:checkVat:types;ENG=urn:ec.europa.eu:taxud:vies:services:checkVat:types';
      VATRegistrationLog@1000 : Record 249;
      VATRegistrationLogMgt@1002 : Codeunit 249;
      VatRegNrValidationWebServiceURLTxt@1001 : TextConst '@@@={Locked};ENU=http://ec.europa.eu/taxation_customs/vies/services/checkVatService;ENG=http://ec.europa.eu/taxation_customs/vies/services/checkVatService';
      MissingWebServiceURLErr@1004 : TextConst 'ENU=The VAT Registration No. Validation URL is not specified in the General Ledger Setup window.;ENG=The VAT Registration No. Validation URL is not specified in the General Ledger Setup window.';

    LOCAL PROCEDURE LookupVatRegistrationFromWebService@7(ShowErrors@1000 : Boolean);
    VAR
      RequestBodyTempBlob@1002 : Record 99008535;
    BEGIN
      RequestBodyTempBlob.INIT;

      SendRequestToVatRegistrationService(RequestBodyTempBlob,ShowErrors);

      InsertLogEntry(RequestBodyTempBlob);

      COMMIT;
    END;

    LOCAL PROCEDURE SendRequestToVatRegistrationService@1(VAR BodyTempBlob@1004 : Record 99008535;ShowErrors@1003 : Boolean);
    VAR
      GeneralLedgerSetup@1005 : Record 98;
      SOAPWebServiceRequestMgt@1001 : Codeunit 1290;
      ResponseInStream@1002 : InStream;
      InStream@1006 : InStream;
      ResponseOutStream@1008 : OutStream;
    BEGIN
      PrepareSOAPRequestBody(BodyTempBlob);

      BodyTempBlob.Blob.CREATEINSTREAM(InStream);

      GeneralLedgerSetup.GET;
      IF GeneralLedgerSetup."VAT Reg. No. Validation URL" = '' THEN
        ERROR(MissingWebServiceURLErr);
      SOAPWebServiceRequestMgt.SetGlobals(InStream,GeneralLedgerSetup."VAT Reg. No. Validation URL",'','');
      SOAPWebServiceRequestMgt.DisableHttpsCheck;
      SOAPWebServiceRequestMgt.SetTimeout(5000);

      IF SOAPWebServiceRequestMgt.SendRequestToWebService THEN BEGIN
        SOAPWebServiceRequestMgt.GetResponseContent(ResponseInStream);

        BodyTempBlob.Blob.CREATEOUTSTREAM(ResponseOutStream);
        COPYSTREAM(ResponseOutStream,ResponseInStream);
      END ELSE
        IF ShowErrors THEN
          SOAPWebServiceRequestMgt.ProcessFaultResponse('');
    END;

    LOCAL PROCEDURE PrepareSOAPRequestBody@12(VAR BodyTempBlob@1000 : Record 99008535);
    VAR
      XMLDOMMgt@1006 : Codeunit 6224;
      BodyContentInputStream@1004 : InStream;
      BodyContentOutputStream@1005 : OutStream;
      BodyContentXmlDoc@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      EnvelopeXmlNode@1012 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CreatedXmlNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      BodyTempBlob.Blob.CREATEINSTREAM(BodyContentInputStream);
      BodyContentXmlDoc := BodyContentXmlDoc.XmlDocument;

      XMLDOMMgt.AddRootElementWithPrefix(BodyContentXmlDoc,'checkVat','',NamespaceTxt,EnvelopeXmlNode);
      XMLDOMMgt.AddElement(EnvelopeXmlNode,'countryCode',VATRegistrationLog.GetCountryCode,NamespaceTxt,CreatedXmlNode);
      XMLDOMMgt.AddElement(EnvelopeXmlNode,'vatNumber',VATRegistrationLog.GetVATRegNo,NamespaceTxt,CreatedXmlNode);

      CLEAR(BodyTempBlob.Blob);
      BodyTempBlob.Blob.CREATEOUTSTREAM(BodyContentOutputStream);
      BodyContentXmlDoc.Save(BodyContentOutputStream);
    END;

    LOCAL PROCEDURE InsertLogEntry@4(ResponseBodyTempBlob@1000 : Record 99008535);
    VAR
      XMLDocOut@1012 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      InStream@1009 : InStream;
    BEGIN
      ResponseBodyTempBlob.Blob.CREATEINSTREAM(InStream);
      XMLDocOut := XMLDocOut.XmlDocument;
      XMLDocOut.Load(InStream);

      VATRegistrationLogMgt.LogVerification(VATRegistrationLog,XMLDocOut,NamespaceTxt);
    END;

    PROCEDURE GetVATRegNrValidationWebServiceURL@5() : Text[250];
    BEGIN
      EXIT(VatRegNrValidationWebServiceURLTxt);
    END;

    BEGIN
    END.
  }
}

