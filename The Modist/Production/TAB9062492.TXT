OBJECT Table 9062492 Tag
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Created By User ID" = '') THEN
                 VALIDATE("Created By User ID", USERID);
               IF ((Visibility = Visibility::Private) AND ("User ID" = '')) THEN
                 VALIDATE("User ID", USERID);

               IF ("Table No."<>0) THEN
                 "Tag Action Filter":=FORMAT("Table No.")+'|0';
             END;

    OnDelete=VAR
               TagEntry@1000000000 : Record 9062493;
             BEGIN
               TagEntry.SETRANGE("Tag Code",Code);
               TagEntry.SETRANGE("Table No.", "Table No.");
               TagEntry.SETRANGE("User ID", "User ID");
               TagEntry.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Tag;
               ENG=Tag];
    LookupPageID=Page9062688;
    DrillDownPageID=Page9062688;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code50        ;OnValidate=BEGIN
                                                                IF (CodeValidation) THEN
                                                                  EXIT;
                                                                CodeValidation := TRUE;
                                                                Rec.VALIDATE(Name, String.ToTitleCase(Rec.Code));
                                                                CodeValidation := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 2   ;   ;Table No.           ;Integer       ;OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");
                                                                "Tag Action Filter":=FORMAT("Table No.")+'|0';
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              ENG=Table No.] }
    { 3   ;   ;User ID             ;Code50        ;TableRelation="User Setup";
                                                   OnValidate=BEGIN
                                                                VALIDATE("User Short Code", GetUserShortCode("User ID"));
                                                              END;

                                                   CaptionML=[ENU=User ID;
                                                              ENG=User ID] }
    { 4   ;   ;Name                ;Text51        ;OnValidate=BEGIN
                                                                IF (NameValidation) THEN
                                                                  EXIT;
                                                                NameValidation := TRUE;
                                                                Rec.VALIDATE(Code, Rec.Name);
                                                                NameValidation := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 5   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 6   ;   ;User Short Code     ;Code10        ;CaptionML=[ENU=User Short Code;
                                                              ENG=User Short Code] }
    { 7   ;   ;Created By User ID  ;Code50        ;TableRelation="User Setup";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Created By User Short Code", GetUserShortCode("Created By User ID"));
                                                              END;

                                                   CaptionML=[ENU=Created By User ID;
                                                              ENG=Created By User ID] }
    { 8   ;   ;Created By User Short Code;Code10  ;CaptionML=[ENU=Created By User Short Code;
                                                              ENG=Created By User Short Code] }
    { 9   ;   ;Visibility          ;Option        ;OnValidate=BEGIN
                                                                IF (Visibility = Visibility::Public) THEN
                                                                  VALIDATE("User ID", '')
                                                                ELSE
                                                                  VALIDATE("User ID", USERID);
                                                              END;

                                                   CaptionML=[ENU=Visibility;
                                                              ENG=Visibility];
                                                   OptionCaptionML=[ENU=Private,Public;
                                                                    ENG=Private,Public];
                                                   OptionString=Private,Public }
    { 97  ;   ;Tagged              ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Tag Entry" WHERE (Tag Code=FIELD(Code),
                                                                                        Table No.=FIELD(Table No.),
                                                                                        Primary Key 1=FIELD(Primary Key 1 Filter),
                                                                                        Primary Key 2=FIELD(Primary Key 2 Filter),
                                                                                        Primary Key 3=FIELD(Primary Key 3 Filter),
                                                                                        Primary Key 4=FIELD(Primary Key 4 Filter),
                                                                                        Primary Key 5=FIELD(Primary Key 5 Filter)));
                                                   CaptionML=[ENU=Tagged;
                                                              ENG=Tagged];
                                                   Editable=No }
    { 98  ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name];
                                                   Editable=No }
    { 99  ;   ;No. Entries (Total) ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Tag Entry" WHERE (Tag Code=FIELD(Code),
                                                                                        Table No.=FIELD(Table No.),
                                                                                        User ID=FIELD(User ID)));
                                                   CaptionML=[ENU=No. Entries (Total);
                                                              ENG=No. Entries (Total)];
                                                   Editable=No }
    { 100 ;   ;No. Entries         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Tag Entry" WHERE (Tag Code=FIELD(Code),
                                                                                        Table No.=FIELD(Table No.),
                                                                                        User ID=FIELD(User ID),
                                                                                        Created By User ID=FIELD(User Filter)));
                                                   CaptionML=[ENU=No. Entries;
                                                              ENG=No. Entries];
                                                   Editable=No }
    { 101 ;   ;User Filter         ;Code50        ;FieldClass=FlowFilter;
                                                   TableRelation=User;
                                                   CaptionML=[ENU=User Filter;
                                                              ENG=User Filter] }
    { 103 ;   ;Primary Key 1 Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Primary Key 1 Filter;
                                                              ENG=Primary Key 1 Filter] }
    { 104 ;   ;Primary Key 2 Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Primary Key 2 Filter;
                                                              ENG=Primary Key 2 Filter] }
    { 105 ;   ;Primary Key 3 Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Primary Key 3 Filter;
                                                              ENG=Primary Key 3 Filter] }
    { 106 ;   ;Primary Key 4 Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Primary Key 4 Filter;
                                                              ENG=Primary Key 4 Filter] }
    { 107 ;   ;Primary Key 5 Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Primary Key 5 Filter;
                                                              ENG=Primary Key 5 Filter] }
    { 200 ;   ;Entry No            ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No;
                                                              ENG=Entry No];
                                                   Description=Not PK, but used as cross reference - for example import log xref }
    { 300 ;   ;No. Actions         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Tag Action" WHERE (Table No.=FIELD(Table No.)));
                                                   CaptionML=[ENU=No. Actions;
                                                              ENG=No. Actions];
                                                   Editable=No }
    { 301 ;   ;Tag Action Filter   ;Text30        ;CaptionML=[ENU=Tag Action Filter;
                                                              ENG=Tag Action Filter] }
  }
  KEYS
  {
    {    ;Code,Table No.,User ID                  ;Clustered=Yes }
    {    ;Table No.,User ID,Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UserSetup@9062231 : Record 9062729;
      String@1000000003 : Codeunit 9062236;
      TNPUtils@1000000000 : Codeunit 9062229;
      errEmptyShortCode@9062229 : TextConst 'ENU=Short Code is nAblement User Setup is empty for user %1;ENG=Short Code is nAblement User Setup is empty for user %1';
      errUserSetupMissing@9062230 : TextConst 'ENU=nAblement Setup is missing for user %1;ENG=nAblement Setup is missing for user %1';
      CodeValidation@1000000001 : Boolean;
      NameValidation@1000000002 : Boolean;

    LOCAL PROCEDURE UpdateCode@9062231();
    BEGIN
      VALIDATE(Code, STRSUBSTNO('%1 %2', "User ID", Rec."User Short Code"));
    END;

    LOCAL PROCEDURE GetUserShortCode@9062229(ForUserId@9062229 : Code[50]) : Code[10];
    BEGIN
      IF (ForUserId <> '') THEN BEGIN
        IF (NOT(UserSetup.GET(ForUserId))) THEN
          ERROR(errUserSetupMissing, ForUserId);
        IF (UserSetup."Short Code" = '') THEN
          ERROR(errEmptyShortCode, ForUserId);
        EXIT(UserSetup."Short Code");
      END ELSE
        EXIT('');
    END;

    BEGIN
    {
      //DOC NA2015.5  AP 11/03/2015 - Added Table No. Field
      //DOC NA2016.5  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.14 JH 06/04/2016 - Code / name validation sorted
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

