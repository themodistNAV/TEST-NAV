OBJECT Page 9062311 File Attachment Queue Entries
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=File Attachment Queue Entries;
               ENG=File Attachment Queue Entries];
    DeleteAllowed=No;
    SourceTable=Table9062271;
    PageType=List;
    PromotedActionCategoriesML=[ENU=New,Process,Report,File Operations;
                                ENG=New,Process,Report,File Operations];
    OnInit=BEGIN
             ShowOutstanding := TRUE;
             ShowProcessed := TRUE;
             ShowErrored := TRUE;
             ShowFlagsVisible := TRUE;
           END;

    OnOpenPage=VAR
                 FAQ@1000000001 : Record 9062270;
                 i@1000000000 : Integer;
               BEGIN
                 QueueCode := GETFILTER("Queue Code");
                 DetectFilters();
                 UpdateFilters(Flag::" ");
               END;

    OnAfterGetRecord=BEGIN
                       Style := Rec.GetStyle();
                     END;

    ActionList=ACTIONS
    {
      { 1000000006;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000007;1 ;Action    ;
                      Name=ProcessEntryAction;
                      CaptionML=[ENU=Process Entry;
                                 ENG=Process Entry];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 FAQFunc@9062229 : Codeunit 9062265;
                               BEGIN
                                 FAQFunc.ProcessEntry(Rec,TRUE);
                               END;
                                }
      { 1000000011;1 ;Action    ;
                      Name=ClearErrorAction;
                      CaptionML=[ENU=Clear Error;
                                 ENG=Clear Error];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ClearLog;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 VALIDATE("AutoProcess Errored",FALSE);
                                 VALIDATE("AutoProcess Error Message",'');
                                 VALIDATE("AutoProcess Error Count",0);
                                 MODIFY(TRUE);
                               END;
                                }
      { 1000000014;1 ;Action    ;
                      Name=RefreshEntriesAction;
                      CaptionML=[ENU=Refresh Entries;
                                 ENG=Refresh Entries];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 FAQ@1000000000 : Record 9062270;
                               BEGIN
                                 IF "Queue Code" <> '' THEN BEGIN
                                   FAQ.GET("Queue Code");
                                   FAQ.RefreshQueueEntries;
                                   EXIT;
                                 END;

                                 IF QueueCode <> '' THEN BEGIN
                                   FAQ.GET(QueueCode);
                                   FAQ.RefreshQueueEntries;
                                   EXIT;
                                 END;
                               END;
                                }
      { 9062233 ;1   ;Separator ;
                      CaptionML=[ENU=----------------;
                                 ENG=----------------] }
      { 9062234 ;1   ;Action    ;
                      Name=OpenFolderAction;
                      CaptionML=[ENU=Open Folder;
                                 ENG=Open Folder];
                      Promoted=Yes;
                      Image=Database;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Rec.OpenFolder();
                               END;
                                }
      { 1000000010;1 ;Action    ;
                      Name=OpenFileAction;
                      CaptionML=[ENU=Open File;
                                 ENG=Open File];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=OpenWorksheet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 HYPERLINK(FilePath);
                               END;
                                }
      { 1000000017;1 ;Action    ;
                      Name=ArchiveFilesAction;
                      CaptionML=[ENU=Archive Files;
                                 ENG=Archive Files];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Archive;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lFileAttachmentQueueEntry@1000000000 : Record 9062271;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(lFileAttachmentQueueEntry);
                                 IF (FAQFuncs.ArchiveFiles(lFileAttachmentQueueEntry)) THEN
                                   CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                CaptionML=[ENU=Group;
                           ENG=Group];
                GroupType=Repeater }

    { 1000000016;2;Field  ;
                SourceExpr="DateTime Created";
                StyleExpr=Style }

    { 1000000002;2;Field  ;
                SourceExpr=Filename;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000003;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000004;2;Field  ;
                SourceExpr="User ID";
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000005;2;Field  ;
                SourceExpr="Document Type";
                StyleExpr=Style }

    { 1000000008;2;Field  ;
                SourceExpr="AutoProcess Errored";
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000009;2;Field  ;
                SourceExpr="AutoProcess Error Message";
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000015;2;Field  ;
                SourceExpr="AutoProcess Error Count";
                Editable=FALSE;
                StyleExpr=Style }

    { 9062229;1;Group     ;
                Name=OptionsGroup;
                CaptionML=[ENU=Options;
                           ENG=Options];
                Visible=ShowFlagsVisible;
                GroupType=Group }

    { 9062230;2;Field     ;
                Name=ShowOutstandingField;
                CaptionML=[ENU=Show Outstanding;
                           ENG=Show Outstanding];
                SourceExpr=ShowOutstanding;
                Visible=ShowFlagsVisible;
                OnValidate=BEGIN
                             UpdateFilters(Flag::Outstanding);
                           END;
                            }

    { 9062231;2;Field     ;
                Name=ShowProcessedField;
                CaptionML=[ENU=Show Processed;
                           ENG=Show Processed];
                SourceExpr=ShowProcessed;
                Visible=ShowFlagsVisible;
                OnValidate=BEGIN
                             UpdateFilters(Flag::Processed);
                           END;
                            }

    { 9062232;2;Field     ;
                Name=ShowErroredField;
                CaptionML=[ENU=Show Errored;
                           ENG=Show Errored];
                SourceExpr=ShowErrored;
                Visible=ShowFlagsVisible;
                OnValidate=BEGIN
                             UpdateFilters(Flag::Errored);
                           END;
                            }

  }
  CODE
  {
    VAR
      FAQFuncs@1000000000 : Codeunit 9062265;
      FileSystem@1000000001 : Codeunit 9062264;
      QueueCode@1000000002 : Code[50];
      Style@9062229 : Text;
      ShowOutstanding@9062230 : Boolean;
      ShowProcessed@9062231 : Boolean;
      ShowErrored@9062232 : Boolean;
      ShowFlagsVisible@9062235 : Boolean;
      Flag@9062233 : ' ,Outstanding,Processed,Errored';

    LOCAL PROCEDURE DetectFilters@9062230();
    VAR
      lFilterGroup@9062229 : Integer;
    BEGIN
      //DOC NA2016.10 - Detecting the page filters

      ShowFlagsVisible := TRUE;

      FOR lFilterGroup := 6 DOWNTO 0 DO
      BEGIN
        Rec.FILTERGROUP(lFilterGroup);
        IF ((Rec.GETFILTER(Processed) <> '') OR
            (Rec.GETFILTER("AutoProcess Errored") <> '')) THEN
        BEGIN
          ShowOutstanding := FALSE;
          ShowProcessed := FALSE;
          ShowErrored := FALSE;
          ShowFlagsVisible := FALSE;
          lFilterGroup := 0;
        END;
      END;
      Rec.FILTERGROUP(0);
    END;

    LOCAL PROCEDURE UpdateFilters@9062231(pFlag@9062230 : Integer);
    VAR
      lFlagsCombination@9062229 : Code[3];
    BEGIN
      //DOC NA2016.10 - Updating the page filters

      IF ( NOT (ShowFlagsVisible)) THEN
        EXIT;

      lFlagsCombination := FORMAT(ShowOutstanding, 1, 2) + FORMAT(ShowProcessed, 1, 2) + FORMAT(ShowErrored, 1, 2);

      Rec.FILTERGROUP(2);
      CASE (lFlagsCombination) OF
        '000':
          BEGIN
            Rec.SETFILTER(Processed, '0&1');
            Rec.SETFILTER("AutoProcess Errored", '0&1');
          END;
        '001':
          BEGIN
            Rec.SETRANGE(Processed);
            Rec.SETRANGE("AutoProcess Errored", TRUE);
          END;
        '010':
          BEGIN
            Rec.SETRANGE(Processed, TRUE);
            Rec.SETRANGE("AutoProcess Errored");
          END;
        '011':
          BEGIN
            IF (pFlag = Flag::Processed) THEN
              ShowErrored := FALSE
            ELSE
              ShowProcessed := FALSE;
            Rec.SETRANGE(Processed);
            Rec.SETRANGE("AutoProcess Errored", TRUE);
          END;
        '100':
          BEGIN
            Rec.SETRANGE(Processed, FALSE);
            Rec.SETRANGE("AutoProcess Errored", FALSE);
          END;
        '101':
          BEGIN
            IF (pFlag = Flag::Outstanding) THEN
              ShowErrored := FALSE
            ELSE
              ShowOutstanding := FALSE;
            Rec.SETRANGE(Processed);
            Rec.SETRANGE("AutoProcess Errored", TRUE);
          END;
        '110':
          BEGIN
            Rec.SETRANGE(Processed);
            Rec.SETRANGE("AutoProcess Errored", FALSE);
          END;
        '111':
          BEGIN
            Rec.SETRANGE(Processed);
            Rec.SETRANGE("AutoProcess Errored");
          END;
      END;
      Rec.FILTERGROUP(0);

      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 31/08/2012 - Migrated to 2013
      //DOC NA2015.3  AP 09/09/2014 - Added ProcessEntry() Button
      //DOC NA2016.3  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

