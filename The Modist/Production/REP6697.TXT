OBJECT Report 6697 Create Ret.-Related Documents
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Ret.-Related Documents;
               ENG=Create Ret.-Related Documents];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  TempRetRelDoc.DELETEALL;

                  IF CreateSO THEN BEGIN
                    SOSalesHeader."Document Type" := SOSalesHeader."Document Type"::Order;
                    CLEAR(CopyDocMgt);
                    CopyDocMgt.SetProperties(TRUE,FALSE,FALSE,TRUE,TRUE,FALSE,FALSE);
                    CopyDocMgt.CopySalesDoc(DocType::"Return Order",SROSalesHeader."No.",SOSalesHeader);
                    TempRetRelDoc."Entry No." := 3;
                    TempRetRelDoc."Document Type" := TempRetRelDoc."Document Type"::"Sales Order";
                    TempRetRelDoc."No." := SOSalesHeader."No.";
                    TempRetRelDoc.INSERT;
                  END;

                  IF CreatePRO THEN BEGIN
                    PROPurchHeader."Document Type" := PROPurchHeader."Document Type"::"Return Order";
                    CLEAR(CopyDocMgt);
                    CopyDocMgt.SetProperties(TRUE,FALSE,FALSE,FALSE,TRUE,FALSE,FALSE);
                    CopyDocMgt.CopyFromSalesToPurchDoc(VendorNo,SROSalesHeader,PROPurchHeader);
                    TempRetRelDoc."Entry No." := 1;
                    TempRetRelDoc."Document Type" := TempRetRelDoc."Document Type"::"Purchase Return Order";
                    TempRetRelDoc."No." := PROPurchHeader."No.";
                    TempRetRelDoc.INSERT;
                  END;

                  IF CreatePO THEN BEGIN
                    POPurchHeader."Document Type" := POPurchHeader."Document Type"::Order;
                    CLEAR(CopyDocMgt);
                    CopyDocMgt.SetProperties(TRUE,FALSE,FALSE,FALSE,TRUE,FALSE,FALSE);
                    CopyDocMgt.CopyFromSalesToPurchDoc(VendorNo,SROSalesHeader,POPurchHeader);
                    TempRetRelDoc."Entry No." := 2;
                    TempRetRelDoc."Document Type" := TempRetRelDoc."Document Type"::"Purchase Order";
                    TempRetRelDoc."No." := POPurchHeader."No.";
                    TempRetRelDoc.INSERT;
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   CreatePRO := TRUE;
                   CreatePO := TRUE;
                   CreateSO := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 1   ;2   ;Group     ;
                  CaptionML=[ENU=Return to Vendor;
                             ENG=Return to Vendor] }

      { 5   ;3   ;Field     ;
                  Name=VendorNo;
                  Lookup=Yes;
                  CaptionML=[ENU=Vendor No.;
                             ENG=Vendor No.];
                  SourceExpr=VendorNo;
                  OnValidate=BEGIN
                               IF VendorNo <> '' THEN
                                 Vend.GET(VendorNo);
                             END;

                  OnLookup=BEGIN
                             IF PAGE.RUNMODAL(0,Vend) = ACTION::LookupOK THEN
                               VendorNo := Vend."No.";
                           END;
                            }

      { 3   ;3   ;Field     ;
                  Name=CreatePurchRetOrder;
                  CaptionML=[ENU=Create Purch. Ret. Order;
                             ENG=Create Purch. Ret. Order];
                  SourceExpr=CreatePRO }

      { 4   ;3   ;Field     ;
                  Name=CreatePurchaseOrder;
                  CaptionML=[ENU=Create Purchase Order;
                             ENG=Create Purchase Order];
                  SourceExpr=CreatePO }

      { 8   ;2   ;Group     ;
                  CaptionML=[ENU=Replacement;
                             ENG=Replacement] }

      { 10  ;3   ;Field     ;
                  Name=CreateSalesOrder;
                  CaptionML=[ENU=Create Sales Order;
                             ENG=Create Sales Order];
                  SourceExpr=CreateSO }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Vend@1004 : Record 23;
      PROPurchHeader@1006 : Record 38;
      POPurchHeader@1007 : Record 38;
      SROSalesHeader@1009 : Record 36;
      SOSalesHeader@1008 : Record 36;
      TempRetRelDoc@1011 : TEMPORARY Record 6670;
      CopyDocMgt@1005 : Codeunit 6620;
      VendorNo@1000 : Code[20];
      DocType@1010 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo';
      CreatePRO@1001 : Boolean;
      CreatePO@1002 : Boolean;
      CreateSO@1003 : Boolean;

    PROCEDURE SetSalesHeader@1(NewSROSalesHeader@1000 : Record 36);
    BEGIN
      SROSalesHeader := NewSROSalesHeader;
    END;

    PROCEDURE ShowDocuments@2();
    BEGIN
      IF TempRetRelDoc.FINDFIRST THEN
        PAGE.RUN(PAGE::"Returns-Related Documents",TempRetRelDoc);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

