OBJECT Table 9062431 System Palette Colour
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Palette Code");
               IF (Rec.ID = 0) THEN
                 Rec.ID := NewId();
               IF (Rec.Sequence = 0) THEN
                 Rec.Sequence := NewSequence();
               mTnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               mTnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    CaptionML=[ENU=System Palette;
               ENG=System Palette];
  }
  FIELDS
  {
    { 1   ;   ;Palette Code        ;Code20        ;CaptionML=[ENU=Palette Code;
                                                              ENG=Palette Code] }
    { 2   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 3   ;   ;Colour Code         ;Code20        ;TableRelation="System Colour";
                                                   OnValidate=BEGIN
                                                                IF (Rec."Colour Code" <> xRec."Colour Code") THEN
                                                                  IF (Rec."Colour Code" <> '') THEN
                                                                  BEGIN
                                                                    mSystemColour.GET(Rec."Colour Code");
                                                                    mSystemColour.CALCFIELDS(Bitmap);
                                                                    Rec."Colour Bitmap" := mSystemColour.Bitmap;
                                                                  END
                                                                  ELSE
                                                                    CLEAR(Rec."Colour Bitmap");
                                                              END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 4   ;   ;Sequence            ;Integer       ;CaptionML=[ENU=Sequence;
                                                              ENG=Sequence] }
    { 5   ;   ;Colour Bitmap       ;BLOB          ;CaptionML=[ENU=Colour Bitmap;
                                                              ENG=Colour Bitmap];
                                                   SubType=Bitmap }
    { 6   ;   ;Colour Value        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("System Colour".Value WHERE (Code=FIELD(Colour Code)));
                                                   CaptionML=[ENU=Colour Value;
                                                              ENG=Colour Value];
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              mTnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              mTnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Palette Code,ID                         ;Clustered=Yes }
    {    ;Palette Code,Sequence                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      mSystemColour@1000000000 : Record 9062336;
      mTnpToolkit@1000000001 : Codeunit 9062230;

    PROCEDURE "--- Initialisation ---"@1000000008();
    BEGIN
    END;

    PROCEDURE NewId@1000000015() : Integer;
    VAR
      lSystemPaletteColour@1000000000 : Record 9062431;
    BEGIN
      //DOC NA2015.4 - Returning new ID
      //  <- The ID

      Rec.TESTFIELD("Palette Code");

      lSystemPaletteColour.RESET();
      lSystemPaletteColour.SETRANGE("Palette Code", Rec."Palette Code");
      IF (lSystemPaletteColour.FINDLAST()) THEN
        EXIT(lSystemPaletteColour.ID + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE NewSequence@1000000000() : Integer;
    VAR
      lSystemPaletteColour@1000000000 : Record 9062431;
    BEGIN
      //DOC NA2015.4 - Returning new sequence
      //  <- The sequence

      lSystemPaletteColour.RESET();
      lSystemPaletteColour.SETCURRENTKEY("Palette Code", Sequence);
      lSystemPaletteColour.SETRANGE("Palette Code", Rec."Palette Code");
      IF (lSystemPaletteColour.FINDLAST()) THEN
        EXIT(lSystemPaletteColour.Sequence + 10)
      ELSE
        EXIT(10);
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 04/10/2013 - Created
      //DOC NA2015.5  JH 07/03/2015 - "Colour Value" added (for backward-compatibility with 2009)
      //DOC NA2016.10 JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

