OBJECT Table 1103 Cost Type
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00,NAVGB7.00;
  }
  PROPERTIES
  {
    Permissions=;
    DataCaptionFields=No.,Name;
    OnInsert=BEGIN
               Modified;
             END;

    OnModify=BEGIN
               Modified;
             END;

    OnDelete=VAR
               GLAccount@1000 : Record 15;
             BEGIN
               // Message if balance  <> 0
               CostEntry.LOCKTABLE;
               IF Type = Type::"Cost Type" THEN BEGIN
                 CALCFIELDS(Balance);
                 TESTFIELD(Balance,0);
               END;

               // Error if movement in not closed fiscal year
               CostEntry.SETCURRENTKEY("Cost Type No.");
               CostEntry.SETRANGE("Cost Type No.","No.");
               AccPeriodGB.SETRANGE(Closed,FALSE);
               IF AccPeriodGB.FINDFIRST THEN
                 CostEntry.SETFILTER("Posting Date",'>=%1',AccPeriodGB."Period Start");
               IF CostEntry.FINDFIRST THEN
                 ERROR(Text000);

               // Renumber to entries to no. 0
               CostEntry.RESET;
               CostEntry.SETCURRENTKEY("Cost Type No.");
               CostEntry.SETRANGE("Cost Type No.","No.");
               CostEntry.MODIFYALL("Cost Type No.",'');

               CostBudgetEntry.SETCURRENTKEY("Budget Name","Cost Type No.");
               CostBudgetEntry.SETRANGE("Cost Type No.","No.");
               CostBudgetEntry.DELETEALL;

               GLAccount.SETRANGE("Cost Type No.","No.");
               GLAccount.MODIFYALL("Cost Type No.",'');
             END;

    OnRename=BEGIN
               "Modified Date" := TODAY;
             END;

    CaptionML=[ENU=Cost Type;
               ENG=Cost Type];
    LookupPageID=Page1100;
    DrillDownPageID=Page1100;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   CaptionML=[ENU=No.;
                                                              ENG=No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                "Search Name" := Name;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 3   ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              ENG=Search Name] }
    { 4   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                // Blocked if <> account
                                                                IF Type <> xRec.Type THEN
                                                                  Blocked := Type <> Type::"Cost Type";

                                                                // CHange only if no entries or budget
                                                                IF Blocked AND NOT xRec.Blocked THEN BEGIN
                                                                  CostEntry.SETCURRENTKEY("Cost Type No.");
                                                                  CostEntry.SETRANGE("Cost Type No.","No.");
                                                                  IF NOT CostEntry.ISEMPTY THEN
                                                                    ERROR(Text001,"No.",CostEntry.TABLECAPTION);
                                                                  CostBudgetEntry.SETCURRENTKEY("Budget Name","Cost Type No.");
                                                                  CostBudgetEntry.SETRANGE("Cost Type No.","No.");
                                                                  IF CostBudgetEntry.FINDFIRST THEN
                                                                    ERROR(Text001,"No.",CostBudgetEntry.TABLECAPTION);
                                                                END;

                                                                Totaling := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=Cost Type,Heading,Total,Begin-Total,End-Total;
                                                                    ENG=Cost Type,Heading,Total,Begin-Total,End-Total];
                                                   OptionString=Cost Type,Heading,Total,Begin-Total,End-Total }
    { 6   ;   ;Cost Center Code    ;Code20        ;TableRelation="Cost Center";
                                                   CaptionML=[ENU=Cost Center Code;
                                                              ENG=Cost Centre Code] }
    { 7   ;   ;Cost Object Code    ;Code20        ;TableRelation="Cost Object";
                                                   CaptionML=[ENU=Cost Object Code;
                                                              ENG=Cost Object Code] }
    { 10  ;   ;Combine Entries     ;Option        ;CaptionML=[ENU=Combine Entries;
                                                              ENG=Combine Entries];
                                                   OptionCaptionML=[ENU=None,Day,Month;
                                                                    ENG=None,Day,Month];
                                                   OptionString=None,Day,Month }
    { 13  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ENG=Blocked] }
    { 17  ;   ;New Page            ;Boolean       ;CaptionML=[ENU=New Page;
                                                              ENG=New Page] }
    { 18  ;   ;Blank Line          ;Boolean       ;CaptionML=[ENU=Blank Line;
                                                              ENG=Blank Line];
                                                   MinValue=No;
                                                   BlankZero=Yes }
    { 19  ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ENG=Indentation];
                                                   MinValue=0 }
    { 20  ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              ENG=Comment] }
    { 22  ;   ;Cost Classification ;Option        ;CaptionML=[ENU=Cost Classification;
                                                              ENG=Cost Classification];
                                                   OptionCaptionML=[ENU=" ,Fixed,Variable,Step Variable";
                                                                    ENG=" ,Fixed,Variable,Step Variable"];
                                                   OptionString=[ ,Fixed,Variable,Step Variable] }
    { 23  ;   ;Fixed Share         ;Text30        ;CaptionML=[ENU=Fixed Share;
                                                              ENG=Fixed Share] }
    { 26  ;   ;Modified Date       ;Date          ;CaptionML=[ENU=Modified Date;
                                                              ENG=Modified Date];
                                                   Editable=No }
    { 27  ;   ;Modified By         ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Modified By");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By];
                                                   Editable=No }
    { 28  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ENG=Date Filter] }
    { 29  ;   ;Cost Center Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Center";
                                                   CaptionML=[ENU=Cost Center Filter;
                                                              ENG=Cost Centre Filter] }
    { 30  ;   ;Cost Object Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Object";
                                                   CaptionML=[ENU=Cost Object Filter;
                                                              ENG=Cost Object Filter] }
    { 31  ;   ;Balance at Date     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                              Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                              Cost Center Code=FIELD(Cost Center Filter),
                                                                                              Cost Object Code=FIELD(Cost Object Filter),
                                                                                              Posting Date=FIELD(UPPERLIMIT(Date Filter))));
                                                   CaptionML=[ENU=Balance at Date;
                                                              ENG=Balance at Date];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 32  ;   ;Net Change          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                              Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                              Cost Center Code=FIELD(Cost Center Filter),
                                                                                              Cost Object Code=FIELD(Cost Object Filter),
                                                                                              Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Net Change;
                                                              ENG=Net Change];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 33  ;   ;Budget Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Budget Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                                     Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                     Cost Center Code=FIELD(Cost Center Filter),
                                                                                                     Cost Object Code=FIELD(Cost Object Filter),
                                                                                                     Date=FIELD(Date Filter),
                                                                                                     Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=[ENU=Budget Amount;
                                                              ENG=Budget Amount];
                                                   BlankZero=Yes }
    { 34  ;   ;Totaling            ;Text250       ;TableRelation="Cost Type";
                                                   OnValidate=BEGIN
                                                                IF NOT (Type IN [Type::Total,Type::"End-Total"]) THEN
                                                                  FIELDERROR(Type);

                                                                CALCFIELDS("Net Change");
                                                              END;

                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupCostTypeFilter(SelectionFilter) THEN
                                                                VALIDATE(Totaling,COPYSTR(SelectionFilter,1,MAXSTRLEN(Totaling)));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Totaling;
                                                              ENG=Totalling] }
    { 35  ;   ;Budget Filter       ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Budget Name";
                                                   CaptionML=[ENU=Budget Filter;
                                                              ENG=Budget Filter] }
    { 36  ;   ;Balance             ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                              Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                              Cost Center Code=FIELD(Cost Center Filter),
                                                                                              Cost Object Code=FIELD(Cost Object Filter)));
                                                   CaptionML=[ENU=Balance;
                                                              ENG=Balance];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 37  ;   ;Budget at Date      ;Decimal       ;CaptionML=[ENU=Budget at Date;
                                                              ENG=Budget at Date];
                                                   Editable=No }
    { 40  ;   ;G/L Account Range   ;Text50        ;TableRelation="G/L Account";
                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupGLAccFilter(SelectionFilter) THEN
                                                                VALIDATE("G/L Account Range",COPYSTR(SelectionFilter,1,MAXSTRLEN("G/L Account Range")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=G/L Account Range;
                                                              ENG=G/L Account Range] }
    { 47  ;   ;Debit Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Debit Amount" WHERE (Cost Type No.=FIELD(No.),
                                                                                                      Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                      Cost Center Code=FIELD(Cost Center Filter),
                                                                                                      Cost Object Code=FIELD(Cost Object Filter),
                                                                                                      Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Debit Amount;
                                                              ENG=Debit Amount] }
    { 48  ;   ;Credit Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Credit Amount" WHERE (Cost Type No.=FIELD(No.),
                                                                                                       Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                       Cost Center Code=FIELD(Cost Center Filter),
                                                                                                       Cost Object Code=FIELD(Cost Object Filter),
                                                                                                       Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Credit Amount;
                                                              ENG=Credit Amount] }
    { 51  ;   ;Balance to Allocate ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                              Cost Center Code=FIELD(Cost Center Filter),
                                                                                              Cost Object Code=FIELD(Cost Object Filter),
                                                                                              Allocated=CONST(No),
                                                                                              Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Balance to Allocate;
                                                              ENG=Balance to Allocate];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;Budget Debit Amount ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Budget Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                                     Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                     Cost Center Code=FIELD(Cost Center Filter),
                                                                                                     Cost Object Code=FIELD(Cost Object Filter),
                                                                                                     Date=FIELD(Date Filter),
                                                                                                     Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=[ENU=Budget Debit Amount;
                                                              ENG=Budget Debit Amount];
                                                   BlankNumbers=BlankNegAndZero }
    { 72  ;   ;Budget Credit Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Cost Budget Entry".Amount WHERE (Cost Type No.=FIELD(No.),
                                                                                                      Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                      Cost Center Code=FIELD(Cost Center Filter),
                                                                                                      Cost Object Code=FIELD(Cost Object Filter),
                                                                                                      Date=FIELD(Date Filter),
                                                                                                      Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=[ENU=Budget Credit Amount;
                                                              ENG=Budget Credit Amount];
                                                   BlankNumbers=BlankNegAndZero }
    { 73  ;   ;Add. Currency Net Change;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Additional-Currency Amount" WHERE (Cost Type No.=FIELD(No.),
                                                                                                                    Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                                    Cost Center Code=FIELD(Cost Center Filter),
                                                                                                                    Cost Object Code=FIELD(Cost Object Filter),
                                                                                                                    Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Add. Currency Net Change;
                                                              ENG=Add. Currency Net Change];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 74  ;   ;Add. Currency Balance at Date;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Additional-Currency Amount" WHERE (Cost Type No.=FIELD(No.),
                                                                                                                    Cost Type No.=FIELD(FILTER(Totaling)),
                                                                                                                    Cost Center Code=FIELD(Cost Center Filter),
                                                                                                                    Cost Object Code=FIELD(Cost Object Filter),
                                                                                                                    Posting Date=FIELD(UPPERLIMIT(Date Filter))));
                                                   CaptionML=[ENU=Add. Currency Balance at Date;
                                                              ENG=Add. Currency Balance at Date] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Type                                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Name,Type                            }
  }
  CODE
  {
    VAR
      CostEntry@1000 : Record 1104;
      CostBudgetEntry@1001 : Record 1109;
      AccPeriodGB@1002 : Record 10560;
      Text000@1003 : TextConst 'ENU=You cannot delete a cost type with entries in an open fiscal year.;ENG=You cannot delete a cost type with entries in an open fiscal year.';
      Text001@1004 : TextConst 'ENU=You cannot change cost type %1. There are %2 associated with it.;ENG=You cannot change cost type %1. There are %2 associated with it.';

    LOCAL PROCEDURE Modified@1();
    BEGIN
      "Modified Date" := TODAY;
      "Modified By" := USERID;
    END;

    PROCEDURE LookupGLAccFilter@2(VAR Text@1000 : Text[1024]) : Boolean;
    VAR
      GLAccList@1001 : Page 18;
    BEGIN
      GLAccList.LOOKUPMODE(TRUE);
      IF GLAccList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := GLAccList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE LookupCostTypeFilter@3(VAR Text@1000 : Text[1024]) : Boolean;
    VAR
      CostTypeList@1001 : Page 1124;
    BEGIN
      CostTypeList.LOOKUPMODE(TRUE);
      IF CostTypeList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := CostTypeList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    BEGIN
    END.
  }
}

