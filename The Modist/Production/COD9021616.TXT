OBJECT Codeunit 9021616 Populate New Content Type Atts
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:17 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rm;
    OnRun=VAR
            TxtFinalMessage@1000000000 : TextConst 'ENU=Done. %1 records inserted.;ENG=Done. %1 records inserted.';
          BEGIN
            IF NOT CONFIRM(Text000) THEN
              ERROR(Text001);
            IF CONFIRM(Text002) THEN
              SetDefault := TRUE
            ELSE
              SetDefault := FALSE;

            IF CONFIRM(Text004) THEN
              ShowInserted := TRUE
            ELSE
              ShowInserted := FALSE;

            ContType.RESET;
            ContTypeAtt.RESET;
            PageContentRec.RESET;
            PageContentValueRec.RESET;

            IF ContType.FIND('-') THEN
            REPEAT
              ContType.CALCFIELDS("Usage Count");
              IF ContType."Usage Count" > 0 THEN BEGIN
                PageContentRec.SETRANGE("Content Type Code",ContType.Code);
                IF PageContentRec.FIND('-') THEN
                REPEAT
                  Inserted := FALSE;
                  IF PageContentRec."Label Code" <> LabelRec.Code THEN
                      LabelRec.GET(PageContentRec."Label Code");
                  ContTypeAtt.SETRANGE("Content Type Code",ContType.Code);
                  IF ContTypeAtt.FIND('-') THEN
                  REPEAT
                    IF NOT PageContentValueRec.GET(PageContentRec."Label Code",PageContentRec."Page GroupGuid",
                           PageContentRec.PlaceholderID,PageContentRec."Line No.",ContTypeAtt.Attribute) THEN BEGIN
                      PageContentValueRec.INIT;
                      PageContentValueRec."Label Code" := PageContentRec."Label Code";
                      PageContentValueRec."Page GroupGuID" := PageContentRec."Page GroupGuid";
                      PageContentValueRec.PlaceholderID := PageContentRec.PlaceholderID;
                      PageContentValueRec."Content Line No." := PageContentRec."Line No.";
                      PageContentValueRec.Attribute := ContTypeAtt.Attribute;
                      PageContentValueRec."Content Type Code" := PageContentRec."Content Type Code";
                      PageContentValueRec."Html Allowed" := ContTypeAtt."Html Allowed";
                      PageContentValueRec.Required := ContTypeAtt.Required;
                      PageContentValueRec."Template Page GroupGuID" := PageContentRec."Template Page GroupGuID";
                      //PageContentValueRec."Value Type" :=
                      PageContentValueRec."Shared Html" := ContTypeAtt."Shared Html";
                      PageContentValueRec."Is Master Data"  := PageContentRec."Is Master Data";
                      IF SetDefault THEN
                        PageContentValueRec."Value Text" := ContTypeAtt."Default Value"
                      ELSE
                        PageContentValueRec."Value Text" := '';


                      IF LabelRec."Multi-Language Mode" THEN BEGIN
                        PageContentValueRec."In Multi Language Mode" := TRUE;
                        PageContentValueRec."Multilanguage Allowed" := ContTypeAtt."Multilanguage Allowed";
                        PageContentValueRec."Multilanguage Key" := ContTypeAtt."Multilanguage Key";
                        IF  PageContentValueRec."Multilanguage Key" <> '' THEN BEGIN
                          MultiLang.RESET;
                          MultiLang.SETCURRENTKEY("Label Code","Base File Name","Language ID","Resx Name");
                          MultiLang.SETRANGE("Label Code",PageContentRec."Label Code");
                          MultiLang.SETRANGE("Base File Name",'LANGUAGE');
                          MultiLang.SETRANGE("Resx Name",PageContentValueRec."Multilanguage Key");
                          IF NOT MultiLang.FINDFIRST THEN BEGIN
                            MultiLang.SETRANGE("Label Code");
                            IF MultiLang.FINDFIRST THEN BEGIN
                              MultiLang1.INIT;
                              MultiLang1 := MultiLang;
                              MultiLang1."Entry No." := 0;
                              MultiLang1."Label Code" := PageContentRec."Label Code";
                              MultiLang1.INSERT(TRUE);
                            END ELSE BEGIN
                              ERROR(Text003,PageContentRec."Page GroupGuid",PageContentRec.PlaceholderID,
                                    PageContentRec."Content Type Code",PageContentValueRec."Multilanguage Key");
                            END;
                          END;
                        END;
                      END ELSE BEGIN
                        PageContentValueRec."In Multi Language Mode" := FALSE;
                        PageContentValueRec."Multilanguage Allowed" := FALSE;
                        PageContentValueRec."Multilanguage Key" := '';
                      END;
                      PageContentValueRec."DateTime Created" := CURRENTDATETIME;
                      PageContentValueRec."DateTime Last Modified" := CURRENTDATETIME;
                      PageContentValueRec.INSERT;
                      Inserted := TRUE;
                      i := i + 1;
                      IF ShowInserted THEN BEGIN
                        IF NOT CONFIRM(Text006,TRUE,PageContentRec."Page GroupGuid",PageContentRec.PlaceholderID,
                                    PageContentRec."Content Type Code",PageContentValueRec.Attribute) THEN
                          ERROR(Text007);
                        j := j + 1;
                        IF j > 5 THEN BEGIN
                          IF NOT CONFIRM(Text005) THEN
                            ShowInserted := FALSE;
                        END;
                      END;
                    END;
                  UNTIL ContTypeAtt.NEXT = 0;
                  IF Inserted THEN BEGIN
                    IF ePage.GET(PageContentRec."Label Code",PageContentRec."Page GroupGuid") THEN BEGIN
                      ePage.VALIDATE("DateTime Last Modified",CURRENTDATETIME);
                      ePage.MODIFY;
                    END;
                  END;
                UNTIL PageContentRec.NEXT = 0;
              END;
            UNTIL ContType.NEXT = 0;

            MESSAGE(TxtFinalMessage, i);
          END;

  }
  CODE
  {
    VAR
      ContType@1000000000 : Record 9021633;
      ContTypeAtt@1000000001 : Record 9021634;
      PageContentValueRec@1000000003 : Record 9021635;
      PageContentRec@1000000002 : Record 9021630;
      ePage@1000000009 : Record 9021628;
      LabelRec@1000000010 : Record 9021618;
      MultiLang@1000000012 : Record 9021603;
      MultiLang1@1000000013 : Record 9021603;
      SetDefault@1000000004 : Boolean;
      Text000@1000000005 : TextConst 'ENU=This codeunit will create Page Content Values from Content Type Attributes if they do not exist.  Are you sure you want to continue?;ENG=This codeunit will create Page Content Values from Content Type Attributes if they do not exist.  Are you sure you want to continue?';
      Text001@1000000006 : TextConst 'ENU=Abandonned.;ENG=Abandonned.';
      Text002@1000000007 : TextConst 'ENU=Do you wish the newly created Page Content Values to be populated with the default values from the Contenty Atribute defaults?;ENG=Do you wish the newly created Page Content Values to be populated with the default values from the Contenty Atribute defaults?';
      Inserted@1000000008 : Boolean;
      Text003@1000000011 : TextConst 'ENU=Cannot find entry in Multi Language Reference Table for Page GroupGUID %1, PlaceholderID %2, Content Type %3,Multilanguage Key %4. (3);ENG=Cannot find entry in Multi Language Reference Table for Page GroupGUID %1, PlaceholderID %2, Content Type %3,Multilanguage Key %4. (3)';
      ShowInserted@1000000015 : Boolean;
      i@1000000014 : Integer;
      j@1000000016 : Integer;
      Text004@1000000017 : TextConst 'ENU=Do you want to show each record that is inserted?;ENG=Do you want to show each record that is inserted?';
      Text005@1000000018 : TextConst 'ENU=Do you wish to carry on seeing each inserted record?;ENG=Do you wish to carry on seeing each inserted record?';
      Text006@1000000019 : TextConst 'ENU=Inserting Page GroupGUID %1, PlaceholderID %2, Content Type %3,  Attribute %4. Continue?;ENG=Inserting Page GroupGUID %1, PlaceholderID %2, Content Type %3,  Attribute %4. Continue?';
      Text007@1000000020 : TextConst 'ENU=Everything abandonned - you must start again as nothing will have been inserted. (7);ENG=Everything abandonned - you must start again as nothing will have been inserted. (7)';

    BEGIN
    {
      //DOC EC2013.3.40 MQ 15/01/2013 - GW130115 New
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

