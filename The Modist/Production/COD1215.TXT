OBJECT Codeunit 1215 Data Exch. Type Selector
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=133;
    OnRun=VAR
            BestDataExchCode@1000 : Code[20];
          BEGIN
            IF Type <> Type::XML THEN
              ERROR(InvalidTypeErr);

            CheckContentHasValue(Rec);

            BestDataExchCode := FindDataExchType(Rec);

            SetResult("Incoming Document Entry No.",BestDataExchCode);
          END;

  }
  CODE
  {
    VAR
      InvalidTypeErr@1000 : TextConst 'ENU=The attachment is not an XML document.;ENG=The attachment is not an XML document.';
      AttachmentEmptyErr@1001 : TextConst 'ENU=The attachment does not contain any data.;ENG=The attachment does not contain any data.';

    LOCAL PROCEDURE FindDataExchType@7(IncomingDocumentAttachment@1000 : Record 133) : Code[20];
    VAR
      DataExch@1005 : Record 1220;
      DataExchangeType@1004 : Record 1213;
      DataExchDef@1003 : Record 1222;
      IntermediateDataImport@1002 : Record 1214;
      BestDataExchCode@1007 : Code[20];
      BestDataExchValue@1006 : Integer;
    BEGIN
      DataExchangeType.FINDSET;
      REPEAT
        IF DataExchDefUsesIntermediate(DataExchangeType."Data Exch. Def. Code") THEN BEGIN
          DataExchDef.GET(DataExchangeType."Data Exch. Def. Code");
          CreateDataExch(DataExch,DataExchDef,IncomingDocumentAttachment);

          // Create Intermediate table records for each Data Exchange Type
          IF TryCreateIntermediate(DataExch,DataExchDef) THEN BEGIN
            IntermediateDataImport.SETRANGE("Data Exch. No.",DataExch."Entry No.");

            // Update best result if this one is better
            IF IntermediateDataImport.COUNT > BestDataExchValue THEN BEGIN
              BestDataExchCode := DataExchDef.Code;
              BestDataExchValue := IntermediateDataImport.COUNT;
            END;

            IntermediateDataImport.DELETEALL(TRUE); // cleanup
          END;

          DataExch.DELETE(TRUE); // cleanup
        END;
      UNTIL DataExchangeType.NEXT = 0;

      EXIT(BestDataExchCode);
    END;

    LOCAL PROCEDURE CreateDataExch@2(VAR DataExch@1000 : Record 1220;DataExchDef@1002 : Record 1222;IncomingDocumentAttachment@1001 : Record 133);
    VAR
      Stream@1003 : InStream;
    BEGIN
      IncomingDocumentAttachment.Content.CREATEINSTREAM(Stream);

      DataExch.INIT;
      DataExch.InsertRec(IncomingDocumentAttachment.Name,Stream,DataExchDef.Code);
      DataExch.VALIDATE("Incoming Entry No.",IncomingDocumentAttachment."Incoming Document Entry No.");
      DataExch.MODIFY(TRUE);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryCreateIntermediate@3(DataExch@1000 : Record 1220;DataExchDef@1001 : Record 1222);
    BEGIN
      IF DataExchDef."Reading/Writing Codeunit" <> 0 THEN BEGIN
        CODEUNIT.RUN(DataExchDef."Reading/Writing Codeunit",DataExch);

        IF DataExchDef."Data Handling Codeunit" <> 0 THEN
          CODEUNIT.RUN(DataExchDef."Data Handling Codeunit",DataExch);
      END;
    END;

    LOCAL PROCEDURE SetResult@6(IncomingDocCode@1000 : Integer;DataExchTypeCode@1001 : Code[20]);
    VAR
      DataExchangeType@1002 : Record 1213;
      IncomingDocument@1003 : Record 130;
    BEGIN
      IF DataExchTypeCode = '' THEN
        EXIT;

      DataExchangeType.SETRANGE("Data Exch. Def. Code",DataExchTypeCode);
      DataExchangeType.FINDFIRST;

      IncomingDocument.GET(IncomingDocCode);
      IncomingDocument.VALIDATE("Data Exchange Type",DataExchangeType.Code);
      IncomingDocument.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE CheckContentHasValue@4(VAR IncomingDocumentAttachment@1000 : Record 133);
    VAR
      LiveIncomingDocumentAttachment@1001 : Record 133;
    BEGIN
      // Is the data already loaded or is it in the db?
      LiveIncomingDocumentAttachment := IncomingDocumentAttachment;
      LiveIncomingDocumentAttachment.CALCFIELDS(Content);
      IF LiveIncomingDocumentAttachment.Content.HASVALUE THEN
        IncomingDocumentAttachment.CALCFIELDS(Content);

      IF NOT IncomingDocumentAttachment.Content.HASVALUE THEN
        ERROR(AttachmentEmptyErr);
    END;

    LOCAL PROCEDURE DataExchDefUsesIntermediate@1(DataExchDefCode@1000 : Code[20]) : Boolean;
    VAR
      DataExchMapping@1001 : Record 1224;
    BEGIN
      // Ensure that the data exch def uses the intermediate table so we don't just start inserting data into the db.
      DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchMapping.SETRANGE("Use as Intermediate Table",FALSE);
      EXIT(NOT DataExchMapping.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

