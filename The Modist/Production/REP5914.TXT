OBJECT Report 5914 Delete Invoiced Service Orders
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invoiced Service Orders;
               ENG=Delete Invoiced Service Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1634;    ;DataItem;                    ;
               DataItemTable=Table5900;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[ENU=Service Order;
                                   ENG=Service Order];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  ItemChargeAssgntService.RESET;
                                  ItemChargeAssgntService.SETRANGE("Document Type","Document Type");
                                  ItemChargeAssgntService.SETRANGE("Document No.","No.");

                                  ServiceOrderLine.RESET;
                                  ServiceOrderLine.SETRANGE("Document Type","Document Type");
                                  ServiceOrderLine.SETRANGE("Document No.","No.");
                                  ServiceOrderLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF ServiceOrderLine.FIND('-') THEN BEGIN
                                    ServiceOrderLine.SETRANGE("Quantity Invoiced");
                                    ServiceOrderLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT ServiceOrderLine.FIND('-') THEN BEGIN
                                      ServiceOrderLine.SETRANGE("Outstanding Quantity");
                                      ServiceOrderLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                                      IF NOT ServiceOrderLine.FIND('-') THEN BEGIN
                                        ServiceOrderLine.LOCKTABLE;
                                        IF NOT ServiceOrderLine.FIND('-') THEN BEGIN
                                          ServiceOrderLine.SETRANGE("Qty. Shipped Not Invoiced");
                                          IF ServiceOrderLine.FIND('-') THEN
                                            REPEAT
                                              ServiceOrderLine.DELETE;
                                            UNTIL ServiceOrderLine.NEXT = 0;

                                          ServiceOrderItemLine.RESET;
                                          ServiceOrderItemLine.SETRANGE("Document Type","Document Type");
                                          ServiceOrderItemLine.SETRANGE("Document No.","No.");
                                          IF ServiceOrderItemLine.FINDSET THEN
                                            REPEAT
                                              ServiceOrderItemLine.DELETE;
                                            UNTIL ServiceOrderItemLine.NEXT = 0;

                                          ServicePost.DeleteHeader("Service Header",ServiceShptHeader,ServiceInvHeader,ServiceCrMemoHeader);

                                          ReserveServiceLine.DeleteInvoiceSpecFromHeader("Service Header");

                                          ServiceCommentLine.SETRANGE(Type,"Document Type");
                                          ServiceCommentLine.SETRANGE("No.","No.");
                                          ServiceCommentLine.DELETEALL;

                                          WhseRequest.SETRANGE("Source Type",DATABASE::"Service Line");
                                          WhseRequest.SETRANGE("Source Subtype","Document Type");
                                          WhseRequest.SETRANGE("Source No.","No.");
                                          WhseRequest.DELETEALL(TRUE);

                                          ServOrderAlloc.RESET;
                                          ServOrderAlloc.SETCURRENTKEY("Document Type");
                                          ServOrderAlloc.SETRANGE("Document Type","Document Type");
                                          ServOrderAlloc.SETRANGE("Document No.","No.");
                                          ServOrderAlloc.SETRANGE(Posted,FALSE);
                                          ServOrderAlloc.DELETEALL;
                                          ServAllocMgt.SetServOrderAllocStatus("Service Header");

                                          ApprovalsMgmt.DeleteApprovalEntry(DATABASE::"Service Header","Document Type","No.");

                                          DELETE;
                                          COMMIT;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Customer No.,Bill-to Customer No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing Service orders #1##########;ENG=Processing Service orders #1##########';
      ServiceOrderItemLine@1012 : Record 5901;
      ServiceOrderLine@1001 : Record 5902;
      ServiceShptHeader@1009 : Record 5990;
      ServiceInvHeader@1010 : Record 5992;
      ServiceCrMemoHeader@1011 : Record 5994;
      ServiceCommentLine@1002 : Record 5906;
      ItemChargeAssgntService@1007 : Record 5809;
      WhseRequest@1008 : Record 5765;
      ServOrderAlloc@1014 : Record 5950;
      ServicePost@1005 : Codeunit 5980;
      ReserveServiceLine@1006 : Codeunit 99000842;
      ServAllocMgt@1013 : Codeunit 5930;
      Window@1004 : Dialog;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

