OBJECT XMLport 9021754 Import Internet Logins
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:32 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Internet Logins;
               ENG=Import Internet Logins];
    Direction=Import;
    DefaultFieldsValidation=No;
    OnInitXMLport=VAR
                    TxtL001@1000000000 : TextConst 'ENU=This import creates Internet Logins from a CSV file with no header and four columns of Customer No., Contact, Email, Password;ENG=This import creates Internet Logins from a CSV file with no header and four columns of Customer No., Contact, Email, Password';
                    TxtL002@1000000001 : TextConst 'ENU=If the Customer is not already an Internet Customer and Is B2B Customer and / or are not ticked, these will be set.;ENG=If the Customer is not already an Internet Customer and Is B2B Customer and / or are not ticked, these will be set.';
                  BEGIN
                    MESSAGE(TxtL001);
                    MESSAGE(TxtL002);
                  END;

    OnPreXMLport=BEGIN
                   LabelCode := GlobalFunctions.GetLabelCode;
                 END;

    OnPostXMLport=VAR
                    TxtL001@1000000000 : TextConst 'ENU=Warning No Skipped %1, No no customer found %2;ENG=Warning No Skipped %1, No no customer found %2';
                    TxtL002@1000000001 : TextConst 'ENU=No Logins updated %1, inserted %2, No Customers setup %3;ENG=No Logins updated %1, inserted %2, No Customers setup %3';
                  BEGIN
                    IF (NoSkip <> 0) OR
                      (NoNoCust <> 0) THEN
                    MESSAGE(TxtL001,NoSkip,NoNoCust);
                    MESSAGE(TxtL002,NoMod,NoIns,NoCustSetup);
                  END;

    Format=Variable Text;
    FieldDelimiter=None;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{A6762B67-792B-4BAF-A8CC-8F47059EB394}];  ;Root                ;Element ;Text     }

    { [{CEDEBF5A-25FE-4D8B-AC95-41C1D9461616}];1 ;InternetLogin       ;Element ;Table   ;
                                                  VariableName=IntLogin;
                                                  SourceTable=Table9021643;
                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 IF (InCustNo = '') OR
                                                                                   (InEmail = '') OR
                                                                                   (InPassword = '') THEN BEGIN
                                                                                   NoSkip := NoSkip + 1;
                                                                                   currXMLport.BREAK;
                                                                                 END;

                                                                                 IF NOT Cust.GET(InCustNo) THEN BEGIN
                                                                                   NoNoCust := NoNoCust + 1;
                                                                                   currXMLport.SKIP;
                                                                                 END ELSE BEGIN
                                                                                   ModCust := FALSE;
                                                                                   IF Cust."Label Code" = '' THEN BEGIN
                                                                                     Cust."Label Code" := LabelCode;
                                                                                     ModCust := TRUE;
                                                                                   END;
                                                                                   IF Cust."Is An Internet Customer" = FALSE THEN BEGIN
                                                                                     Cust.VALIDATE("Is An Internet Customer",TRUE);
                                                                                     ModCust := TRUE;
                                                                                   END;
                                                                                   IF Cust."Is a B2B Internet Customer" = FALSE THEN BEGIN
                                                                                     Cust.VALIDATE("Is a B2B Internet Customer",TRUE);
                                                                                     ModCust := TRUE;
                                                                                   END;
                                                                                   IF ModCust THEN BEGIN
                                                                                     Cust.MODIFY(TRUE);
                                                                                     NoCustSetup := NoCustSetup + 1;
                                                                                   END;
                                                                                 END;

                                                                                 EmailCode := InEmail;
                                                                                 IntLoginT.RESET;
                                                                                 IntLoginT.SETCURRENTKEY("Customer No.");
                                                                                 IntLoginT.SETRANGE("Customer No.",InCustNo);
                                                                                 IntLoginT.SETRANGE("Code Login",EmailCode);
                                                                                 IF IntLoginT.FINDFIRST THEN BEGIN
                                                                                   IF InContact <> IntLoginT.Contact THEN
                                                                                     IntLoginT.VALIDATE(Contact,InContact);
                                                                                   IntLoginT.VALIDATE(Password,InPassword);
                                                                                   IntLoginT.MODIFY(TRUE);
                                                                                   NoMod := NoMod + 1;
                                                                                 END ELSE BEGIN
                                                                                   CLEAR(IntLoginT1);
                                                                                   IntLoginT1.INIT;
                                                                                   IntLoginT1."Customer No." := InCustNo;
                                                                                   IntLoginT1."Label Code" := Cust."Label Code";
                                                                                   IF InContact <> '' THEN
                                                                                     IntLoginT1.VALIDATE(Contact,InContact);
                                                                                   IntLoginT1.VALIDATE("E-Mail",InEmail);
                                                                                   IntLoginT1.VALIDATE(Password,InPassword);
                                                                                   IntLoginT1.INSERT(TRUE);
                                                                                   NoIns := NoIns + 1;
                                                                                 END;

                                                                                 //Code to get around we do not have AutoSave = No
                                                                                 IF IntLogin."No." = '' THEN
                                                                                   currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{4A499CCC-EC52-45B4-9D81-AE40BF6F052A}];2 ;InCustNo            ;Element ;Text     }

    { [{C0E7B5D7-F5F3-4394-A445-D14FDE092DF7}];2 ;InContact           ;Element ;Text     }

    { [{EE281E4C-E34C-4D12-8CE8-55BEC7242E24}];2 ;InEmail             ;Element ;Text     }

    { [{6A80A8E3-5603-45ED-9F8D-E8C6FBFB6939}];2 ;InPassword          ;Element ;Text     }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      IntLoginT@1000000000 : Record 9021643;
      IntLoginT1@1000000003 : Record 9021643;
      Cust@1000000001 : Record 18;
      EmailCode@1000000002 : Code[80];
      NoMod@1000000004 : Integer;
      NoIns@1000000005 : Integer;
      NoSkip@1000000006 : Integer;
      NoNoCust@1000000007 : Integer;
      NoCustSetup@1000000009 : Integer;
      ModCust@1000000008 : Boolean;
      LabelCode@1000000010 : Code[10];
      GlobalFunctions@1000000011 : Codeunit 9021624;

    BEGIN
    {
      //DOC EC2013.4.64 MQ 02/07/2014 - GW140702 New; Creates new logins from a CSV file consisting of (no header row); Customer No., Contact, Email, Password
      //DOC EC2013.4.64 MQ 02/07/2014 -   If Customer is not an Internet Customer sorts this out as well.
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

