OBJECT Codeunit 915 Assembly Info-Pane Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Item@1001 : Record 27;
      AvailableToPromise@1005 : Codeunit 5790;

    PROCEDURE CalcAvailability@18(VAR AsmLine@1000 : Record 901) : Decimal;
    VAR
      GrossRequirement@1002 : Decimal;
      ScheduledReceipt@1003 : Decimal;
      PeriodType@1006 : 'Day,Week,Month,Quarter,Year';
      LookaheadDateformula@1005 : DateFormula;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(
          AvailableToPromise.QtyAvailabletoPromise(
            Item,
            GrossRequirement,
            ScheduledReceipt,
            CalcAvailabilityDate(AsmLine),
            PeriodType,
            LookaheadDateformula));
      END;
    END;

    LOCAL PROCEDURE CalcAvailabilityDate@27(AsmLine@1000 : Record 901) : Date;
    BEGIN
      IF AsmLine."Due Date" <> 0D THEN
        EXIT(AsmLine."Due Date");

      EXIT(WORKDATE);
    END;

    PROCEDURE CalcAvailableInventory@9(VAR AsmLine@1000 : Record 901) : Decimal;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(AvailableToPromise.CalcAvailableInventory(Item));
      END;
    END;

    PROCEDURE CalcScheduledReceipt@21(VAR AsmLine@1001 : Record 901) : Decimal;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(AvailableToPromise.CalcScheduledReceipt(Item));
      END;
    END;

    PROCEDURE CalcGrossRequirement@23(VAR AsmLine@1001 : Record 901) : Decimal;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(AvailableToPromise.CalcGrossRequirement(Item));
      END;
    END;

    PROCEDURE CalcReservedReceipt@25(VAR AsmLine@1001 : Record 901) : Decimal;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(AvailableToPromise.CalcReservedReceipt(Item));
      END;
    END;

    PROCEDURE CalcReservedRequirement@26(VAR AsmLine@1000 : Record 901) : Decimal;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        SetItemFilter(Item,AsmLine);

        EXIT(AvailableToPromise.CalcReservedRequirement(Item));
      END;
    END;

    LOCAL PROCEDURE CalcNoOfSubstitutions@20(VAR AsmLine@1000 : Record 901) : Integer;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        Item.CALCFIELDS("No. of Substitutes");
        EXIT(Item."No. of Substitutes");
      END;
    END;

    LOCAL PROCEDURE ItemCommentExists@12(AsmLine@1000 : Record 901) : Boolean;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        Item.CALCFIELDS(Comment);
        EXIT(Item.Comment);
      END;
    END;

    PROCEDURE LookupItem@7(AsmLine@1000 : Record 901);
    BEGIN
      AsmLine.TESTFIELD(Type,AsmLine.Type::Item);
      AsmLine.TESTFIELD("No.");
      GetItem(AsmLine);
      PAGE.RUNMODAL(PAGE::"Item Card",Item);
    END;

    LOCAL PROCEDURE LookupItemComment@14(AsmLine@1000 : Record 901);
    VAR
      CommentLine@1001 : Record 97;
    BEGIN
      IF GetItem(AsmLine) THEN BEGIN
        CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::Item);
        CommentLine.SETRANGE("No.",AsmLine."No.");
        PAGE.RUNMODAL(PAGE::"Comment Sheet",CommentLine);
      END;
    END;

    LOCAL PROCEDURE GetItem@24(AsmLine@1000 : Record 901) : Boolean;
    BEGIN
      WITH Item DO BEGIN
        IF (AsmLine.Type <> AsmLine.Type::Item) OR (AsmLine."No." = '') THEN
          EXIT(FALSE);

        IF AsmLine."No." <> "No." THEN
          GET(AsmLine."No.");
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE SetItemFilter@4(VAR Item@1000 : Record 27;AsmLine@1001 : Record 901);
    BEGIN
      Item.RESET;
      Item.SETRANGE("Date Filter",0D,CalcAvailabilityDate(AsmLine));
      Item.SETRANGE("Variant Filter",AsmLine."Variant Code");
      Item.SETRANGE("Location Filter",AsmLine."Location Code");
      Item.SETRANGE("Drop Shipment Filter",FALSE);
    END;

    BEGIN
    END.
  }
}

