OBJECT Page 624 Unapply Vendor Entries
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Unapply Vendor Entries;
               ENG=Unapply Vendor Entries];
    InsertAllowed=No;
    SourceTable=Table380;
    DataCaptionExpr=Caption;
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 InsertEntries;
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 42      ;1   ;Action    ;
                      Name=Unapply;
                      CaptionML=[ENU=&Unapply;
                                 ENG=&Unapply];
                      Promoted=Yes;
                      Image=UnApply;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 VendEntryApplyPostedEntries@1000 : Codeunit 227;
                               BEGIN
                                 IF ISEMPTY THEN
                                   ERROR(Text010);
                                 IF NOT CONFIRM(Text011,FALSE) THEN
                                   EXIT;

                                 VendEntryApplyPostedEntries.PostUnApplyVendor(DtldVendLedgEntry2,DocNo,PostingDate);
                                 PostingDate := 0D;
                                 DocNo := '';
                                 DELETEALL;
                                 MESSAGE(Text009);

                                 CurrPage.CLOSE;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Preview;
                      CaptionML=[ENU=Preview Unapply;
                                 ENG=Preview Unapply];
                      Image=ViewPostedOrder;
                      OnAction=VAR
                                 VendEntryApplyPostedEntries@1000 : Codeunit 227;
                               BEGIN
                                 IF ISEMPTY THEN
                                   ERROR(Text010);

                                 VendEntryApplyPostedEntries.PreviewUnapply(DtldVendLedgEntry2,DocNo,PostingDate);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 33  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 34  ;2   ;Field     ;
                Name=DocuNo;
                CaptionML=[ENU=Document No.;
                           ENG=Document No.];
                SourceExpr=DocNo }

    { 35  ;2   ;Field     ;
                Name=PostDate;
                CaptionML=[ENU=Posting Date;
                           ENG=Posting Date];
                SourceExpr=PostingDate }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Posting Date" }

    { 4   ;2   ;Field     ;
                SourceExpr="Entry Type" }

    { 6   ;2   ;Field     ;
                SourceExpr="Document Type" }

    { 8   ;2   ;Field     ;
                SourceExpr="Document No." }

    { 10  ;2   ;Field     ;
                SourceExpr="Vendor No." }

    { 43  ;2   ;Field     ;
                SourceExpr="Initial Document Type" }

    { 40  ;2   ;Field     ;
                CaptionML=[ENU=Initial Document No.;
                           ENG=Initial Document No.];
                SourceExpr=GetDocumentNo }

    { 12  ;2   ;Field     ;
                SourceExpr="Initial Entry Global Dim. 1";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Initial Entry Global Dim. 2";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Currency Code" }

    { 18  ;2   ;Field     ;
                SourceExpr=Amount }

    { 20  ;2   ;Field     ;
                SourceExpr="Amount (LCY)" }

    { 22  ;2   ;Field     ;
                SourceExpr="Initial Entry Due Date" }

    { 24  ;2   ;Field     ;
                SourceExpr="User ID";
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="Source Code";
                Visible=FALSE }

    { 28  ;2   ;Field     ;
                SourceExpr="Reason Code";
                Visible=FALSE }

    { 30  ;2   ;Field     ;
                SourceExpr="Vendor Ledger Entry No.";
                Visible=FALSE }

    { 38  ;2   ;Field     ;
                SourceExpr="Entry No." }

  }
  CODE
  {
    VAR
      DtldVendLedgEntry2@1004 : Record 380;
      Vend@1005 : Record 23;
      DocNo@1000 : Code[20];
      PostingDate@1001 : Date;
      VendLedgEntryNo@1002 : Integer;
      Text009@1008 : TextConst 'ENU=The entries were successfully unapplied.;ENG=The entries were successfully unapplied.';
      Text010@1003 : TextConst 'ENU=There is nothing to unapply.;ENG=There is nothing to unapply.';
      Text011@1006 : TextConst 'ENU=To unapply these entries, correcting entries will be posted.\Do you want to unapply the entries?;ENG=To unapply these entries, correcting entries will be posted.\Do you want to unapply the entries?';

    PROCEDURE SetDtldVendLedgEntry@4(EntryNo@1001 : Integer);
    BEGIN
      DtldVendLedgEntry2.GET(EntryNo);
      VendLedgEntryNo := DtldVendLedgEntry2."Vendor Ledger Entry No.";
      PostingDate := DtldVendLedgEntry2."Posting Date";
      DocNo := DtldVendLedgEntry2."Document No.";
      Vend.GET(DtldVendLedgEntry2."Vendor No.");
    END;

    LOCAL PROCEDURE InsertEntries@1();
    VAR
      DtldVendLedgEntry@1000 : Record 380;
    BEGIN
      IF DtldVendLedgEntry2."Transaction No." = 0 THEN BEGIN
        DtldVendLedgEntry.SETCURRENTKEY("Application No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Application No.",DtldVendLedgEntry2."Application No.");
      END ELSE BEGIN
        DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Transaction No.",DtldVendLedgEntry2."Transaction No.");
      END;
      DtldVendLedgEntry.SETRANGE("Vendor No.",DtldVendLedgEntry2."Vendor No.");
      DELETEALL;
      IF DtldVendLedgEntry.FIND('-') THEN
        REPEAT
          IF (DtldVendLedgEntry."Entry Type" <> DtldVendLedgEntry."Entry Type"::"Initial Entry") AND
             NOT DtldVendLedgEntry.Unapplied
          THEN BEGIN
            Rec := DtldVendLedgEntry;
            INSERT;
          END;
        UNTIL DtldVendLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDocumentNo@7() : Code[20];
    VAR
      VendLedgEntry@1000 : Record 25;
    BEGIN
      IF VendLedgEntry.GET("Vendor Ledger Entry No.") THEN;
      EXIT(VendLedgEntry."Document No.");
    END;

    LOCAL PROCEDURE Caption@5() : Text[100];
    VAR
      VendLedgEntry@1000 : Record 25;
    BEGIN
      EXIT(STRSUBSTNO(
          '%1 %2 %3 %4',
          Vend."No.",
          Vend.Name,
          VendLedgEntry.FIELDCAPTION("Entry No."),
          VendLedgEntryNo));
    END;

    BEGIN
    END.
  }
}

