OBJECT Table 137 Inc. Doc. Attachment Overview
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               IncomingDocumentAttachment@1000 : Record 133;
             BEGIN
               IF IncomingDocumentAttachment.GET("Incoming Document Entry No.","Line No.") THEN
                 IncomingDocumentAttachment.DELETE(TRUE);
             END;

    CaptionML=[ENU=Inc. Doc. Attachment Overview;
               ENG=Inc. Doc. Attachment Overview];
  }
  FIELDS
  {
    { 1   ;   ;Incoming Document Entry No.;Integer;TableRelation="Incoming Document";
                                                   CaptionML=[ENU=Incoming Document Entry No.;
                                                              ENG=Incoming Document Entry No.] }
    { 2   ;   ;Line No.            ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 3   ;   ;Created Date-Time   ;DateTime      ;CaptionML=[ENU=Created Date-Time;
                                                              ENG=Created Date-Time] }
    { 4   ;   ;Created By User Name;Code50        ;CaptionML=[ENU=Created By User Name;
                                                              ENG=Created By User Name] }
    { 5   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ENG=Name];
                                                   Editable=No }
    { 6   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=" ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other";
                                                                    ENG=" ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other"];
                                                   OptionString=[ ,Image,PDF,Word,Excel,PowerPoint,Email,XML,Other];
                                                   Editable=No }
    { 7   ;   ;File Extension      ;Text30        ;CaptionML=[ENU=File Extension;
                                                              ENG=File Extension];
                                                   Editable=No }
    { 100 ;   ;Attachment Type     ;Option        ;CaptionML=[ENU=Attachment Type;
                                                              ENG=Attachment Type];
                                                   OptionCaptionML=[ENU=,Group,Main Attachment,OCR Result,Supporting Attachment,Link;
                                                                    ENG=,Group,Main Attachment,OCR Result,Supporting Attachment,Link];
                                                   OptionString=,Group,Main Attachment,OCR Result,Supporting Attachment,Link;
                                                   Editable=No }
    { 101 ;   ;Sorting Order       ;Integer       ;CaptionML=[ENU=Sorting Order;
                                                              ENG=Sorting Order] }
    { 102 ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ENG=Indentation] }
  }
  KEYS
  {
    {    ;Sorting Order,Incoming Document Entry No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Created Date-Time,Name,File Extension    }
  }
  CODE
  {
    VAR
      SupportingAttachmentsTxt@1000 : TextConst 'ENU=Supporting Attachments;ENG=Supporting Attachments';

    PROCEDURE NameDrillDown@13();
    VAR
      IncomingDocument@1001 : Record 130;
      IncomingDocumentAttachment@1000 : Record 133;
    BEGIN
      CASE "Attachment Type" OF
        "Attachment Type"::Group:
          EXIT;
        "Attachment Type"::Link:
          BEGIN
            IncomingDocument.GET("Incoming Document Entry No.");
            HYPERLINK(IncomingDocument.GetURL);
          END
        ELSE BEGIN
          IF NOT IncomingDocumentAttachment.GET("Incoming Document Entry No.","Line No.") THEN
            EXIT;

          IncomingDocumentAttachment.Export('',TRUE);
        END
      END;
    END;

    PROCEDURE GetStyleTxt@4() : Text;
    BEGIN
      CASE "Attachment Type" OF
        "Attachment Type"::Group,
        "Attachment Type"::"Main Attachment",
        "Attachment Type"::Link:
          EXIT('Strong');
        ELSE
          EXIT('Standard');
      END;
    END;

    PROCEDURE InsertFromIncomingDocument@2(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1003 : TEMPORARY Record 137);
    VAR
      SortingOrder@1004 : Integer;
    BEGIN
      InsertMainAttachment(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder);
      InsertLinkAddress(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder);
      InsertSupportingAttachments(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder,TRUE);
    END;

    PROCEDURE InsertSupportingAttachmentsFromIncomingDocument@1(IncomingDocument@1001 : Record 130;VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137);
    VAR
      SortingOrder@1002 : Integer;
    BEGIN
      InsertSupportingAttachments(IncomingDocument,TempIncDocAttachmentOverview,SortingOrder,FALSE);
    END;

    LOCAL PROCEDURE InsertMainAttachment@3(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer);
    VAR
      IncomingDocumentAttachment@1003 : Record 133;
    BEGIN
      IF NOT IncomingDocument.GetMainAttachment(IncomingDocumentAttachment) THEN
        EXIT;

      InsertFromIncomingDocumentAttachment(
        TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
        TempIncDocAttachmentOverview."Attachment Type"::"Main Attachment",0);

      CLEAR(IncomingDocumentAttachment);
      IF NOT IncomingDocument.GetGeneratedFromOCRAttachment(IncomingDocumentAttachment) THEN
        EXIT;

      InsertFromIncomingDocumentAttachment(
        TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
        TempIncDocAttachmentOverview."Attachment Type"::"OCR Result",1);
    END;

    LOCAL PROCEDURE InsertSupportingAttachments@6(IncomingDocument@1000 : Record 130;VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer;IncludeGroupCaption@1004 : Boolean);
    VAR
      IncomingDocumentAttachment@1003 : Record 133;
    BEGIN
      IF NOT IncomingDocument.GetAdditionalAttachments(IncomingDocumentAttachment) THEN
        EXIT;

      IF IncludeGroupCaption THEN
        InsertGroup(TempIncDocAttachmentOverview,IncomingDocument,SortingOrder,SupportingAttachmentsTxt);

      REPEAT
        InsertFromIncomingDocumentAttachment(
          TempIncDocAttachmentOverview,IncomingDocumentAttachment,SortingOrder,
          TempIncDocAttachmentOverview."Attachment Type"::"Supporting Attachment",1);
      UNTIL IncomingDocumentAttachment.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertLinkAddress@8(IncomingDocument@1003 : Record 130;VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137;VAR SortingOrder@1002 : Integer);
    VAR
      URL@1001 : Text;
    BEGIN
      URL := IncomingDocument.GetURL;
      IF URL = '' THEN
        EXIT;

      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview."Incoming Document Entry No." := IncomingDocument."Entry No.";
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview.Name := COPYSTR(URL,1,MAXSTRLEN(TempIncDocAttachmentOverview.Name));
      TempIncDocAttachmentOverview."Attachment Type" := TempIncDocAttachmentOverview."Attachment Type"::Link;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertFromIncomingDocumentAttachment@7(VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;IncomingDocumentAttachment@1000 : Record 133;VAR SortingOrder@1002 : Integer;AttachmentType@1003 : Option;Indentation@1004 : Integer);
    BEGIN
      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview.TRANSFERFIELDS(IncomingDocumentAttachment);
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview."Attachment Type" := AttachmentType;
      TempIncDocAttachmentOverview.Indentation := Indentation;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE InsertGroup@10(VAR TempIncDocAttachmentOverview@1001 : TEMPORARY Record 137;IncomingDocument@1000 : Record 130;VAR SortingOrder@1002 : Integer;Description@1003 : Text[50]);
    BEGIN
      CLEAR(TempIncDocAttachmentOverview);
      TempIncDocAttachmentOverview.INIT;
      TempIncDocAttachmentOverview."Incoming Document Entry No." := IncomingDocument."Entry No.";
      AssignSortingNo(TempIncDocAttachmentOverview,SortingOrder);
      TempIncDocAttachmentOverview."Attachment Type" := TempIncDocAttachmentOverview."Attachment Type"::Group;
      TempIncDocAttachmentOverview.Type := Type::" ";
      TempIncDocAttachmentOverview.Name := Description;
      TempIncDocAttachmentOverview.INSERT(TRUE);
    END;

    LOCAL PROCEDURE AssignSortingNo@5(VAR TempIncDocAttachmentOverview@1000 : TEMPORARY Record 137;VAR SortingOrder@1001 : Integer);
    BEGIN
      SortingOrder += 1;
      TempIncDocAttachmentOverview."Sorting Order" := SortingOrder;
    END;

    BEGIN
    END.
  }
}

