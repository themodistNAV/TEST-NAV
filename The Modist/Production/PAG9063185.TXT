OBJECT Page 9063185 POS Communication Dialog
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=POS Communication Dialog;
               ENG=POS Communication Dialog];
    PageType=StandardDialog;
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Field  ;
                Name=TextField;
                CaptionML=[ENU=TextField;
                           ENG=TextField];
                SourceExpr=Text;
                Editable=FALSE;
                MultiLine=Yes;
                ControlAddIn=[TheNavPeople.ModernUI30Panel;PublicKeyToken=009db2693a65df93];
                ShowCaption=No }

  }
  CODE
  {
    VAR
      Text@1000000000 : Text;
      NameValueBuffer@1000000002 : TEMPORARY Record 9062730;
      TextNo@1000000003 : Integer;
      DelayValue@1000000004 : Integer;
      MessageInitialized@1000000006 : Boolean;
      DataInitialized@1000000007 : Boolean;

    LOCAL PROCEDURE AddText@1000000001(value@1000000000 : Text);
    BEGIN
      TextNo += 1;
      NameValueBuffer.INIT();
      NameValueBuffer.ID := TextNo;
      NameValueBuffer.Name := value;
      NameValueBuffer.Value := value;
      NameValueBuffer.INSERT();
    END;

    LOCAL PROCEDURE OnPong@1000000002();
    BEGIN
      //CurrPage.TimerField.Stop();
      IF (NameValueBuffer.FINDFIRST()) THEN BEGIN
        Text := NameValueBuffer.Name;
        NameValueBuffer.DELETE();
        UpdateText();
        //CurrPage.TimerField.Ping(DelayValue);
      END ELSE
        CurrPage.CLOSE();
    END;

    LOCAL PROCEDURE Initialize@1000000003();
    VAR
      eMessage01@1000000000 : TextConst 'ENU=Insert Card;ENG=Insert Card';
      eMessage02@1000000001 : TextConst 'ENU=Enter PIN;ENG=Enter PIN';
      eMessage03@1000000002 : TextConst 'ENU=Connecting;ENG=Connecting';
      eMessage04@1000000005 : TextConst 'ENU=Sending Data;ENG=Sending Data';
      eMessage05@1000000003 : TextConst 'ENU=Transaction Accepted;ENG=Transaction Accepted';
      eMessage06@1000000004 : TextConst 'ENU=Press OK;ENG=Press OK';
    BEGIN
      IF (MessageInitialized) {AND (TimerInitialized)} AND (NOT(DataInitialized)) THEN BEGIN
        DataInitialized := TRUE;

        AddText(eMessage01);
        AddText(eMessage02);
        RepeatMessage(eMessage03, 5, '.');
        RepeatMessage(eMessage04, 5, '.');
        AddText(eMessage05);
        RepeatMessage(eMessage06, 5, '');

        DelayValue := 1000;

        CurrPage.TextField.RunScript('function onTimer() {InvokeEvent(''timer'', ''timer'', ''timer'');} var myT = setInterval(onTimer, 1000);');

        Text := '';
        UpdateText();
      END;
    END;

    LOCAL PROCEDURE RepeatMessage@1000000004(pMessage@1000000002 : Text;pCount@1000000003 : Integer;pAddText@1000000004 : Text);
    VAR
      Idx@1000000001 : Integer;
      Msg@1000000000 : Text;
    BEGIN
      Msg := pMessage;
      FOR Idx := 1 TO pCount DO
      BEGIN
        IF pAddText <> '' THEN
          Msg := Msg + pAddText;
        AddText(Msg);
      END;
    END;

    LOCAL PROCEDURE OnTextInitialized@1000000005();
    BEGIN
      MessageInitialized := TRUE;
      Initialize();
    END;

    LOCAL PROCEDURE OnTextFieldEvent@1000000000(source@1000000000 : Text;name@1000000001 : Text;parameters@1000000002 : Text);
    BEGIN
      IF (source = 'timer') THEN
        OnPong();
    END;

    LOCAL PROCEDURE UpdateText@1000000007();
    BEGIN
      CurrPage.TextField.SetData(STRSUBSTNO(
        '<div style="font-size:26px;color:black;text-align:center;margin:20px;">%1</div>',
        Text));
    END;

    EVENT TextField@-1000000001::ControlAddInReady@3();
    BEGIN
      OnTextInitialized();
    END;

    EVENT TextField@-1000000001::ControlEvent@4(source@1000000002 : Text;name@1000000001 : Text;parameters@1000000000 : Text);
    BEGIN
      OnTextFieldEvent(source, name, parameters);
    END;

    BEGIN
    {
      //DOC NH2016.12 BM 11/02/2016 - Created
      //DOC NH2016.15 JH 25/04/2016 - CfMD amendments
    }
    END.
  }
}

