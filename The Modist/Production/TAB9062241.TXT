OBJECT Table 9062241 Base64
{
  OBJECT-PROPERTIES
  {
    Date=27/08/15;
    Time=11:33:47 AM;
    Version List=NA2016.1;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Base64;
               ENG=Base64];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 2   ;   ;Parent Table ID     ;Integer       ;CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 3   ;   ;Parent Table Name   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Parent Table ID)));
                                                   CaptionML=[ENU=Parent Table Name;
                                                              ENG=Parent Table Name];
                                                   Editable=No }
    { 4   ;   ;Parent Position     ;Text250       ;CaptionML=[ENU=Parent Position;
                                                              ENG=Parent Position] }
    { 5   ;   ;Parent Field ID     ;Integer       ;CaptionML=[ENU=Parent Field ID;
                                                              ENG=Parent Field ID] }
    { 6   ;   ;Parent Field Name   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Parent Table ID),
                                                                                             No.=FIELD(Parent Field ID)));
                                                   CaptionML=[ENU=Parent Field Name;
                                                              ENG=Parent Field Name];
                                                   Editable=No }
    { 7   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 8   ;   ;Content 1           ;Text250       ;CaptionML=[ENU=Content 1;
                                                              ENG=Content 1] }
    { 9   ;   ;Content 2           ;Text250       ;CaptionML=[ENU=Content 2;
                                                              ENG=Content 2] }
    { 10  ;   ;Content 3           ;Text250       ;CaptionML=[ENU=Content 3;
                                                              ENG=Content 3] }
    { 11  ;   ;Content 4           ;Text250       ;CaptionML=[ENU=Content 4;
                                                              ENG=Content 4] }
    { 12  ;   ;Content 5           ;Text24        ;CaptionML=[ENU=Content 5;
                                                              ENG=Content 5] }
    { 13  ;   ;Content             ;BLOB          ;CaptionML=[ENU=Content;
                                                              ENG=Content] }
    { 14  ;   ;Content Type        ;Option        ;CaptionML=[ENU=Content Type;
                                                              ENG=Content Type];
                                                   OptionCaptionML=[ENU=" ,Text,BLOB";
                                                                    ENG=" ,Text,BLOB"];
                                                   OptionString=[ ,Text,BLOB] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Parent Table ID,Parent Position,Parent Field ID,Line No. }
    {    ;Parent Table ID,Parent Position,Parent Field ID,Content Type }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetContentForParent@1000000002(pParentTableId@1000000003 : Integer;pParentPosition@1000000004 : Text[1024];pParentFieldId@1000000005 : Integer) : Text;
    VAR
      lInStream@1000000000 : InStream;
      lContent@1000000001 : Text;
      lContentPart@1000000002 : Text[1024];
    BEGIN
      //DOC NA2015.1 - Returning content for specified parent
      //  -> pParentTableId: Parent table ID
      //  -> pParentPosition: Parent position
      //  -> pParentFieldId: Parent field ID
      //  <- The content

      Rec.RESET();
      Rec.SETCURRENTKEY("Parent Table ID", "Parent Position", "Parent Field ID");
      Rec.SETRANGE("Parent Table ID", pParentTableId);
      Rec.SETRANGE("Parent Position", pParentPosition);
      Rec.SETRANGE("Parent Field ID", pParentFieldId);
      IF (Rec.FINDFIRST()) THEN
      BEGIN
        Rec.CALCFIELDS(Content);
        IF (Rec.Content.HASVALUE) THEN
        BEGIN
          Rec.Content.CREATEINSTREAM(lInStream);
          WHILE ( NOT (lInStream.EOS)) DO
          BEGIN
            lInStream.READTEXT(lContentPart, MAXSTRLEN(lContentPart));
            lContent := lContent + lContentPart;
          END;
        END;
      END;

      EXIT(lContent);
    END;

    PROCEDURE GetContent@1000000001() : Text[1024];
    BEGIN
      //DOC NA2015.1 - Returning content
      //  <- The content

      EXIT(Rec."Content 1" + Rec."Content 2" + Rec."Content 3" + Rec."Content 4" + Rec."Content 5");
    END;

    PROCEDURE SetContent@1000000000(pContent@1000000000 : Text[1024]);
    BEGIN
      //DOC NA2015.1 - Setting content
      //  -> pContent: Content to set

      Rec."Content 1" := COPYSTR(pContent, 1, 250);
      Rec."Content 2" := COPYSTR(pContent, 251, 250);
      Rec."Content 3" := COPYSTR(pContent, 501, 250);
      Rec."Content 4" := COPYSTR(pContent, 751, 250);
      Rec."Content 5" := COPYSTR(pContent, 1001, 24);
    END;

    BEGIN
    {
      //DOC NA2015.1 JH 29/04/2013 - Created
      //DOC NA2015.1 JH 20/09/2013 - BLOB content added
      //DOC NA2016.1 JH 27/08/2015 - Upgraded to 2016
    }
    END.
  }
}

