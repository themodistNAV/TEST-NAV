OBJECT Codeunit 1319 Sales by Cust. Grp. Chart Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SalesByCustGrpChartSetup@1003 : Record 1319;
      TotalSalesLCYTxt@1002 : TextConst 'ENU=Total Sales (LCY);ENG=Total Sales (LCY)';

    PROCEDURE OnInitPage@6();
    BEGIN
      GetChartSetupForCurrentUser;
    END;

    PROCEDURE UpdateChart@3(VAR BusChartBuf@1000 : Record 485);
    VAR
      NoOfPeriods@1007 : Integer;
    BEGIN
      GetChartSetupForCurrentUser;

      WITH BusChartBuf DO BEGIN
        Initialize;
        "Period Length" := SalesByCustGrpChartSetup."Period Length";
        "Period Filter Start Date" := SalesByCustGrpChartSetup."Start Date";
        "Period Filter End Date" := 0D;

        NoOfPeriods := 5;
        CalcCustSales(BusChartBuf,NoOfPeriods);
      END;
    END;

    PROCEDURE DrillDown@5(VAR BusChartBuf@1000 : Record 485);
    VAR
      Cust@1001 : Record 18;
      ToDate@1000000001 : Date;
      FromDate@1002 : Date;
      MeasureValueString@1003 : Text;
    BEGIN
      GetChartSetupForCurrentUser;

      WITH BusChartBuf DO BEGIN
        "Period Length" := SalesByCustGrpChartSetup."Period Length";
        ToDate := GetXValueAsDate("Drill-Down X Index");
        FromDate := CalcFromDate(ToDate);
        MeasureValueString := GetMeasureValueString("Drill-Down Measure Index");
      END;

      IF MeasureValueString <> '' THEN
        Cust.SETRANGE("Customer Posting Group",MeasureValueString);
      Cust.SETRANGE("Date Filter",FromDate,ToDate);
      PAGE.RUNMODAL(PAGE::"Mini Customer List",Cust);
    END;

    LOCAL PROCEDURE CalcCustSales@1(VAR BusChartBuf@1006 : Record 485;NoOfPeriods@1002 : Decimal);
    VAR
      Cust@1170000002 : Record 18;
      PreviousCust@1001 : Record 18;
      TotalSalesValue@1007 : ARRAY [100] OF Decimal;
      SalesValue@1000 : ARRAY [100] OF Decimal;
    BEGIN
      BusChartBuf.SetPeriodXAxis;

      AddSalesMeasure(BusChartBuf,TotalSalesLCYTxt,'',BusChartBuf."Chart Type"::Line);

      Cust.SETCURRENTKEY("Customer Posting Group");
      IF Cust.ISEMPTY THEN BEGIN
        BusChartBuf.SetXAxis('Empty',BusChartBuf."Data Type"::String);
        EXIT;
      END;

      Cust.FINDSET;
      REPEAT
        IF NOT (PreviousCust."Customer Posting Group" IN ['',Cust."Customer Posting Group"]) THEN BEGIN
          AddSalesMeasure(
            BusChartBuf,
            PreviousCust."Customer Posting Group",
            PreviousCust."Customer Posting Group",
            BusChartBuf."Chart Type"::Column);
          AddSalesValues(BusChartBuf,PreviousCust."Customer Posting Group",SalesValue,NoOfPeriods);
        END;

        AddCustSales(BusChartBuf,Cust,SalesValue,TotalSalesValue,NoOfPeriods);

        PreviousCust := Cust;
      UNTIL Cust.NEXT = 0;

      AddSalesMeasure(
        BusChartBuf,
        PreviousCust."Customer Posting Group",
        PreviousCust."Customer Posting Group",
        BusChartBuf."Chart Type"::Column);
      AddSalesValues(BusChartBuf,PreviousCust."Customer Posting Group",SalesValue,NoOfPeriods);

      AddSalesValues(BusChartBuf,TotalSalesLCYTxt,TotalSalesValue,NoOfPeriods);
    END;

    LOCAL PROCEDURE AddCustSales@16(VAR BusChartBuf@1003 : Record 485;Cust@1000 : Record 18;VAR SalesValues@1001 : ARRAY [100] OF Decimal;VAR TotalSalesValues@1002 : ARRAY [100] OF Decimal;NoOfPeriods@1004 : Integer);
    VAR
      FromDate@1006 : Date;
      ToDate@1005 : Date;
      PeriodNo@1007 : Integer;
    BEGIN
      FromDate := BusChartBuf.CalcFromDate(BusChartBuf."Period Filter Start Date");
      FOR PeriodNo := 1 TO NoOfPeriods DO BEGIN
        ToDate := BusChartBuf.CalcToDate(FromDate);

        Cust.SETRANGE("Date Filter",FromDate,ToDate);
        Cust.CALCFIELDS("Sales (LCY)");
        SalesValues[PeriodNo] += Cust."Sales (LCY)";
        TotalSalesValues[PeriodNo] += Cust."Sales (LCY)";

        FromDate := CALCDATE('<1D>',ToDate);
      END;

      IF BusChartBuf."Period Filter End Date" = 0D THEN BEGIN
        BusChartBuf."Period Filter End Date" := ToDate;
        BusChartBuf.AddPeriods(BusChartBuf.CalcFromDate(BusChartBuf."Period Filter Start Date"),ToDate);
      END;
    END;

    LOCAL PROCEDURE AddSalesMeasure@11(VAR BusChartBuf@1002 : Record 485;Measure@1000 : Text;MeasureValue@1003 : Text;ChartType@1001 : Integer);
    BEGIN
      BusChartBuf.AddMeasure(Measure,MeasureValue,BusChartBuf."Data Type"::Decimal,ChartType);
    END;

    LOCAL PROCEDURE AddSalesValues@12(VAR BusChartBuf@1001 : Record 485;Measure@1000 : Text;VAR SalesValues@1003 : ARRAY [100] OF Decimal;NoOfPeriods@1004 : Integer);
    VAR
      PeriodNo@1002 : Integer;
    BEGIN
      FOR PeriodNo := 1 TO NoOfPeriods DO BEGIN
        BusChartBuf.SetValue(Measure,PeriodNo - 1,SalesValues[PeriodNo]);
        SalesValues[PeriodNo] := 0;
      END;
    END;

    LOCAL PROCEDURE GetChartSetupForCurrentUser@2();
    BEGIN
      WITH SalesByCustGrpChartSetup DO
        IF NOT GET(USERID) THEN BEGIN
          "User ID" := USERID;
          "Start Date" := WORKDATE;
          "Period Length" := "Period Length"::Week;
          INSERT;
        END;
    END;

    BEGIN
    END.
  }
}

