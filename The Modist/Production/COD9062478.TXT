OBJECT Codeunit 9062478 Record Variable Collection Mgt
{
  OBJECT-PROPERTIES
  {
    Date=25/09/15;
    Time=[ 1:36:38 PM];
    Version List=NA2016.9;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NameValueBuffer@1000000000 : TEMPORARY Record 9062730;
      EntryNo@1000000001 : Integer;

    PROCEDURE Clear@1000000000();
    BEGIN
      NameValueBuffer.DELETEALL();
      EntryNo := 1;
    END;

    PROCEDURE SetValue@1000000001(name@1000000000 : Text;value@1000000001 : Text);
    BEGIN
      name := UPPERCASE(name);
      NameValueBuffer.RESET();
      NameValueBuffer.SETRANGE(Name, name);
      IF (NOT(NameValueBuffer.FINDFIRST())) THEN BEGIN
        EntryNo += 1;
        NameValueBuffer.INIT();
        NameValueBuffer.ID := EntryNo;
        NameValueBuffer.Name := name;
        NameValueBuffer.Value := value;
        NameValueBuffer.INSERT();
      END ELSE BEGIN
        NameValueBuffer.Value := value;
        NameValueBuffer.MODIFY();
      END;
    END;

    PROCEDURE GetValue@1000000002(name@1000000000 : Text) : Text;
    BEGIN
      name := UPPERCASE(name);
      NameValueBuffer.RESET();
      NameValueBuffer.SETRANGE(Name, name);
      IF (NameValueBuffer.FINDFIRST()) THEN
        EXIT(NameValueBuffer.Value);
      EXIT('');
    END;

    PROCEDURE ClearValue@1000000003(name@1000000000 : Text);
    BEGIN
      name := UPPERCASE(name);
      NameValueBuffer.RESET();
      NameValueBuffer.SETRANGE(Name, name);
      IF (NameValueBuffer.FINDFIRST()) THEN
        NameValueBuffer.DELETE();
    END;

    PROCEDURE SetRecRefValue@1000000005(name@1000000001 : Text;VAR value@1000000000 : RecordRef);
    BEGIN
      SetValue(name, value.GETPOSITION(FALSE));
    END;

    PROCEDURE GetRecRefValue@1000000006(name@1000000001 : Text;VAR value@1000000000 : RecordRef) : Boolean;
    VAR
      pos@1000000002 : Text;
    BEGIN
      pos := GetValue(name);
      IF (pos <> '') THEN BEGIN
        value.SETPOSITION(pos);
        value.FIND('=');
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    {
      //DOC NA2015.9 AZ 05/08/2015 - Object renamed
      //DOC NA2016.9 JH 28/08/2015 - Upgraded to 2016
    }
    END.
  }
}

