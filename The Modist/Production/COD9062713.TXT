OBJECT Codeunit 9062713 Config Package Cleardown
{
  OBJECT-PROPERTIES
  {
    Date=14/07/16;
    Time=12:35:18 PM;
    Version List=NH2016.16;
  }
  PROPERTIES
  {
    TableNo=8623;
    OnRun=BEGIN

            DeleteTableDataUsingPackage(Rec);
          END;

  }
  CODE
  {
    VAR
      ImportToolkit@1000000000 : Codeunit 9062701;
      Dlg@1000000001 : Dialog;
      EnhancedRapidStartMgt@1000000002 : Codeunit 9062714;
      tNotInBuildMode@1000000003 : TextConst 'ENU=To use the ClearDown function, please Tick the "In in Build Mode" field on the Enablement Setup Card.;ENG=To use the ClearDown function, please Tick the "In in Build Mode" field on the Enablement Setup Card.';

    LOCAL PROCEDURE DeleteTableDataUsingPackage@1000000000(ConfigPackage@1000000000 : Record 8623);
    VAR
      ConfigPackageTable@1000000001 : Record 8613;
      eConfirmation@1000000002 : TextConst 'ENU=This will DELETE records in the tables specified by this Configuration Package.\\There are %1 Tables about to be cleared \\Continue?;ENG=This will DELETE records in the tables specified by this Configuration Package.\\There are %1 Tables about to be cleared \\Continue?';
      eDialogTitle@1000000003 : TextConst 'ENU=Processing table;ENG=Processing table';
      eResetNoSeriesConfirmation1@1000000004 : TextConst 'ENU=Do you want to clear down the NUMBER SERIES last used numbering?;ENG=Do you want to clear down the NUMBER SERIES last used numbering?';
      eResetNoSeriesConfirmation2@1000000005 : TextConst 'ENU=Are you sure?;ENG=Are you sure?';
    BEGIN

      IF NOT ImportToolkit.CheckSystemInBuildMode THEN BEGIN
        MESSAGE(tNotInBuildMode); //DOC NH2016.16 PC 20160512
        EXIT;
      END;

      ConfigPackageTable.SETRANGE("Package Code",ConfigPackage.Code);

      IF ConfigPackageTable.FINDSET THEN BEGIN
        IF NOT CONFIRM(eConfirmation,FALSE,ConfigPackageTable.COUNT) THEN
        EXIT;

      Dlg.OPEN(eDialogTitle + ' #1####################');

          REPEAT
            ConfigPackageTable.CALCFIELDS(ConfigPackageTable."Table Name");
            Dlg.UPDATE(1,ConfigPackageTable."Table Name");
            DeleteTableData(ConfigPackageTable);
          UNTIL ConfigPackageTable.NEXT=0;

      Dlg.CLOSE;
      END;

      IF CONFIRM(eResetNoSeriesConfirmation1,FALSE) THEN
        IF CONFIRM(eResetNoSeriesConfirmation2,FALSE) THEN
          EnhancedRapidStartMgt.ResetNoSeries(TRUE,TRUE);
    END;

    LOCAL PROCEDURE DeleteTableData@1000000001(ConfigPackageTable@1000000000 : Record 8613);
    VAR
      RecRef@1000000001 : RecordRef;
      EnhancedFieldValue@1000000002 : Record 9062328;
    BEGIN

      RecRef.OPEN(ConfigPackageTable."Table ID");
      ApplyPackageFilter(ConfigPackageTable,RecRef);

      IF RecRef.FINDSET THEN
        BEGIN
          RecRef.DELETEALL;
          EnhancedFieldValue.SETRANGE(EnhancedFieldValue."Table No.",RecRef.NUMBER);
          EnhancedFieldValue.DELETEALL();

        END;
    END;

    LOCAL PROCEDURE ApplyPackageFilter@7(ConfigPackageTable@1001 : Record 8613;VAR RecRef@1000 : RecordRef);
    VAR
      ConfigPackageFilter@1002 : Record 8626;
      FieldRef@1003 : FieldRef;
    BEGIN
      ConfigPackageFilter.SETRANGE("Package Code",ConfigPackageTable."Package Code");
      ConfigPackageFilter.SETRANGE("Table ID",ConfigPackageTable."Table ID");
      IF ConfigPackageFilter.FINDSET THEN
        REPEAT
          IF ConfigPackageFilter."Field Filter" <> '' THEN BEGIN
            FieldRef := RecRef.FIELD(ConfigPackageFilter."Field ID");
            FieldRef.SETFILTER(STRSUBSTNO('%1',ConfigPackageFilter."Field Filter"));
          END;
        UNTIL ConfigPackageFilter.NEXT = 0;
    END;

    PROCEDURE DeleteSingleTable@1000000002(ConfigPackageTable@1000000000 : Record 8613);
    VAR
      RecRef@1000000001 : RecordRef;
      EnhancedFieldValue@1000000002 : Record 9062328;
      eConfirmation@1000000003 : TextConst 'ENU=Do you want to Clear this table?;ENG=Do you want to Clear this table?';
    BEGIN

      IF NOT CONFIRM(eConfirmation,FALSE) THEN
        EXIT;

      RecRef.OPEN(ConfigPackageTable."Table ID");
      ApplyPackageFilter(ConfigPackageTable,RecRef);

      IF RecRef.FINDSET THEN
        BEGIN
          RecRef.DELETEALL;
          EnhancedFieldValue.SETRANGE(EnhancedFieldValue."Table No.",RecRef.NUMBER);
          EnhancedFieldValue.DELETEALL();

        END;
    END;

    BEGIN
    {
      //DOC NH2016.15 JH 26/04/2016 - CfMD amendments
      //DOC NH2016.16 PC 20160512 - Added message if system not in build mode
    }
    END.
  }
}

