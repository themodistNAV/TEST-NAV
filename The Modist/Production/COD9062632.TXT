OBJECT Codeunit 9062632 Enhanced Customer Management
{
  OBJECT-PROPERTIES
  {
    Date=08/02/16;
    Time=10:21:33 AM;
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NhancedCustomer@9062230 : Record 9062764;
      DialogManagement@9062231 : Codeunit 9062249;

    [EventSubscriber(Table,18,OnAfterInsertEvent)]
    LOCAL PROCEDURE OnCustomerInsert@9062237(VAR Rec@9062230 : Record 18;RunTrigger@9062231 : Boolean);
    BEGIN
      //DOC NA2016.10 - On customer insert event handler
      //  -> Rec: The customer
      //  -> RunTrigger: Flag to run the trigger

      UpdateNhancedCustomer(Rec, FALSE);
    END;

    [EventSubscriber(Table,18,OnAfterDeleteEvent)]
    LOCAL PROCEDURE OnCustomerDelete@9062238(VAR Rec@9062230 : Record 18;RunTrigger@9062231 : Boolean);
    BEGIN
      //DOC NA2016.10 - On customer insert event handler
      //  -> Rec: The customer
      //  -> RunTrigger: Flag to run the trigger

      UpdateNhancedCustomer(Rec, TRUE);
    END;

    [EventSubscriber(Table,18,OnAfterRenameEvent)]
    LOCAL PROCEDURE OnCustomerRename@9062239(VAR Rec@9062230 : Record 18;VAR xRec@9062231 : Record 18;RunTrigger@9062232 : Boolean);
    BEGIN
      //DOC NA2016.10 - On customer insert event handler
      //  -> Rec: The customer after rename
      //  -> xRec: The customer before rename
      //  -> RunTrigger: Flag to run the trigger

      UpdateNhancedCustomer(xRec, TRUE);
      UpdateNhancedCustomer(Rec, FALSE);
    END;

    PROCEDURE UpdateNhancedCustomers@9062231(pInteraction@9062230 : Boolean) : Boolean;
    VAR
      eConfirmation@9062231 : TextConst 'ENU=Do you want to update all customers?;ENG=Do you want to update all customers?';
      lCustomer@9062232 : Record 18;
      eDialogTitle@9062234 : TextConst 'ENU=Updating customers...;ENG=Updating customers...';
      eDialogProgressCaption@9062233 : TextConst 'ENU=Progress;ENG=Progress';
      eFinalMessage@9062235 : TextConst 'ENU=Customers have been updated.;ENG=Customers have been updated.';
    BEGIN
      //DOC NA2016.10 - Updating all enhanced customers
      //  -> pInteraction: Interaction flag
      //  <- TRUE = Confirmed and updated, FALSE = Cancelled

      pInteraction := ((pInteraction) AND (GUIALLOWED()));
      IF (pInteraction) THEN
        IF ( NOT (CONFIRM(eConfirmation, TRUE))) THEN
          EXIT(FALSE);

      DialogManagement.Add(eDialogProgressCaption, 10, 1, 20, TRUE, TRUE);
      DialogManagement.Open(eDialogTitle);

      lCustomer.RESET();
      DialogManagement.SetCounterCount(1, lCustomer.COUNT());
      IF (lCustomer.FINDSET()) THEN
      REPEAT
        DialogManagement.StepCounter(1);
        UpdateNhancedCustomer(lCustomer, FALSE);
      UNTIL (lCustomer.NEXT() = 0);

      DialogManagement.Close();

      IF (pInteraction) THEN
        MESSAGE(eFinalMessage);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE UpdateNhancedCustomer@9062230(pCustomer@9062230 : Record 18;pDelete@9062231 : Boolean);
    VAR
      lNhancedCustomerExists@9062232 : Boolean;
    BEGIN
      //DOC NA2016.10 - Updating one enhanced customer
      //  -> pCustomer: Corresponding customer record to update enhanced customer for
      //  -> pDelete: Delete flag

      lNhancedCustomerExists := NhancedCustomer.GET(pCustomer."No.");
      IF ( NOT (pDelete)) THEN
      BEGIN
        IF ( NOT (lNhancedCustomerExists)) THEN
        BEGIN
          CLEAR(NhancedCustomer);
          NhancedCustomer.INIT();
          NhancedCustomer."Customer No." := pCustomer."No.";
          NhancedCustomer.INSERT(TRUE);
        END;
      END
      ELSE
        IF (lNhancedCustomerExists) THEN
          NhancedCustomer.DELETE(TRUE);
    END;

    BEGIN
    {
      //DOC NA2016.10 JH 21/10/2015 - Created
      //DOC NA2016.11 JH 13/11/2015 - Bugfixes
      //DOC NA2016.12 JH 08/02/2016 - Name + version changed
    }
    END.
  }
}

