OBJECT Codeunit 9062590 Selection Filter Management
{
  OBJECT-PROPERTIES
  {
    Date=28/08/15;
    Time=[ 9:52:28 AM];
    Version List=NA2016.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    LOCAL PROCEDURE "--- Specific Functions ---"@1000000000();
    BEGIN
    END;

    PROCEDURE GetSelectionFilterForNablementUser@2(VAR nAblementUser@1000 : Record 9062729) : Text;
    VAR
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(nAblementUser);
      EXIT(GetSelectionFilter(RecRef, nAblementUser.FIELDNO("User ID")));
    END;

    LOCAL PROCEDURE "--- Internal ---"@1000000001();
    BEGIN
    END;

    LOCAL PROCEDURE GetSelectionFilter@1(VAR TempRecRef@1008 : RecordRef;SelectionFieldID@1001 : Integer) : Text;
    VAR
      RecRef@1000 : RecordRef;
      FieldRef@1007 : FieldRef;
      FirstRecRef@1006 : Text;
      LastRecRef@1005 : Text;
      SelectionFilter@1004 : Text;
      SavePos@1009 : Text;
      TempRecRefCount@1003 : Integer;
      More@1002 : Boolean;
    BEGIN
      IF TempRecRef.ISTEMPORARY THEN BEGIN
        RecRef := TempRecRef.DUPLICATE;
        RecRef.RESET;
      END ELSE
        RecRef.OPEN(TempRecRef.NUMBER);

      TempRecRefCount := TempRecRef.COUNT;
      IF TempRecRefCount > 0 THEN BEGIN
        TempRecRef.FIND('-');
        WHILE TempRecRefCount > 0 DO BEGIN
          TempRecRefCount := TempRecRefCount - 1;
          RecRef.SETPOSITION(TempRecRef.GETPOSITION);
          RecRef.FIND;
          FieldRef := RecRef.FIELD(SelectionFieldID);
          FirstRecRef := FORMAT(FieldRef.VALUE);
          LastRecRef := FirstRecRef;
          More := TempRecRefCount > 0;
          WHILE More DO
            IF RecRef.NEXT = 0 THEN
              More := FALSE
            ELSE BEGIN
              SavePos := TempRecRef.GETPOSITION;
              TempRecRef.SETPOSITION(RecRef.GETPOSITION);
              IF NOT TempRecRef.FIND THEN BEGIN
                More := FALSE;
                TempRecRef.SETPOSITION(SavePos);
              END ELSE BEGIN
                FieldRef := RecRef.FIELD(SelectionFieldID);
                LastRecRef := FORMAT(FieldRef.VALUE);
                TempRecRefCount := TempRecRefCount - 1;
                IF TempRecRefCount = 0 THEN
                  More := FALSE;
              END;
            END;
          IF SelectionFilter <> '' THEN
            SelectionFilter := SelectionFilter + '|';
          IF FirstRecRef = LastRecRef THEN
            SelectionFilter := SelectionFilter + AddQuotes(FirstRecRef)
          ELSE
            SelectionFilter := SelectionFilter + AddQuotes(FirstRecRef) + '..' + AddQuotes(LastRecRef);
          IF TempRecRefCount > 0 THEN
            TempRecRef.NEXT;
        END;
        EXIT(SelectionFilter);
      END;
    END;

    PROCEDURE AddQuotes@11(inString@1000 : Text[1024]) : Text;
    BEGIN
      EXIT('''' + inString + '''');
    END;

    BEGIN
    {
      //DOC NA2016.10 JH 28/08/2015 - Cloned from standard one
    }
    END.
  }
}

