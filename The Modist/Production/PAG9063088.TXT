OBJECT Page 9063088 Enhanced Inf. - Quest. Part
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Enhanced Inf. - Quest. Part;
               ENG=Enhanced Inf. - Quest. Part];
    SourceTable=Table9062732;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnAfterGetRecord=BEGIN
                       LineStyleText := FORMAT("Line Style");
                     END;

    ActionList=ACTIONS
    {
      { 1000000004;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000005;1 ;Action    ;
                      CaptionML=[ENU=Show Questionnaire;
                                 ENG=Show Questionnaire];
                      Image=Questionaire;
                      OnAction=BEGIN
                                 OpenQuestionnaire();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                CaptionML=[ENU=Group;
                           ENG=Group];
                IndentationColumnName=Indent;
                IndentationControls=Text Value 1;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                CaptionML=[ENU=Question;
                           ENG=Question];
                SourceExpr="Text Value 1";
                StyleExpr=LineStyleText }

    { 1000000003;2;Field  ;
                CaptionML=[ENU=Answer;
                           ENG=Answer];
                SourceExpr="Text Value 2";
                StyleExpr=LineStyleText }

  }
  CODE
  {
    VAR
      EntryNo@1000000000 : Integer;
      LineStyleText@1000000001 : Text INDATASET;

    PROCEDURE CollectLines@1000000000(TableNo@1000000000 : Integer;PKValues@1000000001 : ARRAY [5] OF Text);
    VAR
      Questionnaire@1000000002 : Record 9062704;
      QuestLine@1000000003 : Record 9062705;
      FirstQuestionnaire@1000000005 : Boolean;
      LineStyle@1000000004 : Integer;
    BEGIN
      Rec.RESET();
      Rec.DELETEALL();
      EntryNo := 0;
      Questionnaire.RESET();
      Questionnaire.SETCURRENTKEY("Related Table No.", "Related Record PK Value 1", "Related Record PK Value 2", "Related Record PK Value 3", "Created Date Time");
      Questionnaire.SETRANGE("Related Table No.", TableNo);
      IF (PKValues[1] <> '') THEN
        Questionnaire.SETRANGE("Related Record PK Value 1", PKValues[1]);
      IF (PKValues[2] <> '') THEN
        Questionnaire.SETRANGE("Related Record PK Value 2", PKValues[2]);
      IF (PKValues[3] <> '') THEN
        Questionnaire.SETRANGE("Related Record PK Value 3", PKValues[3]);
      IF (PKValues[4] <> '') THEN
        Questionnaire.SETRANGE("Related Record PK Value 4", PKValues[4]);
      IF (PKValues[5] <> '') THEN
        Questionnaire.SETRANGE("Related Record PK Value 5", PKValues[5]);

      FirstQuestionnaire := TRUE;
      IF (Questionnaire.FINDSET()) THEN BEGIN
        REPEAT
          //IF (NOT(FirstQuestionnaire)) THEN
          //  InsertLine(0, '', '', 0, "Line Style"::Standard);
          InsertLine(0,
            STRSUBSTNO('%1 (%2)', Questionnaire.Name, Questionnaire."Created Date Time"),
            '', 0, "Line Style"::Standard, Questionnaire."Questionnaire No.");
          QuestLine.RESET();
          QuestLine.SETCURRENTKEY("Questionnaire No.", Sequence);
          QuestLine.SETFILTER("Line Type", '%1|%2', QuestLine."Line Type"::Question, QuestLine."Line Type"::"Group Header");
          QuestLine.SETRANGE("Questionnaire No.", Questionnaire."Questionnaire No.");
          IF (QuestLine.FINDSET()) THEN BEGIN
            REPEAT
              LineStyle := "Line Style"::Standard;
              CASE (QuestLine."Line Type") OF
                QuestLine."Line Type"::Question:
                  BEGIN
                    IF (QuestLine.Hidden) OR (QuestLine.Skip) THEN
                      LineStyle := "Line Style"::Subordinate;
                    IF (QuestLine.Required) THEN
                      LineStyle := "Line Style"::Strong;
                  END;
                QuestLine."Line Type"::"Group Header":
                  LineStyle := "Line Style"::StrongAccent;
                QuestLine."Line Type"::Block:
                  LineStyle := "Line Style"::StrongAccent;
              END;
              InsertLine(QuestLine."Question Indent" + 1, QuestLine."Question Text", QuestLine."Text Input", 0, LineStyle, Questionnaire."Questionnaire No.");
            UNTIL (QuestLine.NEXT() = 0);
          END;
          FirstQuestionnaire := FALSE;
        UNTIL (Questionnaire.NEXT() = 0);
      END;
    END;

    LOCAL PROCEDURE InsertLine@1000000004(IndentValue@1000000002 : Integer;Question@1000000000 : Text;Answer@1000000001 : Text;LineType@1000000003 : Integer;LineStyle@1000000004 : Integer;QuestionnaireNo@1000000005 : Integer);
    BEGIN
      EntryNo += 1;
      Rec."Entry No." := EntryNo;
      Rec."Line Type" := LineType;
      Rec.Indent := IndentValue;
      Rec."Text Value 1" := Question;
      Rec."Text Value 2" := Answer;
      Rec."Line Style" := LineStyle;
      Rec."Integer Value 1" := QuestionnaireNo;
      Rec.INSERT();
    END;

    LOCAL PROCEDURE OpenQuestionnaire@1000000001();
    VAR
      Questionnaire@1000000000 : Record 9062704;
    BEGIN
      IF ("Integer Value 1" <> 0) THEN BEGIN
        Questionnaire.GET("Integer Value 1");
        Questionnaire.SETRECFILTER();
        PAGE.RUN(PAGE::"Questionnaire Card", Questionnaire);
      END;
    END;

    BEGIN
    {
      //DOC NA2016.9  AZ 08/09/2015 - Nav 2016 Upgrade
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

