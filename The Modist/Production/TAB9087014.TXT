OBJECT Table 9087014 Style Colour Size
{
  OBJECT-PROPERTIES
  {
    Date=16/01/18;
    Time=[ 5:10:01 PM];
    Modified=Yes;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Style No.");
               Rec.TESTFIELD("Colour Code");
               Rec.TESTFIELD("Size Group Code");
               Rec.TESTFIELD("Size Code");
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               //Rec.TESTFIELD("Item No.", '');
               //Rec.TESTFIELD("Variant Code", '');
             END;

    CaptionML=[ENU=Style Colour/Size;
               ENG=Style Colour/Size];
  }
  FIELDS
  {
    { 1   ;   ;Style No.           ;Code20        ;TableRelation=Style;
                                                   CaptionML=[ENU=Style No.;
                                                              ENG=Style No.] }
    { 2   ;   ;Colour Code         ;Code20        ;TableRelation=Colour;
                                                   OnValidate=BEGIN
                                                                IF (Rec."Colour Code" <> xRec."Colour Code") THEN
                                                                  Rec.TESTFIELD("Item No.", '');
                                                              END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 3   ;   ;Size Group Code     ;Code20        ;TableRelation="Size Group";
                                                   OnValidate=BEGIN
                                                                IF (Rec."Size Group Code" <> xRec."Size Group Code") THEN
                                                                  Rec.TESTFIELD("Variant Code", '');
                                                              END;

                                                   CaptionML=[ENU=Size Group Code;
                                                              ENG=Size Group Code] }
    { 4   ;   ;Size Code           ;Code20        ;TableRelation=Size.Code WHERE (Size Group Code=FIELD(Size Group Code));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Size Code" <> xRec."Size Code") THEN
                                                                  Rec.TESTFIELD("Variant Code", '');
                                                                Rec.CALCFIELDS("Size Name");
                                                              END;

                                                   CaptionML=[ENU=Size Code;
                                                              ENG=Size Code] }
    { 10  ;   ;Enabled             ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF (Rec.Enabled <> xRec.Enabled) THEN
                                                                BEGIN
                                                                  //Rec.TESTFIELD("Item No.", '');
                                                                  //Rec.TESTFIELD("Variant Code", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Enabled;
                                                              ENG=Enabled] }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF (Rec."Item No." <> xRec."Item No.") THEN
                                                                  Rec.VALIDATE("Variant Code", '');
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.] }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ENG=Variant Code] }
    { 30  ;   ;Sequence            ;Integer       ;CaptionML=[ENU=Sequence;
                                                              ENG=Sequence] }
    { 44  ;   ;Size Name           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Size.Name WHERE (Size Group Code=FIELD(Size Group Code),
                                                                                       Code=FIELD(Size Code)));
                                                   CaptionML=[ENU=Size Name;
                                                              ENG=Size Name];
                                                   Editable=No }
    { 50  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ENG=Date Filter] }
    { 60  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              ENG=Global Dimension 1 Filter];
                                                   CaptionClass='1,3,1' }
    { 61  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              ENG=Global Dimension 2 Filter];
                                                   CaptionClass='1,3,2' }
    { 70  ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              ENG=Location Filter] }
    { 80  ;   ;Inventory           ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry".Quantity WHERE (Item No.=FIELD(Item No.),
                                                                                                       Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                       Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                       Location Code=FIELD(Location Filter),
                                                                                                       Variant Code=FIELD(Variant Code)));
                                                   CaptionML=[ENU=Inventory;
                                                              ENG=Inventory];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 90  ;   ;Purchases (Qty.)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry"."Invoiced Quantity" WHERE (Entry Type=CONST(Purchase),
                                                                                                                  Item No.=FIELD(Item No.),
                                                                                                                  Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                  Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                  Location Code=FIELD(Location Filter),
                                                                                                                  Variant Code=FIELD(Variant Code),
                                                                                                                  Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Purchases (Qty.);
                                                              ENG=Purchases (Qty.)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 91  ;   ;Purchases (LCY)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Purchase Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Purchase),
                                                                                                                   Item No.=FIELD(Item No.),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Location Code=FIELD(Location Filter),
                                                                                                                   Variant Code=FIELD(Variant Code),
                                                                                                                   Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Purchases (LCY);
                                                              ENG=Purchases (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 100 ;   ;Sales (Qty.)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Value Entry"."Invoiced Quantity" WHERE (Item Ledger Entry Type=CONST(Sale),
                                                                                                             Item No.=FIELD(Item No.),
                                                                                                             Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                             Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                             Location Code=FIELD(Location Filter),
                                                                                                             Variant Code=FIELD(Variant Code),
                                                                                                             Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (Qty.);
                                                              ENG=Sales (Qty.)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 101 ;   ;Sales (LCY)         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Sales Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Sale),
                                                                                                                Item No.=FIELD(Item No.),
                                                                                                                Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                Location Code=FIELD(Location Filter),
                                                                                                                Variant Code=FIELD(Variant Code),
                                                                                                                Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (LCY);
                                                              ENG=Sales (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 110 ;   ;Show                ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Show;
                                                              ENG=Show];
                                                   OptionCaptionML=[ENU=" ,Enabled Flags,Items,Inventory,Purchases (Quantity),Purchases (Amount),Sales (Quantity),Sales (Amount)";
                                                                    ENG=" ,Enabled Flags,Items,Inventory,Purchases (Quantity),Purchases (Amount),Sales (Quantity),Sales (Amount)"];
                                                   OptionString=[ ,Enabled Flags,Items,Inventory,Purchases (Quantity),Purchases (Amount),Sales (Quantity),Sales (Amount)] }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                               TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Style No.,Colour Code,Size Group Code,Size Code;
                                                   Clustered=Yes }
    {    ;Style No.,Colour Code,Size Group Code,Sequence,Size Code }
    {    ;Item No.,Variant Code                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      String@1000000002 : Codeunit 9062236;
      TnpToolkit@1000000000 : Codeunit 9062230;

    PROCEDURE ItemAndVariantText@1000000000() : Code[50];
    VAR
      eEmpty@1000000002 : TextConst 'ENU=(EMPTY);ENG=(EMPTY)';
      eItemAndVariant@1000000003 : TextConst 'ENU=%1 / %2;ENG=%1 / %2';
    BEGIN
      //DOC NS2015.1 - Returning item and variant text (Item No. / Variant Code or (EMPTY) if any of them is empty)
      //  <- The text

      IF ((Rec."Item No." = '') OR (Rec."Variant Code" = '')) THEN
        EXIT(eEmpty)
      ELSE
        EXIT(STRSUBSTNO(eItemAndVariant, Rec."Item No.", Rec."Variant Code"));
    END;

    PROCEDURE SelectWhatToShow@1000000007(VAR pShow@1000000001 : Integer) : Boolean;
    VAR
      lFieldLookup@1000000002 : Codeunit 9062237;
      lShowText@1000000000 : Text;
      lResult@1000000003 : Boolean;
    BEGIN
      //DOC NS2015.1 - Selecting what to show
      //  <> pShow: Source / target show option
      //  <- Lookup result

      lShowText := FORMAT(pShow);
      lResult := lFieldLookup.Lookup(DATABASE::"Style Colour Size", Rec.FIELDNO(Show), lShowText);
      IF (lResult) THEN
      BEGIN
        EVALUATE(Rec.Show, lShowText);
        pShow := Rec.Show;
      END;

      EXIT(lResult);
    END;

    PROCEDURE ShowItemVariant@1000000008();
    VAR
      lItemVariant@1000000001 : Record 5401;
    BEGIN
      //DOC NS2015.1 - Showing item variant

      Rec.TESTFIELD("Item No.");
      Rec.TESTFIELD("Variant Code");
      lItemVariant.RESET();
      lItemVariant.SETRANGE("Item No.", Rec."Item No.");
      IF (lItemVariant.GET(Rec."Item No.", Rec."Variant Code")) THEN;
      PAGE.RUN(0, lItemVariant);
    END;

    PROCEDURE ShowInventory@1000000001();
    BEGIN
      //DOC NS2015.1 - Showing inventory

      ShowItemLedgerEntries(-1);
    END;

    PROCEDURE ShowPurchasesQuantity@1000000002();
    VAR
      lItemLedgerEntry@1000000000 : Record 32;
    BEGIN
      //DOC NS2015.1 - Showing purchases quantity

      ShowItemLedgerEntries(lItemLedgerEntry."Entry Type"::Purchase);
    END;

    PROCEDURE ShowSalesQuantity@1000000003();
    VAR
      lItemLedgerEntry@1000000000 : Record 32;
    BEGIN
      //DOC NS2015.1 - Showing sales quantity

      ShowItemLedgerEntries(lItemLedgerEntry."Entry Type"::Sale);
    END;

    PROCEDURE ShowPurchasesAmount@1000000004();
    VAR
      lValueEntry@1000000000 : Record 5802;
    BEGIN
      //DOC NS2015.1 - Showing purchases amount

      ShowValueEntries(lValueEntry."Item Ledger Entry Type"::Purchase);
    END;

    PROCEDURE ShowSalesAmount@1000000005();
    VAR
      lValueEntry@1000000001 : Record 5802;
    BEGIN
      //DOC NS2015.1 - Showing sales amount

      ShowValueEntries(lValueEntry."Item Ledger Entry Type"::Sale);
    END;

    LOCAL PROCEDURE ShowItemLedgerEntries@1000000009(pEntryType@1000000000 : Integer);
    VAR
      lItemLedgerEntry@1000000001 : Record 32;
    BEGIN
      //DOC NS2015.1 - Showing item ledger entries
      //  -> pEntryType: Entry type (if -1 - not filtered)

      lItemLedgerEntry.RESET();
      lItemLedgerEntry.SETCURRENTKEY("Item No.", "Entry Type", "Variant Code", "Drop Shipment", "Global Dimension 1 Code", "Global Dimension 2 Code",
        "Location Code", "Posting Date");
      IF (pEntryType > -1) THEN
        lItemLedgerEntry.SETRANGE("Entry Type", pEntryType);
      lItemLedgerEntry.SETRANGE("Item No.", Rec."Item No.");
      lItemLedgerEntry.SETRANGE("Variant Code", Rec."Variant Code");
      IF (Rec."Global Dimension 1 Filter" <> '') THEN
        lItemLedgerEntry.SETFILTER("Global Dimension 1 Code", Rec."Global Dimension 1 Filter");
      IF (Rec."Global Dimension 2 Filter" <> '') THEN
        lItemLedgerEntry.SETFILTER("Global Dimension 2 Code", Rec."Global Dimension 2 Filter");
      IF (Rec."Location Filter" <> '') THEN
        lItemLedgerEntry.SETFILTER("Location Code", Rec."Location Filter");
      IF (FORMAT(Rec."Date Filter") <> '') THEN
        lItemLedgerEntry.SETFILTER("Posting Date", FORMAT(Rec."Date Filter"));

      PAGE.RUN(0, lItemLedgerEntry);
    END;

    LOCAL PROCEDURE ShowValueEntries@1000000010(pEntryType@1000000001 : Integer);
    VAR
      lValueEntry@1000000000 : Record 5802;
    BEGIN
      //DOC NS2015.1 - Showing value entries
      //  -> pEntryType: Entry type (if -1 - not filtered)

      lValueEntry.RESET();
      lValueEntry.SETCURRENTKEY("Item No.", "Posting Date", "Item Ledger Entry Type", "Entry Type", "Variance Type", "Item Charge No.", "Location Code",
        "Variant Code", "Global Dimension 1 Code", "Global Dimension 2 Code", "Source Type", "Source No.");
      IF (pEntryType > -1) THEN
        lValueEntry.SETRANGE("Item Ledger Entry Type", pEntryType);
      lValueEntry.SETRANGE("Item No.", Rec."Item No.");
      lValueEntry.SETRANGE("Variant Code", Rec."Variant Code");
      IF (Rec."Global Dimension 1 Filter" <> '') THEN
        lValueEntry.SETFILTER("Global Dimension 1 Code", Rec."Global Dimension 1 Filter");
      IF (Rec."Global Dimension 2 Filter" <> '') THEN
        lValueEntry.SETFILTER("Global Dimension 2 Code", Rec."Global Dimension 2 Filter");
      IF (Rec."Location Filter" <> '') THEN
        lValueEntry.SETFILTER("Location Code", Rec."Location Filter");
      IF (FORMAT(Rec."Date Filter") <> '') THEN
        lValueEntry.SETFILTER("Posting Date", FORMAT(Rec."Date Filter"));

      PAGE.RUN(0, lValueEntry);
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 08/01/2014 - Created
      //DOC NS2016.16 JH 06/09/2016 - nSemble 2016
    }
    END.
  }
}

