OBJECT Table 9062722 Quest. Template Action Header
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TemplateHeader@1000000000 : Record 9062722;
             BEGIN
               CheckOpen();

               IF (Id = 0) THEN BEGIN
                 TemplateHeader.RESET();
                 TemplateHeader.SETRANGE("Questionnaire Template Code", "Questionnaire Template Code");
                 IF (TemplateHeader.FINDLAST()) THEN
                   Id := TemplateHeader.Id + 1
                 ELSE
                   Id := 1;
               END;
             END;

    OnModify=BEGIN
               CheckOpen();
             END;

    OnDelete=VAR
               QuestTemplateLineLogicCriteria@1000000000 : Record 9062707;
             BEGIN
               CheckOpen();

               DeleteLines();

               QuestTemplateLineLogicCriteria.SETRANGE("Questionnaire Template Code","Questionnaire Template Code");
               QuestTemplateLineLogicCriteria.SETRANGE("Source Entry No.", Id);
               QuestTemplateLineLogicCriteria.SETRANGE("Line Type", QuestTemplateLineLogicCriteria."Line Type"::"Questionnaire Action");
               QuestTemplateLineLogicCriteria.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               CheckOpen();
             END;

    CaptionML=[ENU=Quest. Template Action Header;
               ENG=Quest. Template Action Header];
    LookupPageID=Page9063065;
    DrillDownPageID=Page9063065;
  }
  FIELDS
  {
    { 1   ;   ;Questionnaire Template Code;Code20 ;TableRelation="Questionnaire Template";
                                                   OnValidate=BEGIN
                                                                IF ("Questionnaire Template Code" <> xRec."Questionnaire Template Code") THEN
                                                                  DeleteLines();
                                                              END;

                                                   CaptionML=[ENU=Questionnaire Template Code;
                                                              ENG=Questionnaire Template Code] }
    { 3   ;   ;Id                  ;Integer       ;CaptionML=[ENU=Id;
                                                              ENG=Id] }
    { 4   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 5   ;   ;Table Id            ;Integer       ;OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");
                                                                TemplateActionLine.RESET();
                                                                TemplateActionLine.SETRANGE("Questionnaire Template Code", "Questionnaire Template Code");
                                                                TemplateActionLine.SETRANGE("Quest. Action Template Id", Id);
                                                                TemplateActionLine.MODIFYALL("Table Id", "Table Id", TRUE);
                                                              END;

                                                   CaptionML=[ENU=Table Id;
                                                              ENG=Table Id] }
    { 6   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Table Id)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name];
                                                   Editable=No }
    { 8   ;   ;Indent              ;Integer       ;CaptionML=[ENU=Indent;
                                                              ENG=Indent] }
    { 9   ;   ;Questionnaire Related Record;Boolean;
                                                   CaptionML=[ENU=Questionnaire Related Record;
                                                              ENG=Questionnaire Related Record] }
    { 10  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ENG=Status];
                                                   OptionCaptionML=[ENU=Open,Released;
                                                                    ENG=Open,Released];
                                                   OptionString=Open,Released;
                                                   Editable=No }
    { 11  ;   ;Block Line No.      ;Integer       ;TableRelation="Questionnaire Template Line"."Line No." WHERE (Questionnaire Template Code=FIELD(Questionnaire Template Code),
                                                                                                                 Line Type=CONST(Block));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Block Line Description");
                                                              END;

                                                   CaptionML=[ENU=Block Line No.;
                                                              ENG=Block Line No.] }
    { 14  ;   ;Block Line Description;Text80      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Questionnaire Template Line"."Question Description" WHERE (Questionnaire Template Code=FIELD(Questionnaire Template Code),
                                                                                                                                  Line No.=FIELD(Block Line No.)));
                                                   CaptionML=[ENU=Block Line Description;
                                                              ENG=Block Line Description];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Questionnaire Template Code,Id          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TemplateActionLine@1000000000 : Record 9062723;
      cnfRelease@1000000002 : TextConst 'ENU=Do you want to release questionnaire template action %1?;ENG=Do you want to release questionnaire template action %1?';
      cnfReopen@1000000001 : TextConst 'ENU=Do you want to reopen questionnaire template action %1?;ENG=Do you want to reopen questionnaire template action %1?';
      QuestManagement@1000000003 : Codeunit 9062540;

    LOCAL PROCEDURE DeleteLines@1000000000();
    BEGIN
      TemplateActionLine.RESET();
      TemplateActionLine.SETRANGE("Questionnaire Template Code", "Questionnaire Template Code");
      TemplateActionLine.SETRANGE("Quest. Action Template Id", Id);
      TemplateActionLine.DELETEALL();
    END;

    PROCEDURE CheckOpen@1000000002();
    VAR
      QuestTemplHeader@1000000000 : Record 9062702;
    BEGIN
      TESTFIELD(Status, QuestTemplHeader.Status::Open);
      QuestTemplHeader.GET("Questionnaire Template Code");
      QuestTemplHeader.CheckOpen();
    END;

    PROCEDURE Reopen@1000000003(Ask@1000000000 : Boolean);
    BEGIN
      IF (GUIALLOWED) AND (Ask) THEN
        IF (NOT(CONFIRM(STRSUBSTNO(cnfReopen, Name)))) THEN
          EXIT;

      QuestManagement.ReopenTemplateAction(Rec);
    END;

    PROCEDURE Release@1000000001(Ask@1000000000 : Boolean);
    BEGIN
      IF (GUIALLOWED) AND (Ask) THEN
        IF (NOT(CONFIRM(STRSUBSTNO(cnfRelease, Name)))) THEN
          EXIT;

      QuestManagement.ReleaseTemplateAction(Rec);
    END;

    PROCEDURE LookupBlockLineNo@1000000004();
    VAR
      QuestTemplLine@1000000000 : Record 9062703;
    BEGIN
      QuestTemplLine.RESET();
      QuestTemplLine.SETRANGE("Questionnaire Template Code", "Questionnaire Template Code");
      QuestTemplLine.SETRANGE("Line Type", QuestTemplLine."Line Type"::Block);
      IF ("Block Line No." <> 0) THEN
        IF (QuestTemplLine.GET("Questionnaire Template Code", "Block Line No.")) THEN;
      IF (PAGE.RUNMODAL(0, QuestTemplLine) IN [ACTION::LookupOK, ACTION::OK]) THEN
        VALIDATE("Block Line No.", QuestTemplLine."Line No.");
    END;

    PROCEDURE ValidateBlockLineDescription@1000000006(VAR LineDescription@1000000000 : Text[80]) : Boolean;
    VAR
      QuestTemplLine@1000000001 : Record 9062703;
      LineFound@1000000002 : Boolean;
    BEGIN
      QuestTemplLine.RESET();
      QuestTemplLine.SETRANGE("Questionnaire Template Code", "Questionnaire Template Code");
      QuestTemplLine.SETRANGE("Line Type", QuestTemplLine."Line Type"::Block);
      QuestTemplLine.SETRANGE("Question Description", LineDescription);
      LineFound := QuestTemplLine.FINDFIRST();
      IF (NOT(LineFound)) THEN BEGIN
        QuestTemplLine.SETFILTER("Question Description", LineDescription + '*');
        LineFound := QuestTemplLine.FINDFIRST();
      END;

      IF (LineFound) THEN BEGIN
        LineDescription := QuestTemplLine."Question Description";
        VALIDATE("Block Line No.", QuestTemplLine."Line No.");
      END;

      EXIT(LineFound);
    END;

    BEGIN
    {
      //DOC NA2015.9  AZ 05/08/2015 - Questionnaire actions, criteria changes and bugfixes
      //DOC NA2015.9  AZ 18/08/2015 - Question groups and loops
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

