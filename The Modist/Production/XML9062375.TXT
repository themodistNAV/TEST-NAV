OBJECT XMLport 9062375 Record Browser Selection Data
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Record Browser Selection Data;
               ENG=Record Browser Selection Data];
    Encoding=UTF-8;
    XMLVersionNo=1.1;
    Format/Evaluate=XML Format/Evaluate;
    OnPreXMLport=BEGIN
                   AsArray1 := 'true';
                 END;

    OnPostXMLport=BEGIN
                    CloseRecord();
                  END;

  }
  ELEMENTS
  {
    { [{BA886BEF-E804-4926-A0BF-C6DE9CB8D4C3}];  ;Browser             ;Element ;Table   ;
                                                  VariableName=RecordBrowser;
                                                  SourceTable=Table9062795 }

    { [{D7CDF9B2-7950-4D22-8A0B-E90CC9B613B5}];1 ;NoOfRecords         ;Element ;Text    ;
                                                  VariableName=RecordBrowser_NoOfRecords }

    { [{D739E2CB-F0DF-4040-95E4-D68EE640DC75}];1 ;Record              ;Element ;Table   ;
                                                  VariableName=SelectedRecord;
                                                  SourceTable=Table9062797;
                                                  Temporary=Yes;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         OpenRecord(RecordBrowser."Table No.");
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             RecRef.SETPOSITION(SelectedRecord."Source Position");
                                                                             IF (NOT(RecRef.FIND('='))) THEN
                                                                               currXMLport.SKIP();
                                                                             SelectedRecord_Picture := GetPictureUrl();
                                                                           END;
                                                                            }

    { [{FA5EC05E-9538-49B3-A059-D346C5B4213F}];2 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray1 }

    { [{0804269A-E23D-4D1B-A99E-A0DF0AB24D0A}];2 ;Position            ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=SelectedRecord::Source Position }

    { [{4491FF24-5027-4D00-8B05-C7A51F02C889}];2 ;Picture             ;Element ;Text    ;
                                                  VariableName=SelectedRecord_Picture }

    { [{F22AF45D-A4FA-46BF-8062-2DCA5CB28FFA}];2 ;Field               ;Element ;Table   ;
                                                  VariableName=RecordField;
                                                  SourceTable=Table9062798;
                                                  SourceTableView=SORTING(Field1,Field2,Field3)
                                                                  WHERE(Field2=CONST(0),
                                                                        Field11=CONST(1));
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=RecordBrowser;
                                                  MinOccurs=Zero;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             RecordField_Value := GetFieldValue(RecordField."Source Field ID");
                                                                           END;
                                                                            }

    { [{D344BB8D-1F7B-4BE2-BDCC-DCEF0BE8A5DA}];3 ;name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=RecordField::Name }

    { [{0FD62189-B4A5-4E03-9B2E-4CD6D6BDCD19}];3 ;value               ;Attribute;Text   ;
                                                  VariableName=RecordField_Value }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      RecRef@1000000001 : RecordRef;
      FldRef@1000000000 : FieldRef;
      RecordOpen@1000000002 : Boolean;

    PROCEDURE SetData@1000000000(VAR NewSelectedRecords@1000000001 : Record 9062797);
    BEGIN
      SelectedRecord.RESET();
      SelectedRecord.DELETEALL();
      IF (NewSelectedRecords.FINDSET()) THEN BEGIN
        REPEAT
          SelectedRecord.TRANSFERFIELDS(NewSelectedRecords);
          SelectedRecord.INSERT();
        UNTIL (NewSelectedRecords.NEXT() = 0);
      END;
      SelectedRecord.RESET();
      RecordBrowser_NoOfRecords := FORMAT(SelectedRecord.COUNT, 0, 9);
    END;

    LOCAL PROCEDURE OpenRecord@1000000003(TableNo@1000000000 : Integer);
    BEGIN
      IF (RecordOpen) THEN
        RecRef.CLOSE();
      RecRef.OPEN(TableNo);
      RecordOpen := TRUE;
    END;

    LOCAL PROCEDURE CloseRecord@1000000009();
    BEGIN
      IF (RecordOpen) THEN BEGIN
        RecRef.CLOSE();
        RecordOpen := FALSE;
      END;
    END;

    LOCAL PROCEDURE GetFieldValue@1000000006(RecordFieldNo@1000000000 : Integer) : Text;
    BEGIN
      IF (RecordFieldNo <> 0) THEN BEGIN
        FldRef := RecRef.FIELD(RecordFieldNo);
        EXIT(FORMAT(FldRef.VALUE, 0, 9));
      END;
      EXIT('');
    END;

    LOCAL PROCEDURE GetPictureUrl@1000000001() : Text;
    VAR
      BaseUrl@1000000001 : Text;
    BEGIN
      //!!! TO-DO !!!!!
      //!!! REMOVE THIS FUNCTION !!!!
      //!!! DIRTY DEMO HACK !!!!
      BaseUrl := 'http://content.navondemand.co.uk/demos/carpetright/small/';
      CASE (RecRef.NUMBER) OF
        DATABASE::"Item Category":
          EXIT(BaseUrl + 'ic_' + LOWERCASE(FORMAT(RecRef.FIELD(1).VALUE)) + '.jpg');
        DATABASE::"Product Group":
          EXIT(BaseUrl + 'pg_' + LOWERCASE(FORMAT(RecRef.FIELD(1).VALUE)) + '_' + LOWERCASE(FORMAT(RecRef.FIELD(2).VALUE)) + '.jpg');
        DATABASE::Item:
          EXIT(BaseUrl + 'i_' + LOWERCASE(FORMAT(RecRef.FIELD(1).VALUE)) + '.jpg');
      END;
      EXIT(BaseUrl + 'default_picture.jpg');
    END;

    BEGIN
    {
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

