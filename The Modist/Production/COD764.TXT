OBJECT Codeunit 764 Aged Acc. Payable
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ChartDescriptionMsg@1000 : TextConst 'ENU=Shows pending payment amounts to vendors summed for a period that you select.\\The first column shows the amount on pending payments that are not past the due date. The following column or columns show overdue amounts within the selected period from the payment due date. The chart shows overdue payment amounts going back up to five years from today''s date depending on the period that you select.;ENG=Shows pending payment amounts to vendors summed for a period that you select.\\The first column shows the amount on pending payments that are not past the due date. The following column or columns show overdue amounts within the selected period from the payment due date. The chart shows overdue payment amounts going back up to five years from today''s date depending on the period that you select.';
      ChartPerVendorDescriptionMsg@1001 : TextConst 'ENU=Shows pending payment amount to the vendor summed for a period that you select.\The first column shows the amount on pending payments that are not past the due date. The following column or columns show overdue amounts within the selected period from the payment due date. The chart shows overdue payment amounts going back up to five years from today''s date depending on the period that you select.;ENG=Shows pending payment amount to the vendor summed for a period that you select.\The first column shows the amount on pending payments that are not past the due date. The following column or columns show overdue amounts within the selected period from the payment due date. The chart shows overdue payment amounts going back up to five years from today''s date depending on the period that you select.';

    PROCEDURE UpdateData@7(VAR BusChartBuf@1000 : Record 485;VAR TempEntryNoAmountBuf@1002 : TEMPORARY Record 386);
    VAR
      PeriodIndex@1006 : Integer;
      PeriodLength@1001 : Text[1];
      NoOfPeriods@1004 : Integer;
    BEGIN
      WITH BusChartBuf DO BEGIN
        Initialize;
        SetXAxis(OverDueText,"Data Type"::String);
        AddMeasure(AmountText,1,"Data Type"::Decimal,"Chart Type"::Column);

        InitParameters(BusChartBuf,PeriodLength,NoOfPeriods,TempEntryNoAmountBuf);
        CalculateAgedAccPayable(
          "Period Filter Start Date",PeriodLength,NoOfPeriods,
          TempEntryNoAmountBuf);

        IF TempEntryNoAmountBuf.FINDSET THEN
          REPEAT
            PeriodIndex := TempEntryNoAmountBuf."Entry No.";
            AddColumn(FormatColumnName(PeriodIndex,PeriodLength,NoOfPeriods,"Period Length"));
            SetValueByIndex(0,PeriodIndex,RoundAmount(TempEntryNoAmountBuf.Amount));
          UNTIL TempEntryNoAmountBuf.NEXT = 0
      END;
    END;

    PROCEDURE UpdateDataPerVendor@2(VAR BusChartBuf@1000 : Record 485;VendorNo@1007 : Code[20];VAR TempEntryNoAmountBuf@1002 : TEMPORARY Record 386);
    VAR
      PeriodIndex@1006 : Integer;
      PeriodLength@1001 : Text[1];
      NoOfPeriods@1004 : Integer;
    BEGIN
      WITH BusChartBuf DO BEGIN
        Initialize;
        SetXAxis(OverDueText,"Data Type"::String);
        AddMeasure(AmountText,1,"Data Type"::Decimal,"Chart Type"::Column);

        InitParameters(BusChartBuf,PeriodLength,NoOfPeriods,TempEntryNoAmountBuf);
        CalculateAgedAccPayablePerVendor(
          VendorNo,'',"Period Filter Start Date",PeriodLength,NoOfPeriods,
          TempEntryNoAmountBuf);

        IF TempEntryNoAmountBuf.FINDSET THEN
          REPEAT
            PeriodIndex := TempEntryNoAmountBuf."Entry No.";
            AddColumn(FormatColumnName(PeriodIndex,PeriodLength,NoOfPeriods,"Period Length"));
            SetValueByIndex(0,PeriodIndex,RoundAmount(TempEntryNoAmountBuf.Amount));
          UNTIL TempEntryNoAmountBuf.NEXT = 0
      END;
    END;

    LOCAL PROCEDURE CalculateAgedAccPayable@1(StartDate@1001 : Date;PeriodLength@1002 : Text[1];NoOfPeriods@1003 : Integer;VAR TempEntryNoAmountBuffer@1004 : TEMPORARY Record 386);
    VAR
      VendLedgEntry@1006 : Record 25;
      RemainingAmountLCY@1008 : Decimal;
      EndDate@1007 : Date;
      Index@1005 : Integer;
    BEGIN
      VendLedgEntry.RESET;
      VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
      VendLedgEntry.SETRANGE(Open,TRUE);
      VendLedgEntry.SETAUTOCALCFIELDS("Remaining Amt. (LCY)");

      FOR Index := 0 TO NoOfPeriods - 1 DO BEGIN
        RemainingAmountLCY := 0;
        VendLedgEntry.SETFILTER(
          "Due Date",
          DateFilterByAge(Index,StartDate,PeriodLength,NoOfPeriods,EndDate));
        IF VendLedgEntry.FINDSET THEN
          REPEAT
            RemainingAmountLCY += VendLedgEntry."Remaining Amt. (LCY)";
          UNTIL VendLedgEntry.NEXT = 0;

        InsertAmountBuffer(Index,'',-RemainingAmountLCY,StartDate,EndDate,TempEntryNoAmountBuffer)
      END;
    END;

    LOCAL PROCEDURE CalculateAgedAccPayablePerVendor@4(VendorNo@1000 : Code[20];CustomerGroupCode@1009 : Code[10];StartDate@1001 : Date;PeriodLength@1002 : Text[1];NoOfPeriods@1003 : Integer;VAR TempEntryNoAmountBuffer@1004 : TEMPORARY Record 386);
    VAR
      VendLedgEntryRemainAmt@1006 : Query 25;
      RemainingAmountLCY@1008 : Decimal;
      EndDate@1007 : Date;
      Index@1005 : Integer;
    BEGIN
      IF VendorNo <> '' THEN
        VendLedgEntryRemainAmt.SETRANGE(Vendor_No,VendorNo);
      IF CustomerGroupCode <> '' THEN
        VendLedgEntryRemainAmt.SETRANGE(Vendor_Posting_Group,CustomerGroupCode);
      VendLedgEntryRemainAmt.SETRANGE(IsOpen,TRUE);

      FOR Index := 0 TO NoOfPeriods - 1 DO BEGIN
        RemainingAmountLCY := 0;
        VendLedgEntryRemainAmt.SETFILTER(
          Due_Date,
          DateFilterByAge(Index,StartDate,PeriodLength,NoOfPeriods,EndDate));
        VendLedgEntryRemainAmt.OPEN;
        IF VendLedgEntryRemainAmt.READ THEN
          RemainingAmountLCY := VendLedgEntryRemainAmt.Sum_Remaining_Amt_LCY;

        InsertAmountBuffer(Index,CustomerGroupCode,RemainingAmountLCY,StartDate,EndDate,TempEntryNoAmountBuffer)
      END;
    END;

    LOCAL PROCEDURE DateFilterByAge@14(Index@1000 : Integer;VAR StartDate@1007 : Date;PeriodLength@1006 : Text[1];NoOfPeriods@1005 : Integer;VAR EndDate@1002 : Date) : Text;
    VAR
      AgedAccReceivable@1001 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.DateFilterByAge(Index,StartDate,PeriodLength,NoOfPeriods,EndDate));
    END;

    LOCAL PROCEDURE InsertAmountBuffer@3(Index@1000 : Integer;BussUnitCode@1005 : Code[10];AmountLCY@1001 : Decimal;StartDate@1003 : Date;EndDate@1004 : Date;VAR TempEntryNoAmountBuffer@1002 : TEMPORARY Record 386);
    VAR
      AgedAccReceivable@1006 : Codeunit 763;
    BEGIN
      AgedAccReceivable.InsertAmountBuffer(
        Index,BussUnitCode,AmountLCY,StartDate,EndDate,TempEntryNoAmountBuffer);
    END;

    LOCAL PROCEDURE InitParameters@13(BusChartBuf@1000 : Record 485;VAR PeriodLength@1001 : Text[1];VAR NoOfPeriods@1002 : Integer;VAR TempEntryNoAmountBuf@1005 : TEMPORARY Record 386);
    VAR
      AgedAccReceivable@1004 : Codeunit 763;
    BEGIN
      AgedAccReceivable.InitParameters(BusChartBuf,PeriodLength,NoOfPeriods,TempEntryNoAmountBuf);
    END;

    LOCAL PROCEDURE FormatColumnName@6(Index@1000 : Integer;PeriodLength@1001 : Text[1];NoOfColumns@1003 : Integer;Period@1002 : Option) : Text;
    VAR
      AgedAccReceivable@1007 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.FormatColumnName(Index,PeriodLength,NoOfColumns,Period));
    END;

    PROCEDURE DrillDown@18(VAR BusChartBuf@1000 : Record 485;VendorNo@1001 : Code[20];VAR TempEntryNoAmountBuf@1003 : TEMPORARY Record 386);
    VAR
      MeasureName@1004 : Text;
      VendorGroupCode@1002 : Code[10];
    BEGIN
      WITH TempEntryNoAmountBuf DO BEGIN
        IF VendorNo <> '' THEN
          VendorGroupCode := ''
        ELSE BEGIN
          MeasureName := BusChartBuf.GetMeasureName(BusChartBuf."Drill-Down Measure Index");
          VendorGroupCode := COPYSTR(MeasureName,1,MAXSTRLEN(VendorGroupCode));
        END;
        IF GET('',BusChartBuf."Drill-Down X Index") THEN
          DrillDownVendLedgEntries(VendorNo,"Start Date","End Date");
      END;
    END;

    PROCEDURE DrillDownByGroup@9(VAR BusChartBuf@1001 : Record 485;VAR TempEntryNoAmountBuf@1000 : TEMPORARY Record 386);
    BEGIN
      DrillDown(BusChartBuf,'',TempEntryNoAmountBuf);
    END;

    LOCAL PROCEDURE DrillDownVendLedgEntries@21(VendorNo@1003 : Code[20];StartDate@1000 : Date;EndDate@1001 : Date);
    VAR
      VendLedgEntry@1002 : Record 25;
    BEGIN
      VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
      IF VendorNo <> '' THEN
        VendLedgEntry.SETRANGE("Vendor No.",VendorNo);
      IF EndDate = 0D THEN
        VendLedgEntry.SETFILTER("Due Date",'>=%1',StartDate)
      ELSE
        VendLedgEntry.SETRANGE("Due Date",StartDate,EndDate);
      VendLedgEntry.SETRANGE(Open,TRUE);
      IF VendLedgEntry.ISEMPTY THEN
        EXIT;
      PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
    END;

    PROCEDURE Description@10(PerVendor@1000 : Boolean) : Text;
    BEGIN
      IF PerVendor THEN
        EXIT(ChartPerVendorDescriptionMsg);
      EXIT(ChartDescriptionMsg);
    END;

    PROCEDURE UpdateStatusText@11(BusChartBuf@1000 : Record 485) : Text;
    VAR
      AgedAccReceivable@1001 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.UpdateStatusText(BusChartBuf));
    END;

    PROCEDURE SaveSettings@8(BusChartBuf@1001 : Record 485);
    VAR
      BusChartUserSetup@1000 : Record 487;
    BEGIN
      BusChartUserSetup."Period Length" := BusChartBuf."Period Length";
      BusChartUserSetup.SaveSetupCU(BusChartUserSetup,CODEUNIT::"Aged Acc. Payable");
    END;

    LOCAL PROCEDURE RoundAmount@5(Amount@1000 : Decimal) : Decimal;
    VAR
      AgedAccReceivable@1001 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.RoundAmount(Amount));
    END;

    LOCAL PROCEDURE OverDueText@16() : Text;
    VAR
      AgedAccReceivable@1000 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.OverDueText);
    END;

    LOCAL PROCEDURE AmountText@17() : Text;
    VAR
      AgedAccReceivable@1000 : Codeunit 763;
    BEGIN
      EXIT(AgedAccReceivable.AmountText);
    END;

    BEGIN
    END.
  }
}

