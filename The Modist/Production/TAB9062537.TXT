OBJECT Table 9062537 Auto Clear Entry
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(Code);
             END;

    CaptionML=[ENU=Auto Clear Entry;
               ENG=Auto Clear Entry];
    LookupPageID=Page9062736;
    DrillDownPageID=Page9062736;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                TnpToolkit.CodeToName(Rec.Code, Rec.Description);
                                                                Rec.VALIDATE(Description);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 3   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=NAV Records,Files on Disk;
                                                                    ENG=NAV Records,Files on Disk];
                                                   OptionString=NAV Records,Files on Disk }
    { 4   ;   ;Table ID            ;Integer       ;OnValidate=BEGIN
                                                                "Table Name" := ObjectFieldToolkit.GetTableName("Table ID");
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 5   ;   ;Folder Path         ;Text250       ;CaptionML=[ENU=Folder Path;
                                                              ENG=Folder Path] }
    { 6   ;   ;Include Subfolders  ;Boolean       ;CaptionML=[ENU=Include Subfolders;
                                                              ENG=Include Subfolders] }
    { 7   ;   ;Table Name          ;Text30        ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateTableName("Table ID","Table Name", FALSE);
                                                                VALIDATE("Table ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ObjectFieldToolkit.TableLookup("Table ID");
                                                              VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name] }
    { 100 ;   ;Date Field ID       ;Integer       ;OnValidate=VAR
                                                                RecRef@1000000000 : RecordRef;
                                                                FieldRef@1000000001 : FieldRef;
                                                                eFieldNotADate@1000000002 : TextConst 'ENU=The field "%1" in table "%2" is neither a Date or DateTime field;ENG=The field "%1" in table "%2" is neither a Date or DateTime field';
                                                              BEGIN
                                                                IF "Date Field ID" <> 0 THEN BEGIN
                                                                  RecRef.OPEN("Table ID");
                                                                  FieldRef := RecRef.FIELD("Date Field ID");
                                                                  IF NOT ( (FORMAT(FieldRef.TYPE) = 'Date') OR
                                                                           (FORMAT(FieldRef.TYPE) = 'DateTime')) THEN
                                                                    ERROR(eFieldNotADate, FieldRef.CAPTION, RecRef.CAPTION);
                                                                END;

                                                                "Date Field Name" := ObjectFieldToolkit.GetFieldName("Table ID","Date Field ID");
                                                              END;

                                                   CaptionML=[ENU=Date Field ID;
                                                              ENG=Date Field ID] }
    { 101 ;   ;Date Formula        ;DateFormula   ;CaptionML=[ENU=Date Formula;
                                                              ENG=Date Formula] }
    { 102 ;   ;Date to Compare To  ;Option        ;OnValidate=BEGIN
                                                                IF "Date to Compare To" = "Date to Compare To"::"Current Date" THEN
                                                                  VALIDATE("Specified Date",0D);
                                                              END;

                                                   CaptionML=[ENU=Date to Compare To;
                                                              ENG=Date to Compare To];
                                                   OptionCaptionML=[ENU=Current Date,Specified Date;
                                                                    ENG=Current Date,Specified Date];
                                                   OptionString=Current Date,Specified Date }
    { 103 ;   ;Specified Date      ;Date          ;CaptionML=[ENU=Specified Date;
                                                              ENG=Specified Date] }
    { 104 ;   ;Date Field Name     ;Text30        ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateFieldName("Table ID","Date Field ID","Date Field Name",FALSE);
                                                                VALIDATE("Date Field ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ObjectFieldToolkit.FieldLookup("Table ID","Date Field ID");
                                                              VALIDATE("Date Field ID");
                                                            END;

                                                   CaptionML=[ENU=Date Field Name;
                                                              ENG=Date Field Name] }
    { 105 ;   ;Delete All Records  ;Boolean       ;CaptionML=[ENU=Delete All Records;
                                                              ENG=Delete All Records] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Logger@1000000001 : Record 9062280;
      AutoClearMgt@1000000002 : Codeunit 9062411;
      ObjectFieldToolkit@1000000003 : Codeunit 9062343;
      TnpUtils@1000000000 : Codeunit 9062229;
      TnpToolkit@1000000004 : Codeunit 9062230;

    PROCEDURE ClearEntries@1000000000(ConfirmDelete@1000000003 : Boolean) RecCount : Integer;
    VAR
      RecRef@1000000000 : RecordRef;
      FieldRef@1000000001 : FieldRef;
      DateFilter@1000000002 : Text[1024];
      MaxDate@1000000016 : Date;
      tmpDate@1000000004 : Date;
      tmpDateTime@1000000005 : DateTime;
      eClearingDown@1000000006 : TextConst 'ENU=Clearing down "%2" (%1);ENG=Clearing down "%2" (%1)';
      eDateFieldIdMissing@1000000007 : TextConst 'ENU=A "%1" must be specified for entry %1;ENG=A "%1" must be specified for entry %1';
      eRecordsConfirmation@1000000008 : TextConst 'ENU=%1 entries will be deleted from %2.\\Do you wish to continue?;ENG=%1 entries will be deleted from %2.\\Do you wish to continue?';
      eFolderPathNotSet@1000000009 : TextConst 'ENU=Entry %1 must have a "%1" set;ENG=Entry %1 must have a "%1" set';
      eFilesConfirmation@1000000010 : TextConst 'ENU=Are you sure you want to delete files older than %1 from \\"%2"?;ENG=Are you sure you want to delete files older than %1 from \\"%2"?';
    BEGIN
      Logger.Log('AUTOCLEAR',Logger.Type::Information,STRSUBSTNO(eClearingDown,Code,Description),'');
      MaxDate := CalcMaxDate;
      DateFilter := CalcDateFilter(MaxDate);

      CASE Type OF
        Type::"NAV Records" :
          BEGIN
            RecRef.OPEN("Table ID");
            IF ("Date Field ID" = 0) AND (NOT "Delete All Records") THEN ERROR(eDateFieldIdMissing, Rec.FIELDCAPTION("Date Field ID"), Code);

            IF (NOT "Delete All Records") THEN BEGIN
              FieldRef := RecRef.FIELD("Date Field ID");
              IF FORMAT(FieldRef.TYPE) = 'DateTime' THEN
                DateFilter := STRSUBSTNO('..%1',CREATEDATETIME(MaxDate,235959T));
              FieldRef.SETFILTER(DateFilter);
            END;

            IF RecRef.FINDSET THEN BEGIN
              IF ConfirmDelete THEN
                IF NOT CONFIRM(eRecordsConfirmation, TRUE, RecRef.COUNT, RecRef.NAME) THEN EXIT(0);
              REPEAT
                RecRef.DELETE(TRUE);
              UNTIL RecRef.NEXT = 0;
            END;
          END;
        Type::"Files on Disk" :
          BEGIN
            IF "Folder Path" = '' THEN ERROR(eFolderPathNotSet, Rec.Code, Rec.FIELDCAPTION("Folder Path"));
              IF ConfirmDelete THEN
                IF NOT CONFIRM(eFilesConfirmation, FALSE, MaxDate, "Folder Path")
                  THEN EXIT(0);
              AutoClearMgt.DeleteFilesInFolder("Folder Path","Include Subfolders",MaxDate);
          END;
      END;
    END;

    PROCEDURE CalcDateFilter@1000000001(MaxDate@1000000000 : Date) DateFilter : Text[1024];
    BEGIN
      DateFilter := STRSUBSTNO('..%1',MaxDate);
    END;

    PROCEDURE CalcMaxDate@1000000002() MaxDate : Date;
    BEGIN
      CASE "Date to Compare To" OF
        "Date to Compare To"::"Current Date" :
            MaxDate := CALCDATE("Date Formula",TODAY);
        "Date to Compare To"::"Specified Date" :
          BEGIN
            IF "Specified Date" = 0D THEN
              MaxDate := 0D
            ELSE
              MaxDate := CALCDATE("Date Formula","Specified Date");
          END;
      END;
    END;

    BEGIN
    {
      //DOC NA2015.1  PC 26/03/2014 - Created
      //DOC NA2015.4  JH 09/02/2015 - Captions sorted
      //DOC NA2015.7  AP 26/06/2015 - Added "Delete All Records" field and functionality
      //DOC NA2016.10 JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 15/04/2016 - CfMD amendments
    }
    END.
  }
}

