OBJECT Table 9062331 SQL Query
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(Code);
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               SqlQueryResultField.RESET();
               SqlQueryResultField.SETRANGE("Query Code", Rec.Code);
               SqlQueryResultField.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=SQL Query;
               ENG=SQL Query];
    LookupPageID=Page9062872;
    DrillDownPageID=Page9062872;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                Rec.VALIDATE(Description, String.ToTitleCase(Rec.Code));
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 3   ;   ;Query Text          ;BLOB          ;CaptionML=[ENU=Query Text;
                                                              ENG=Query Text] }
    { 4   ;   ;Text                ;Text250       ;CaptionML=[ENU=Text;
                                                              ENG=Text] }
    { 5   ;   ;Updated Date Time   ;DateTime      ;CaptionML=[ENU=Updated Date Time;
                                                              ENG=Updated Date Time] }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SqlQueryResultField@1000000001 : Record 9062387;
      String@1000000002 : Codeunit 9062236;
      TnpToolkit@1000000000 : Codeunit 9062230;

    PROCEDURE QueryTextHasValue@1000000001() : Boolean;
    BEGIN
      //DOC NA2015.1 - Returning if the query text has value
      //  <- Result

      Rec.CALCFIELDS("Query Text");
      EXIT(Rec."Query Text".HASVALUE);
    END;

    PROCEDURE GetQueryText@1000000000() : Text;
    VAR
      lQueryText@1000000000 : Text;
      InStream@1000000002 : InStream;
      Line@1000000004 : Text[1024];
    BEGIN
      //DOC NA2015.1 - Returning query text
      //  <- The text

      Rec.CALCFIELDS("Query Text");
      Rec."Query Text".CREATEINSTREAM(InStream);

      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(Line);
        lQueryText := lQueryText + ' ' + Line;
      END;

      EXIT(lQueryText);
    END;

    PROCEDURE SetQueryText@1000000002(pQueryText@1000000000 : Text);
    VAR
      lOutStream@1000000001 : OutStream;
      lQueryTextBuffer@1000000002 : BigText;
    BEGIN
      //DOC NA2015.1 - Setting query text
      //  -> pQueryText: The text

      CLEAR(Rec."Query Text");
      Rec."Query Text".CREATEOUTSTREAM(lOutStream);
      lQueryTextBuffer.ADDTEXT(pQueryText);
      lQueryTextBuffer.WRITE(lOutStream);

      Text := COPYSTR(pQueryText,1,MAXSTRLEN(Text));
    END;

    PROCEDURE EditQuery@1000000003() : Boolean;
    VAR
      TempBlob@1000000000 : TEMPORARY Record 99008535;
      CodeEditorMgt@1000000001 : Codeunit 9062658;
    BEGIN
      //DOC NA2016.12

      CLEAR(TempBlob);
      CALCFIELDS(Rec."Query Text");
      IF (Rec."Query Text".HASVALUE) THEN
        TempBlob.Blob := "Query Text";

      IF (CodeEditorMgt.EditBlob(TempBlob, 'sqlserver')) THEN BEGIN
        Rec."Query Text" := TempBlob.Blob;
        Rec.MODIFY();
      END;
    END;

    PROCEDURE GetTimestampDateTime@1000000004() : DateTime;
    BEGIN
      //DOC NA2016.14 - Returning timestamp datetime
      //  <- The timestamp datetime

      EXIT(TnpToolkit.DefaultDateTime(Rec."Modified Date Time", Rec."Created Date Time"));
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 16/05/2013 - Created
      //DOC NA2015.4  AP 27/08/2014 - Added "Text" field for the Extended Text on the page
      //DOC NA2015.7  JH 26/05/2015 - Result fields added, fixes
      //DOC NA2016.7  JH 27/08/2015 - Upgraded to 2016
      //DOC NA2016.12 PC 26/11/2015 - Added code to SetQueryText function to update "Text" field
      //DOC NA2016.12 AZ 14/01/2015 - Added EditQuery function to edit sql query text in code editor, TextExtensions removed
      //DOC NA2016.14 JH 23/02/2016 - Added audit fields
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

