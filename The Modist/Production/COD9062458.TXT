OBJECT Codeunit 9062458 Enhanced Workflow Clean Up
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    TableNo=9062734;
    OnRun=VAR
            Workflow@1000000006 : Record 9062258;
            GroupWorkflow@1000000000 : Record 9062265;
            WorkflowActLine@1000000004 : Record 9062266;
            RecRef@1000000001 : RecordRef;
            NAVObject@1000000002 : Record 2000000001;
            KeepGroup@1000000003 : Boolean;
            CloseReason@1000000005 : Text[1024];
            eFinalMessage@1000000007 : TextConst 'ENU=Headers Closed: %1\Lines Closed: %2;ENG=Headers Closed: %1\Lines Closed: %2';
          BEGIN
            IF GUIALLOWED THEN IF NOT CONFIRM(AreYouSure) THEN ERROR(Err_OppCancel);

            GroupWorkflow.SETRANGE(Status,GroupWorkflow.Status::Open);
            GroupWorkflow.SETRANGE("Company Name",COMPANYNAME);
            IF GroupWorkflow.FINDSET THEN BEGIN
              REPEAT
                KeepGroup := TRUE;

                IF KeepGroup THEN BEGIN
                  NAVObject.SETRANGE(Type,NAVObject.Type::TableData);
                  NAVObject.SETRANGE(ID,GroupWorkflow."Table ID");
                  IF NOT NAVObject.FINDFIRST THEN BEGIN
                    KeepGroup := FALSE;
                    CloseReason := CloseReason_TableNotFound;
                  END;
                END;


                IF KeepGroup THEN BEGIN
                  IF GroupWorkflow."Get Position Text" = '' THEN BEGIN
                    KeepGroup := FALSE;
                    CloseReason := CloseReason_NoPositionText;
                  END;
                END;

                IF KeepGroup THEN BEGIN
                  RecRef.OPEN(GroupWorkflow."Table ID");
                  RecRef.SETPOSITION(GroupWorkflow."Get Position Text");
                  RecRef.SETRECFILTER;
                  IF NOT RecRef.FINDFIRST THEN  BEGIN
                    KeepGroup := FALSE;
                    CloseReason := CloseReason_RecordNotFound;
                  END;

                  RecRef.CLOSE;
                END;

                IF KeepGroup THEN BEGIN
                  IF NOT Workflow.GET(GroupWorkflow."Workflow No.") THEN BEGIN
                    KeepGroup := FALSE;
                    CloseReason := CloseReason_WorkflowNotFound;
                  END;
                END;

                IF NOT KeepGroup THEN BEGIN
                  GroupWorkflow.Status := GroupWorkflow.Status::Closed;
                  GroupWorkflow.MODIFY(FALSE);

                  Logger.Log('WFLOW',Logger.Type::Information,STRSUBSTNO(Log_GroupClosed,GroupWorkflow."No.",CloseReason),USERID);
                  HeaderCount += 1;

                  WorkflowActLine.SETRANGE("Group Workflow No.",GroupWorkflow."No.");
                  WorkflowActLine.SETRANGE(Status,WorkflowActLine.Status::Open);
                  IF WorkflowActLine.FINDSET THEN BEGIN
                    REPEAT
                      WorkflowActLine.Status := WorkflowActLine.Status::Closed;
                      WorkflowActLine."My Comment" := CloseReason;
                      WorkflowActLine.MODIFY(FALSE);

                      Logger.Log('WFLOW',Logger.Type::Information,STRSUBSTNO(Log_ActivityClosed,WorkflowActLine."GUID No.",WorkflowActLine."Group Workflow No.",CloseReason),USERID);
                      LineCount += 1;
                    UNTIL WorkflowActLine.NEXT = 0;
                  END;
                END;
              UNTIL GroupWorkflow.NEXT = 0;
            END;

            MESSAGE(eFinalMessage,HeaderCount,LineCount);
          END;

  }
  CODE
  {
    VAR
      TNPUtils@1000000000 : Codeunit 9062229;
      AreYouSure@1000000008 : TextConst 'ENU=Are you sure you want clear down Workflows that should no longer be Open?;ENG=Are you sure you want clear down Workflows that should no longer be Open?';
      Err_OppCancel@1000000009 : TextConst 'ENU=Operation Cancelled;ENG=Operation Cancelled';
      CloseReason_TableNotFound@1000000002 : TextConst 'ENU=Related Table no longer exists;ENG=Related Table no longer exists';
      CloseReason_NoPositionText@1000000001 : TextConst 'ENU=Workflow not related to any record;ENG=Workflow not related to any record';
      CloseReason_RecordNotFound@1000000003 : TextConst 'ENU=The Related Record no longer exists;ENG=The Related Record no longer exists';
      CloseReason_WorkflowNotFound@1000000004 : TextConst 'ENU=The Workflow Type not longer exists;ENG=The Workflow Type not longer exists';
      Logger@1000000005 : Record 9062280;
      Log_GroupClosed@1000000006 : TextConst 'ENU=Closed Group Workflow %1 - %2;ENG=Closed Group Workflow %1 - %2';
      Log_ActivityClosed@1000000007 : TextConst 'ENU=Closed Workflow Activity %1 for group %2 - %3;ENG=Closed Workflow Activity %1 for group %2 - %3';
      HeaderCount@1000000010 : Integer;
      LineCount@1000000011 : Integer;

    BEGIN
    {
      //DOC NA2015.3  PC 29/08/2014 - Created
      //DOC NA2016.10 JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 18/04/2016 - CfMD amendments
    }
    END.
  }
}

