OBJECT Codeunit 9021620 Web Attribute Mgt
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:18 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LabelRec@1000000010 : Record 9021618;
      WebAttRec@1000000000 : Record 9021701;
      WebAttRec1@1000000018 : Record 9021701;
      WebAttRec2@1000000020 : Record 9021701;
      WebAttValue@1000000019 : Record 9021702;
      WebAttValue1@1190000016 : Record 9021702;
      WebGlobal@1000000025 : Record 9021695;
      KeyField1@1000000001 : Code[50];
      KeyField2@1000000002 : Code[50];
      KeyFieldInteger@1000000003 : Integer;
      TableID@1000000004 : Integer;
      WebAttCode@1000000005 : Code[50];
      LabelCode@1000000006 : Code[10];
      IntegerVar@1000000013 : Integer;
      TextVar@1000000014 : Text[250];
      CodeVar@1000000007 : Code[30];
      DecimalVar@1000000015 : Decimal;
      BooleanVar@1000000008 : Boolean;
      DateVar@1000000009 : Date;
      Text000@1000000011 : TextConst 'ENU=%1 cannot be %2.;ENG=%1 cannot be %2.';
      Text001@1000000012 : TextConst 'ENU=Data Type %1 is not supported.;ENG=Data Type %1 is not supported.';
      TimeVar@1000000016 : Time;
      DeleteAttribute@1000000017 : Boolean;
      Text003@1000000023 : TextConst 'ENU=No %1 record is set up with %2 ''%3'';ENG=No %1 record is set up with %2 ''%3''';
      Text004@1000000024 : TextConst 'ENU=This function only works where the Web Attribute has a Multiple Values Exist On set to something other than No Multi Values.;ENG=This function only works where the Web Attribute has a Multiple Values Exist On set to something other than No Multi Values.';
      Text005@1000000026 : TextConst 'ENU=The DeleteBlankValues parameter was set to TRUE, but the "%1" field for %2 "%3" is TRUE, so values cannot be deleted.;ENG=The DeleteBlankValues parameter was set to TRUE, but the "%1" field for %2 "%3" is TRUE, so values cannot be deleted.';
      PrevIntegerVar@1190000006 : Integer;
      PrevTextVar@1190000007 : Text[250];
      PrevCodeVar@1190000008 : Code[30];
      PrevDecimalVar@1190000009 : Decimal;
      PrevBooleanVar@1190000010 : Boolean;
      PrevDateVar@1190000011 : Date;
      Text006@1190000013 : TextConst 'ENU=This function only works where the Web Attribute has a Multiple Values Exist On set to No Multi Values.;ENG=This function only works where the Web Attribute has a Multiple Values Exist On set to No Multi Values.';
      Text008@1190000015 : TextConst 'ENU=Deleting Web Attribute Value... found %1 instead of one Web Attribute Value...... This is not right.;ENG=Deleting Web Attribute Value... found %1 instead of one Web Attribute Value...... This is not right.';
      Text009@1000000021 : TextConst 'ENU=Global Code of %1 in Web Attribute %2 does not match input global of %3.;ENG=Global Code of %1 in Web Attribute %2 does not match input global of %3.';
      TxtLabelCode@1000000030 : TextConst 'ENU=Label Code;ENG=Label Code';
      TxtBlank@1000000029 : TextConst 'ENU=blank;ENG=blank';
      TxtTableId@1000000028 : TextConst 'ENU=Table ID;ENG=Table ID';
      TxtZero@1000000027 : TextConst 'ENU=zero;ENG=zero';
      TxtWebAttributeCode@1000000022 : TextConst 'ENU=Web Attribute Code;ENG=Web Attribute Code';

    PROCEDURE ControlWebAttributes@1000000001(InputKeyField1@1000000003 : Code[50];InputKeyField2@1000000004 : Code[50];InputKeyInteger@1000000005 : Integer;InputTableID@1000000006 : Integer;InputLabelCode@1000000000 : Code[10];InputWebAttCode@1000000001 : Code[50];InputIntegerVar@1000000009 : Integer;InputTextVar@1000000010 : Text[250];InputCodeVar@1000000002 : Code[30];InputDecimalVar@1000000011 : Decimal;InputBooleanVar@1000000007 : Boolean;InputDateVar@1000000008 : Date;InputTimeVar@1000000012 : Time);
    BEGIN
      //ControlWebAttributes(KF1,KF2,KInt,Table,Label,Webattcode,Int,Text,Code,Dec,Boo,Date,Time);

      //GW 100720 This is original control of Web Attributes

      KeyField1 := InputKeyField1;
      KeyField2 := InputKeyField2;
      KeyFieldInteger := InputKeyInteger;
      TableID := InputTableID;
      LabelCode := InputLabelCode;
      WebAttCode := InputWebAttCode;  //DOC EC2009.2.69 changed to Code 50
      IntegerVar := InputIntegerVar;
      TextVar := InputTextVar;
      CodeVar := InputCodeVar;
      DecimalVar := InputDecimalVar;
      BooleanVar := InputBooleanVar;
      DateVar := InputDateVar;
      TimeVar := InputTimeVar;

      IF LabelCode = '' THEN ERROR(Text000,TxtLabelCode,TxtBlank);
      IF TableID = 0 THEN ERROR(Text000,TxtTableId,TxtZero);
      IF WebAttCode = '' THEN ERROR(Text000,TxtWebAttributeCode,TxtBlank);

      DeleteAttribute := FALSE;
      WebAttRec.GET(LabelCode,TableID,WebAttCode);
      IF WebAttRec."Multiple Values Exist On" <> WebAttRec."Multiple Values Exist On"::"No Multiple Values" THEN
        ERROR(Text006);

      CASE WebAttRec."Data Type" OF
        WebAttRec."Data Type"::Integer :
        BEGIN
          IF IntegerVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Text :
        BEGIN
          IF TextVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Code :
        BEGIN
          IF CodeVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Decimal :
        BEGIN
          IF DecimalVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Boolean :
        BEGIN
          IF BooleanVar = FALSE THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Date :
        BEGIN
          IF DateVar = 0D THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Time :
        BEGIN
          IF TimeVar = 0T THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::"Text+Dec" :
        BEGIN
          IF (TextVar = '') AND
             (DecimalVar = 0) THEN
            DeleteAttribute := TRUE;
        END;
        ELSE
          ERROR(Text001,WebAttRec."Data Type");
      END;

      IF WebAttRec."Apply to All Labels" THEN BEGIN
        LabelRec.RESET;
        LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
        IF LabelRec.FIND('-') THEN
        REPEAT
          IF WebAttRec1.GET(LabelRec.Code,TableID,WebAttCode) THEN
            IF WebAttRec1."Apply to All Labels" THEN
              PopulateWebValues(LabelRec.Code);
        UNTIL LabelRec.NEXT = 0;
      END ELSE BEGIN
        WebAttRec1 := WebAttRec;
        PopulateWebValues(LabelCode);
      END;
    END;

    PROCEDURE PopulateWebValues@1000000000(PopLabelCode@1000000000 : Code[10]);
    BEGIN
      IF WebAttValue.GET(WebAttRec1."Label Code",TableID,WebAttCode,KeyField1,KeyField2,KeyFieldInteger) THEN BEGIN
        IF DeleteAttribute THEN BEGIN
          WebAttValue.DELETE;
          EXIT;
        END;
      END ELSE BEGIN
        IF DeleteAttribute THEN
          EXIT;
        //MQ 26/01/2011 - GW110126 WebAttRec1.TESTFIELD("Default Image URL");
        WebAttValue.INIT;
        WebAttValue."Label Code" := WebAttRec1."Label Code";
        WebAttValue."Table ID" := WebAttRec1."Table ID";
        WebAttValue.VALIDATE("Attribute Code",WebAttCode);  //DOC EC2013.3.20 changed to Validate
        WebAttValue."Key Field String 1" := KeyField1;
        WebAttValue."Key Field String 2" := KeyField2;
        WebAttValue."Key Field Integer 1" := KeyFieldInteger;
        WebAttValue."Image URL" := WebAttRec1."Default Image URL";
        WebAttValue.INSERT(TRUE);
      END;

      UpdateValue;
    END;

    PROCEDURE ControlMultiValueWebAttributes@1190000005(InputKeyField1@1000000003 : Code[50];InputKeyField2@1000000004 : Code[50];InputKeyInteger@1000000005 : Integer;InputTableID@1000000006 : Integer;InputLabelCode@1000000000 : Code[10];InputWebAttCode@1000000001 : Code[50];InputIntegerVar@1000000009 : Integer;InputTextVar@1000000010 : Text[250];InputCodeVar@1000000002 : Code[30];InputDecimalVar@1000000011 : Decimal;InputBooleanVar@1000000007 : Boolean;InputDateVar@1000000008 : Date;xRecIntegerVar@1190000005 : Integer;xRecTextVar@1190000006 : Text[250];xRecCodeVar@1190000007 : Code[30];xRecDecimalVar@1190000008 : Decimal;xRecBooleanVar@1190000009 : Boolean;xRecDateVar@1190000010 : Date);
    VAR
      TxtNoMultipleForBoolean@1000000012 : TextConst 'ENU=Its not logical to have multiple values on a boolean value.;ENG=Its not logical to have multiple values on a boolean value.';
    BEGIN
      //ControlMultiValueWebAttributes(KF1,KF2,KInt,Table,Label,Webattcode,Int,Text,Code,Dec,Boo,Date
      //                               PrevInt,PrevText,PrevCode,PrevDec,PrevBoo,PrevDate);
      //Does not work for time as otherwise Expression is too complex and get error.

      KeyField1 := InputKeyField1;
      KeyField2 := InputKeyField2;
      KeyFieldInteger := InputKeyInteger;
      TableID := InputTableID;
      LabelCode := InputLabelCode;
      WebAttCode := InputWebAttCode;   //DOC EC2009.2.69 changed to Code 50
      IntegerVar := InputIntegerVar;
      TextVar := InputTextVar;
      CodeVar := InputCodeVar;
      DecimalVar := InputDecimalVar;
      BooleanVar := InputBooleanVar;
      DateVar := InputDateVar;

      PrevIntegerVar := xRecIntegerVar;
      PrevTextVar := xRecTextVar;
      PrevCodeVar := xRecCodeVar;
      PrevDecimalVar := xRecDecimalVar;
      PrevBooleanVar := xRecBooleanVar;
      PrevDateVar := xRecDateVar;

      IF LabelCode = '' THEN ERROR(Text000,TxtLabelCode,TxtBlank);
      IF TableID = 0 THEN ERROR(Text000,TxtTableId,TxtZero);
      IF WebAttCode = '' THEN ERROR(Text000,TxtWebAttributeCode,TxtBlank);

      DeleteAttribute := FALSE;
      WebAttRec.GET(LabelCode,TableID,WebAttCode);
      IF WebAttRec."Multiple Values Exist On" = WebAttRec."Multiple Values Exist On"::"No Multiple Values" THEN
        ERROR(Text004);

      CASE WebAttRec."Data Type" OF
        WebAttRec."Data Type"::Integer :
        BEGIN
          IF IntegerVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Text :
        BEGIN
          IF TextVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Code :
        BEGIN
          IF CodeVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Decimal :
        BEGIN
          IF DecimalVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Boolean :
        BEGIN
          //MQ 22/05/2014 - GW140522 -
          //IF BooleanVar = FALSE THEN DeleteAttribute := TRUE;
          ERROR(TxtNoMultipleForBoolean);
          //MQ 22/05/2014 - GW140522 +
        END;
        WebAttRec."Data Type"::Date :
        BEGIN
          IF DateVar = 0D THEN DeleteAttribute := TRUE;
        END;
        ELSE
          ERROR(Text001,WebAttRec."Data Type");
      END;

      IF WebAttRec."Apply to All Labels" THEN BEGIN
        LabelRec.RESET;
        LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
        IF LabelRec.FIND('-') THEN
        REPEAT
          IF WebAttRec1.GET(LabelRec.Code,TableID,WebAttCode) THEN
            IF WebAttRec1."Apply to All Labels" THEN
              PopulateMultiValueWebValues(LabelRec.Code);
        UNTIL LabelRec.NEXT = 0;
      END ELSE BEGIN
        WebAttRec1 := WebAttRec;
        PopulateMultiValueWebValues(LabelCode);
      END;
    END;

    PROCEDURE PopulateMultiValueWebValues@1190000006(PopLabelCode@1000000000 : Code[10]);
    BEGIN
      IF DeleteAttribute THEN BEGIN
        WebAttValue.RESET;
        WebAttValue.SETRANGE("Label Code",WebAttRec1."Label Code");
        WebAttValue.SETRANGE("Table ID",TableID);
        WebAttValue.SETRANGE("Attribute Code",WebAttCode);
        CASE WebAttRec1."Multiple Values Exist On" OF
          WebAttRec1."Multiple Values Exist On"::"No Multiple Values" :
          BEGIN
            ERROR(Text004);
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Field 1" :
          BEGIN
            //WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN
              WebAttValue.DELETE;
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Field 2" :
          BEGIN
            WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            //WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN
              WebAttValue.DELETE;
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Integer" :
          BEGIN
            WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            //WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN
              WebAttValue.DELETE;
          END;
        END;
      END ELSE BEGIN
        WebAttValue.RESET;
        WebAttValue.SETRANGE("Label Code",WebAttRec1."Label Code");
        WebAttValue.SETRANGE("Table ID",TableID);
        WebAttValue.SETRANGE("Attribute Code",WebAttCode);
        CASE WebAttRec1."Multiple Values Exist On" OF
          WebAttRec1."Multiple Values Exist On"::"No Multiple Values" :
          BEGIN
            ERROR(Text004);
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Field 1" :
          BEGIN
            //WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF WebAttValue.FINDFIRST THEN
                WebAttValue.DELETE
              ELSE
                UpdateValue;
            END ELSE BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF NOT WebAttValue.FINDFIRST THEN BEGIN
                InsertValue;
                UpdateValue;
              END;
            END;
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Field 2" :
          BEGIN
            WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            //WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF WebAttValue.FINDFIRST THEN
                WebAttValue.DELETE
              ELSE
                UpdateValue;
            END ELSE BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF NOT WebAttValue.FINDFIRST THEN BEGIN
                InsertValue;
                UpdateValue;
              END;
            END;
          END;
          WebAttRec1."Multiple Values Exist On"::"Key Integer" :
          BEGIN
            WebAttValue.SETRANGE("Key Field String 1",KeyField1);
            WebAttValue.SETRANGE("Key Field String 2",KeyField2);
            //WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
            FilterOnDataType(PrevIntegerVar,PrevTextVar,PrevCodeVar,PrevDecimalVar,PrevBooleanVar,PrevDateVar);
            IF WebAttValue.COUNT > 1 THEN
              ERROR(Text008,WebAttValue.COUNT);
            IF WebAttValue.FINDFIRST THEN BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF WebAttValue.FINDFIRST THEN
                WebAttValue.DELETE
              ELSE
                UpdateValue;
            END ELSE BEGIN
              FilterOnDataType(IntegerVar,TextVar,CodeVar,DecimalVar,BooleanVar,DateVar);
              IF NOT WebAttValue.FINDFIRST THEN BEGIN
                InsertValue;
                UpdateValue;
              END;
            END;
          END;
        END;
      END;
    END;

    PROCEDURE FilterOnDataType@1190000007(InIntegerVar@1190000005 : Integer;InTextVar@1190000006 : Text[250];InCodeVar@1190000007 : Code[30];InDecimalVar@1190000008 : Decimal;InBooleanVar@1190000009 : Boolean;InDateVar@1190000010 : Date);
    BEGIN
      //Just used by multiple values
      CASE WebAttRec1."Data Type" OF
        WebAttRec1."Data Type"::Integer :
        BEGIN
          WebAttValue.SETRANGE(IntegerVar,InIntegerVar);
        END;
        WebAttRec1."Data Type"::Text :
        BEGIN
          WebAttValue.SETRANGE(TextVar,InTextVar);
        END;
        WebAttRec1."Data Type"::Code :
        BEGIN
          WebAttValue.SETRANGE(CodeVar,InCodeVar);
        END;
        WebAttRec1."Data Type"::Decimal :
        BEGIN
          WebAttValue.SETRANGE(DecimalVar,InDecimalVar);
        END;
        WebAttRec."Data Type"::Boolean :
        BEGIN
          WebAttValue.SETRANGE(BooleanVar,InBooleanVar);
        END;
        WebAttRec1."Data Type"::Date :
        BEGIN
          WebAttValue.SETRANGE(DateVar,InDateVar);
        END;
        ELSE
          ERROR(Text001,WebAttRec1."Data Type");
      END;
    END;

    PROCEDURE UpdateValue@1190000008();
    BEGIN
      //Used by both single and multiple values
      CASE WebAttRec1."Data Type" OF
        WebAttRec1."Data Type"::Integer :
        BEGIN
          WebAttValue.VALIDATE(IntegerVar,IntegerVar);
        END;
        WebAttRec1."Data Type"::Text :
        BEGIN
          WebAttValue.VALIDATE(TextVar,TextVar);
          WebAttValue.CodeVar := CodeVar;  //DOC EC2013.3.70
        END;
        WebAttRec1."Data Type"::Code :
        BEGIN
          WebAttValue.VALIDATE(CodeVar,CodeVar);
        END;
        WebAttRec1."Data Type"::Decimal :
        BEGIN
          WebAttValue.VALIDATE(DecimalVar,DecimalVar);
        END;
        WebAttRec."Data Type"::Boolean :
        BEGIN
          WebAttValue.VALIDATE(BooleanVar,BooleanVar);
        END;
        WebAttRec1."Data Type"::Date :
        BEGIN
          WebAttValue.VALIDATE(DateVar,DateVar);
        END;
        WebAttRec1."Data Type"::Time :
        BEGIN
          WebAttValue.VALIDATE(TimeVar,TimeVar);
        END;
        WebAttRec1."Data Type"::"Text+Dec" :
        BEGIN
          WebAttValue.VALIDATE(TextVar,TextVar);
          WebAttValue.VALIDATE(DecimalVar,DecimalVar);
        END;
      END;
      WebAttValue.MODIFY;
    END;

    PROCEDURE InsertValue@1190000009();
    BEGIN
      //Just used by multiple values
      WebAttValue1.RESET;
      WebAttValue1.SETRANGE("Label Code",WebAttRec1."Label Code");
      WebAttValue1.SETRANGE("Table ID",TableID);
      WebAttValue1.SETRANGE("Attribute Code",WebAttCode);

      WebAttValue.INIT;
      WebAttValue."Label Code" := WebAttRec1."Label Code";
      WebAttValue."Table ID" := WebAttRec1."Table ID";
      WebAttValue.VALIDATE("Attribute Code",WebAttCode); //DOC EC2013.3.20 changed to Validate

      CASE WebAttRec1."Multiple Values Exist On" OF
        WebAttRec1."Multiple Values Exist On"::"Key Field 1" :
        BEGIN
          //WebAttValue1.SETRANGE("Key Field String 1",KeyField1);
          WebAttValue1.SETRANGE("Key Field String 2",KeyField2);
          WebAttValue1.SETRANGE("Key Field Integer 1",KeyFieldInteger);
          IF WebAttValue1.FINDLAST THEN BEGIN
            WebAttValue."Key Field String 1" := INCSTR(WebAttValue1."Key Field String 1");
          END ELSE
            WebAttValue."Key Field String 1" := '1';
          //WebAttValue."Key Field String 1" := KeyField1;
          WebAttValue."Key Field String 2" := KeyField2;
          WebAttValue."Key Field Integer 1" := KeyFieldInteger;
        END;
        WebAttRec1."Multiple Values Exist On"::"Key Field 2" :
        BEGIN
          WebAttValue1.SETRANGE("Key Field String 1",KeyField1);
          //WebAttValue1.SETRANGE("Key Field String 2",KeyField2);
          WebAttValue1.SETRANGE("Key Field Integer 1",KeyFieldInteger);
          IF WebAttValue1.FINDLAST THEN BEGIN
            WebAttValue."Key Field String 2" := INCSTR(WebAttValue1."Key Field String 2");
          END ELSE
            WebAttValue."Key Field String 2" := '1';
          WebAttValue."Key Field String 1" := KeyField1;
          //WebAttValue."Key Field String 2" := KeyField2;
          WebAttValue."Key Field Integer 1" := KeyFieldInteger;
        END;
        WebAttRec1."Multiple Values Exist On"::"Key Integer" :
        BEGIN
          WebAttValue1.SETRANGE("Key Field String 1",KeyField1);
          WebAttValue1.SETRANGE("Key Field String 2",KeyField2);
          //WebAttValue1.SETRANGE("Key Field Integer 1",KeyFieldInteger);
          IF WebAttValue1.FINDLAST THEN BEGIN
            WebAttValue."Key Field Integer 1" := WebAttValue1."Key Field Integer 1" + 1;
          END ELSE
            WebAttValue."Key Field Integer 1" := 1;
          WebAttValue."Key Field String 1" := KeyField1;
          WebAttValue."Key Field String 2" := KeyField2;
          //WebAttValue."Key Field Integer 1" := KeyFieldInteger;
        END;
      END;
      WebAttValue."Image URL" := WebAttRec1."Default Image URL";
      WebAttValue.INSERT(TRUE);
    END;

    PROCEDURE DeleteWebAttValuesForRecord@1190000010(InLabelC@1190000010 : Code[10];InTableID@1190000005 : Integer;InKeyString1@1190000006 : Code[50];InKeyString2@1190000007 : Code[50];InKeyInteger1@1190000008 : Integer;InUseOption@1190000011 : Boolean;InOption@1190000009 : '0,1,2,3,4,5,6');
    VAR
      TxtNoDeleteWebAttr@1000000000 : TextConst 'ENU=CU9021620 is not set to delete Web Attribute Values for Table No. %1;ENG=CU9021620 is not set to delete Web Attribute Values for Table No. %1';
    BEGIN
      //This deletes all Web Atrribute Values for this record in this table.
      //Use if the record is being deleted.
      IF InTableID = 0 THEN ERROR(Text000,'Table ID','zero');

      IF InUseOption THEN BEGIN
        CASE InTableID OF
          36,37 :
          BEGIN
            CASE InOption OF
              InOption::"0" : InKeyString1 := '0';
              InOption::"1" : InKeyString1 := '1';
              InOption::"2" : InKeyString1 := '2';
              InOption::"3" : InKeyString1 := '3';
              InOption::"4" : InKeyString1 := '4';
              InOption::"5" : InKeyString1 := '5';
            END;
          END;
        END;
      END;

      WebAttValue.RESET;
      WebAttValue1.RESET;
      IF InLabelC <> '' THEN BEGIN
        WebAttValue.SETRANGE("Label Code",InLabelC);
        WebAttValue1.SETRANGE("Label Code",InLabelC);
      END ELSE BEGIN
        WebAttValue.SETRANGE("Label Code");
        WebAttValue1.SETRANGE("Label Code");
      END;
      WebAttValue.SETRANGE("Table ID",InTableID);
      CASE InTableID OF
        13,18,27 :
        //Tables using Key Field String 1 to identify record
        BEGIN
          WebAttValue.SETRANGE("Key Field String 1",InKeyString1);
        END;
        21 :
        //Tables using Key Field Integer 1 to identify record
        BEGIN
          WebAttValue.SETRANGE("Key Field Integer 1",InKeyInteger1);
        END;
        36,110,112,114 :
        //Tables using Key Field String 1 & 2 to identify record
        //Note records 110, 112 and 114 only have document no. but these are in Key String 2
        //with Key String 1 being a 1 for 112 and 2 for 114.
        //and delete attributes for lines tables which using WebAttValue1.
        BEGIN
          WebAttValue.SETRANGE("Key Field String 1",InKeyString1);
          WebAttValue.SETRANGE("Key Field String 2",InKeyString2);
          WebAttValue1.SETRANGE("Table ID",(InTableID+1));
          WebAttValue1.SETRANGE("Key Field String 1",InKeyString1);
          WebAttValue1.SETRANGE("Key Field String 2",InKeyString2);
          IF WebAttValue1.FIND('-') THEN
          REPEAT
            WebAttValue1.DELETE(TRUE);
          UNTIL WebAttValue1.NEXT = 0;
        END;
        37:
        //Tables using all Key Field to identify record
        BEGIN
          WebAttValue.SETRANGE("Key Field String 1",InKeyString1);
          WebAttValue.SETRANGE("Key Field String 2",InKeyString2);
          WebAttValue.SETRANGE("Key Field Integer 1",InKeyInteger1);
        END;
        //DOC EC2009.2.74 -
        113,115 :
        BEGIN
          WebAttValue.SETRANGE("Key Field String 1",InKeyString1);
          WebAttValue.SETRANGE("Key Field String 2",InKeyString2);
        END;
        //DOC EC2009.2.74 +
        ELSE
          ERROR(TxtNoDeleteWebAttr,InTableID);
      END;

      //DOC EC2009.2.69 -
      WebAttValue1.RESET;
      //IF WebAttValue.FIND('-') THEN
      IF WebAttValue.FINDSET(FALSE,FALSE) THEN
      //DOC EC2009.2.69 +
      REPEAT
        WebAttValue1 := WebAttValue;  //DOC EC2009.2.69
        WebAttValue1.DELETE(TRUE);
      UNTIL WebAttValue.NEXT = 0;
    END;

    PROCEDURE ControlBoWebAttributes@1040010(TableIDIn@1040003 : Integer;KeyField1@1040004 : Code[50];KeyField2@1040005 : Code[50];KeyInt@1040006 : Integer;WebAttCode@1040000 : Code[50];BoVar@1040001 : Boolean);
    VAR
      WebAValue@1040002 : Record 9021702;
    BEGIN
      //Can call this code from e.g. Item to add web attributes - used by Biocare

      IF (KeyField1 = '') AND
         (KeyField2 = '') AND
         (KeyInt = 0) THEN
        EXIT;
      LabelRec.RESET;
      LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
      LabelRec.FINDFIRST;
      IF BoVar THEN BEGIN
        IF NOT WebAValue.GET(LabelRec.Code,TableIDIn,WebAttCode,KeyField1,KeyField2,KeyInt) THEN BEGIN
          WebAValue.INIT;
          WebAValue."Label Code" := LabelRec.Code;
          WebAValue."Table ID" := TableIDIn;
          WebAValue.VALIDATE("Attribute Code",WebAttCode);  //DOC EC2013.3.20 changed to Validate
          WebAValue."Key Field String 1" := KeyField1;
          WebAValue."Key Field String 2" := KeyField2;
          WebAValue."Key Field Integer 1" := KeyInt;
          WebAValue.INSERT(TRUE);
        END;
        WebAValue.VALIDATE(BooleanVar,BoVar);
        WebAValue.MODIFY;
      END ELSE BEGIN
        IF WebAValue.GET(LabelRec.Code,TableIDIn,WebAttCode,KeyField1,KeyField2,KeyInt) THEN
          WebAValue.DELETE;
      END;
    END;

    PROCEDURE ControlCodeWebAttributes@1040013(TableIDIn@1040004 : Integer;KeyField1@1040005 : Code[50];KeyField2@1040006 : Code[50];KeyInt@1040007 : Integer;GlobalCode@1040003 : Code[20];WebAttCode@1040000 : Code[50];InCodeVar@1040001 : Code[20]);
    VAR
      WebAValue@1040002 : Record 9021702;
      TxtWebAttrDoesNotExist@1000000000 : TextConst 'ENU=The Web Attribute Code %1 does not exist.;ENG=The Web Attribute Code %1 does not exist.';
    BEGIN
      //Can call this code from e.g. Item to add web attributes - used by Biocare
      //GM120425 only use for Biocare - this Validates BooleanVar at end of function
      //which seems wrong.

      IF (KeyField1 = '') AND
         (KeyField2 = '') AND
         (KeyInt = 0) THEN
        EXIT;
      LabelRec.RESET;
      LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
      LabelRec.FINDFIRST;

      //For Cap Size, InCodeVar is Cap Size Code
      IF InCodeVar = '' THEN BEGIN
          WebAttValue.RESET;
          WebAttValue.SETRANGE("Label Code",LabelRec.Code);
          WebAttValue.SETRANGE("Table ID",TableIDIn);
          WebAttValue.SETRANGE("Key Field String 1",KeyField1);
          WebAttValue.SETRANGE("Key Field String 2",KeyField2);
          WebAttValue.SETRANGE("Key Field Integer 1",KeyInt);
          WebAttValue.SETRANGE("Global Code",GlobalCode);
          WebAttValue.SETCURRENTKEY("Label Code","Table ID","Key Field String 1",
                                       "Key Field String 2","Key Field Integer 1","Global Code");
          IF WebAttValue.FINDFIRST THEN
            WebAttValue.DELETE;
      END ELSE BEGIN
        IF NOT WebAttRec.GET(LabelRec.Code,TableIDIn,WebAttCode) THEN
          ERROR(TxtWebAttrDoesNotExist,WebAttCode);
        WebAttRec.TESTFIELD("Default Image URL");
        WebAttValue.RESET;
        WebAttValue.SETRANGE("Label Code",LabelRec.Code);
        WebAttValue.SETRANGE("Table ID",TableIDIn);
        WebAttValue.SETRANGE("Key Field String 1",KeyField1);
        WebAttValue.SETRANGE("Key Field String 2",KeyField2);
        WebAttValue.SETRANGE("Key Field Integer 1",KeyInt);
        WebAttValue.SETRANGE("Global Code",GlobalCode);
        WebAttValue.SETCURRENTKEY("Label Code","Table ID","Key Field String 1",
                                     "Key Field String 2","Key Field Integer 1","Global Code");
        IF WebAttValue.FINDFIRST THEN
          WebAttValue.DELETE;
        WebAValue.INIT;
        WebAValue."Label Code" := LabelRec.Code;
        WebAValue."Table ID" := TableIDIn;
        WebAValue.VALIDATE("Attribute Code",WebAttCode);  //DOC EC2013.3.20 changed to Validate
        WebAValue."Key Field String 1" := KeyField1;
        WebAValue."Key Field String 2" := KeyField2;
        WebAValue."Key Field Integer 1" := KeyInt;
        WebAValue.INSERT(TRUE);
        WebAValue."Image URL" := WebAttRec."Default Image URL";
        WebAValue.VALIDATE(BooleanVar,TRUE);
        WebAValue.MODIFY;
      END;
    END;

    PROCEDURE ControlTextWebAttributes@1040011(TableIDIn@1040003 : Integer;KeyField1@1040004 : Code[50];KeyField2@1040005 : Code[50];KeyInt@1040006 : Integer;WebAttCode@1040000 : Code[50];TextVar@1040001 : Text[250]);
    VAR
      WebAValue@1040002 : Record 9021702;
    BEGIN
      //Can call this code from e.g. Item to add web attributes - used by Biocare
      IF (KeyField1 = '') AND
         (KeyField2 = '') AND
         (KeyInt = 0) THEN
        EXIT;
      LabelRec.RESET;
      LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
      LabelRec.FINDFIRST;
      IF TextVar = '' THEN BEGIN
        IF WebAValue.GET(LabelRec.Code,TableIDIn,WebAttCode,KeyField1,KeyField2,KeyInt) THEN
          WebAValue.DELETE;
      END ELSE BEGIN
        IF NOT WebAValue.GET(LabelRec.Code,TableIDIn,WebAttCode,KeyField1,KeyField2,KeyInt) THEN BEGIN
          WebAValue.INIT;
          WebAValue."Label Code" := LabelRec.Code;
          WebAValue."Table ID" := TableIDIn;
          WebAValue.VALIDATE("Attribute Code",WebAttCode);  //DOC EC2013.3.20 changed to Validate
          WebAValue."Key Field String 1" := KeyField1;
          WebAValue."Key Field String 2" := KeyField2;
          WebAValue."Key Field Integer 1" := KeyInt;
          WebAValue.INSERT(TRUE);
        END;
        WebAValue.VALIDATE(TextVar,TextVar);
        WebAValue.MODIFY;
      END;
    END;

    PROCEDURE ControlWebAttributeValue@1000000010(LabelCode@1000000000 : Code[10];TableId@1040004 : Integer;KeyField1@1040005 : Code[50];KeyField2@1040006 : Code[50];KeyInt@1040007 : Integer;GlobalCode@1040003 : Code[20];WebAttCode@1040000 : Code[50];AttributeValue@1040001 : Code[50];DeleteBlankValues@1000000001 : Boolean);
    VAR
      DoDeleteValue@1000000002 : Boolean;
      WebAValue@1040002 : Record 9021702;
    BEGIN
      //Call this code to insert/delete (if blank) web attribute values...
      //Do not use for attribute codes allowing multiple values...

      //exit if all key fields are blank...
      IF (KeyField1 = '') AND
         (KeyField2 = '') AND
         (KeyInt = 0) THEN
        EXIT;

      IF LabelCode = '' THEN BEGIN
        //find the default Label...
        LabelRec.RESET;
        LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
        LabelRec.SETRANGE("Default Label in Company",TRUE);
        LabelRec.FINDFIRST;
        LabelCode := LabelRec.Code;
      END;

      IF NOT WebAttRec.GET(LabelCode,TableId,WebAttCode) THEN BEGIN
        //the attribute is not set up...
        ERROR(Text003,WebAttRec.TABLENAME,WebAttRec.FIELDCAPTION(Code),WebAttCode);
      END;

      IF DeleteBlankValues THEN BEGIN
        //error if contrary to attribute code setup...
        IF WebAttRec."Values Must be Listed" = TRUE THEN
          ERROR(Text005,WebAttRec.FIELDCAPTION("Values Must be Listed"),WebAttRec.TABLECAPTION,WebAttCode);

        //check whether new values is blank according to data type setup...
        CASE WebAttRec."Data Type" OF
          //Integer,Text,Code,Decimal,Boolean,Date,Time,Any,Text+Dec
          WebAttRec."Data Type"::Integer : IF AttributeValue = '0' THEN DoDeleteValue := TRUE;
          WebAttRec."Data Type"::Text : IF AttributeValue = '' THEN DoDeleteValue := TRUE;
          WebAttRec."Data Type"::Code : IF AttributeValue = '' THEN DoDeleteValue := TRUE;
          WebAttRec."Data Type"::Decimal : IF AttributeValue = '0' THEN DoDeleteValue := TRUE;
          //DOC EC2013.3.20 wrong no empty Boolean value, so Booleans never assumed blank, so never deleted here.
          //DOC EC2013.3.20 we do want to delete Booleans if AttributeValue is NO
          WebAttRec."Data Type"::Boolean :  IF AttributeValue = 'NO' THEN DoDeleteValue := TRUE; //DOC EC2013.3.20
          WebAttRec."Data Type"::Date : IF AttributeValue = '0D' THEN DoDeleteValue := TRUE;
          WebAttRec."Data Type"::Time : IF AttributeValue = '0T' THEN DoDeleteValue := TRUE;
          ELSE
            BEGIN
              IF AttributeValue = '' THEN DoDeleteValue := TRUE;
            END;
        END;
      END;

      //if new value is blank (and no value is required), delete attribute value record...
      IF DoDeleteValue THEN BEGIN
        WebAttValue.RESET;
        WebAttValue.SETRANGE("Label Code",LabelCode);
        WebAttValue.SETRANGE("Table ID",TableId);
        WebAttValue.SETRANGE("Key Field String 1",KeyField1);
        WebAttValue.SETRANGE("Key Field String 2",KeyField2);
        WebAttValue.SETRANGE("Key Field Integer 1",KeyInt);
        WebAttValue.SETRANGE("Global Code",GlobalCode);
        WebAttValue.SETCURRENTKEY("Label Code","Table ID","Key Field String 1",
                                     "Key Field String 2","Key Field Integer 1","Global Code");
        IF WebAttValue.FINDFIRST THEN
          WebAttValue.DELETE;
      END ELSE BEGIN

        //find any existing value...
        WebAttValue.RESET;
        WebAttValue.SETRANGE("Label Code",LabelCode);
        WebAttValue.SETRANGE("Table ID",TableId);
        WebAttValue.SETRANGE("Key Field String 1",KeyField1);
        WebAttValue.SETRANGE("Key Field String 2",KeyField2);
        WebAttValue.SETRANGE("Key Field Integer 1",KeyInt);
        WebAttValue.SETRANGE("Global Code",GlobalCode);
        WebAttValue.SETRANGE("Attribute Code",WebAttCode);
        WebAttValue.SETCURRENTKEY("Label Code","Table ID","Key Field String 1",
                                     "Key Field String 2","Key Field Integer 1","Global Code");

          //delete existing values...
        IF WebAttValue.FINDFIRST THEN
          WebAttValue.DELETE;

        //insert new value...
        WebAValue.INIT;
        WebAValue."Label Code" := LabelCode;
        WebAValue."Table ID" := TableId;
        WebAValue.VALIDATE("Attribute Code",WebAttCode);  //DOC EC2013.3.20 changed to Validate
        WebAValue."Key Field String 1" := KeyField1;
        WebAValue."Key Field String 2" := KeyField2;
        WebAValue."Key Field Integer 1" := KeyInt;
        WebAValue.INSERT(TRUE);
        WebAValue."Image URL" := WebAttRec."Default Image URL";
        WebAValue.VALIDATE(Value,AttributeValue);
        WebAValue.MODIFY;
      END;
    END;

    PROCEDURE ContMultiValueWebAttributes@1000000002(InputKeyField1@1000000003 : Code[50];InputKeyField2@1000000004 : Code[50];InputKeyInteger@1000000005 : Integer;InputTableID@1000000006 : Integer;InputLabelCode@1000000000 : Code[10];InputWebAttCode@1000000001 : Code[50];InputIntegerVar@1000000009 : Integer;InputTextVar@1000000010 : Text[250];InputCodeVar@1000000002 : Code[30];InputDecimalVar@1000000011 : Decimal;InputBooleanVar@1000000007 : Boolean;xRecIntegerVar@1190000005 : Integer;xRecTextVar@1190000006 : Text[250];xRecCodeVar@1190000007 : Code[30];xRecDecimalVar@1190000008 : Decimal;xRecBooleanVar@1190000009 : Boolean);
    BEGIN
      //Copy of ControlMultiValueWebAttributes but with date taken out as when erroring with NAV 2009
      //ControlMultiValueWebAttributes(KF1,KF2,KInt,Table,Label,Webattcode,Int,Text,Code,Dec,Boo,
      //                               PrevInt,PrevText,PrevCode,PrevDec,PrevBoo);

      KeyField1 := InputKeyField1;
      KeyField2 := InputKeyField2;
      KeyFieldInteger := InputKeyInteger;
      TableID := InputTableID;
      LabelCode := InputLabelCode;
      WebAttCode := InputWebAttCode;   //DOC EC2009.2.69 changed to Code 50
      IntegerVar := InputIntegerVar;
      TextVar := InputTextVar;
      CodeVar := InputCodeVar;
      DecimalVar := InputDecimalVar;
      BooleanVar := InputBooleanVar;

      PrevIntegerVar := xRecIntegerVar;
      PrevTextVar := xRecTextVar;
      PrevCodeVar := xRecCodeVar;
      PrevDecimalVar := xRecDecimalVar;
      PrevBooleanVar := xRecBooleanVar;


      IF LabelCode = '' THEN ERROR(Text000,TxtLabelCode,TxtBlank);
      IF TableID = 0 THEN ERROR(Text000,TxtTableId,TxtZero);
      IF WebAttCode = '' THEN ERROR(Text000,TxtWebAttributeCode,TxtBlank);

      DeleteAttribute := FALSE;
      WebAttRec.GET(LabelCode,TableID,WebAttCode);
      IF WebAttRec."Multiple Values Exist On" = WebAttRec."Multiple Values Exist On"::"No Multiple Values" THEN
        ERROR(Text004);

      CASE WebAttRec."Data Type" OF
        WebAttRec."Data Type"::Integer :
        BEGIN
          IF IntegerVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Text :
        BEGIN
          IF TextVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Code :
        BEGIN
          IF CodeVar = '' THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Decimal :
        BEGIN
          IF DecimalVar = 0 THEN DeleteAttribute := TRUE;
        END;
        WebAttRec."Data Type"::Boolean :
        BEGIN
          IF BooleanVar = FALSE THEN DeleteAttribute := TRUE;
        END;
        ELSE
          ERROR(Text001,WebAttRec."Data Type");
      END;

      IF WebAttRec."Apply to All Labels" THEN BEGIN
        LabelRec.RESET;
        LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
        IF LabelRec.FIND('-') THEN
        REPEAT
          IF WebAttRec1.GET(LabelRec.Code,TableID,WebAttCode) THEN
            IF WebAttRec1."Apply to All Labels" THEN
              PopulateMultiValueWebValues(LabelRec.Code);
        UNTIL LabelRec.NEXT = 0;
      END ELSE BEGIN
        WebAttRec1 := WebAttRec;
        PopulateMultiValueWebValues(LabelCode);
      END;
    END;

    PROCEDURE ControlWebAttsWithGlobal@1000000003(InputKeyField1@1000000015 : Code[50];InputKeyField2@1000000014 : Code[50];InputKeyInteger@1000000013 : Integer;InputTableID@1000000012 : Integer;InputLabelCode@1000000011 : Code[10];InputWebAttCode@1000000006 : Code[50];InputIntegerVar@1000000005 : Integer;InputTextVar@1000000004 : Text[250];InputCodeVar@1000000003 : Code[30];InputDecimalVar@1000000002 : Decimal;InputBooleanVar@1000000001 : Boolean;InputGlobal@1000000010 : Code[20]);
    BEGIN
      //DOC EC2013.4.63 -
      //this function assumes that the web attribute code is the same as field value from where this function is called
      //you can also pass the previous field value and this function will delete the corresponding web attribute value
      //used in Lifemarque

      KeyField1 := InputKeyField1;
      KeyField2 := InputKeyField2;
      KeyFieldInteger := InputKeyInteger;
      TableID := InputTableID;
      LabelCode := InputLabelCode;
      WebAttCode := InputWebAttCode;
      IntegerVar := InputIntegerVar;
      TextVar := InputTextVar;
      CodeVar := InputCodeVar;
      DecimalVar := InputDecimalVar;
      BooleanVar := InputBooleanVar;

      //exit if all key fields are blank...
      IF (KeyField1 = '') AND
         (KeyField2 = '') AND
         (KeyFieldInteger = 0) THEN
        EXIT;

      IF TableID = 0 THEN ERROR(Text000,TxtTableId,TxtZero);

      IF LabelCode = '' THEN BEGIN
        //find the default Label...
        LabelRec.RESET;
        LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
        LabelRec.SETRANGE("Default Label in Company",TRUE);
        LabelRec.FINDFIRST;
        LabelCode := LabelRec.Code;
      END;

      WebAttValue.RESET;
      WebAttValue.SETCURRENTKEY("Label Code","Table ID","Key Field String 1","Key Field String 2","Key Field Integer 1","Global Code");
      WebAttValue.SETRANGE("Label Code",LabelCode);
      WebAttValue.SETRANGE("Table ID",TableID);
      WebAttValue.SETRANGE("Key Field String 1",KeyField1);
      WebAttValue.SETRANGE("Key Field String 2",KeyField2);
      WebAttValue.SETRANGE("Key Field Integer 1",KeyFieldInteger);
      WebAttValue.SETRANGE("Global Code",InputGlobal);
      WebAttValue.DELETEALL;

      IF WebAttCode = '' THEN BEGIN
        EXIT;
      END ELSE BEGIN
        //if cannot retrieve the web attribute rec then it is not relevant (i.e. that web attribute is not used)
        IF WebAttRec.GET(LabelCode,TableID,WebAttCode) THEN BEGIN
          IF WebAttRec."Global Code" <> InputGlobal THEN
            //DOC EC2015.5.02 ERROR(Text009,WebAttRec."Global Code",WebAttCode,InputGlobal);
            EXIT; //DOC EC2015.5.02 there could be Web Attributes for other Globals which are valid so just ignore
        END ELSE
          EXIT;
      END;

      //MQ 22/05/2014 - GW140522 -
      ControlWebAttributes(KeyField1,KeyField2,KeyFieldInteger,TableID,LabelCode,WebAttCode,IntegerVar,TextVar,CodeVar,
                           DecimalVar,BooleanVar,DateVar,0T);
      //DOC EC2013.4.63 +
    END;

    BEGIN
    {
      //DOC EC2009.2    GW 29/09/2010 - Was Attribute n Questionnaire Mgt 20/07/10 15:17 CU 9021620
      //DOC EC2009.2    MA 20/01/2011 - Added function ControlWebAttributeValue for simple attribute value management
      //DOC EC2009.2    MQ 26/01/2011 - GW110126 Code corrections
      //DOC EC2009.2.69 MQ 10/09/2011 - GW110910 Increase field lengths to reflect field lengths on tables
      //DOC EC2009.2.69 MQ 29/09/2011 - GW110929 Use FINDSET as much faster.
      //DOC EC2009.2.74 MQ 01/01/2012 - Handle delete of web attributes from tables 113 and 115 as well.
      //DOC EC2013.3.20 MQ 29/10/2012 - GW121029 called Validate instead of assign with Attribute Code to trigger code moved from OnInsert to OnValidate in Web Atttribute Value table.
      //DOC EC2013.3.20 MQ 06/11/2012 - GW121106 Delete Boolean if value is NO
      //DOC EC2013.3.70 MQ 12/07/2013 - GW130712 ControlMultiValueWebAttributes copied to ContMultiValueWebAttributes with less parameters cos too many for some versions of NAV (2009 R2)
      //DOC EC2013.4.63 SB 22/05/2014 - SB140522 New function ControlWebAttsWithGlobals (originally called ControlWebValueAttributes) used only when attribute code are same as field values
      //DOC EC2013.4.63 MQ 23/05/2014 - GW140523 Enhanced. Stopped Multivalues attributes when the variable is Boolean as it does not make sense to have multiple TRUEs as it is either TRUE or does not exit.
      //DOC EC2015.5.02 MQ 19/02/2015 - GW150219 Enhanced ControlWebAttsWith Globals
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

