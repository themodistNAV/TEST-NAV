OBJECT Page 593 Change Log Setup (Table) List
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Change Log Setup (Table) List;
               ENG=Change Log Setup (Table) List];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000058;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 FILTERGROUP(2);
                 SETRANGE("Object Type","Object Type"::Table);
                 SETRANGE("Object ID",0,2000000000);
                 FILTERGROUP(0);
               END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                CaptionML=[ENU=ID;
                           ENG=ID];
                SourceExpr="Object ID";
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                CaptionML=[ENU=Name;
                           ENG=Name];
                SourceExpr="Object Caption";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=LogInsertion;
                CaptionML=[ENU=Log Insertion;
                           ENG=Log Insertion];
                OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                 ENG=" ,Some Fields,All Fields"];
                SourceExpr=ChangeLogSetupTable."Log Insertion";
                OnValidate=VAR
                             NewValue@1001 : Option;
                           BEGIN
                             IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                               BEGIN
                               NewValue := ChangeLogSetupTable."Log Insertion";
                               GetRec;
                               ChangeLogSetupTable."Log Insertion" := NewValue;
                             END;

                             IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                               BEGIN
                               IF (xChangeLogSetupTable."Log Insertion" = xChangeLogSetupTable."Log Insertion"::"Some Fields") AND
                                  (xChangeLogSetupTable."Log Insertion" <> ChangeLogSetupTable."Log Insertion")
                               THEN
                                 IF CONFIRM(
                                      STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Insertion"),xChangeLogSetupTable."Log Insertion"),FALSE)
                                 THEN
                                   ChangeLogSetupTable.DelChangeLogFields(0);
                             END;
                             ChangeLogSetupTableLogInsertio;
                           END;

                OnAssistEdit=BEGIN
                               WITH ChangeLogSetupTable DO
                                 TESTFIELD("Log Insertion","Log Insertion"::"Some Fields");
                               AssistEdit;
                             END;
                              }

    { 8   ;2   ;Field     ;
                Name=LogModification;
                CaptionML=[ENU=Log Modification;
                           ENG=Log Modification];
                OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                 ENG=" ,Some Fields,All Fields"];
                SourceExpr=ChangeLogSetupTable."Log Modification";
                OnValidate=VAR
                             NewValue@1001 : Option;
                           BEGIN
                             IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                               BEGIN
                               NewValue := ChangeLogSetupTable."Log Modification";
                               GetRec;
                               ChangeLogSetupTable."Log Modification" := NewValue;
                             END;

                             IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                               BEGIN
                               IF (xChangeLogSetupTable."Log Modification" = xChangeLogSetupTable."Log Modification"::"Some Fields") AND
                                  (xChangeLogSetupTable."Log Modification" <> ChangeLogSetupTable."Log Modification")
                               THEN
                                 IF CONFIRM(
                                      STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Modification"),xChangeLogSetupTable."Log Modification"),FALSE)
                                 THEN
                                   ChangeLogSetupTable.DelChangeLogFields(1);
                             END;
                             ChangeLogSetupTableLogModifica;
                           END;

                OnAssistEdit=BEGIN
                               WITH ChangeLogSetupTable DO
                                 TESTFIELD("Log Modification","Log Modification"::"Some Fields");
                               AssistEdit;
                             END;
                              }

    { 10  ;2   ;Field     ;
                Name=LogDeletion;
                CaptionML=[ENU=Log Deletion;
                           ENG=Log Deletion];
                OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                 ENG=" ,Some Fields,All Fields"];
                SourceExpr=ChangeLogSetupTable."Log Deletion";
                OnValidate=VAR
                             NewValue@1001 : Option;
                           BEGIN
                             IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                               BEGIN
                               NewValue := ChangeLogSetupTable."Log Deletion";
                               GetRec;
                               ChangeLogSetupTable."Log Deletion" := NewValue;
                             END;

                             IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                               BEGIN
                               IF (xChangeLogSetupTable."Log Deletion" = xChangeLogSetupTable."Log Deletion"::"Some Fields") AND
                                  (xChangeLogSetupTable."Log Deletion" <> ChangeLogSetupTable."Log Deletion")
                               THEN
                                 IF CONFIRM(
                                      STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Deletion"),xChangeLogSetupTable."Log Deletion"),FALSE)
                                 THEN
                                   ChangeLogSetupTable.DelChangeLogFields(2);
                             END;
                             ChangeLogSetupTableLogDeletion;
                           END;

                OnAssistEdit=BEGIN
                               WITH ChangeLogSetupTable DO
                                 TESTFIELD("Log Deletion","Log Deletion"::"Some Fields");
                               AssistEdit;
                             END;
                              }

  }
  CODE
  {
    VAR
      ChangeLogSetupTable@1000 : Record 403;
      xChangeLogSetupTable@1002 : Record 403;
      Text002@1003 : TextConst 'ENU=You have changed the %1 field to no longer be %2. Do you want to remove the field selections?;ENG=You have changed the %1 field to no longer be %2. Do you want to remove the field selections?';

    LOCAL PROCEDURE AssistEdit@1();
    VAR
      Field@1001 : Record 2000000041;
      ChangeLogSetupFieldList@1002 : Page 594;
    BEGIN
      Field.FILTERGROUP(2);
      Field.SETRANGE(TableNo,"Object ID");
      Field.FILTERGROUP(0);
      WITH ChangeLogSetupTable DO
        ChangeLogSetupFieldList.SelectColumn(
          "Log Insertion" = "Log Insertion"::"Some Fields",
          "Log Modification" = "Log Modification"::"Some Fields",
          "Log Deletion" = "Log Deletion"::"Some Fields");
      ChangeLogSetupFieldList.SETTABLEVIEW(Field);
      ChangeLogSetupFieldList.RUN;
    END;

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      WITH ChangeLogSetupTable DO
        IF ("Log Insertion" = "Log Insertion"::" ") AND ("Log Modification" = "Log Modification"::" ") AND
           ("Log Deletion" = "Log Deletion"::" ")
        THEN BEGIN
          IF DELETE THEN;
        END ELSE
          IF NOT MODIFY THEN
            INSERT;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT ChangeLogSetupTable.GET("Object ID") THEN BEGIN
        ChangeLogSetupTable.INIT;
        ChangeLogSetupTable."Table No." := "Object ID";
      END;
    END;

    PROCEDURE SetSource@4();
    VAR
      AllObjWithCaption@1001 : Record 2000000058;
    BEGIN
      DELETEALL;

      AllObjWithCaption.SETCURRENTKEY("Object Type","Object ID");
      AllObjWithCaption.SETRANGE("Object Type","Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID",0,2000000006);

      IF AllObjWithCaption.FIND('-') THEN
        REPEAT
          Rec := AllObjWithCaption;
          INSERT;
        UNTIL AllObjWithCaption.NEXT = 0;
    END;

    LOCAL PROCEDURE ChangeLogSetupTableLogInsertio@19011102();
    BEGIN
      UpdateRec;
    END;

    LOCAL PROCEDURE ChangeLogSetupTableLogModifica@19025218();
    BEGIN
      UpdateRec;
    END;

    LOCAL PROCEDURE ChangeLogSetupTableLogDeletion@19043090();
    BEGIN
      UpdateRec;
    END;

    BEGIN
    END.
  }
}

