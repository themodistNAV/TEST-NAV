OBJECT Page 11102110 OM - Renumber Elements
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Renumber Elements;
               ENU=Renumber Elements;
               ESP=Renumber Elements;
               FRA=Renumber Elements;
               NLD=Elementen omnummeren;
               DEA=Renumber Elements];
    SaveValues=Yes;
    SourceTable=Table11102076;
    DelayedInsert=Yes;
    SourceTableView=SORTING(Entry No.);
    PageType=Worksheet;
    OnOpenPage=VAR
                 Integer@1000 : Record 2000000026;
                 WhereUsedStatus@1001 : Codeunit 11102047;
                 FileHandler@1002 : Codeunit 11102097;
               BEGIN
                 SetEnabled;

                 IF FormInitialized = FormInitialized::" " THEN BEGIN
                   RunType := RunType::"Update in DB";
                   FormInitialized := FormInitialized::Yes;
                 END;

                 IF FormInitialized <> FormInitialized::"Id changed" THEN BEGIN

                   StartFunctionsAt := Integer.Number;
                   StartVariablesAt := Integer.Number;
                   StartControlsAt := Integer.Number;

                   IF StartFunctionsAt < 1 THEN
                     StartFunctionsAt := 1;

                   IF StartVariablesAt < 1000 THEN
                     StartVariablesAt := 1000;

                   IF StartControlsAt < 1 THEN
                     StartControlsAt := 1;
                 END;

                 UpdateReferencingObjects := TRUE;
                 SetModifyFlag := TRUE;

                 WebClient := FileHandler.IsWebClient;
                 IF WebClient THEN
                   SourceFileName := '';

                 WhereUsedStatus.WhereUsedUpToDate;

                 SetEnabled;

                 RefreshNoOfInvolvedObjects := TRUE;
               END;

    OnAfterGetRecord=BEGIN
                       IF "Object No." = 0 THEN
                         ObjectNo := ''
                       ELSE
                         ObjectNo := FORMAT("Object No.");

                       SetLineStyle;
                     END;

    OnNewRecord=BEGIN
                  "Object Type" := xRec."Object Type";

                  ObjectNo := '';
                END;

    OnInsertRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    OnModifyRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IF "No." <> 0 THEN
                             Delta := "New Number" - "No.";

                           IF RefreshNoOfInvolvedObjects THEN BEGIN
                             NoOfInvolvedObjects := CalcNoOfInvolvedObjects;
                             RefreshNoOfInvolvedObjects := FALSE;
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unctions;
                                 ENU=F&unctions;
                                 ESP=F&unctions;
                                 FRA=F&unctions;
                                 NLD=F&uncties;
                                 DEA=F&unctions] }
      { 3       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+G;
                      Ellipsis=Yes;
                      CaptionML=[DEU=&Add Elements;
                                 ENU=&Add Elements;
                                 ESP=&Add Elements;
                                 FRA=&Add Elements;
                                 NLD=Elementen &toevoegen;
                                 DEA=&Add Elements];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AddRenumberElement@1000 : Report 11102047;
                               BEGIN
                                 IF KeepDeltaEqual THEN
                                   AddRenumberElement.SetDelta(Delta);

                                 AddRenumberElement.RUNMODAL;
                                 IF FINDFIRST THEN
                                   ;
                                 CurrPage.UPDATE(FALSE);

                                 RefreshNoOfInvolvedObjects := TRUE;
                               END;
                                }
      { 4       ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add Elements from &Source File;
                                 ENU=Add Elements from &Source File;
                                 ESP=Add Elements from &Source File;
                                 FRA=Add Elements from &Source File;
                                 NLD=Elementen toevoegen vanuit &bronbestand;
                                 DEA=Add Elements from &Source File];
                      Image=Add;
                      OnAction=VAR
                                 TmpCALHistoryObject@1000 : TEMPORARY Record 11102052;
                                 TmpCALHistoryLine@1001 : TEMPORARY Record 11102053;
                                 Object@1002 : Record 2000000001;
                                 ImExportMgt@1003 : Codeunit 11102043;
                                 Progressbar@1004 : Codeunit 11102044;
                                 SourceControl@1005 : Codeunit 11102050;
                                 RenumberElements@1006 : Codeunit 11102074;
                                 FileHandler@1007 : Codeunit 11102097;
                                 ServerFileName@1008 : Text;
                                 ctDefaultFileName@1009 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctReadingElements@1010 : TextConst 'DEU=Reading elements...;ENU=Reading elements...;ESP=Reading elements...;FRA=Reading elements...;NLD=Elementen lezen...;DEA=Reading elements...';
                               BEGIN
                                 ServerFileName := FileHandler.UploadToServer(SourceFileName, ctDefaultFileName);

                                 Progressbar.Open(ctReadingElements, 1);
                                 Progressbar.BarSize := ImExportMgt.LengthOfFile(ServerFileName);

                                 SourceControl.ImportTextFile2(ServerFileName, 1, Progressbar, 0, TmpCALHistoryObject, TmpCALHistoryLine, TRUE);

                                 Progressbar.Close;

                                 TmpCALHistoryObject.SETFILTER("Object Type", '<>%1', TmpCALHistoryObject."Object Type"::MenuSuite);
                                 IF TmpCALHistoryObject.FINDSET THEN
                                   REPEAT
                                     Object.Type := TmpCALHistoryObject."Object Type";
                                     Object.ID := TmpCALHistoryObject."Object No.";

                                     TmpCALHistoryLine.SETRANGE("Entry No.", TmpCALHistoryObject."Entry No.");
                                     RenumberElements.AddElements(Object, TmpCALHistoryLine, TRUE, TRUE, TRUE, Delta);

                                   UNTIL TmpCALHistoryObject.NEXT = 0;

                                 IF FINDFIRST THEN
                                   ;
                               END;
                                }
      { 5       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=[DEU=Update Where Used;
                                 ENU=Update Where Used;
                                 ESP=Update Where Used;
                                 FRA=Update Where Used;
                                 NLD=Waar-gebruikt bijwerken;
                                 DEA=Update Where Used];
                      Image=Refresh;
                      OnAction=VAR
                                 WhereUsedMgt@1000 : Codeunit 11102045;
                               BEGIN
                                 WhereUsedMgt.UpdateObjects;
                               END;
                                }
      { 6       ;2   ;Separator  }
      { 7       ;2   ;Action    ;
                      CaptionML=[DEU=Fill New &Numbers;
                                 ENU=Fill New &Numbers;
                                 ESP=Fill New &Numbers;
                                 FRA=Fill New &Numbers;
                                 NLD=Nieuwe nummers vullen;
                                 DEA=Fill New &Numbers];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Calculate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ElementToRenumber@1000 : Record 11102076;
                                 ElementToRenumber2@1001 : Record 11102076;
                                 ObjectsSelected@1002 : Boolean;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ElementToRenumber);
                                 IF ElementToRenumber.COUNT <= 1 THEN
                                   ElementToRenumber.RESET
                                 ELSE
                                   ObjectsSelected := TRUE;

                                 IF ObjectsSelected THEN
                                   IF ElementToRenumber.FINDSET THEN
                                     REPEAT
                                       ElementToRenumber.VALIDATE("New Number", -1);
                                       ElementToRenumber.MODIFY;
                                     UNTIL ElementToRenumber.NEXT = 0;

                                 IF ElementToRenumber.FINDSET THEN
                                   REPEAT

                                     IF ObjectsSelected THEN BEGIN

                                       CASE ElementToRenumber.Type OF
                                         ElementToRenumber.Type::"Function" :
                                           ElementToRenumber.VALIDATE("New Number", StartFunctionsAt);
                                         ElementToRenumber.Type::Variable :
                                           ElementToRenumber.VALIDATE("New Number", StartVariablesAt);
                                         ElementToRenumber.Type::Control :
                                           ElementToRenumber.VALIDATE("New Number", StartControlsAt);
                                       END;

                                       ElementToRenumber2.SETCURRENTKEY("Object Type", "Object No.", "Parent Name", Type, "New Number");
                                       ElementToRenumber2.SETRANGE("Object Type", ElementToRenumber."Object Type");
                                       ElementToRenumber2.SETRANGE("Object No.", ElementToRenumber."Object No.");
                                       ElementToRenumber2.SETRANGE("Parent Name", ElementToRenumber."Parent Name");
                                       ElementToRenumber2.SETRANGE(Type, ElementToRenumber.Type);
                                       ElementToRenumber2.SETRANGE("New Number", ElementToRenumber."New Number");
                                       WHILE NOT ElementToRenumber2.ISEMPTY DO BEGIN
                                         ElementToRenumber.VALIDATE("New Number", ElementToRenumber."New Number" + 1);
                                         ElementToRenumber2.SETRANGE("New Number", ElementToRenumber."New Number");
                                       END;
                                       ElementToRenumber.MODIFY;

                                     END ELSE BEGIN
                                       ElementToRenumber2.SETCURRENTKEY("Entry No.");
                                       ElementToRenumber2.SETRANGE("Object Type", ElementToRenumber."Object Type");
                                       ElementToRenumber2.SETRANGE("Object No.", ElementToRenumber."Object No.");
                                       ElementToRenumber2.SETRANGE("Parent Name", ElementToRenumber."Parent Name");
                                       ElementToRenumber2.SETRANGE(Type, ElementToRenumber.Type);
                                       ElementToRenumber2.SETFILTER("Entry No.", '<%1', ElementToRenumber."Entry No.");
                                       IF ElementToRenumber2.FINDLAST THEN BEGIN
                                         ElementToRenumber.VALIDATE("New Number", ElementToRenumber2."New Number" + 1);
                                         ElementToRenumber.MODIFY;
                                       END ELSE BEGIN
                                         CASE ElementToRenumber.Type OF
                                           ElementToRenumber.Type::"Function" :
                                             ElementToRenumber.VALIDATE("New Number", StartFunctionsAt);
                                           ElementToRenumber.Type::Variable :
                                             ElementToRenumber.VALIDATE("New Number", StartVariablesAt);
                                           ElementToRenumber.Type::Control :
                                             ElementToRenumber.VALIDATE("New Number", StartControlsAt);
                                         END;
                                         ElementToRenumber.MODIFY;
                                       END;
                                     END;
                                   UNTIL ElementToRenumber.NEXT = 0;

                                 CalcNoOfInvolvedObjects;
                               END;
                                }
      { 8       ;2   ;Separator  }
      { 9       ;2   ;Action    ;
                      ShortCutKey=Ctrl+O;
                      CaptionML=[DEU=Open File;
                                 ENU=Open File;
                                 ESP=Open File;
                                 FRA=Open File;
                                 NLD=Open bestand;
                                 DEA=Open File];
                      Image=Import;
                      OnAction=BEGIN
                                 PickFile;
                               END;
                                }
      { 10      ;2   ;ActionGroup;
                      CaptionML=[DEU=Mar&k Objects;
                                 ENU=Mar&k Objects;
                                 ESP=Mar&k Objects;
                                 FRA=Mar&k Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mar&k Objects];
                      Image=EditFilter }
      { 11      ;3   ;Action    ;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter;
                      OnAction=VAR
                                 ElementToRenumber@1000 : Record 11102076;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.CheckMarkLock;

                                 CurrPage.SETSELECTIONFILTER(ElementToRenumber);
                                 IF ElementToRenumber.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(ElementToRenumber."Object Type", '', ElementToRenumber."Object No.") THEN
                                       VersionControl.MarkObject(Object, TRUE);
                                   UNTIL ElementToRenumber.NEXT = 0;
                               END;
                                }
      { 12      ;3   ;Action    ;
                      CaptionML=[DEU=Remove Marks;
                                 ENU=Remove Marks;
                                 ESP=Remove Marks;
                                 FRA=Remove Marks;
                                 NLD=Verwijder markeringen;
                                 DEA=Remove Marks];
                      Image=RemoveFilterLines;
                      OnAction=VAR
                                 ElementToRenumber@1000 : Record 11102076;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ElementToRenumber);
                                 IF ElementToRenumber.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(ElementToRenumber."Object Type", '', ElementToRenumber."Object No.") THEN
                                       VersionControl.RemoveObjectMark(Object);
                                   UNTIL ElementToRenumber.NEXT = 0;

                                 VersionControl.RemoveMarkLock(TRUE);
                               END;
                                }
      { 13      ;3   ;Action    ;
                      ShortCutKey=Shift+Ctrl+M;
                      CaptionML=[DEU=Remove &all Marks;
                                 ENU=Remove &all Marks;
                                 ESP=Remove &all Marks;
                                 FRA=Remove &all Marks;
                                 NLD=Verwijder &alle markeringen;
                                 DEA=Remove &all Marks];
                      Image=ClearFilter;
                      OnAction=VAR
                                 VersionControl@1000 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.RemoveObjectMarks(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      CaptionML=[DEU=&Start;
                                 ENU=&Start;
                                 ESP=&Start;
                                 FRA=&Start;
                                 NLD=&Starten;
                                 DEA=&Start];
                      Promoted=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 RenumberElements@1000 : Codeunit 11102074;
                                 FileHandler@1001 : Codeunit 11102097;
                                 FileToCreate@1002 : Text;
                                 ServerExportFileName@1003 : Text;
                                 ServerSourceFileName@1004 : Text;
                                 ctDefaultFileName@1005 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctEnterFileName@1006 : TextConst 'DEU=No file name entered.;ENU=No file name entered.;ESP=No file name entered.;FRA=No file name entered.;NLD=Geen bestandsnaam opgegeven.;DEA=No file name entered.';
                               BEGIN
                                 CASE RunType OF

                                   RunType::Export :
                                     BEGIN
                                       IF NOT WebClient THEN BEGIN
                                         IF ExportFileName = '' THEN
                                           ERROR(ctEnterFileName);
                                         FileHandler.QueryReplace(ExportFileName);
                                       END;
                                       FileToCreate := ExportFileName;
                                       ServerExportFileName := FileHandler.CreateServerFileName(ExportFileName, ctDefaultFileName);
                                     END;

                                   RunType::"Update Text File" :
                                     BEGIN
                                       IF SourceFileName = '' THEN
                                         ERROR(ctEnterFileName);

                                       FileToCreate := FileHandler.CheckUpdateExisting(SourceFileName, ExportFileName);
                                       ServerExportFileName := FileHandler.CreateServerFileName(FileToCreate, ctDefaultFileName);
                                       ServerSourceFileName := FileHandler.UploadToServer(SourceFileName, '');
                                     END;

                                 END;

                                 RenumberElements.SetRunType(RunType);
                                 RenumberElements.SetSourceFileName(ServerSourceFileName);
                                 RenumberElements.SetExportFileName(ServerExportFileName);
                                 RenumberElements.SetUpdateReferencingObjects(UpdateReferencingObjects);
                                 RenumberElements.SetSetModifyFlag(SetModifyFlag);
                                 RenumberElements.RUN(Rec);

                                 IF RunType = RunType::"Update Text File" THEN
                                   FileHandler.FinalizeUpdateExisting(SourceFileName);

                                 IF RunType IN [RunType::Export, RunType::"Update Text File"] THEN
                                   FileHandler.DownloadToClient(ServerExportFileName, FileToCreate, ctDefaultFileName);

                                 RenumberElements.ShowSucceedMessage;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 15  ;0   ;Container ;
                ContainerType=ContentArea }

    { 16  ;1   ;Group     ;
                CaptionML=[DEU=General;
                           ENU=General;
                           ESP=General;
                           FRA=General;
                           NLD=Algemeen;
                           DEA=General] }

    { 17  ;2   ;Field     ;
                CaptionML=[DEU=Export / Update in DB;
                           ENU=Export / Update in DB;
                           ESP=Export / Update in DB;
                           FRA=Export / Update in DB;
                           NLD=Exporteren / Bijwerken in DB;
                           DEA=Export / Update in DB];
                OptionCaptionML=[DEU=Update in DB,Export,Update Text File;
                                 ENU=Update in DB,Export,Update Text File;
                                 ESP=Update in DB,Export,Update Text File;
                                 FRA=Update in DB,Export,Update Text File;
                                 NLD=Bijwerken in DB,Exporteren,Tekstbestand bijwerken;
                                 DEA=Update in DB,Export,Update Text File];
                SourceExpr=RunType;
                OnValidate=BEGIN
                             SetEnabled;
                           END;
                            }

    { 18  ;2   ;Field     ;
                CaptionML=[DEU=Source File Name;
                           ENU=Source File Name;
                           ESP=Source File Name;
                           FRA=Source File Name;
                           NLD=Bronbestandsnaam;
                           DEA=Source File Name];
                SourceExpr=SourceFileName;
                Enabled=SourceFileNameEnabled;
                Editable=NOT WebClient;
                OnLookup=BEGIN
                           PickFile;
                         END;
                          }

    { 19  ;2   ;Field     ;
                CaptionML=[DEU=File Name;
                           ENU=File Name;
                           ESP=File Name;
                           FRA=File Name;
                           NLD=Bestandsnaam;
                           DEA=File Name];
                SourceExpr=ExportFileName;
                Visible=NOT WebClient;
                Enabled=ExportFileNameEnabled;
                OnLookup=VAR
                           CommonDialog@1000 : Codeunit 11102059;
                         BEGIN
                           CommonDialog.AddKnownExtension('txt-o');
                           CommonDialog.GetSaveFileName(ExportFileName);
                         END;
                          }

    { 20  ;2   ;Field     ;
                CaptionML=[DEU=No. of Involved Objects;
                           ENU=No. of Involved Objects;
                           ESP=No. of Involved Objects;
                           FRA=No. of Involved Objects;
                           NLD=Aantal betrokken objecten;
                           DEA=No. of Involved Objects];
                SourceExpr=NoOfInvolvedObjects;
                Editable=FALSE;
                OnDrillDown=VAR
                              RenumberElements@1000 : Codeunit 11102074;
                            BEGIN
                              RenumberElements.DrillDownNoOfInvolvedObjects(Rec, UpdateReferencingObjects);
                            END;
                             }

    { 21  ;2   ;Field     ;
                CaptionML=[DEU=Keep Delta Equal;
                           ENU=Keep Delta Equal;
                           ESP=Keep Delta Equal;
                           FRA=Keep Delta Equal;
                           NLD=Afwijking gelijk houden;
                           DEA=Keep Delta Equal];
                SourceExpr=KeepDeltaEqual }

    { 22  ;2   ;Field     ;
                CaptionML=[DEU=Delta;
                           ENU=Delta;
                           ESP=Delta;
                           FRA=Delta;
                           NLD=Afwijking;
                           DEA=Delta];
                SourceExpr=Delta;
                OnValidate=VAR
                             ElementToRenumber@1000 : Record 11102076;
                             ElementToRenumber2@1001 : Record 11102076;
                           BEGIN
                             IF KeepDeltaEqual THEN BEGIN

                               CurrPage.SETSELECTIONFILTER(ElementToRenumber);
                               IF ElementToRenumber.COUNT <= 1 THEN
                                 ElementToRenumber.RESET;

                               IF ElementToRenumber.FINDSET THEN
                                 REPEAT
                                   ElementToRenumber2 := ElementToRenumber;
                                   ElementToRenumber2.VALIDATE("New Number", ElementToRenumber2."No." + Delta);
                                   ElementToRenumber2.MODIFY;
                                 UNTIL ElementToRenumber.NEXT = 0;

                             END ELSE BEGIN
                               VALIDATE("New Number", "No." + Delta);
                               MODIFY;
                             END;
                             CurrPage.UPDATE(FALSE);

                             RefreshNoOfInvolvedObjects := TRUE;
                           END;
                            }

    { 23  ;1   ;Group     ;
                GroupType=Repeater }

    { 24  ;2   ;Field     ;
                SourceExpr="Object Type";
                StyleExpr=LineStyleExpr }

    { 25  ;2   ;Field     ;
                Width=13;
                BlankZero=Yes;
                SourceExpr=ObjectNo;
                CaptionClass=FIELDCAPTION("Object No.");
                StyleExpr=LineStyleExpr;
                OnValidate=VAR
                             Mgt@1000 : Codeunit 11102035;
                           BEGIN
                             Mgt.AfterInputObjectNo("Object Type", ObjectNo);
                             EVALUATE("Object No.", ObjectNo);
                             VALIDATE("Object No.");
                           END;

                OnLookup=VAR
                           Mgt@1000 : Codeunit 11102035;
                         BEGIN
                           EXIT(Mgt.LookupObjectNo("Object Type", Text));
                         END;
                          }

    { 26  ;2   ;Field     ;
                SourceExpr="Object Name";
                StyleExpr=LineStyleExpr }

    { 27  ;2   ;Field     ;
                SourceExpr="Parent Name";
                StyleExpr=LineStyleExpr }

    { 28  ;2   ;Field     ;
                SourceExpr=Type;
                StyleExpr=LineStyleExpr }

    { 29  ;2   ;Field     ;
                SourceExpr="No.";
                StyleExpr=LineStyleExpr;
                OnValidate=BEGIN
                             IF (Delta <> 0) AND KeepDeltaEqual THEN
                               VALIDATE("New Number", "No." + Delta);
                           END;
                            }

    { 30  ;2   ;Field     ;
                SourceExpr=Name;
                StyleExpr=LineStyleExpr }

    { 31  ;2   ;Field     ;
                SourceExpr="New Number";
                StyleExpr=LineStyleExpr;
                OnValidate=VAR
                             OriginalElementToRenumber@1000 : Record 11102076;
                           BEGIN
                             IF KeepDeltaEqual THEN BEGIN

                               OriginalElementToRenumber := Rec;

                               Delta := "New Number" - "No.";
                               IF FINDSET THEN
                                 REPEAT
                                   IF ("Object No." <> OriginalElementToRenumber."Object No.") OR
                                      ("No." <> OriginalElementToRenumber."No.")
                                   THEN BEGIN
                                     VALIDATE("New Number", "No." + Delta);
                                     MODIFY;
                                   END;
                                 UNTIL NEXT = 0;

                               Rec := OriginalElementToRenumber;
                             END;

                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 39  ;2   ;Field     ;
                SourceExpr=Renumber;
                Visible=FALSE }

    { 32  ;1   ;Field     ;
                SourceExpr=FormInitialized;
                Visible=FALSE }

    { 33  ;1   ;Group     ;
                CaptionML=[DEU=Options;
                           ENU=Options;
                           ESP=Options;
                           FRA=Options;
                           NLD=Opties;
                           DEA=Options];
                GroupType=Group }

    { 34  ;2   ;Field     ;
                CaptionML=[DEU=Start Functions at;
                           ENU=Start Functions at;
                           ESP=Start Functions at;
                           FRA=Start Functions at;
                           NLD=Begin functies bij;
                           DEA=Start Functions at];
                SourceExpr=StartFunctionsAt;
                OnValidate=BEGIN
                             FormInitialized := FormInitialized::"Id changed";
                           END;
                            }

    { 35  ;2   ;Field     ;
                CaptionML=[DEU=Start Variables at;
                           ENU=Start Variables at;
                           ESP=Start Variables at;
                           FRA=Start Variables at;
                           NLD=Begin variabelen bij;
                           DEA=Start Variables at];
                SourceExpr=StartVariablesAt;
                OnValidate=BEGIN
                             FormInitialized := FormInitialized::"Id changed";
                           END;
                            }

    { 36  ;2   ;Field     ;
                CaptionML=[DEU=Start Controls at;
                           ENU=Start Controls at;
                           ESP=Start Controls at;
                           FRA=Start Controls at;
                           NLD=Begin controls bij;
                           DEA=Start Controls at];
                SourceExpr=StartControlsAt;
                OnValidate=BEGIN
                             FormInitialized := FormInitialized::"Id changed";
                           END;
                            }

    { 38  ;2   ;Field     ;
                CaptionML=[DEU=Update Referencing Objects;
                           ENU=Update Referencing Objects;
                           ESP=Update Referencing Objects;
                           FRA=Update Referencing Objects;
                           NLD=Refererende objecten bijwerken;
                           DEA=Update Referencing Objects];
                SourceExpr=UpdateReferencingObjects;
                OnValidate=BEGIN
                             NoOfInvolvedObjects := CalcNoOfInvolvedObjects;
                           END;
                            }

    { 37  ;2   ;Field     ;
                CaptionML=[DEU=Set Modify Flag;
                           ENU=Set Modify Flag;
                           ESP=Set Modify Flag;
                           FRA=Set Modify Flag;
                           NLD=Wijzigvlag zetten;
                           DEA=Set Modify Flag];
                SourceExpr=SetModifyFlag }

  }
  CODE
  {
    VAR
      ExportFileName@1000 : Text;
      LineStyleExpr@1001 : Text INDATASET;
      ObjectNo@1002 : Text INDATASET;
      SourceFileName@1003 : Text;
      Delta@1004 : Integer;
      NoOfInvolvedObjects@1005 : Integer;
      StartControlsAt@1006 : Integer;
      StartFunctionsAt@1007 : Integer;
      StartVariablesAt@1008 : Integer;
      FormInitialized@1009 : ' ,Yes,Id changed';
      RunType@1010 : 'Update in DB,Export,Update Text File';
      ExportFileNameEnabled@1011 : Boolean INDATASET;
      KeepDeltaEqual@1012 : Boolean;
      RefreshNoOfInvolvedObjects@1013 : Boolean;
      SetModifyFlag@1014 : Boolean;
      SourceFileNameEnabled@1015 : Boolean INDATASET;
      UpdateReferencingObjects@1016 : Boolean;
      WebClient@1017 : Boolean;

    LOCAL PROCEDURE SetLineStyle@1();
    BEGIN
      CASE TRUE OF

        NewNumberDouble :
          LineStyleExpr := 'Attention';

        NOT ("New Number" IN [0, "No."]) :
          LineStyleExpr := 'Favorable';

        ELSE
          LineStyleExpr := '';

      END;
    END;

    LOCAL PROCEDURE CalcNoOfInvolvedObjects@2() : Integer;
    VAR
      RenumberElements@1000 : Codeunit 11102074;
    BEGIN
      EXIT(RenumberElements.NoOfInvolvedObjects(Rec, UpdateReferencingObjects));
    END;

    LOCAL PROCEDURE SetEnabled@3();
    BEGIN
      SourceFileNameEnabled := RunType = RunType::"Update Text File";
      ExportFileNameEnabled := RunType IN [RunType::Export, RunType::"Update Text File"];
    END;

    LOCAL PROCEDURE PickFile@4();
    VAR
      CommonDialog@1000 : Codeunit 11102059;
    BEGIN
      IF SourceFileNameEnabled THEN BEGIN
        CommonDialog.AddKnownExtension('txt-o');
        CommonDialog.GetOpenFileName(SourceFileName, WebClient);
      END;
    END;

    BEGIN
    END.
  }
}

