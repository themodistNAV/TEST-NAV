OBJECT Table 9021688 Web Assembly Component
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:08 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               WebAssRec1.GET("Label Code","Assembly GuID");
               IF WebAssRec1.Verified = FALSE THEN
                 ERROR(Text001,WebAssRec1."Assembly Name");

               SetModifiedDateTime();
             END;

    OnModify=BEGIN
               SetModifiedDateTime();
             END;

    CaptionML=[ENU=Web Assembly Component;
               ENG=Web Assembly Component];
    LookupPageID=Page9021751;
    DrillDownPageID=Page9021751;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code] }
    { 3   ;   ;Assembly GuID       ;Integer       ;CaptionML=[ENU=Assembly GuID;
                                                              ENG=Assembly GuID] }
    { 7   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=Item,Resource;
                                                                    ENG=Item,Resource];
                                                   OptionString=Item,Resource }
    { 9   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource;
                                                   OnValidate=BEGIN
                                                                IF "No." = '' THEN
                                                                  Description := ''
                                                                ELSE BEGIN
                                                                  IF Type = Type::Item THEN BEGIN
                                                                    ItemRec.GET("No.");
                                                                    Description := ItemRec.Description;
                                                                  END;
                                                                  IF Type = Type::Resource THEN BEGIN
                                                                    ResourceRec.GET("No.");
                                                                    Description := ResourceRec.Name;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ENG=No.] }
    { 13  ;   ;DateTimeInsertedModified;DateTime  ;CaptionML=[ENU=DateTimeInsertedModified;
                                                              ENG=DateTimeInsertedModified] }
    { 18  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 20  ;   ;Assembly Name       ;Text150       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Web Assembly"."Assembly Name" WHERE (Label Code=FIELD(Label Code),
                                                                                                            Assembly GuID=FIELD(Assembly GuID)));
                                                   CaptionML=[ENU=Assembly Name;
                                                              ENG=Assembly Name] }
    { 100 ;   ;Order               ;Integer       ;CaptionML=[ENU=Order;
                                                              ENG=Order] }
    { 101 ;   ;New Order           ;Integer       ;CaptionML=[ENU=New Order;
                                                              ENG=New Order] }
  }
  KEYS
  {
    {    ;Label Code,Assembly GuID,Type,No.       ;Clustered=Yes }
    {    ;Label Code,Assembly GuID,Order           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000008 : TextConst 'ENU=Before you add items, please first verify the Assembly (Label %1, description %2) . (1);ENG=Before you add items, please first verify the Assembly (Label %1, description %2) . (1)';
      WebCompRec1@1000000010 : Record 9021688;
      WebCompRec2@1000000011 : Record 9021688;
      WebAssRec1@1000000013 : Record 9021687;
      ItemRec@1000000000 : Record 27;
      ResourceRec@1000000001 : Record 156;
      OrderNo@1000000014 : Integer;

    PROCEDURE Reorder@1000000000(LabelCode@1000000000 : Code[10];ConfigID@1000000003 : Integer);
    VAR
      i1@1000000001 : Integer;
      i2@1000000002 : Integer;
    BEGIN
      WebCompRec1.RESET;
      WebCompRec1.SETRANGE("Label Code",LabelCode);
      WebCompRec1.SETRANGE("Assembly GuID",ConfigID);
      WebCompRec1.SETCURRENTKEY("Label Code","Assembly GuID",Order);

      i1 := 0;
      IF WebCompRec1.FIND('-') THEN
      REPEAT
        IF i1 = 0 THEN BEGIN
          i1 := 1;
          i2 := 1;
          WebCompRec2 := WebCompRec1;
          WebCompRec2."New Order" := 1;
        END ELSE BEGIN
          WebCompRec2 := WebCompRec1;
          i2 := i2 + 1;
          WebCompRec2."New Order" := i2;
        END;
        WebCompRec2.MODIFY;
      UNTIL WebCompRec1.NEXT = 0;

      WebCompRec1.RESET;
      WebCompRec1.SETRANGE("Label Code",LabelCode);
      WebCompRec1.SETRANGE("Assembly GuID",ConfigID);
      IF WebCompRec1.FIND('-') THEN
      REPEAT
        WebCompRec2 := WebCompRec1;
        WebCompRec2.Order := WebCompRec2."New Order";
        WebCompRec2.MODIFY;
      UNTIL WebCompRec1.NEXT = 0;
    END;

    PROCEDURE MoveUp@1000000001(VAR LocalRec@1000000000 : Record 9021688);
    BEGIN
      IF LocalRec.Order = 1 THEN
      EXIT;

      WebCompRec1.RESET;
      WebCompRec1.GET(LocalRec."Label Code",LocalRec."Assembly GuID",
                          LocalRec.Type,LocalRec."No.");
      OrderNo := WebCompRec1.Order;
      //IF OrderNo = 1 THEN
      //  EXIT;

      WebCompRec1.RESET;
      WebCompRec1.SETRANGE("Label Code",LocalRec."Label Code");
      WebCompRec1.SETRANGE("Assembly GuID",LocalRec."Assembly GuID");
      WebCompRec1.SETRANGE(Type,LocalRec.Type);
      WebCompRec1.SETFILTER(Order,'%1',OrderNo - 1);
      WebCompRec1.SETCURRENTKEY("Label Code","Assembly GuID",Order);
      IF WebCompRec1.FIND('-') THEN BEGIN
        WebCompRec2 := WebCompRec1;
        WebCompRec2.Order := OrderNo;
        WebCompRec2.MODIFY;
        LocalRec.Order := OrderNo - 1;
      END;
      //LocalRec.Order := OrderNo - 1;
    END;

    PROCEDURE MoveDown@1000000002(VAR LocalRec@1000000000 : Record 9021688);
    BEGIN
      WebCompRec1.GET(LocalRec."Label Code",LocalRec."Assembly GuID",
                          LocalRec.Type,LocalRec."No.");
      WebCompRec1.RESET;
      OrderNo := WebCompRec1.Order;

      WebCompRec1.RESET;
      WebCompRec1.SETRANGE("Label Code",LocalRec."Label Code");
      WebCompRec1.SETRANGE("Assembly GuID",LocalRec."Assembly GuID");
      WebCompRec1.SETRANGE(Type,LocalRec.Type);
      WebCompRec1.SETFILTER(Order,'%1',OrderNo + 1);
      WebCompRec1.SETCURRENTKEY("Label Code","Assembly GuID",Order);
      IF WebCompRec1.FIND('-') THEN BEGIN
        WebCompRec2 := WebCompRec1;
        WebCompRec2.Order := OrderNo;
        WebCompRec2.MODIFY;
        LocalRec.Order := OrderNo + 1;
      END;
    END;

    PROCEDURE SetModifiedDateTime@1000000003();
    BEGIN
      DateTimeInsertedModified:=CURRENTDATETIME;
      IF WebAssRec1.GET("Label Code","Assembly GuID") THEN BEGIN
        WebAssRec1.VALIDATE(DateTimeInsertedModified,CURRENTDATETIME);
        WebAssRec1.MODIFY;
      END;
    END;

    BEGIN
    {
      //DOC EC2016.6 JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

