OBJECT Page 11102054 OM - Transport Subform
{
  OBJECT-PROPERTIES
  {
    Date=01/10/12;
    Time=12:00:00 PM;
    Version List=OMA9.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Transport Subform;
               ENU=Transport Subform;
               ESP=Transport Subform;
               FRA=Transport Subform;
               NLD=Transport subform;
               DEA=Transport Subform];
    InsertAllowed=No;
    SourceTable=Table11102036;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   TmpProject.COPYFILTERS(Rec);
                   TmpProject.FILTERGROUP(2);
                   TmpProject.SETRANGE("Transport No.");
                   TmpProject.FILTERGROUP(0);

                   TmpProject := Rec;

                   IF TmpProject.FIND(Which) THEN BEGIN
                     Rec := TmpProject;
                     EXIT(TRUE);
                   END;
                 END;

    OnNextRecord=VAR
                   CurrentSteps@1000 : Integer;
                 BEGIN
                   TmpProject.COPYFILTERS(Rec);
                   TmpProject.FILTERGROUP(2);
                   TmpProject.SETRANGE("Transport No.");
                   TmpProject.FILTERGROUP(0);

                   TmpProject := Rec;

                   CurrentSteps := TmpProject.NEXT(Steps);
                   IF CurrentSteps <> 0 THEN BEGIN
                     Rec := TmpProject;
                     EXIT(CurrentSteps);
                   END;
                 END;

    OnAfterGetRecord=VAR
                       Project@1000 : Record 11102036;
                     BEGIN
                       IF Project.GET("No.") THEN BEGIN
                         Rec := Project;
                         IF Project."Transport No." = gTransport."No." THEN
                           LineStyleExpr := ''
                         ELSE
                           LineStyleExpr := 'Subordinate';
                       END;
                     END;

    OnDeleteRecord=VAR
                     Project@1000 : Record 11102036;
                     TransportProjectHistory@1001 : Record 11102066;
                     ctRemovePojectFromHistory@1002 : TextConst 'DEU=Do you want to remove the project from the transport history?;ENU=Do you want to remove the project from the transport history?;ESP=Do you want to remove the project from the transport history?;FRA=Do you want to remove the project from the transport history?;NLD=Wilt u het project uit de transporthistorie verwijderen?;DEA=Do you want to remove the project from the transport history?';
                   BEGIN
                     IF Project.GET("No.") THEN
                       IF Project."Transport No." = gTransport."No." THEN BEGIN
                         Project.VALIDATE("Transport No.", '');
                         Project.MODIFY(TRUE);
                         Rec := Project;
                       END ELSE
                         IF TransportProjectHistory.GET(gTransport."No.", "No.") THEN
                           IF CONFIRM(ctRemovePojectFromHistory, TRUE) THEN
                             TransportProjectHistory.DELETE(TRUE);

                     SetTransport(gTransport, FALSE);
                     CurrPage.UPDATE(FALSE);

                     EXIT(FALSE);
                   END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;ActionGroup;
                      CaptionML=[DEU=P&roject;
                                 ENU=P&roject;
                                 ESP=P&roject;
                                 FRA=P&roject;
                                 NLD=P&roject;
                                 DEA=P&roject] }
      { 3       ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[DEU=Card;
                                 ENU=Card;
                                 ESP=Card;
                                 FRA=Card;
                                 NLD=Kaart;
                                 DEA=Card];
                      Image=EditLines;
                      OnAction=BEGIN
                                 ShowCard;
                               END;
                                }
      { 4       ;2   ;Action    ;
                      CaptionML=[DEU=Objects;
                                 ENU=Objects;
                                 ESP=Objects;
                                 FRA=Objects;
                                 NLD=O&bjecten;
                                 DEA=Objects];
                      OnAction=BEGIN
                                 ShowObjects;
                               END;
                                }
      { 5       ;2   ;Action    ;
                      CaptionML=[DEU=History;
                                 ENU=History;
                                 ESP=History;
                                 FRA=History;
                                 NLD=Historie;
                                 DEA=History];
                      OnAction=BEGIN
                                 ShowHistory;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 6   ;0   ;Container ;
                ContainerType=ContentArea }

    { 7   ;1   ;Group     ;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                SourceExpr="No.";
                Editable=FALSE;
                StyleExpr=LineStyleExpr }

    { 9   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE;
                StyleExpr=LineStyleExpr }

  }
  CODE
  {
    VAR
      TmpProject@1000 : TEMPORARY Record 11102036;
      gTransport@1001 : Record 11102043;
      LineStyleExpr@1002 : Text;

    PROCEDURE SetTransport@1(VAR Transport@1000 : Record 11102043;OnlyIfChanged@1001 : Boolean);
    VAR
      Project@1002 : Record 11102036;
      TransportProjectHistory@1003 : Record 11102066;
    BEGIN
      IF (gTransport."No." = Transport."No.") AND OnlyIfChanged THEN
        EXIT;

      gTransport := Transport;

      TmpProject.RESET;
      TmpProject.DELETEALL;

      IF Transport."No." <> '' THEN BEGIN
        Project.SETCURRENTKEY("Transport No.");
        Project.SETRANGE("Transport No.", gTransport."No.");
        IF Project.FINDSET THEN
          REPEAT
            TmpProject := Project;
            TmpProject.INSERT;
          UNTIL Project.NEXT = 0;
        Project.SETRANGE("Transport No.");

        TransportProjectHistory.SETRANGE("Transport No.", gTransport."No.");
        IF TransportProjectHistory.FINDSET THEN
          REPEAT
            IF Project.GET(TransportProjectHistory."Project No.") THEN
              IF NOT TmpProject.GET(Project."No.") THEN BEGIN
                TmpProject := Project;
                TmpProject.INSERT;
              END;
          UNTIL TransportProjectHistory.NEXT = 0;
      END;

      IF TmpProject.FINDFIRST THEN
        ;

      IF FINDFIRST THEN
        ;

      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

