OBJECT Page 952 Manager Time Sheet
{
  OBJECT-PROPERTIES
  {
    Date=23/03/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45480;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Manager Time Sheet;
               ENG=Manager Time Sheet];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table951;
    DataCaptionFields=Time Sheet No.;
    PageType=Worksheet;
    AutoSplitKey=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Navigate,Show;
                                ENG=New,Process,Report,Navigate,Show];
    OnOpenPage=BEGIN
                 IF "Time Sheet No." <> '' THEN
                   CurrTimeSheetNo := "Time Sheet No."
                 ELSE
                   CurrTimeSheetNo := TimeSheetHeader.FindLastTimeSheetNo(TimeSheetHeader.FIELDNO("Approver User ID"));

                 TimeSheetMgt.SetTimeSheetNo(CurrTimeSheetNo,Rec);
                 UpdateControls;
               END;

    OnAfterGetRecord=BEGIN
                       AfterGetCurrentRecord;
                     END;

    OnNewRecord=BEGIN
                  AfterGetCurrentRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 7       ;1   ;ActionGroup;
                      CaptionML=[ENU=&Time Sheet;
                                 ENG=&Time Sheet];
                      Image=Timesheet }
      { 34      ;2   ;Action    ;
                      Name=PreviousPeriod;
                      ShortCutKey=Ctrl+PgUp;
                      CaptionML=[ENU=&Previous Period;
                                 ENG=&Previous Period];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 FindTimeSheet(SetWanted::Previous);
                               END;
                                }
      { 35      ;2   ;Action    ;
                      Name=NextPeriod;
                      ShortCutKey=Ctrl+PgDn;
                      CaptionML=[ENU=&Next Period;
                                 ENG=&Next Period];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 FindTimeSheet(SetWanted::Next);
                               END;
                                }
      { 40      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Line;
                                 ENG=&Line];
                      Image=Line }
      { 24      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Posting E&ntries;
                                 ENG=Posting E&ntries];
                      Promoted=Yes;
                      Image=PostingEntries;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 TimeSheetMgt.ShowPostingEntries("Time Sheet No.","Line No.");
                               END;
                                }
      { 39      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Activity &Details;
                                 ENG=Activity &Details];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 ShowLineDetails(TRUE);
                               END;
                                }
      { 32      ;1   ;ActionGroup;
                      CaptionML=[ENU=Co&mments;
                                 ENG=Co&mments];
                      Image=ViewComments }
      { 22      ;2   ;Action    ;
                      Name=TimeSheetComment2;
                      CaptionML=[ENU=&Time Sheet Comments;
                                 ENG=&Time Sheet Comments];
                      RunObject=Page 955;
                      RunPageLink=No.=FIELD(Time Sheet No.),
                                  Time Sheet Line No.=CONST(0);
                      Promoted=Yes;
                      Image=ViewComments;
                      PromotedCategory=Category5 }
      { 18      ;2   ;Action    ;
                      Name=LineComments;
                      CaptionML=[ENU=&Line Comments;
                                 ENG=&Line Comments];
                      RunObject=Page 955;
                      RunPageLink=No.=FIELD(Time Sheet No.),
                                  Time Sheet Line No.=FIELD(Line No.);
                      Promoted=Yes;
                      Image=ViewComments;
                      PromotedCategory=Category5 }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 33      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ENG=F&unctions];
                      Image=Action }
      { 31      ;2   ;Action    ;
                      Name=Approve;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=[ENU=&Approve;
                                 ENG=&Approve];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Approve;
                               END;
                                }
      { 28      ;2   ;Action    ;
                      Name=Reject;
                      CaptionML=[ENU=&Reject;
                                 ENG=&Reject];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reject;
                               END;
                                }
      { 30      ;2   ;Action    ;
                      Name=Reopen;
                      CaptionML=[ENU=Re&open;
                                 ENG=Re&open];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reopen;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 26  ;1   ;Group     ;
                GroupType=Group }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Time Sheet No;
                           ENG=Time Sheet No];
                SourceExpr=CurrTimeSheetNo;
                OnValidate=BEGIN
                             TimeSheetHeader.RESET;
                             TimeSheetMgt.FilterTimeSheets(TimeSheetHeader,TimeSheetHeader.FIELDNO("Approver User ID"));
                             TimeSheetMgt.CheckTimeSheetNo(TimeSheetHeader,CurrTimeSheetNo);
                             CurrPage.SAVERECORD;
                             TimeSheetMgt.SetTimeSheetNo(CurrTimeSheetNo,Rec);
                             UpdateControls;
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           TimeSheetMgt.LookupApproverTimeSheet(CurrTimeSheetNo,Rec,TimeSheetHeader);
                           UpdateControls;
                         END;
                          }

    { 14  ;2   ;Field     ;
                Name=ResourceNo;
                CaptionML=[ENU=Resource No.;
                           ENG=Resource No.];
                SourceExpr=TimeSheetHeader."Resource No.";
                Editable=false }

    { 16  ;2   ;Field     ;
                Name=ApproverUserID;
                CaptionML=[ENU=Approver User ID;
                           ENG=Approver User ID];
                SourceExpr=TimeSheetHeader."Approver User ID";
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                Name=StartingDate;
                CaptionML=[ENU=Starting Date;
                           ENG=Starting Date];
                SourceExpr=TimeSheetHeader."Starting Date";
                Editable=FALSE }

    { 5   ;2   ;Field     ;
                Name=EndingDate;
                CaptionML=[ENU=Ending Date;
                           ENG=Ending Date];
                SourceExpr=TimeSheetHeader."Ending Date";
                Editable=FALSE }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 13  ;2   ;Field     ;
                SourceExpr=Type;
                Editable=FALSE }

    { 23  ;2   ;Field     ;
                SourceExpr="Job No.";
                Visible=FALSE;
                Editable=FALSE }

    { 29  ;2   ;Field     ;
                SourceExpr="Job Task No.";
                Visible=FALSE;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               ShowLineDetails(TRUE);
                               CurrPage.UPDATE;
                             END;
                              }

    { 6   ;2   ;Field     ;
                SourceExpr="Cause of Absence Code";
                Visible=FALSE;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr=Chargeable;
                Visible=FALSE;
                Editable=ChargeableAllowEdit }

    { 2   ;2   ;Field     ;
                SourceExpr="Work Type Code";
                Visible=FALSE;
                Editable=WorkTypeCodeAllowEdit }

    { 36  ;2   ;Field     ;
                SourceExpr="Service Order No.";
                Visible=FALSE;
                Editable=FALSE }

    { 37  ;2   ;Field     ;
                SourceExpr="Assembly Order No.";
                Visible=FALSE }

    { 9   ;2   ;Field     ;
                SourceExpr=Posted;
                Visible=FALSE }

    { 11  ;2   ;Field     ;
                Name=Field1;
                Width=6;
                DecimalPlaces=0:2;
                BlankZero=Yes;
                SourceExpr=CellData[1];
                CaptionClass='3,' + ColumnCaption[1];
                Editable=FALSE }

    { 15  ;2   ;Field     ;
                Name=Field2;
                Width=6;
                DecimalPlaces=0:2;
                BlankZero=Yes;
                SourceExpr=CellData[2];
                CaptionClass='3,' + ColumnCaption[2];
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                Name=Field3;
                Width=6;
                DecimalPlaces=0:2;
                BlankZero=Yes;
                SourceExpr=CellData[3];
                CaptionClass='3,' + ColumnCaption[3];
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                Name=Field4;
                Width=6;
                DecimalPlaces=0:2;
                BlankZero=Yes;
                SourceExpr=CellData[4];
                CaptionClass='3,' + ColumnCaption[4];
                Editable=FALSE }

    { 21  ;2   ;Field     ;
                Name=Field5;
                Width=6;
                DecimalPlaces=0:2;
                BlankZero=Yes;
                SourceExpr=CellData[5];
                CaptionClass='3,' + ColumnCaption[5];
                Editable=FALSE }

    { 25  ;2   ;Field     ;
                Name=Field6;
                Width=6;
                BlankZero=Yes;
                SourceExpr=CellData[6];
                CaptionClass='3,' + ColumnCaption[6];
                Visible=FALSE;
                Editable=FALSE }

    { 27  ;2   ;Field     ;
                Name=Field7;
                Width=6;
                BlankZero=Yes;
                SourceExpr=CellData[7];
                CaptionClass='3,' + ColumnCaption[7];
                Visible=FALSE;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr=Status }

    { 48  ;2   ;Field     ;
                DrillDown=No;
                CaptionML=[ENU=Total;
                           ENG=Total];
                SourceExpr="Total Quantity";
                Visible=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 12  ;1   ;Part      ;
                Name=TimeSheetStatusFactBox;
                CaptionML=[ENU=Time Sheet Status;
                           ENG=Time Sheet Status];
                PagePartID=Page957;
                PartType=Page }

    { 1905767507;1;Part   ;
                Name=ActualSchedSummaryFactBox;
                CaptionML=[ENU=Actual/Scheduled Summary;
                           ENG=Actual/Scheduled Summary];
                PagePartID=Page956;
                Visible=TRUE;
                PartType=Page }

    { 38  ;1   ;Part      ;
                Name=ActivityDetailsFactBox;
                CaptionML=[ENU=Activity Details;
                           ENG=Activity Details];
                SubPageLink=Time Sheet No.=FIELD(Time Sheet No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page971;
                PartType=Page }

  }
  CODE
  {
    VAR
      TimeSheetHeader@1006 : Record 950;
      TimeSheetDetail@1003 : Record 952;
      ColumnRecords@1004 : ARRAY [32] OF Record 2000000007;
      TimeSheetMgt@1007 : Codeunit 950;
      TimeSheetApprovalMgt@1010 : Codeunit 951;
      NoOfColumns@1002 : Integer;
      CellData@1001 : ARRAY [32] OF Decimal;
      ColumnCaption@1000 : ARRAY [32] OF Text[1024];
      CurrTimeSheetNo@1008 : Code[20];
      SetWanted@1009 : 'Previous,Next';
      WorkTypeCodeAllowEdit@1012 : Boolean;
      ChargeableAllowEdit@1011 : Boolean;

    PROCEDURE SetColumns@11();
    VAR
      Calendar@1003 : Record 2000000007;
    BEGIN
      CLEAR(ColumnCaption);
      CLEAR(ColumnRecords);
      CLEAR(Calendar);
      CLEAR(NoOfColumns);

      TimeSheetHeader.GET(CurrTimeSheetNo);
      Calendar.SETRANGE("Period Type",Calendar."Period Type"::Date);
      Calendar.SETRANGE("Period Start",TimeSheetHeader."Starting Date",TimeSheetHeader."Ending Date");
      IF Calendar.FINDSET THEN
        REPEAT
          NoOfColumns += 1;
          ColumnRecords[NoOfColumns]."Period Start" := Calendar."Period Start";
          ColumnCaption[NoOfColumns] := TimeSheetMgt.FormatDate(Calendar."Period Start",1);
        UNTIL Calendar.NEXT = 0;
    END;

    LOCAL PROCEDURE AfterGetCurrentRecord@8();
    VAR
      i@1000 : Integer;
    BEGIN
      i := 0;
      WHILE i < NoOfColumns DO BEGIN
        i := i + 1;
        IF ("Line No." <> 0) AND TimeSheetDetail.GET(
             "Time Sheet No.",
             "Line No.",
             ColumnRecords[i]."Period Start")
        THEN
          CellData[i] := TimeSheetDetail.Quantity
        ELSE
          CellData[i] := 0;
      END;
      UpdateFactBoxes;
      WorkTypeCodeAllowEdit := GetAllowEdit(FIELDNO("Work Type Code"),TRUE);
      ChargeableAllowEdit := GetAllowEdit(FIELDNO(Chargeable),TRUE);
    END;

    LOCAL PROCEDURE FindTimeSheet@7(Which@1000 : 'Prev,Next');
    BEGIN
      CurrTimeSheetNo := TimeSheetMgt.FindTimeSheet(TimeSheetHeader,Which);
      TimeSheetMgt.SetTimeSheetNo(CurrTimeSheetNo,Rec);
      UpdateControls;
    END;

    LOCAL PROCEDURE UpdateFactBoxes@9();
    BEGIN
      CurrPage.ActualSchedSummaryFactBox.PAGE.UpdateData(TimeSheetHeader);
      CurrPage.TimeSheetStatusFactBox.PAGE.UpdateData(TimeSheetHeader);
      IF "Line No." = 0 THEN
        CurrPage.ActivityDetailsFactBox.PAGE.SetEmptyLine;
    END;

    LOCAL PROCEDURE UpdateControls@12();
    BEGIN
      SetColumns;
      UpdateFactBoxes;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Process@1(Action@1000 : 'Approve Selected,Approve All,Reopen Selected,Reopen All,Reject Selected,Reject All');
    VAR
      TimeSheetLine@1001 : Record 951;
      ActionType@1002 : 'Approve,Reopen,Reject';
    BEGIN
      CurrPage.SAVERECORD;
      CASE Action OF
        Action::"Approve All",
        Action::"Reject All":
          FilterAllLines(TimeSheetLine,ActionType::Approve);
        Action::"Reopen All":
          FilterAllLines(TimeSheetLine,ActionType::Reopen);
        ELSE
          CurrPage.SETSELECTIONFILTER(TimeSheetLine);
      END;
      IF TimeSheetLine.FINDSET THEN
        REPEAT
          CASE Action OF
            Action::"Approve Selected",
            Action::"Approve All":
              TimeSheetApprovalMgt.Approve(TimeSheetLine);
            Action::"Reopen Selected",
            Action::"Reopen All":
              TimeSheetApprovalMgt.ReopenApproved(TimeSheetLine);
            Action::"Reject Selected",
            Action::"Reject All":
              TimeSheetApprovalMgt.Reject(TimeSheetLine);
          END;
        UNTIL TimeSheetLine.NEXT = 0;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Approve@2();
    VAR
      Action@1001 : 'Approve Selected,Approve All,Reopen Selected,Reopen All,Reject Selected,Reject All';
      ActionType@1002 : 'Approve,Reopen,Reject';
    BEGIN
      CASE ShowDialog(ActionType::Approve) OF
        1:
          Process(Action::"Approve All");
        2:
          Process(Action::"Approve Selected");
      END;
    END;

    LOCAL PROCEDURE Reopen@14();
    VAR
      ActionType@1002 : 'Approve,Reopen,Reject';
      Action@1001 : 'Approve Selected,Approve All,Reopen Selected,Reopen All,Reject Selected,Reject All';
    BEGIN
      CASE ShowDialog(ActionType::Reopen) OF
        1:
          Process(Action::"Reopen All");
        2:
          Process(Action::"Reopen Selected");
      END;
    END;

    LOCAL PROCEDURE Reject@3();
    VAR
      ActionType@1002 : 'Approve,Reopen,Reject';
      Action@1001 : 'Approve Selected,Approve All,Reopen Selected,Reopen All,Reject Selected,Reject All';
    BEGIN
      CASE ShowDialog(ActionType::Reject) OF
        1:
          Process(Action::"Reject All");
        2:
          Process(Action::"Reject Selected");
      END;
    END;

    LOCAL PROCEDURE GetDialogText@16(ActionType@1000 : 'Approve,Reopen,Reject') : Text[100];
    VAR
      TimeSheetLine@1003 : Record 951;
    BEGIN
      FilterAllLines(TimeSheetLine,ActionType);
      EXIT(TimeSheetApprovalMgt.GetManagerTimeSheetDialogText(ActionType,TimeSheetLine.COUNT));
    END;

    LOCAL PROCEDURE FilterAllLines@22(VAR TimeSheetLine@1000 : Record 951;ActionType@1001 : 'Approve,Reopen,Reject');
    BEGIN
      TimeSheetLine.SETRANGE("Time Sheet No.",CurrTimeSheetNo);
      TimeSheetLine.COPYFILTERS(Rec);
      TimeSheetLine.FILTERGROUP(2);
      TimeSheetLine.SETFILTER(Type,'<>%1',TimeSheetLine.Type::" ");
      TimeSheetLine.FILTERGROUP(0);
      CASE ActionType OF
        ActionType::Approve,
        ActionType::Reject:
          TimeSheetLine.SETRANGE(Status,TimeSheetLine.Status::Submitted);
        ActionType::Reopen:
          TimeSheetLine.SETRANGE(Status,TimeSheetLine.Status::Approved);
      END;
    END;

    LOCAL PROCEDURE ShowDialog@4(ActionType@1000 : 'Approve,Reopen,Reject') : Integer;
    BEGIN
      EXIT(STRMENU(GetDialogText(ActionType),1,TimeSheetApprovalMgt.GetManagerTimeSheetDialogInstruction(ActionType)));
    END;

    BEGIN
    END.
  }
}

