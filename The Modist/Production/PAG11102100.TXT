OBJECT Page 11102100 OM - Translation Tool Captions
{
  OBJECT-PROPERTIES
  {
    Date=01/10/13;
    Time=12:00:00 PM;
    Version List=OMA9.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Translation Tool Captions;
               ENU=Translation Tool Captions;
               ESP=Translation Tool Captions;
               FRA=Translation Tool Captions;
               NLD=Vertaaltoolcaptions;
               DEA=Translation Tool Captions];
    SourceTable=Table11102068;
    PopulateAllFields=Yes;
    SourceTableView=WHERE(Captions=CONST(Yes));
    PageType=List;
    OnOpenPage=BEGIN
                 IF ExistingTranslationsView THEN BEGIN
                   CurrPage.EDITABLE := FALSE;
                   NoOfTimesVisible := TRUE;
                 END ELSE
                   NoOfTimesVisible := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       FormatLanguageId;
                       FormatCaption;
                     END;

    OnNewRecord=BEGIN
                  FormatLanguageId;
                  FormatCaption;
                END;

    OnInsertRecord=VAR
                     Line@1000 : Record 11102068;
                     PrevRec@1001 : Record 11102068;
                   BEGIN
                     TESTFIELD("Caption Entry No.");

                     Line.SETCURRENTKEY("Caption Entry No.");
                     Line.SETRANGE("Caption Entry No.", "Caption Entry No.");
                     Line.FINDFIRST;
                     Line.TESTFIELD(Captions, TRUE);

                     PrevRec := Rec;

                     Rec := Line;
                     "Entry No." := 0;
                     "Language Id" := PrevRec."Language Id";
                     FillCaption := PrevRec.TotalCaption;
                     INSERT;

                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     IF TotalCaption <> '' THEN
                       ValidateCaption('');
                     EXIT(FALSE);
                   END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                CaptionML=[DEU=Language Id;
                           ENU=Language Id;
                           ESP=Language Id;
                           FRA=Language Id;
                           NLD=Taal id;
                           DEA=Language Id];
                SourceExpr=DisplayLanguageId;
                OnValidate=VAR
                             TranslationMgt@1000 : Codeunit 11102051;
                           BEGIN
                             "Language Id" := TranslationMgt.LanguageIdOfInput(DisplayLanguageId);
                             FormatLanguageId;
                           END;

                OnLookup=VAR
                           TranslationMgt@1000 : Codeunit 11102051;
                         BEGIN
                           EXIT(TranslationMgt.LookupLanguageId(Text));
                         END;
                          }

    { 4   ;2   ;Field     ;
                CaptionML=[DEU=Caption;
                           ENU=Caption;
                           ESP=Caption;
                           FRA=Caption;
                           NLD=Caption;
                           DEA=Caption];
                SourceExpr=DisplayCaption;
                OnValidate=BEGIN
                             ValidateCaption(DisplayCaption);
                             FormatCaption;
                           END;
                            }

    { 5   ;2   ;Field     ;
                SourceExpr="No. of Times";
                Visible=NoOfTimesVisible }

  }
  CODE
  {
    VAR
      DisplayCaption@1000 : Text INDATASET;
      DisplayLanguageId@1001 : Text INDATASET;
      NoOfTimesVisible@1002 : Boolean INDATASET;

    LOCAL PROCEDURE FormatLanguageId@1();
    VAR
      TranslationMgt@1000 : Codeunit 11102051;
    BEGIN
      DisplayLanguageId := TranslationMgt.LanguageCodeOfId("Language Id");
    END;

    LOCAL PROCEDURE FormatCaption@2();
    BEGIN
      DisplayCaption := TotalCaption;
    END;

    LOCAL PROCEDURE ExistingTranslationsView@4() ReturnValue : Boolean;
    BEGIN
      FILTERGROUP(10);
      ReturnValue := GETFILTER("No. of Times") <> '';
      SETRANGE("No. of Times");
      FILTERGROUP(0);
    END;

    BEGIN
    END.
  }
}

