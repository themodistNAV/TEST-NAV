OBJECT Codeunit 9062667 Code Editor Session Management
{
  OBJECT-PROPERTIES
  {
    Date=24/05/16;
    Time=11:46:49 AM;
    Version List=NH2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      HtmlControlRequestProxy@1000000000 : Codeunit 9062665;
      Initialized@1000000003 : Boolean;
      EditorText@1000000002 : Text;
      EditorMode@1000000014 : Text;
      EditorTheme@1000000015 : Text;
      MessageMode@1000000001 : ' ,GetValue,GetValueAndClose,ExportValue';
      txtImportFile@1000000011 : TextConst 'ENU=Import from file;ENG=Import from file';
      txtExportFile@1000000010 : TextConst 'ENU=Export to file;ENG=Export to file';
      txtDefaultFileName@1000000017 : TextConst 'ENU=File.txt;ENG=File.txt';
      FileTypeFilter@1000000013 : Text;
      DefaultFileName@1000000016 : Text;
      txtFileTypeText@1000000012 : TextConst 'ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*;ENG=Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      txtFileTypeHTML@1000000009 : TextConst 'ENU=HTML Files (*.html)|*.html|Text Files (*.txt)|*.txt|All Files (*.*)|*.*;ENG=HTML Files (*.html)|*.html|Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      txtFileTypeJavaScript@1000000008 : TextConst 'ENU=JavaScript Files (*.js)|*.js|Text Files (*.txt)|*.txt|All Files (*.*)|*.*;ENG=JavaScript Files (*.js)|*.js|Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      txtFileTypeCss@1000000007 : TextConst 'ENU=Css Files (*.css)|*.css|Text Files (*.txt)|*.txt|All Files (*.*)|*.*;ENG=Css Files (*.css)|*.css|Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      txtFileNameText@1000000006 : TextConst 'ENU=Text.txt;ENG=Text.txt';
      txtFileNameHtml@1000000005 : TextConst 'ENU=Html.txt;ENG=Html.txt';
      txtFileNameJavaScript@1000000004 : TextConst 'ENU=JavaScript.js;ENG=JavaScript.js';

    PROCEDURE Initialize@1000000000(VAR Value@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessageEvent";NewHeight@1000000001 : Text);
    VAR
      UIManipulationMgt@1000000002 : Codeunit 9062672;
    BEGIN
      Initialized := FALSE;
      //initialize control and libraries
      HtmlControlRequestProxy.SetRequestEvent(Value);
      HtmlControlRequestProxy.AddScriptLink('libraries/ace-builds-master/src-min/ace.js');
      HtmlControlRequestProxy.AddScriptLink('libraries/tnp.codeeditor/tnp.codeeditor.js');
      HtmlControlRequestProxy.SendResourceLinkRequest();
      //update client ui
      UIManipulationMgt.ClearScript();
      UIManipulationMgt.UseWindowHeight();
      IF (UIManipulationMgt.HasScript) THEN
        HtmlControlRequestProxy.SendJavaScriptRequest(UIManipulationMgt.GetScript);
    END;

    PROCEDURE LinksLoaded@1000000001();
    BEGIN
      HtmlControlRequestProxy.SendJavaScriptRequest('InitEditor("controlAddIn");');
      Initialized := TRUE;
      IF (EditorMode <> '') THEN
        SendSetModeRequest(EditorMode);
      IF (EditorTheme <> '') THEN
        SendSetThemeRequest(EditorTheme);
      IF (EditorText <> '') THEN
        SendSetValueRequest(EditorText);
    END;

    PROCEDURE HandleControlMessage@1000000002(ControlMessage@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.ControlMessage";VAR ClosePage@1000000001 : Boolean);
    BEGIN
      ClosePage := FALSE;
      CASE (ControlMessage.Type) OF
        'getvalue': HandleGetValue(ControlMessage.Content.Item('Result'), ClosePage);
      END;
    END;

    LOCAL PROCEDURE HandleGetValue@1000000013(NewValue@1000000000 : Text;VAR ClosePage@1000000001 : Boolean);
    BEGIN
      EditorText := NewValue;
      CASE (MessageMode) OF
        MessageMode::ExportValue: ExportToFile();
        MessageMode::GetValueAndClose: ClosePage := TRUE;
      END;
      MessageMode := MessageMode::" ";
    END;

    LOCAL PROCEDURE SendSetValueRequest@1000000007(NewValue@1000000000 : Text);
    BEGIN
      HtmlControlRequestProxy.SendCommandRequest('setvalue', NewValue);
    END;

    LOCAL PROCEDURE SendGetValueRequest@1000000008(NewMessageMode@1000000000 : Integer);
    BEGIN
      HtmlControlRequestProxy.SendCommandRequest('getvalue', '');
      MessageMode := NewMessageMode;
    END;

    LOCAL PROCEDURE SendSetModeRequest@1000000009(NewMode@1000000000 : Text);
    BEGIN
      HtmlControlRequestProxy.SendCommandRequest('setmode', NewMode);
    END;

    LOCAL PROCEDURE SendSetThemeRequest@1000000021(NewTheme@1000000000 : Text);
    BEGIN
      HtmlControlRequestProxy.SendCommandRequest('settheme', NewTheme);
    END;

    PROCEDURE ImportFromFile@1000000003();
    VAR
      InputStream@1000000002 : InStream;
      FileName@1000000001 : Text;
      BigText@1000000000 : BigText;
      Text@1000000003 : Text;
    BEGIN
      FileName := '';
      IF (UPLOADINTOSTREAM(txtImportFile, '', FileTypeFilter, FileName, InputStream)) THEN BEGIN
        BigText.READ(InputStream);
        Text := '';
        IF (BigText.LENGTH > 0) THEN
          BigText.GETSUBTEXT(Text, 1);
        SetText(Text);
      END;
    END;

    PROCEDURE ExportToFile@1000000004();
    VAR
      TempBlob@1000000001 : TEMPORARY Record 99008535;
      OutputStream@1000000002 : OutStream;
      InputStream@1000000003 : InStream;
      FileName@1000000000 : Text;
      Result@1000000004 : Boolean;
    BEGIN
      IF (MessageMode = MessageMode::ExportValue) THEN BEGIN
        IF (DefaultFileName = '') THEN
          FileName := txtDefaultFileName
        ELSE
          FileName := DefaultFileName;
        TempBlob.Blob.CREATEOUTSTREAM(OutputStream);
        OutputStream.WRITETEXT(EditorText);
        TempBlob.Blob.CREATEINSTREAM(InputStream);
        Result := DOWNLOADFROMSTREAM(InputStream, txtExportFile, '', FileTypeFilter, FileName);
      END ELSE
        SendGetValueRequest(MessageMode::ExportValue);
    END;

    PROCEDURE GetValueAndClose@1000000010();
    BEGIN
      SendGetValueRequest(MessageMode::GetValueAndClose);
    END;

    PROCEDURE SetText@1000000005(Text@1000000000 : Text);
    BEGIN
      EditorText := Text;
      IF (Initialized) THEN
        SendSetValueRequest(EditorText);
    END;

    PROCEDURE GetText@1000000006() : Text;
    BEGIN
      EXIT(EditorText);
    END;

    PROCEDURE SetMode@1000000017(NewMode@1000000000 : Text;NewFileTypeFilter@1000000001 : Text;NewFileName@1000000002 : Text);
    BEGIN
      EditorMode := NewMode;
      FileTypeFilter := NewFileTypeFilter;
      DefaultFileName := NewFileName;
      IF (Initialized) THEN
        SendSetModeRequest(EditorMode);
    END;

    PROCEDURE SetTheme@1000000018(NewTheme@1000000000 : Text);
    BEGIN
      EditorTheme := NewTheme;
      IF (Initialized) THEN
        SendSetThemeRequest(EditorTheme);
    END;

    BEGIN
    {
      //DOC NA2016.12 AZ 21/01/2016 - Created
      //DOC NA2016.15 JH 09/05/2016 - CfMD modifications
      //DOC NA2016.16 AZ 24/05/2016 - CfMD amendments
    }
    END.
  }
}

