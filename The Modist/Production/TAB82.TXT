OBJECT Table 82 Item Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=23/03/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45480;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               ItemJnlLine.SETRANGE("Journal Template Name",Name);
               ItemJnlLine.DELETEALL(TRUE);
               ItemJnlBatch.SETRANGE("Journal Template Name",Name);
               ItemJnlBatch.DELETEALL;
             END;

    OnRename=BEGIN
               ReservEngineMgt.RenamePointer(DATABASE::"Item Journal Line",
                 0,xRec.Name,'',0,0,
                 0,Name,'',0,0);
             END;

    CaptionML=[ENU=Item Journal Template;
               ENG=Item Journal Template];
    LookupPageID=Page261;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ENG=Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Test Report ID;
                                                              ENG=Test Report ID] }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Page));
                                                   OnValidate=BEGIN
                                                                IF "Page ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Page ID;
                                                              ENG=Page ID] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Posting Report ID;
                                                              ENG=Posting Report ID] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[ENU=Force Posting Report;
                                                              ENG=Force Posting Report] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"Inventory Posting - Test";
                                                                "Posting Report ID" := REPORT::"Item Register - Quantity";
                                                                "Whse. Register Report ID" := REPORT::"Warehouse Register - Quantity";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Item Journal";
                                                                      "Page ID" := PAGE::"Item Journal";
                                                                    END;
                                                                  Type::Transfer:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Item Reclass. Journal";
                                                                      "Page ID" := PAGE::"Item Reclass. Journal";
                                                                    END;
                                                                  Type::"Phys. Inventory":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Phys. Inventory Journal";
                                                                      "Page ID" := PAGE::"Phys. Inventory Journal";
                                                                    END;
                                                                  Type::Revaluation:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Revaluation Journal";
                                                                      "Page ID" := PAGE::"Revaluation Journal";
                                                                      "Test Report ID" := REPORT::"Revaluation Posting - Test";
                                                                      "Posting Report ID" := REPORT::"Item Register - Value";
                                                                    END;
                                                                  Type::Consumption:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Consumption Journal";
                                                                      "Page ID" := PAGE::"Consumption Journal";
                                                                    END;
                                                                  Type::Output:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Output Journal";
                                                                      "Page ID" := PAGE::"Output Journal";
                                                                    END;
                                                                  Type::Capacity:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Capacity Journal";
                                                                      "Page ID" := PAGE::"Capacity Journal";
                                                                    END;
                                                                  Type::"Prod. Order":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Production Journal";
                                                                      "Page ID" := PAGE::"Production Journal";
                                                                    END;
                                                                END;
                                                                IF Recurring THEN
                                                                  CASE Type OF
                                                                    Type::Item:
                                                                      "Page ID" := PAGE::"Recurring Item Jnl.";
                                                                    Type::Consumption:
                                                                      "Page ID" := PAGE::"Recurring Consumption Journal";
                                                                    Type::Output:
                                                                      "Page ID" := PAGE::"Recurring Output Journal";
                                                                    Type::Capacity:
                                                                      "Page ID" := PAGE::"Recurring Capacity Journal";
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order;
                                                                    ENG=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order];
                                                   OptionString=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                ItemJnlLine.SETRANGE("Journal Template Name",Name);
                                                                ItemJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              ENG=Source Code] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ENG=Reason Code] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=Recurring;
                                                              ENG=Recurring] }
    { 15  ;   ;Test Report Caption ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[ENU=Test Report Caption;
                                                              ENG=Test Report Caption];
                                                   Editable=No }
    { 16  ;   ;Page Caption        ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Page),
                                                                                                                Object ID=FIELD(Page ID)));
                                                   CaptionML=[ENU=Page Caption;
                                                              ENG=Page Caption];
                                                   Editable=No }
    { 17  ;   ;Posting Report Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[ENU=Posting Report Caption;
                                                              ENG=Posting Report Caption];
                                                   Editable=No }
    { 19  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ENG=No. Series] }
    { 20  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ENG=Posting No. Series] }
    { 21  ;   ;Whse. Register Report ID;Integer   ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   AccessByPermission=TableData 7302=R;
                                                   CaptionML=[ENU=Whse. Register Report ID;
                                                              ENG=Whse. Register Report ID] }
    { 22  ;   ;Whse. Register Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Whse. Register Report ID)));
                                                   AccessByPermission=TableData 7302=R;
                                                   CaptionML=[ENU=Whse. Register Report Caption;
                                                              ENG=Whse. Register Report Caption];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ENG=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1;ENG=must not be %1';
      ItemJnlBatch@1002 : Record 233;
      ItemJnlLine@1003 : Record 83;
      SourceCodeSetup@1004 : Record 242;
      ReservEngineMgt@1005 : Codeunit 99000831;

    BEGIN
    END.
  }
}

