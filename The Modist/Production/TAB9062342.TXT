OBJECT Table 9062342 Planning Board Table Style
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Planning Board Code");
               Rec.TESTFIELD("Planning Board Table Type");
               Rec.TESTFIELD("Style Application");
               CheckModifyPermission();
               IF (Rec.ID = 0) THEN
                 Rec.VALIDATE(ID, FindNewId());
               IF (Rec.Sequence = 0) THEN
                 Rec.VALIDATE(Sequence, FindNewIndex());
               InvalidateCache();
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               CheckModifyPermission();
               InvalidateCache();
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CheckModifyPermission();
               InvalidateCache();
             END;

    OnRename=BEGIN
               CheckModifyPermission();
             END;

    CaptionML=[ENU=Planning Board Table Style;
               ENG=Planning Board Table Style];
  }
  FIELDS
  {
    { 1   ;   ;Planning Board Code ;Code20        ;TableRelation="Planning Board Header";
                                                   CaptionML=[ENU=Planning Board Code;
                                                              ENG=Planning Board Code] }
    { 2   ;   ;Planning Board Table Type;Option   ;CaptionML=[ENU=Planning Board Table Type;
                                                              ENG=Planning Board Table Type];
                                                   OptionCaptionML=[ENU=" ,Resource,Entry";
                                                                    ENG=" ,Resource,Entry"];
                                                   OptionString=[ ,Resource,Entry] }
    { 3   ;   ;Style Application   ;Option        ;CaptionML=[ENU=Style Application;
                                                              ENG=Style Application];
                                                   OptionCaptionML=[ENU=" ,Foreground,Background,Border";
                                                                    ENG=" ,Foreground,Background,Border"];
                                                   OptionString=[ ,Foreground,Background,Border] }
    { 4   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Sequence            ;Integer       ;OnValidate=BEGIN
                                                                CASE (Rec."Style Application") OF
                                                                  Rec."Style Application"::Foreground:
                                                                    IF (Rec.Sequence > 16) THEN
                                                                      Rec.Sequence := 16;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sequence;
                                                              ENG=Sequence];
                                                   MinValue=0 }
    { 20  ;   ;Filter              ;Text250       ;OnLookup=BEGIN
                                                              PlanningBoardStyleMgt.FilterLookup(Rec);
                                                            END;

                                                   CaptionML=[ENU=Filter;
                                                              ENG=Filter] }
    { 21  ;   ;Colour Code         ;Code20        ;TableRelation="System Colour";
                                                   OnValidate=BEGIN
                                                                UpdateColourBitmap();
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (PlanningBoardToolkit.SystemColourLookup(Rec."Colour Code")) THEN
                                                                Rec.VALIDATE("Colour Code");
                                                            END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 22  ;   ;Font Size           ;Option        ;CaptionML=[ENU=Font Size;
                                                              ENG=Font Size];
                                                   OptionCaptionML=[ENU=" ,Very Small,Small,Medium,Large,Very Large";
                                                                    ENG=" ,Very Small,Small,Medium,Large,Very Large"];
                                                   OptionString=[ ,Very Small,Small,Medium,Large,Very Large] }
    { 23  ;   ;Font Style          ;Option        ;CaptionML=[ENU=Font Style;
                                                              ENG=Font Style];
                                                   OptionCaptionML=[ENU=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline";
                                                                    ENG=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline"];
                                                   OptionString=[ ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline] }
    { 24  ;   ;Line Width          ;Option        ;CaptionML=[ENU=Line Width;
                                                              ENG=Line Width];
                                                   OptionCaptionML=[ENU=" ,Very Thin,Thin,Medium,Thick,Very Thick";
                                                                    ENG=" ,Very Thin,Thin,Medium,Thick,Very Thick"];
                                                   OptionString=[ ,Very Thin,Thin,Medium,Thick,Very Thick] }
    { 25  ;   ;Line Style          ;Option        ;CaptionML=[ENU=Line Style;
                                                              ENG=Line Style];
                                                   OptionCaptionML=[ENU=" ,None,Solid,Dashed,Dotted";
                                                                    ENG=" ,None,Solid,Dashed,Dotted"];
                                                   OptionString=[ ,None,Solid,Dashed,Dotted] }
    { 26  ;   ;Colour Bitmap       ;BLOB          ;CaptionML=[ENU=Colour Bitmap;
                                                              ENG=Colour Bitmap];
                                                   SubType=Bitmap }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Planning Board Code,Planning Board Table Type,Style Application,ID;
                                                   Clustered=Yes }
    {    ;Planning Board Code,Planning Board Table Type,Style Application,Sequence }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlanningBoardHeader@1000000001 : Record 9062337;
      PlanningBoardTable@9062230 : Record 9062338;
      SystemColour@9062233 : Record 9062336;
      PlanningBoardStyleCaching@1000000000 : Codeunit 9062308;
      PlanningBoardStyleMgt@9062232 : Codeunit 9062317;
      PlanningBoardToolkit@9062231 : Codeunit 9062301;
      TnpToolkit@9062229 : Codeunit 9062230;

    PROCEDURE "--- General ---"@9062231();
    BEGIN
    END;

    PROCEDURE FindNewId@9062234() : Integer;
    VAR
      lPlanningBoardTableStyle@9062229 : Record 9062342;
    BEGIN
      //DOC NA2016.12 - Returning new ID
      //  <- The ID

      lPlanningBoardTableStyle.RESET();
      lPlanningBoardTableStyle.SETRANGE("Planning Board Code", Rec."Planning Board Code");
      lPlanningBoardTableStyle.SETRANGE("Planning Board Table Type", Rec."Planning Board Table Type");
      lPlanningBoardTableStyle.SETRANGE("Style Application", Rec."Style Application");
      IF (lPlanningBoardTableStyle.FINDLAST()) THEN
        EXIT(lPlanningBoardTableStyle.ID + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE FindNewIndex@9062233() : Integer;
    VAR
      lPlanningBoardTableStyle@9062229 : Record 9062342;
    BEGIN
      //DOC NA2016.12 - Returning new index
      //  <- The index

      lPlanningBoardTableStyle.RESET();
      lPlanningBoardTableStyle.SETCURRENTKEY("Planning Board Code", "Planning Board Table Type", "Style Application", Sequence);
      lPlanningBoardTableStyle.SETRANGE("Planning Board Code", Rec."Planning Board Code");
      lPlanningBoardTableStyle.SETRANGE("Planning Board Table Type", Rec."Planning Board Table Type");
      lPlanningBoardTableStyle.SETRANGE("Style Application", Rec."Style Application");
      IF (lPlanningBoardTableStyle.FINDLAST()) THEN
        EXIT(lPlanningBoardTableStyle.Sequence + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE InitialiseNewRecord@9062229();
    BEGIN
      //DOC NA2016.12 - Initialising new record

      Rec.VALIDATE(ID, FindNewId());
      Rec.VALIDATE(Sequence, FindNewIndex());
    END;

    PROCEDURE UpdateColourBitmap@9062230();
    BEGIN
      //DOC NA2016.12 - Updating colour bitmap from corresponding colour

      IF (SystemColour.GET(Rec."Colour Code")) THEN
      BEGIN
        SystemColour.CALCFIELDS(Bitmap);
        Rec."Colour Bitmap" := SystemColour.Bitmap;
      END
      ELSE
        CLEAR(Rec."Colour Bitmap");
    END;

    LOCAL PROCEDURE CheckModifyPermission@1000000039();
    VAR
      lEnhancedPermissionsMgt@1000000000 : Codeunit 9062277;
    BEGIN
      //DOC NA2016.15 - Checking modify permission for the planning board

      IF ( NOT (PlanningBoardHeader.GET(Rec."Planning Board Code"))) THEN
        EXIT;
      lEnhancedPermissionsMgt.CheckPlanningBoardModifyPermission(PlanningBoardHeader, USERID());
    END;

    LOCAL PROCEDURE "--- Internal ---"@1000000000();
    BEGIN
    END;

    LOCAL PROCEDURE InvalidateCache@1000000001();
    BEGIN
      //DOC NA2016.12 - Invalidating cache of the style

      PlanningBoardStyleCaching.InvalidateCache(Rec."Planning Board Code", Rec."Planning Board Table Type", Rec."Style Application");
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 25/09/2015 - Created
      //DOC NA2016.15 JH 05/05/2016 - Enhanced permissions
    }
    END.
  }
}

