OBJECT Page 11102089 OM - Search String in C/AL
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Search String in C/AL Code;
               ENU=Search String in C/AL Code;
               ESP=Search String in C/AL Code;
               FRA=Search String in C/AL Code;
               NLD=String zoeken in C/AL historie;
               DEA=Search String in C/AL Code];
    SaveValues=Yes;
    SourceTable=Table11102052;
    SourceTableView=SORTING(Object Type,Object No.)
                    WHERE(C/AL Code Present=CONST(Yes));
    PageType=Worksheet;
    OnOpenPage=VAR
                 CheckCALHistory@1000 : Codeunit 11102063;
               BEGIN
                 CheckCALHistory.SetCheckType(2);
                 IF CheckCALHistory.RUN THEN
                   ;
               END;

    OnFindRecord=BEGIN
                   EXIT(OnFindRecord2(Which));
                 END;

    OnNextRecord=BEGIN
                   EXIT(OnNextRecord2(Steps));
                 END;

    OnAfterGetRecord=BEGIN
                       NoOfTimesFoundHideValue := NOT TmpCALHistoryObject.GET("Entry No.");
                     END;

    OnAfterGetCurrRecord=BEGIN
                           ;
                         END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Name=Search;
                      CaptionML=[DEU=&Search;
                                 ENU=&Search;
                                 ESP=&Search;
                                 FRA=&Search;
                                 NLD=&Zoeken;
                                 DEA=&Search];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Find;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Search(SearchString, CaseSensitive, SearchInHistory);
                               END;
                                }
      { 3       ;1   ;ActionGroup;
                      CaptionML=[DEU=Functions;
                                 ENU=Functions;
                                 ESP=Functions;
                                 FRA=Functions;
                                 NLD=Functies;
                                 DEA=Functions] }
      { 4       ;2   ;ActionGroup;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter }
      { 5       ;3   ;Action    ;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.CheckMarkLock;

                                 GetSelectedRecords(CALHistoryObject);
                                 IF CALHistoryObject.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(CALHistoryObject."Object Type", '', CALHistoryObject."Object No.") THEN
                                       VersionControl.MarkObject(Object, TRUE);
                                   UNTIL CALHistoryObject.NEXT = 0;
                               END;
                                }
      { 6       ;3   ;Action    ;
                      CaptionML=[DEU=Remove Marks;
                                 ENU=Remove Marks;
                                 ESP=Remove Marks;
                                 FRA=Remove Marks;
                                 NLD=Verwijder markeringen;
                                 DEA=Remove Marks];
                      Image=RemoveFilterLines;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 GetSelectedRecords(CALHistoryObject);
                                 IF CALHistoryObject.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(CALHistoryObject."Object Type", '', CALHistoryObject."Object No.") THEN
                                       VersionControl.RemoveObjectMark(Object);
                                   UNTIL CALHistoryObject.NEXT = 0;

                                 VersionControl.RemoveMarkLock(TRUE);
                               END;
                                }
      { 7       ;3   ;Action    ;
                      ShortCutKey=Shift+Ctrl+M;
                      CaptionML=[DEU=Remove &all Marks;
                                 ENU=Remove &all Marks;
                                 ESP=Remove &all Marks;
                                 FRA=Remove &all Marks;
                                 NLD=Verwijder &alle markeringen;
                                 DEA=Remove &all Marks];
                      Image=ClearFilter;
                      OnAction=VAR
                                 VersionControl@1000 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.RemoveObjectMarks(FALSE);
                               END;
                                }
      { 8       ;1   ;ActionGroup;
                      CaptionML=[DEU=Object;
                                 ENU=Object;
                                 ESP=Object;
                                 FRA=Object;
                                 NLD=Object;
                                 DEA=Object] }
      { 9       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F2;
                      CaptionML=[DEU=Design;
                                 ENU=Design;
                                 ESP=Design;
                                 FRA=Design;
                                 NLD=Design;
                                 DEA=Design];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Design;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 TESTFIELD("Object Type");
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.DesignObject(Object);
                               END;
                                }
      { 10      ;2   ;Action    ;
                      ShortCutKey=Ctrl+R;
                      CaptionML=[DEU=Run;
                                 ENU=Run;
                                 ESP=Run;
                                 FRA=Run;
                                 NLD=Run;
                                 DEA=Run];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 TESTFIELD("Object Type");
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.RunObject(Object);
                               END;
                                }
      { 40      ;2   ;Action    ;
                      CaptionML=[DEU=Navigate;
                                 ENU=Navigate;
                                 ESP=Navigate;
                                 FRA=Navigate;
                                 NLD=Navigeren;
                                 DEA=Navigate];
                      Image=Navigate;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 ObjectMgt@1001 : Codeunit 11102046;
                               BEGIN
                                 Object.GET("Object Type", '', "Object No.");
                                 ObjectMgt.Navigate(Object);
                               END;
                                }
      { 11      ;2   ;Action    ;
                      ShortCutKey=Ctrl+D;
                      CaptionML=[DEU=&Code;
                                 ENU=&Code;
                                 ESP=&Code;
                                 FRA=&Code;
                                 NLD=&Code;
                                 DEA=&Code];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=DesignCodeBehind;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowFoundCode(SearchString, CaseSensitive);
                               END;
                                }
      { 12      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Export;
                                 ENU=Export;
                                 ESP=Export;
                                 FRA=Export;
                                 NLD=Exporteren;
                                 DEA=Export];
                      Image=Export;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Progressbar@1001 : Codeunit 11102044;
                                 SourceControl@1002 : Codeunit 11102050;
                                 CommonDialog@1003 : Codeunit 11102059;
                                 FileHandler@1004 : Codeunit 11102097;
                                 oFile@1005 : File;
                                 ServerFileName@1006 : Text;
                                 ctDefaultFileName@1007 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctExporting@1008 : TextConst 'DEU=Exporting...;ENU=Exporting...;ESP=Exporting...;FRA=Exporting...;NLD=Exporteren...;DEA=Exporting...';
                               BEGIN
                                 GetSelectedRecords(CALHistoryObject);
                                 CALHistoryObject.FINDSET;

                                 CommonDialog.AddKnownExtension('txt-o');
                                 IF NOT CommonDialog.SaveFile(ExportFileName, ServerFileName, ctDefaultFileName) THEN
                                   EXIT;

                                 oFile.TEXTMODE(TRUE);
                                 oFile.WRITEMODE(TRUE);
                                 oFile.CREATE(ServerFileName);
                                 Progressbar.Open(ctExporting, 1);
                                 Progressbar.BarSize := CALHistoryObject.COUNT;
                                 REPEAT
                                   Progressbar.Text1 :=
                                     STRSUBSTNO(
                                       '%1 %2 %3',
                                       CALHistoryObject."Object Type", CALHistoryObject."Object No.", CALHistoryObject."Object Name");
                                   Progressbar.AddToBar(1);
                                   SourceControl.AppendHistoryObjectToFile(oFile, CALHistoryObject."Entry No.");
                                 UNTIL CALHistoryObject.NEXT = 0;
                                 Progressbar.Close;
                                 oFile.CLOSE;

                                 FileHandler.DownloadToClient(ServerFileName, ExportFileName, ctDefaultFileName);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=[DEU=Update C/AL History;
                                 ENU=Update C/AL History;
                                 ESP=Update C/AL History;
                                 FRA=Update C/AL History;
                                 NLD=C/AL Historie &bijwerken;
                                 DEA=Update C/AL History];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 SourceControl@1001 : Codeunit 11102050;
                               BEGIN
                                 Object.SETFILTER(Type, '<>%1', Object.Type::TableData);
                                 Object.SETFILTER("BLOB Size", '>0');
                                 SourceControl.UpdateCALHistory(Object, FALSE, TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      CaptionML=[DEU=Set My Objects Filter;
                                 ENU=Set My Objects Filter;
                                 ESP=Set My Objects Filter;
                                 FRA=Set My Objects Filter;
                                 NLD=Zet mijn objectenfilter;
                                 DEA=Set My Objects Filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditFilter;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 User@1000 : Record 11102056;
                                 Object@1001 : Record 2000000001;
                                 TableFilterMgt@1002 : Codeunit 11102100;
                                 RecRef@1003 : RecordRef;
                               BEGIN
                                 User.GetActiveUser(FALSE);
                                 IF FORMAT(User."My Objects Filter") IN ['', TableFilterMgt.EmptyTableFilter(DATABASE::Object)] THEN
                                   EXIT;

                                 RecRef.GETTABLE(Object);
                                 TableFilterMgt.FilterRecRef(RecRef, FORMAT(User."My Objects Filter"), 0);
                                 RecRef.SETTABLE(Object);

                                 Object.COPYFILTER(Type, "Object Type");
                                 Object.COPYFILTER(ID, "Object No.");

                                 IF Object.GETFILTER(Name) = '' THEN
                                   SETRANGE("Object Name")
                                 ELSE
                                   SETFILTER("Object Name", Object.GETFILTER(Name));

                                 Object.COPYFILTER(Modified, Modified);
                                 Object.COPYFILTER(Date, "Object Date");
                                 Object.COPYFILTER(Time, "Object Time");

                                 IF Object.GETFILTER("Version List") = '' THEN
                                   SETRANGE("Version List")
                                 ELSE
                                   SETFILTER("Version List", Object.GETFILTER("Version List"));

                                 IF Object.GETFILTER(Caption) = '' THEN
                                   SETRANGE("Object Caption")
                                 ELSE
                                   SETFILTER("Object Caption", Object.GETFILTER(Caption));
                               END;
                                }
      { 15      ;0   ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 16      ;1   ;ActionGroup;
                      CaptionML=[DEU=Ob&ject;
                                 ENU=Ob&ject;
                                 ESP=Ob&ject;
                                 FRA=Ob&ject;
                                 NLD=Ob&ject;
                                 DEA=Ob&ject] }
      { 17      ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+O;
                      CaptionML=[DEU=Modi&fications;
                                 ENU=Modi&fications;
                                 ESP=Modi&fications;
                                 FRA=Modi&fications;
                                 NLD=Wijzigingen;
                                 DEA=Modi&fications];
                      RunObject=Page 11102049;
                      RunPageView=SORTING(Object Type,Object No.,Insert Date,Insert Time);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Image=Reconcile }
      { 18      ;2   ;Action    ;
                      CaptionML=[DEU=Projects;
                                 ENU=Projects;
                                 ESP=Projects;
                                 FRA=Projects;
                                 NLD=Projecten;
                                 DEA=Projects];
                      RunObject=Page 11102042;
                      RunPageView=SORTING(Object Type,Object No.);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Image=Journals }
      { 19      ;2   ;Separator  }
      { 20      ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+H;
                      CaptionML=[DEU=C/AL &History;
                                 ENU=C/AL &History;
                                 ESP=C/AL &History;
                                 FRA=C/AL &History;
                                 NLD=C/AL &Historie;
                                 DEA=C/AL &History];
                      RunObject=Page 11102072;
                      RunPageView=SORTING(Object Type,Object No.);
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Image=History }
      { 21      ;2   ;Action    ;
                      ShortCutKey=Ctrl+Alt+C;
                      CaptionML=[DEU=Last &Changes;
                                 ENU=Last &Changes;
                                 ESP=Last &Changes;
                                 FRA=Last &Changes;
                                 NLD=Laatste wij&zigingen;
                                 DEA=Last &Changes];
                      Image=ShowMatrix;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 RightCALHistoryObject@1001 : Record 11102052;
                                 SourceControl@1002 : Codeunit 11102050;
                                 ctError@1003 : TextConst 'DEU=There is only one version of the C/AL code present.;ENU=There is only one version of the C/AL code present.;ESP=There is only one version of the C/AL code present.;FRA=There is only one version of the C/AL code present.;NLD=Er is maar ‚‚n versie van de C/AL code aanwezig.;DEA=There is only one version of the C/AL code present.';
                               BEGIN
                                 CALHistoryObject.GoToLastRecord("Object Type", "Object No.");
                                 RightCALHistoryObject := CALHistoryObject;

                                 IF CALHistoryObject.NEXT(-1) = 0 THEN
                                   ERROR(ctError);

                                 SourceControl.Compare(CALHistoryObject, RightCALHistoryObject);
                               END;
                                }
      { 22      ;2   ;Separator  }
      { 23      ;2   ;Action    ;
                      CaptionML=[DEU=Where Used;
                                 ENU=Where Used;
                                 ESP=Where Used;
                                 FRA=Where Used;
                                 NLD=Waar-&gebruikt;
                                 DEA=Where Used];
                      RunObject=Page 11102087;
                      RunPageLink=Object Type=FIELD(Object Type),
                                  Object No.=FIELD(Object No.);
                      Image=Where-Used }
    }
  }
  CONTROLS
  {
    { 24  ;0   ;Container ;
                ContainerType=ContentArea }

    { 25  ;1   ;Group      }

    { 26  ;2   ;Field     ;
                CaptionML=[DEU=Search for;
                           ENU=Search for;
                           ESP=Search for;
                           FRA=Search for;
                           NLD=Zoeken naar;
                           DEA=Search for];
                SourceExpr=SearchString }

    { 27  ;2   ;Field     ;
                CaptionML=[DEU=Search in History;
                           ENU=Search in History;
                           ESP=Search in History;
                           FRA=Search in History;
                           NLD=In historie zoeken;
                           DEA=Search in History];
                SourceExpr=SearchInHistory;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 28  ;2   ;Field     ;
                CaptionML=[DEU=Case Sensitive;
                           ENU=Case Sensitive;
                           ESP=Case Sensitive;
                           FRA=Case Sensitive;
                           NLD=Hoofdlettergevoelig;
                           DEA=Case Sensitive];
                SourceExpr=CaseSensitive }

    { 29  ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 30  ;2   ;Field     ;
                SourceExpr="Entry No.";
                Visible=FALSE }

    { 31  ;2   ;Field     ;
                SourceExpr="Object Type" }

    { 32  ;2   ;Field     ;
                SourceExpr="Object No.";
                LookupPageID=OM - Object List }

    { 33  ;2   ;Field     ;
                SourceExpr="Object Name" }

    { 34  ;2   ;Field     ;
                SourceExpr="Object Caption";
                Visible=FALSE }

    { 35  ;2   ;Field     ;
                CaptionML=[DEU=No. of Times Found;
                           ENU=No. of Times Found;
                           ESP=No. of Times Found;
                           FRA=No. of Times Found;
                           NLD=Aantal keer gevonden;
                           DEA=No. of Times Found];
                SourceExpr=NoOfTimesFound;
                HideValue=NoOfTimesFoundHideValue;
                OnDrillDown=BEGIN
                              ShowFoundCode(SearchString, CaseSensitive);
                            END;
                             }

    { 36  ;2   ;Field     ;
                SourceExpr="Version List" }

    { 41  ;2   ;Field     ;
                SourceExpr="Object Date" }

    { 37  ;2   ;Field     ;
                SourceExpr="Object Time" }

    { 38  ;1   ;Field     ;
                SourceExpr=ExportFileName;
                Visible=FALSE }

    { 39  ;1   ;Field     ;
                SourceExpr=ImportFileName;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      TmpCALHistoryObject@1000 : TEMPORARY Record 11102052;
      TmpCALHistoryLine@1001 : TEMPORARY Record 11102053;
      ExportFileName@1002 : Text;
      ImportFileName@1003 : Text;
      SearchString@1004 : Text;
      Found@1005 : Integer;
      CaseSensitive@1006 : Boolean;
      NoOfTimesFoundHideValue@1007 : Boolean INDATASET;
      SearchInHistory@1008 : Boolean;

    LOCAL PROCEDURE OnFindRecord2@1(Which@1000 : Text) : Boolean;
    VAR
      PreviousRecord@1001 : Record 11102052;
    BEGIN
      IF SearchInHistory THEN
        EXIT(FIND(Which))
      ELSE BEGIN

        PreviousRecord := Rec;

        IF NOT FIND(Which) THEN
          EXIT(FALSE);

        IF NOT IsLastEntry(Rec) THEN
          CASE Which OF
            '-', '=><' :
              WHILE NOT IsLastEntry(Rec) DO
                IF NEXT(1) = 0 THEN BEGIN
                  Rec := PreviousRecord;
                  EXIT(FALSE);
                END;

            '+' :
              WHILE NOT IsLastEntry(Rec) DO
                IF NEXT(-1) = 0 THEN BEGIN
                  Rec := PreviousRecord;
                  EXIT(FALSE);
                END;
          END;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE OnNextRecord2@2(Steps@1000 : Integer) : Integer;
    VAR
      PreviousRecord@1001 : Record 11102052;
      OneStep@1002 : Integer;
      StepsDone@1003 : Integer;
      EndOfLoop@1004 : Boolean;
    BEGIN
      IF SearchInHistory THEN
        EXIT(NEXT(Steps))
      ELSE BEGIN
        PreviousRecord := Rec;

        IF Steps > 0 THEN
          OneStep := 1
        ELSE
          OneStep := -1;

        WHILE (ABS(Steps) > ABS(StepsDone)) AND NOT EndOfLoop DO BEGIN
          EndOfLoop := NEXT(OneStep) = 0;
          IF NOT EndOfLoop THEN
            IF IsLastEntry(Rec) THEN BEGIN
              PreviousRecord := Rec;
              StepsDone += OneStep;
            END;

        END;

        Rec := PreviousRecord;

        EXIT(StepsDone);

      END;
    END;

    LOCAL PROCEDURE Search@3(SearchString2@1000 : Text;CaseSensitive2@1001 : Boolean;SearchInHistory2@1002 : Boolean);
    VAR
      CALHistoryObject@1003 : Record 11102052;
      CALHistoryLine@1004 : Record 11102053;
      Object@1005 : Record 2000000001;
      Progressbar@1006 : Codeunit 11102044;
      BeginLastHalf@1007 : Text;
      EndFirstHalf@1008 : Text;
      HalfNoOfCharacters@1009 : Integer;
      SkipThisOne@1010 : Boolean;
      ctEnterASearchString@1011 : TextConst 'DEU=Enter a search string.;ENU=Enter a search string.;ESP=Enter a search string.;FRA=Enter a search string.;NLD=Geef een zoekstring op.;DEA=Enter a search string.';
      ctSearching@1012 : TextConst 'DEU=Searching...;ENU=Searching...;ESP=Searching...;FRA=Searching...;NLD=Zoeken...;DEA=Searching...';
    BEGIN
      IF SearchString2 = '' THEN
        ERROR(FORMAT(ctEnterASearchString));

      TmpCALHistoryObject.RESET;
      TmpCALHistoryObject.DELETEALL;
      TmpCALHistoryLine.RESET;
      TmpCALHistoryLine.DELETEALL;

      Found := 0;

      CurrPage.SETSELECTIONFILTER(CALHistoryObject);
      IF CALHistoryObject.COUNT <= 1 THEN BEGIN
        CALHistoryObject.RESET;
        CALHistoryObject.COPYFILTERS(Rec);
      END;

      MARKEDONLY(TRUE);
      CLEARMARKS;

      CALHistoryObject.SETCURRENTKEY("Object Type", "Object No.");

      HalfNoOfCharacters := ROUND(STRLEN(SearchString2) / 2, 1, '<');

      EndFirstHalf := COPYSTR(SearchString2, 1, HalfNoOfCharacters);
      BeginLastHalf := COPYSTR(SearchString2, STRLEN(SearchString2) - HalfNoOfCharacters + 1);

      Progressbar.Open(ctSearching, 2);
      Progressbar.BarSize := CALHistoryObject.COUNT;
      IF CALHistoryObject.FINDSET THEN
        REPEAT

          TmpCALHistoryObject := CALHistoryObject;
          TmpCALHistoryObject."Branch Order" := 0;
          TmpCALHistoryObject.INSERT;

          Progressbar.AddToBar(1);

          CALHistoryObject.FillObjectFields(Object);

          SkipThisOne := FALSE;
          IF NOT SearchInHistory2 THEN
            SkipThisOne := NOT IsLastEntry(CALHistoryObject);

          IF NOT SkipThisOne THEN BEGIN
            Progressbar.Text1Object := Object;

            CALHistoryLine.SETRANGE("Entry No.", CALHistoryObject."Entry No.");
            CALHistoryLine.SETFILTER(Code, CreateFilter(SearchString2, CaseSensitive2));

            IF CALHistoryLine.FINDSET THEN
              REPEAT
                IF CaseSensitive2 THEN BEGIN
                  IF STRPOS(CALHistoryLine.Code, SearchString2) > 0 THEN
                    AddToFoundRecords(CALHistoryObject, CALHistoryLine, Progressbar);
                END ELSE
                  IF STRPOS(LOWERCASE(CALHistoryLine.Code), LOWERCASE(SearchString2)) > 0 THEN
                    AddToFoundRecords(CALHistoryObject, CALHistoryLine, Progressbar);
              UNTIL CALHistoryLine.NEXT = 0;

            CALHistoryLine.SETFILTER(Code, CreateFilter(EndFirstHalf, CaseSensitive2));
            IF CALHistoryLine.FINDSET THEN
              REPEAT
                IF StringPresentInSubLines(CALHistoryLine, SearchString2, CaseSensitive2) THEN
                  AddToFoundRecords(CALHistoryObject, CALHistoryLine, Progressbar);
              UNTIL CALHistoryLine.NEXT = 0;

            CALHistoryLine.SETFILTER(Code, CreateFilter(BeginLastHalf, CaseSensitive2));
            IF CALHistoryLine.FINDSET THEN
              REPEAT
                IF StringPresentInSubLines(CALHistoryLine, SearchString2, CaseSensitive2) THEN
                  AddToFoundRecords(CALHistoryObject, CALHistoryLine, Progressbar);
              UNTIL CALHistoryLine.NEXT = 0;
          END;

        UNTIL CALHistoryObject.NEXT = 0;
    END;

    LOCAL PROCEDURE StringPresentInSubLines@4(VAR CALHistoryLine@1000 : Record 11102053;SearchString2@1001 : Text;CaseSensitive2@1002 : Boolean) : Boolean;
    VAR
      CALHistoryLine2@1003 : Record 11102053;
      LongCALLine@1004 : Text;
    BEGIN
      CALHistoryLine2.SETRANGE("Entry No.", CALHistoryLine."Entry No.");
      CALHistoryLine2.SETRANGE("Line No.", CALHistoryLine."Line No.");
      IF CALHistoryLine2.COUNT = 1 THEN
        EXIT;

      IF CALHistoryLine2.FINDSET THEN
        REPEAT
          LongCALLine += CALHistoryLine2.Code;
          IF CaseSensitive2 THEN BEGIN
            IF STRPOS(LongCALLine, SearchString2) > 0 THEN
              EXIT(TRUE);
          END ELSE
            IF STRPOS(LOWERCASE(LongCALLine), LOWERCASE(SearchString2)) > 0 THEN
              EXIT(TRUE);

          IF STRLEN(LongCALLine) > 750 THEN
            LongCALLine := COPYSTR(LongCALLine, 250);

        UNTIL CALHistoryLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE AddToFoundRecords@5(VAR CALHistoryObject@1000 : Record 11102052;VAR CALHistoryLine@1001 : Record 11102053;VAR Progressbar@1002 : Codeunit 11102044);
    VAR
      CurrentRecord@1003 : Record 11102052;
      ctFound@1004 : TextConst 'DEU=No. of Times Found: %1;ENU=No. of Times Found: %1;ESP=No. of Times Found: %1;FRA=No. of Times Found: %1;NLD=Aantal keer gevonden: %1;DEA=No. of Times Found: %1';
    BEGIN
      IF NOT TmpCALHistoryLine.GET(CALHistoryLine."Entry No.", CALHistoryLine."Line No.", 1) THEN BEGIN
        Found += 1;

        Progressbar.Text2 := STRSUBSTNO(ctFound, Found);

        TmpCALHistoryLine := CALHistoryLine;
        TmpCALHistoryLine."Sub Line No." := 1;
        TmpCALHistoryLine.INSERT;

        CurrentRecord := Rec;
        Rec := CALHistoryObject;
        MARK(TRUE);
        Rec := CurrentRecord;

        IF TmpCALHistoryObject.GET(CALHistoryObject."Entry No.") THEN BEGIN
          TmpCALHistoryObject."Branch Order" += 1;
          TmpCALHistoryObject.MODIFY;
        END ELSE BEGIN
          TmpCALHistoryObject := CALHistoryObject;
          TmpCALHistoryObject."Branch Order" := 1;
          TmpCALHistoryObject.INSERT;
        END;

      END;
    END;

    LOCAL PROCEDURE NoOfTimesFound@6() : Integer;
    BEGIN
      IF TmpCALHistoryObject.GET("Entry No.") THEN
        EXIT(TmpCALHistoryObject."Branch Order");
    END;

    LOCAL PROCEDURE ShowFoundCode@7(SearchString@1000 : Text;MatchCase@1001 : Boolean);
    VAR
      Setup@1002 : Record 11102035;
      CALHistoryLines@1003 : Page 11102073;
      NavgateEditor@1004 : Page 11175819;
    BEGIN
      TmpCALHistoryLine.SETRANGE("Entry No.", "Entry No.");
      IF TmpCALHistoryLine.FINDFIRST THEN
        ;
      Setup.CustomGet;
      CASE Setup.Editor OF

        Setup.Editor::"Object Manager" :
          BEGIN
            CALHistoryLines.SetCALHistoryEntryNo("Entry No.");
            CALHistoryLines.SetHighLightedLines(TmpCALHistoryLine);
            CALHistoryLines.RUN;
          END;

        Setup.Editor::Navgate :
          BEGIN
            NavgateEditor.SetCALHistoryEntryNo("Entry No.");
            NavgateEditor.SetSearchString(SearchString, MatchCase);
            NavgateEditor.RUN;
          END;

      END;
    END;

    LOCAL PROCEDURE IsLastEntry@8(VAR CALHistoryObject@1000 : Record 11102052) : Boolean;
    VAR
      CALHistoryObject2@1001 : Record 11102052;
      Object@1002 : Record 2000000001;
    BEGIN
      CALHistoryObject2.SETCURRENTKEY("Object Type", "Object No.", "Entry No.");
      CALHistoryObject2.SETRANGE("Object Type", CALHistoryObject."Object Type");
      CALHistoryObject2.SETRANGE("Object No.", CALHistoryObject."Object No.");
      CALHistoryObject2.SETFILTER("Entry No.", '>%1', CALHistoryObject."Entry No.");
      CALHistoryObject2.SETRANGE("C/AL Code Present", TRUE);
      IF CALHistoryObject2.ISEMPTY THEN BEGIN

        Object.SETRANGE(Type, CALHistoryObject."Object Type");
        Object.SETRANGE(ID, CALHistoryObject."Object No.");
        IF Object.ISEMPTY THEN
          EXIT(FALSE);

        EXIT(CALHistoryObject."Entry No." <> 0);
      END;
    END;

    LOCAL PROCEDURE CreateFilter@9(String@1000 : Text;CaseSensitive@1001 : Boolean) : Text;
    VAR
      i@1002 : Integer;
    BEGIN
      IF STRPOS(String, '''') > 0 THEN BEGIN
        i := 1;
        WHILE i <= STRLEN(String) DO
          IF String[i] = '''' THEN BEGIN
            String := DELSTR(String, i + 1) + COPYSTR(String, i);
            i += 2;
          END ELSE
            i += 1;
      END;

      IF CaseSensitive THEN
        EXIT('''*' + String + '*''')
      ELSE
        EXIT('''@*' + String + '*''');
    END;

    LOCAL PROCEDURE GetSelectedRecords@10(VAR CALHistoryObject@1000 : TEMPORARY Record 11102052);
    VAR
      OriginalRec@1001 : Record 11102052;
    BEGIN
      CurrPage.SETSELECTIONFILTER(CALHistoryObject);
      CALHistoryObject.MARKEDONLY(TRUE);
      IF CALHistoryObject.ISEMPTY OR (CALHistoryObject.COUNT = 1) THEN BEGIN

        CALHistoryObject.RESET;

        OriginalRec := Rec;
        IF OnFindRecord2('-') THEN
          REPEAT
            CALHistoryObject := Rec;
            CALHistoryObject.MARK(TRUE);
          UNTIL OnNextRecord2(1) = 0;
        Rec := OriginalRec;

        CALHistoryObject.MARKEDONLY(TRUE);

      END;
    END;

    BEGIN
    END.
  }
}

