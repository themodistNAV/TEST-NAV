OBJECT Codeunit 9062733 Razor Template Engine Cache
{
  OBJECT-PROPERTIES
  {
    Date=27/06/16;
    Time=[ 5:39:13 PM];
    Version List=NA2016.16;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EngineCache@1000000000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Hashtable";

    PROCEDURE Initialize@1000000001();
    BEGIN
      IF (ISNULL(EngineCache)) THEN
        EngineCache := EngineCache.Hashtable();
    END;

    PROCEDURE Clear@1000000002();
    BEGIN
      Initialize();
      EngineCache.Clear();
    END;

    PROCEDURE Add@1000000003(Name@1000000000 : Text;VAR TemplateEngine@1000000001 : DotNet "'TheNavPeople.RazorTemplateEngine.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7648a032f3787148'.TheNavPeople.RazorTemplateEngine.XmlEngine");
    BEGIN
      EngineCache.Add(Name, TemplateEngine);
    END;

    PROCEDURE HasValue@1000000004(Name@1000000000 : Text) : Boolean;
    BEGIN
      EXIT(EngineCache.ContainsKey(Name));
    END;

    PROCEDURE Get@1000000005(Name@1000000000 : Text;VAR EngineTemplate@1000000001 : DotNet "'TheNavPeople.RazorTemplateEngine.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7648a032f3787148'.TheNavPeople.RazorTemplateEngine.XmlEngine") : Boolean;
    BEGIN
      IF (NOT(HasValue(Name))) THEN BEGIN
        CLEAR(EngineTemplate);
        EXIT(FALSE);
      END;

      EngineTemplate := EngineCache.Item(Name);
      EXIT(TRUE);
    END;

    PROCEDURE Remove@1000000006(Name@1000000000 : Text);
    BEGIN
      IF (ISNULL(EngineCache)) THEN
        EXIT;
      IF (HasValue(Name)) THEN
        EngineCache.Remove(Name);
    END;

    PROCEDURE GetCacheKey@1000000000(TemplateCode@1000000000 : Code[50];ViewCode@1000000001 : Code[50]) : Text;
    BEGIN
      EXIT(TemplateCode + ':a:' + ViewCode);
    END;

    PROCEDURE HtmlResourceModified@1000000007(ResourceCode@1000000000 : Code[50]);
    VAR
      HtmlControlResource@1000000001 : Record 9062801;
    BEGIN
      IF (ISNULL(EngineCache)) THEN
        EXIT;
      HtmlControlResource.RESET();
      HtmlControlResource.SETRANGE("Html Resource Code", ResourceCode);
      IF (HtmlControlResource.FINDSET()) THEN BEGIN
        REPEAT
          Remove(GetCacheKey(HtmlControlResource."Template Code", HtmlControlResource."View Code"));
        UNTIL (HtmlControlResource.NEXT() = 0);
      END;
    END;

    BEGIN
    {
      //DOC NA2016.15 AZ 14/04/2016 - Created
      //DOC NA2016.16 AZ 02/06/2016 - Bugfixes
    }
    END.
  }
}

