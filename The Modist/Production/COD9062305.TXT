OBJECT Codeunit 9062305 Planning Board Event Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=10/11/15;
    Time=11:02:24 PM;
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PlanningBoardSetup@9062231 : Record 9062335;
      PlanningBoardHeader@9062230 : Record 9062337;
      PlanningBoardEvent@1000000000 : Record 9062345;
      PlanningBoardAction@9062232 : Record 9062344;

    PROCEDURE VerifyEvents@1000000003(pPlanningBoardHeader@1000000000 : Record 9062337);
    BEGIN
      //DOC NA2016.12 - Verifying events for planning board
      //  -> pPlanningBoardHeader: The planning board

      PlanningBoardHeader := pPlanningBoardHeader;

      VerifyEvent(PlanningBoardEvent."Event Type"::"Resource Double-Clicked");
      VerifyEvent(PlanningBoardEvent."Event Type"::"Entry Double-Clicked");
      VerifyEvent(PlanningBoardEvent."Event Type"::"Entry Dropped");
      VerifyEvent(PlanningBoardEvent."Event Type"::"Entry Resized");
      VerifyEvent(PlanningBoardEvent."Event Type"::"Entry Cloned");
    END;

    LOCAL PROCEDURE VerifyEvent@1000000006(pEventType@1000000001 : Integer);
    BEGIN
      //DOC NA2016.12 - Verifying an event for planning board
      //  -> pEventType: Event type
      //  -> pHandle: Flag to handle this command as default or not

      PlanningBoardEvent.RESET();
      PlanningBoardEvent.SETRANGE("Planning Board Code", PlanningBoardHeader.Code);
      PlanningBoardEvent.SETRANGE("Event Type", pEventType);
      IF (PlanningBoardEvent.ISEMPTY()) THEN
      BEGIN
        CLEAR(PlanningBoardEvent);
        PlanningBoardEvent.INIT();
        PlanningBoardEvent."Planning Board Code" := PlanningBoardHeader.Code;
        PlanningBoardEvent.INSERT(TRUE);
        PlanningBoardEvent.VALIDATE("Event Type", pEventType);
        PlanningBoardEvent.MODIFY(TRUE);
      END;
    END;

    PROCEDURE EventTypeToActionType@9062232(pEventType@9062230 : Integer) : Integer;
    VAR
      lActionType@9062231 : Integer;
    BEGIN
      //DOC NA2016.12 - Converting event type to action type
      //  -> pEventType: The event type
      //  <- The action type

      CLEAR(lActionType);

      CASE (pEventType) OF
        PlanningBoardEvent."Event Type"::"Resource Double-Clicked":
          CASE (PlanningBoardSetup."On Resource Double-Click") OF
            PlanningBoardSetup."On Resource Double-Click"::"View Resource":
              lActionType := PlanningBoardAction."Action Type"::"View Resource";
            PlanningBoardSetup."On Resource Double-Click"::"Edit Resource":
              lActionType := PlanningBoardAction."Action Type"::"Edit Resource";
          END;
        PlanningBoardEvent."Event Type"::"Entry Double-Clicked":
          CASE (PlanningBoardSetup."On Entry Double-Click") OF
            PlanningBoardSetup."On Entry Double-Click"::"View Entry":
              lActionType := PlanningBoardAction."Action Type"::"View Entry";
            PlanningBoardSetup."On Entry Double-Click"::"Edit Entry":
              lActionType := PlanningBoardAction."Action Type"::"Edit Entry";
          END;
        PlanningBoardEvent."Event Type"::"Entry Dropped":
          lActionType := PlanningBoardAction."Action Type"::"Drag Entry";
        PlanningBoardEvent."Event Type"::"Entry Resized":
          lActionType := PlanningBoardAction."Action Type"::"Resize Entry";
        PlanningBoardEvent."Event Type"::"Entry Cloned":
          lActionType := PlanningBoardAction."Action Type"::"Clone Entry";
      END;

      EXIT(lActionType);
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 11/10/2015 - Created
    }
    END.
  }
}

