OBJECT Page 10510 Check Preview GB
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVGB7.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Check Preview GB;
               ENG=Cheque Preview GB];
    SourceTable=Table81;
    DataCaptionExpr="Document No." + ' ' + CheckToAddr[1];
    PageType=Card;
    OnOpenPage=BEGIN
                 CompanyInfo.GET;
                 FormatAddr.Company(CompanyAddr,CompanyInfo);
               END;

    OnAfterGetRecord=BEGIN
                       CalcCheck;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group      }

    { 1040005;2;Field     ;
                CaptionML=[ENU=Pay;
                           ENG=Pay];
                SourceExpr=UPPERCASE(CheckToAddr[1]) }

    { 1040003;2;Field     ;
                Name=AmountInText;
                SourceExpr=NumberText[1] }

    { 1040004;2;Field     ;
                SourceExpr=NumberText[2] }

    { 1040000;2;Field     ;
                SourceExpr=CheckStatusText }

    { 1040001;2;Field     ;
                SourceExpr=CheckDateText }

    { 1040002;2;Field     ;
                SourceExpr=CheckAmountText }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Printed Check;ENG=Printed Cheque';
      Text001@1001 : TextConst 'ENU=Cheque Not Printed;ENG=Cheque Not Printed';
      GenJnlLine@1002 : Record 81;
      Cust@1003 : Record 18;
      Vend@1004 : Record 23;
      BankAcc@1005 : Record 270;
      CompanyInfo@1006 : Record 79;
      CheckReport@1007 : Report 1401;
      FormatAddr@1008 : Codeunit 365;
      CheckToAddr@1009 : ARRAY [8] OF Text[50];
      CompanyAddr@1010 : ARRAY [8] OF Text[50];
      NumberText@1011 : ARRAY [2] OF Text[80];
      CheckStatusText@1012 : Text[30];
      CheckDateText@1040000 : Text[30];
      CheckAmountText@1040001 : Text[20];
      CheckAmount@1013 : Decimal;

    LOCAL PROCEDURE CalcCheck@1();
    BEGIN
      IF "Check Printed" THEN BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        IF "Bal. Account No." = '' THEN
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::" ")
        ELSE
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::"Computer Check");
        GenJnlLine.SETRANGE("Check Printed",TRUE);
        CheckStatusText := Text000;
      END ELSE BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        GenJnlLine.SETRANGE("Account Type","Account Type");
        GenJnlLine.SETRANGE("Account No.","Account No.");
        GenJnlLine.SETRANGE("Bal. Account Type","Bal. Account Type");
        GenJnlLine.SETRANGE("Bal. Account No.","Bal. Account No.");
        GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type");
        CheckStatusText := Text001;
      END;

      CheckAmount := 0;
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          CheckAmount := CheckAmount + GenJnlLine.Amount;
        UNTIL GenJnlLine.NEXT = 0;

      IF CheckAmount < 0 THEN
        CheckAmount := 0;

      CheckReport.InitTextVariable;
      CheckReport.FormatNoText(NumberText,CheckAmount,GenJnlLine."Currency Code");

      CASE GenJnlLine."Account Type" OF
        GenJnlLine."Account Type"::"G/L Account":
          BEGIN
            CLEAR(CheckToAddr);
            CheckToAddr[1] := GenJnlLine.Description;
          END;
        GenJnlLine."Account Type"::Customer:
          BEGIN
            Cust.GET(GenJnlLine."Account No.");
            Cust.Contact := '';
            FormatAddr.Customer(CheckToAddr,Cust);
          END;
        GenJnlLine."Account Type"::Vendor:
          BEGIN
            Vend.GET(GenJnlLine."Account No.");
            Vend.Contact := '';
            FormatAddr.Vendor(CheckToAddr,Vend);
          END;
        GenJnlLine."Account Type"::"Bank Account":
          BEGIN
            BankAcc.GET(GenJnlLine."Account No.");
            BankAcc.Contact := '';
            FormatAddr.BankAcc(CheckToAddr,BankAcc);
          END;
        GenJnlLine."Account Type"::"Fixed Asset":
          GenJnlLine.FIELDERROR("Account Type");
      END;

      CheckDateText := UPPERCASE(FORMAT("Posting Date",9,'<Day,2><Month Text,3><Year4>'));

      CheckAmountText := FORMAT(CheckAmount,0,'**<Sign><Integer>-<Decimals,3>**');
      CheckAmountText := DELCHR(CheckAmountText,'=','.');
    END;

    BEGIN
    END.
  }
}

