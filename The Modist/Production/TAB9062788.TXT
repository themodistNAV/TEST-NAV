OBJECT Table 9062788 POS Button
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Button@9062230 : Record 9062788;
             BEGIN
               IF ("No." = 0) THEN BEGIN
                 Button.RESET();
                 Button.SETRANGE("Button Group Code", "Button Group Code");
                 IF (Button.FINDLAST()) THEN
                   "No." := Button."No." + 1
                 ELSE
                   "No." := 1;
               END;
             END;

    OnDelete=VAR
               ButtonCommand@9062230 : Record 9062790;
             BEGIN
               ButtonCommand.RESET();
               ButtonCommand.SETRANGE("Button Group Code", "Button Group Code");
               ButtonCommand.SETRANGE("Button No.", "No.");
               ButtonCommand.DELETEALL();
             END;

    CaptionML=[ENU=POS Button;
               ENG=POS Button];
    LookupPageID=Page9063174;
    DrillDownPageID=Page9063174;
  }
  FIELDS
  {
    { 1   ;   ;Button Group Code   ;Code20        ;CaptionML=[ENU=Button Group Code;
                                                              ENG=Button Group Code] }
    { 2   ;   ;No.                 ;Integer       ;CaptionML=[ENU=No.;
                                                              ENG=No.] }
    { 3   ;   ;Row No.             ;Integer       ;CaptionML=[ENU=Row No.;
                                                              ENG=Row No.] }
    { 4   ;   ;Column No.          ;Integer       ;CaptionML=[ENU=Column No.;
                                                              ENG=Column No.] }
    { 5   ;   ;Caption             ;Text50        ;CaptionML=[ENU=Caption;
                                                              ENG=Caption] }
    { 6   ;   ;Caption 2           ;Text50        ;CaptionML=[ENU=Caption 2;
                                                              ENG=Caption 2] }
    { 7   ;   ;Column Span         ;Integer       ;InitValue=1;
                                                   CaptionML=[ENU=Column Span;
                                                              ENG=Column Span] }
    { 8   ;   ;Row Span            ;Integer       ;InitValue=1;
                                                   CaptionML=[ENU=Row Span;
                                                              ENG=Row Span] }
    { 11  ;   ;Style Name          ;Text50        ;CaptionML=[ENU=Style Name;
                                                              ENG=Style Name] }
    { 14  ;   ;Icon Image Url      ;Text150       ;CaptionML=[ENU=Icon Image Url;
                                                              ENG=Icon Image Url] }
    { 15  ;   ;Background Image Url;Text150       ;CaptionML=[ENU=Background Image Url;
                                                              ENG=Background Image Url] }
    { 16  ;   ;Background Color Code;Code20       ;TableRelation="System Colour";
                                                   OnValidate=VAR
                                                                SystemColor@9062231 : Record 9062336;
                                                                FgColor@1000000000 : Record 9062336;
                                                                ChangeColor@1000000001 : Boolean;
                                                                ColorVal@1000000002 : Integer;
                                                              BEGIN
                                                                CLEAR("Background Color Image");
                                                                IF ("Background Color Code" <> '') THEN BEGIN
                                                                  SystemColor.GET("Background Color Code");
                                                                  SystemColor.CALCFIELDS(Bitmap);
                                                                  "Background Color Image" := SystemColor.Bitmap;

                                                                  //try to update foreground color
                                                                  //functionality currently disabled
                                                                  {
                                                                  ChangeColor := TRUE;
                                                                  IF ("Color Code" <> '') THEN BEGIN
                                                                    FgColor.GET("COlor Code");
                                                                    ChangeColor := (FgColor.Red IN [0, 256]) AND (FgColor.Green IN [0, 255]) AND (FgColor.Blue IN [0, 255]);
                                                                  END;

                                                                  IF (ChangeColor) THEN BEGIN
                                                                    FgColor.RESET();
                                                                    IF (SystemColor.Red > 128) OR (SystemColor.Green > 128) OR (SystemColor.Blue > 128) THEN
                                                                      ColorVal := 0
                                                                    ELSE
                                                                      ColorVal := 255;
                                                                    FgColor.SETRANGE(Red, ColorVal);
                                                                    FgColor.SETRANGE(Green, ColorVal);
                                                                    FgColor.SETRANGE(Blue, ColorVal);
                                                                    IF (FgColor.FINDFIRST()) THEN
                                                                      VALIDATE("Color COde", FgColor.Code);
                                                                  END;
                                                                  }

                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupBackColor();
                                                            END;

                                                   CaptionML=[ENU=Background Color Code;
                                                              ENG=Background Color Code] }
    { 17  ;   ;Color Code          ;Code20        ;TableRelation="System Colour";
                                                   OnValidate=VAR
                                                                SystemColor@9062230 : Record 9062336;
                                                              BEGIN
                                                                CLEAR("Color Image");
                                                                IF ("Color Code" <> '') THEN BEGIN
                                                                  SystemColor.GET("Color Code");
                                                                  SystemColor.CALCFIELDS(Bitmap);
                                                                  "Color Image" := SystemColor.Bitmap;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupTextColor();
                                                            END;

                                                   CaptionML=[ENU=Color Code;
                                                              ENG=Color Code] }
    { 18  ;   ;Icon Image          ;BLOB          ;CaptionML=[ENU=Icon Image;
                                                              ENG=Icon Image];
                                                   SubType=Bitmap }
    { 19  ;   ;Background Image    ;BLOB          ;CaptionML=[ENU=Background Image;
                                                              ENG=Background Image];
                                                   SubType=Bitmap }
    { 20  ;   ;Background Color Image;BLOB        ;CaptionML=[ENU=Background Color Image;
                                                              ENG=Background Color Image];
                                                   SubType=Bitmap }
    { 21  ;   ;Color Image         ;BLOB          ;CaptionML=[ENU=Color Image;
                                                              ENG=Color Image];
                                                   SubType=Bitmap }
    { 22  ;   ;Html Style          ;Text250       ;CaptionML=[ENU=Html Style;
                                                              ENG=Html Style] }
    { 23  ;   ;Html Class          ;Text50        ;CaptionML=[ENU=Html Class;
                                                              ENG=Html Class] }
    { 40  ;   ;Client Side Action Type;Option     ;CaptionML=[ENU=Client Side Action Type;
                                                              ENG=Client Side Action Type];
                                                   OptionCaptionML=[ENU=None,Append to Input,Clear Input,Backspace;
                                                                    ENG=None,Append to Input,Clear Input,Backspace];
                                                   OptionString=None,Append to Input,Clear Input,Backspace }
    { 41  ;   ;Client Side Action Parameter;Text30;CaptionML=[ENU=Client Side Action Parameter;
                                                              ENG=Client Side Action Parameter] }
    { 49  ;   ;No. of Commands     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("POS Button Command" WHERE (Button Group Code=FIELD(Button Group Code),
                                                                                                 Button No.=FIELD(No.)));
                                                   CaptionML=[ENU=No. of Commands;
                                                              ENG=No. of Commands];
                                                   Editable=No }
    { 50  ;   ;Confirmation Text   ;Text250       ;CaptionML=[ENU=Confirmation Text;
                                                              ENG=Confirmation Text] }
    { 51  ;   ;Background Image Style;Option      ;CaptionML=[ENU=Background Image Style;
                                                              ENG=Background Image Style];
                                                   OptionCaptionML=[ENU=Stretch,Cover,Contain,Repeat;
                                                                    ENG=Stretch,Cover,Contain,Repeat];
                                                   OptionString=Stretch,Cover,Contain,Repeat }
  }
  KEYS
  {
    {    ;Button Group Code,No.                   ;Clustered=Yes }
    {    ;Button Group Code,Row No.,Column No.     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FieldLookup@9062230 : Codeunit 9062237;

    PROCEDURE LoadImageFromLibrary@1000000047() : Boolean;
    VAR
      lSystemImage@1000000001 : Record 9062240;
      lSystemImageManagement@1000000000 : Codeunit 9062244;
      lImageSelection@1000000003 : Integer;
      lResult@1000000002 : Boolean;
    BEGIN
      //DOC NA2015.1 - Loading image from library
      //  <- Result

      lResult := FALSE;

      IF (lSystemImageManagement.LoadImageFromLibrary(lSystemImage, lImageSelection)) THEN
      BEGIN
        CASE (lImageSelection) OF
          lSystemImage."Image Selection"::Image:
            Rec.VALIDATE("Icon Image", lSystemImage.Image);
          lSystemImage."Image Selection"::"Alternative Image":
            Rec.VALIDATE("Icon Image", lSystemImage."Alternative Image");
        END;
        Rec.MODIFY(TRUE);
        lResult := TRUE;
      END;

      EXIT(lResult);
    END;

    LOCAL PROCEDURE SelectColor@9062230(VAR ColorCode@9062230 : Code[20]) : Boolean;
    VAR
      SystemColor@9062231 : Record 9062336;
    BEGIN
      SystemColor.RESET();
      IF (ColorCode <> '') THEN
        IF (SystemColor.GET(ColorCode)) THEN;
      IF (PAGE.RUNMODAL(PAGE::"System Colour List", SystemColor) = ACTION::LookupOK) THEN BEGIN
        ColorCode := SystemColor.Code;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE LookupTextColor@9062234();
    BEGIN
      IF (SelectColor("Color Code")) THEN
        VALIDATE("Color Code");
    END;

    PROCEDURE LookupBackColor@9062235();
    BEGIN
      IF (SelectColor("Background Color Code")) THEN
        VALIDATE("Background Color Code");
    END;

    PROCEDURE GetItemNo@1000000000() : Code[20];
    VAR
      BtnCommand@1000000000 : Record 9062790;
    BEGIN
      BtnCommand.RESET();
      BtnCommand.SETRANGE("Button Group Code", "Button Group Code");
      BtnCommand.SETRANGE("Button No.", "No.");
      BtnCommand.SETRANGE("Action Type", BtnCommand."Action Type"::"POS Command");
      BtnCommand.SETRANGE("Command Code", 'ADDITEM');
      IF (BtnCommand.FINDFIRST()) THEN
        EXIT(BtnCommand."Action Parameter 1");
      EXIT('');
    END;

    PROCEDURE SetItemNo@1000000001(ItemNo@1000000000 : Code[20]);
    VAR
      Item@1000000001 : Record 27;
      BtnCommand@1000000002 : Record 9062790;
      LineNo@1000000003 : Integer;
    BEGIN
      BtnCommand.RESET();
      BtnCommand.SETRANGE("Button Group Code", "Button Group Code");
      BtnCommand.SETRANGE("Button No.", "No.");
      BtnCommand.SETRANGE("Action Type", BtnCommand."Action Type"::"POS Command");
      BtnCommand.SETRANGE("Command Code", 'ADDITEM');
      IF (ItemNo = '') THEN BEGIN
        IF (BtnCommand.FINDFIRST()) THEN
          BtnCommand.DELETE(TRUE);
      END ELSE BEGIN
        IF (NOT(BtnCommand.FINDFIRST())) THEN BEGIN
          BtnCommand.RESET();
          BtnCommand.SETRANGE("Button Group Code", "Button Group Code");
          BtnCommand.SETRANGE("Button No.", "No.");
          IF (BtnCommand.FINDLAST()) THEN
            LineNo := BtnCommand."Line No."
          ELSE
            LineNo := 0;

          BtnCommand.INIT();
          BtnCommand.VALIDATE("Button Group Code", "Button Group Code");
          BtnCommand.VALIDATE("Button No.", "No.");
          BtnCommand.VALIDATE("Line No.", LineNo + 10000);
          BtnCommand.VALIDATE("Action Type", BtnCommand."Action Type"::"POS Command");
          BtnCommand.VALIDATE("Command Code", 'ADDITEM');
          BtnCommand.INSERT(TRUE);
        END;

        BtnCommand.VALIDATE("Action Parameter 1", ItemNo);
        BtnCommand.MODIFY(TRUE);

        Item.GET(ItemNo);
        Rec.Caption := Item.Description;
        Rec."Caption 2" := Item."Description 2";
        Rec.MODIFY();

      END;
    END;

    BEGIN
    {
      //DOC NH2016.12 BM 11/02/2016 - Created
      //DOC NH2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

