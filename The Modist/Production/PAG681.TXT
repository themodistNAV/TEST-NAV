OBJECT Page 681 Report Inbox Part
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Report Inbox;
               ENG=Report Inbox];
    SourceTable=Table477;
    SourceTableView=SORTING(User ID,Created Date-Time)
                    ORDER(Descending);
    PageType=ListPart;
    OnOpenPage=BEGIN
                 SETRANGE("User ID",USERID);
                 SETAUTOCALCFIELDS;
                 ShowAll := TRUE;
                 UpdateVisibility;
                 AddInReady := FALSE;
               END;

    OnQueryClosePage=BEGIN
                       IF AddInReady THEN
                         CurrPage.PingPong.Stop;
                       EXIT(TRUE);
                     END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      CaptionML=[ENU=Report;
                                 ENG=Report];
                      ActionContainerType=ActionItems }
      { 8       ;1   ;Action    ;
                      Name=Show;
                      ShortCutKey=Return;
                      CaptionML=[ENU=Show;
                                 ENG=Show];
                      Image=Report;
                      OnAction=BEGIN
                                 ShowReport;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 11      ;1   ;Separator  }
      { 12      ;1   ;Action    ;
                      Name=Unread;
                      CaptionML=[ENU=Unread Reports;
                                 ENG=Unread Reports];
                      Enabled=ShowAll;
                      Image=FilterLines;
                      OnAction=BEGIN
                                 ShowAll := FALSE;
                                 UpdateVisibility;
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=All;
                      CaptionML=[ENU=All Reports;
                                 ENG=All Reports];
                      Enabled=NOT ShowAll;
                      Image=AllLines;
                      OnAction=BEGIN
                                 ShowAll := TRUE;
                                 UpdateVisibility;
                               END;
                                }
      { 14      ;1   ;Separator  }
      { 9       ;1   ;Action    ;
                      Name=Delete;
                      CaptionML=[ENU=Delete;
                                 ENG=Delete];
                      Image=Delete;
                      OnAction=VAR
                                 ReportInbox@1000 : Record 477;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ReportInbox);
                                 ReportInbox.DELETEALL;
                               END;
                                }
      { 18      ;1   ;Separator  }
      { 19      ;1   ;Action    ;
                      Name=ShowQueue;
                      CaptionML=[ENU=Show Queue;
                                 ENG=Show Queue];
                      Image=List;
                      OnAction=VAR
                                 JobQueueEntry@1000 : Record 472;
                               BEGIN
                                 JobQueueEntry.FILTERGROUP(2);
                                 JobQueueEntry.SETRANGE("User ID",USERID);
                                 JobQueueEntry.FILTERGROUP(0);
                                 PAGE.RUN(PAGE::"Job Queue Entries",JobQueueEntry);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                SourceExpr="User ID";
                Visible=false;
                Style=Strong;
                StyleExpr=NOT Read }

    { 4   ;2   ;Field     ;
                SourceExpr="Created Date-Time";
                Style=Strong;
                StyleExpr=NOT Read }

    { 5   ;2   ;Field     ;
                SourceExpr="Report ID";
                Visible=false;
                Style=Strong;
                StyleExpr=NOT Read }

    { 6   ;2   ;Field     ;
                SourceExpr="Report Name";
                Visible=FALSE;
                Style=Strong;
                StyleExpr=NOT Read;
                OnDrillDown=BEGIN
                              ShowReport;
                              CurrPage.UPDATE;
                            END;
                             }

    { 15  ;2   ;Field     ;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=NOT Read;
                OnDrillDown=BEGIN
                              ShowReport;
                              CurrPage.UPDATE;
                            END;
                             }

    { 10  ;2   ;Field     ;
                SourceExpr="Output Type";
                Style=Strong;
                StyleExpr=NOT Read }

    { 16  ;1   ;Group     ;
                GroupType=Group }

    { 17  ;2   ;Field     ;
                Name=PingPong;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.PingPong;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      ShowAll@1000 : Boolean;
      PrevNumberOfRecords@1001 : Integer;
      AddInReady@1002 : Boolean;

    LOCAL PROCEDURE UpdateVisibility@1();
    BEGIN
      IF ShowAll THEN
        SETRANGE(Read)
      ELSE
        SETRANGE(Read,FALSE);
      IF FINDFIRST THEN;
      CurrPage.UPDATE(FALSE);
    END;

    EVENT PingPong@-17::AddInReady@2();
    BEGIN
      AddInReady := TRUE;
      PrevNumberOfRecords := COUNT;
      CurrPage.PingPong.Ping(10000);
    END;

    EVENT PingPong@-17::Pong@3();
    VAR
      CurrNumberOfRecords@1000 : Integer;
    BEGIN
      CurrNumberOfRecords := COUNT;
      IF PrevNumberOfRecords <> CurrNumberOfRecords THEN
        CurrPage.UPDATE(FALSE);
      PrevNumberOfRecords := CurrNumberOfRecords;
      CurrPage.PingPong.Ping(10000);
    END;

    BEGIN
    END.
  }
}

