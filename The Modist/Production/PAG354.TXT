OBJECT Page 354 Item Turnover Lines
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00,NAVGB7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ENG=Lines];
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,ItemPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,ItemPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       Item.CALCFIELDS("Purchases (Qty.)","Purchases (LCY)","Sales (Qty.)","Sales (LCY)");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ENG=Period Start];
                SourceExpr="Period Start" }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ENG=Period Name];
                SourceExpr="Period Name" }

    { 4   ;2   ;Field     ;
                Name=PurchasesQty;
                DrillDown=Yes;
                CaptionML=[ENU=Purchases (Qty.);
                           ENG=Purchases (Qty.)];
                DecimalPlaces=0:5;
                SourceExpr=Item."Purchases (Qty.)";
                OnDrillDown=BEGIN
                              ShowItemEntries(FALSE);
                            END;
                             }

    { 6   ;2   ;Field     ;
                Name=PurchasesLCY;
                DrillDown=Yes;
                CaptionML=[ENU=Purchases (LCY);
                           ENG=Purchases (LCY)];
                SourceExpr=Item."Purchases (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowItemEntries(FALSE);
                            END;
                             }

    { 8   ;2   ;Field     ;
                Name=SalesQty;
                DrillDown=Yes;
                CaptionML=[ENU=Sales (Qty.);
                           ENG=Sales (Qty.)];
                DecimalPlaces=0:5;
                SourceExpr=Item."Sales (Qty.)";
                OnDrillDown=BEGIN
                              ShowItemEntries(TRUE);
                            END;
                             }

    { 10  ;2   ;Field     ;
                Name=SalesLCY;
                DrillDown=Yes;
                CaptionML=[ENU=Sales (LCY);
                           ENG=Sales (LCY)];
                SourceExpr=Item."Sales (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowItemEntries(TRUE);
                            END;
                             }

  }
  CODE
  {
    VAR
      Item@1000 : Record 27;
      ItemLedgEntry@1001 : Record 32;
      PeriodFormMgt@1002 : Codeunit 359;
      ItemPeriodLength@1003 : 'Day,Week,Month,Quarter,Year';
      AmountType@1004 : 'Net Change,Balance at Date';

    PROCEDURE Set@1(VAR NewItem@1000 : Record 27;NewItemPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date';VAR CalendarSource@1040000 : ' ,System,AccPeriod');
    BEGIN
      Item.COPY(NewItem);
      ItemPeriodLength := NewItemPeriodLength;
      AmountType := NewAmountType;
      PeriodFormMgt.SetCalendarSource(CalendarSource);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ShowItemEntries@2(ShowSales@1000 : Boolean);
    BEGIN
      SetDateFilter;
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");





      ItemLedgEntry.SETRANGE("Item No.",Item."No.");
      ItemLedgEntry.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      ItemLedgEntry.SETFILTER("Drop Shipment",Item.GETFILTER("Drop Shipment Filter"));
      ItemLedgEntry.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      ItemLedgEntry.SETFILTER("Global Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      ItemLedgEntry.SETFILTER("Global Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      ItemLedgEntry.SETFILTER("Posting Date",Item.GETFILTER("Date Filter"));
      IF ShowSales THEN
        ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Sale)
      ELSE
        ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Purchase);
      PAGE.RUN(0,ItemLedgEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        Item.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        Item.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}

