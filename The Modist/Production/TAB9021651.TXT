OBJECT Table 9021651 Promotion Group
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:04 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Label Code" = '' THEN
                 "Label Code" := MQGlobalFunc.GetLabelCode;

               //DOC EC2015.5.01 -
               IF "Label Code" IN ['DERM','DERMUK2015'] THEN BEGIN //DOC EC2015.5.02
                 IF COPYSTR(Code,1,7) = 'ITEMANA' THEN
                   ERROR(Text003);
               END;
               //DOC EC2015.5.01 +

               "Not On Active Promotion" := FALSE; //DOC EC2015.5.03
             END;

    OnDelete=BEGIN
               //DOC EC2013.3.60 -
               PromoQL.RESET;
               PromoQL.SETRANGE("Applies-to Type",PromoQL."Applies-to Type"::"Promotion Group");
               PromoQL.SETRANGE("No.",Code);
               IF PromoQL.FIND('-') THEN
               REPEAT
                 Promo.GET(PromoQL."Promotion No.");
                 IF Promo.Released THEN
                   ERROR(Text002,TABLECAPTION,Promo."No.");
               UNTIL PromoQL.NEXT = 0;
               //DOC EC2013.3.60 +

               ItemsInPromoGroup.RESET;
               ItemsInPromoGroup.SETRANGE("Label Code","Label Code");
               ItemsInPromoGroup.SETRANGE("Promotion Group Code",Code);
               ItemsInPromoGroup.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=[ENU=Promotion Group;
               ENG=Promotion Group];
    LookupPageID=Page9021694;
    DrillDownPageID=Page9021694;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                LabelRec.GET("Label Code");
                                                                IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                                                                  ERROR(Text001,LabelRec."Used-in Company");
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code] }
    { 3   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              ENG=Description];
                                                   Description=EC2015.5.01 from T50 MQ 02/01/2015 - GW150102 }
    { 25  ;   ;ePage GuID          ;Integer       ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code));
                                                   CaptionML=[ENU=ePage GuID;
                                                              ENG=ePage GuID] }
    { 26  ;   ;Auto Manage Items on Web;Boolean   ;CaptionML=[ENU=Auto Manage Items on Web;
                                                              ENG=Auto Manage Items on Web] }
    { 30  ;   ;Automatically Maintained;Boolean   ;CaptionML=[ENU=Automatically Maintained;
                                                              ENG=Automatically Maintained];
                                                   Description=EC2015.5.01 add field MQ 02/01/2015 - GW150102 }
    { 40  ;   ;Filter Group 1      ;Code20        ;TableRelation="Dimension Value".Code;
                                                   CaptionML=[ENU=Filter Group 1;
                                                              ENG=Filter Group 1];
                                                   Description=EC2015.5.01 add field MQ 02/01/2015 - GW150102 }
    { 42  ;   ;Filter Group 2      ;Code20        ;TableRelation="Dimension Value".Code;
                                                   CaptionML=[ENU=Filter Group 2;
                                                              ENG=Filter Group 2];
                                                   Description=EC2015.5.01 add field MQ 02/01/2015 - GW150102 }
    { 44  ;   ;Filter Group 3      ;Code20        ;TableRelation="Dimension Value".Code;
                                                   CaptionML=[ENU=Filter Group 3;
                                                              ENG=Filter Group 3];
                                                   Description=EC2015.5.01 add field MQ 02/01/2015 - GW150102 }
    { 46  ;   ;Filter Group 4      ;Code20        ;TableRelation="Dimension Value".Code;
                                                   CaptionML=[ENU=Filter Group 4;
                                                              ENG=Filter Group 4];
                                                   Description=EC2015.5.01 add field MQ 02/01/2015 - GW150102 }
    { 50  ;   ;Not On Active Promotion;Boolean    ;OnValidate=BEGIN
                                                                //DOC EC2015.5.03 An Action Promotion is Release and Start Date <=TODAY, End Data => TODAY
                                                              END;

                                                   CaptionML=[ENU=Not On Active Promotion;
                                                              ENG=Not On Active Promotion];
                                                   Description=EC2015.5.03 add field MQ 02/07/2015 - GW150702;
                                                   Editable=No }
    { 100 ;   ;No. of Items in Group;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Items in Promotion Group" WHERE (Label Code=FIELD(Label Code),
                                                                                                       Promotion Group Code=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Items in Group;
                                                              ENG=No. of Items in Group];
                                                   Description=flowfield }
    { 102 ;   ;No. of Qualifying Lines;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Promotion Qualifying Line" WHERE (Label Code=FIELD(Label Code),
                                                                                                        Applies-to Type=FILTER(Promotion Group|Distinct Promo Group Items),
                                                                                                        No.=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Qualifying Lines;
                                                              ENG=No. of Qualifying Lines];
                                                   Description=flowfield GW add 140409,enhanced 150102 }
  }
  KEYS
  {
    {    ;Label Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000000000 : TextConst 'ENU=You cannot rename a Promotion Group.  Please insert a new one.;ENG=You cannot rename a Promotion Group.  Please insert a new one.';
      LabelRec@1040000 : Record 9021618;
      Text001@1040001 : TextConst 'ENU=This Label can only be used in company %1.;ENG=This Label can only be used in company %1.';
      ItemsInPromoGroup@1190000005 : Record 9021650;
      PromoQL@1000000001 : Record 9021657;
      Promo@1000000002 : Record 9021656;
      PromoGroup@1000000005 : Record 9021651;
      PromoGroup1@1000000006 : Record 9021651;
      MQGlobalFunc@1190000006 : Codeunit 9021624;
      Text002@1000000003 : TextConst 'ENU=This %1 is being used on Released Promotion %2 so cannot be deleted.;ENG=This %1 is being used on Released Promotion %2 so cannot be deleted.';
      Text003@1000000004 : TextConst 'ENU=You cannot manually add Promotion Groups starting with ITEMANA.;ENG=You cannot manually add Promotion Groups starting with ITEMANA.';

    PROCEDURE PopulateNotActive@1000000000(LabelCode@1000000000 : Code[10];PromoGroupCode@1000000001 : Code[10]);
    VAR
      OnActivePromo@1000000002 : Boolean;
    BEGIN
      PromoGroup.RESET;
      IF LabelCode <> '' THEN
        PromoGroup.SETRANGE("Label Code",LabelCode);
      IF PromoGroupCode <> '' THEN
        PromoGroup.SETRANGE(Code,PromoGroupCode);
      IF PromoGroup.FIND('-') THEN
      REPEAT
        OnActivePromo := FALSE;
        PromoGroup.CALCFIELDS("No. of Items in Group");
        IF PromoGroup."No. of Items in Group" = 0 THEN BEGIN
          IF PromoGroup."Not On Active Promotion" = FALSE THEN BEGIN
            PromoGroup1 := PromoGroup;
            PromoGroup1."Not On Active Promotion" := TRUE;
            PromoGroup1.MODIFY;
          END;
        END ELSE BEGIN
          PromoGroup.CALCFIELDS("No. of Qualifying Lines");
          IF PromoGroup."No. of Qualifying Lines" = 0 THEN BEGIN
            IF PromoGroup."Not On Active Promotion" = FALSE THEN BEGIN
              PromoGroup1 := PromoGroup;
              PromoGroup1."Not On Active Promotion" := TRUE;
              PromoGroup1.MODIFY;
            END;
          END ELSE BEGIN
            PromoQL.RESET;
            PromoQL.SETRANGE("Label Code",PromoGroup."Label Code");
            PromoQL.SETFILTER("Applies-to Type",'%1|%2',
                    PromoQL."Applies-to Type"::"Promotion Group",
                    PromoQL."Applies-to Type"::"Distinct Promo Group Items");
            PromoQL.SETRANGE("No.",PromoGroup.Code);
            PromoQL.FIND('-');
            REPEAT
              Promo.GET(PromoQL."Promotion No.");
              IF Promo.Released = TRUE THEN
                OnActivePromo := TRUE;
            UNTIL PromoQL.NEXT = 0;
            IF OnActivePromo THEN BEGIN
              IF PromoGroup."Not On Active Promotion" = TRUE THEN BEGIN
                PromoGroup1 := PromoGroup;
                PromoGroup1."Not On Active Promotion" := FALSE;
                PromoGroup1.MODIFY;
              END;
            END ELSE BEGIN
              IF PromoGroup."Not On Active Promotion" = FALSE THEN BEGIN
                PromoGroup1 := PromoGroup;
                PromoGroup1."Not On Active Promotion" := TRUE;
                PromoGroup1.MODIFY;
              END;
            END;
          END;
        END;
      UNTIL PromoGroup.NEXT = 0;
    END;

    BEGIN
    {
      //DOC EC2009.2.67 MQ 06/07/2011 - GW110706 OnDelete, deleting Items in Promo Group; Get Label Code in OnInsert
      //DOC EC2013.3.60 MQ 01/07/2013 - GW130701 Enhanced error checking
      //DOC EC2013.4.60 MQ 09/04/2014 - GW140409 Add flowfield No. of Qualifying Lines
      //DOC EC2015.5.01 MQ 02/01/2015 - GW150102 Add field Filter Group 1 to 4 and Automatically Maintained and code in OnInsert.  Description from T50 to T100
      //DOC EC2015.5.01 MQ 02/01/2015 - Enhanced flowfield No. of Qualifying Lines
      //DOC EC2015.5.02 MQ 16/04/2015 - GW150416 Add support for DERMUK2015 x1
      //DOC EC2015.5.03 MQ 02/07/2015 - GW150702 Add field 50 and code in OnInsert
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

