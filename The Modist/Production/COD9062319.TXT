OBJECT Codeunit 9062319 Planning Board Field Buffer
{
  OBJECT-PROPERTIES
  {
    Date=02/03/16;
    Time=11:02:25 AM;
    Version List=NA2016.14;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EnhancedFieldDefinition@1000000003 : Record 9062327;
      EnhancedFieldManagement@1000000002 : Codeunit 9062298;
      RecordRefManagement@1000000011 : Codeunit 9062241;
      RecordReference@1000000001 : RecordRef;
      FieldReference@1000000000 : FieldRef;
      TableId@1000000004 : Integer;
      FieldType@1000000005 : ' ,Field,Enhanced Field';
      FieldId@1000000006 : Integer;
      Enabled@1000000007 : Boolean;
      Enhanced@1000000008 : Boolean;
      RelationId@1000000009 : Integer;
      RecordFound@1000000010 : Boolean;

    PROCEDURE Set@1000000005(pPlanningBoardTable@1000000005 : Record 9062338;VAR pRecordReference@1000000000 : RecordRef;pFieldType@1000000001 : Integer;pFieldId@1000000002 : Integer;pRelationId@1000000003 : Integer;VAR pPlanningBoardTableRelation@1000000004 : Record 9062341);
    BEGIN
      //DOC NA2016.12 - Setting the buffer
      //  -> pPlanningBoardTable: Planning board table record
      //  <> pRecordReference: Record reference
      //  -> pFieldType: Field type (field / enhanced field)
      //  -> pFieldId: Field ID
      //  -> pRelationId: Relation ID
      //  <> pPlanningBoardTableRelation: Relation recordset

      IF (pRelationId <> 0) THEN
        TableId := pPlanningBoardTableRelation."Table ID"
      ELSE
        TableId := pPlanningBoardTable."Table ID";

      FieldType := pFieldType;
      FieldId := pFieldId;
      Enabled := ((TableId <> 0) AND (FieldType <> FieldType::" ") AND (FieldId <> 0));
      Enhanced := (FieldType = FieldType::"Enhanced Field");
      RelationId := pRelationId;

      IF (Enabled) THEN
      BEGIN
        IF (pRelationId <> 0) THEN
        BEGIN
          pPlanningBoardTableRelation.GET(pPlanningBoardTable."Planning Board Code", pPlanningBoardTable."Table Type", pRelationId);
          pPlanningBoardTableRelation.GetRecordReference(RecordReference);
        END
        ELSE
          RecordReference := pRecordReference;
        FieldReference := RecordReference.FIELD(FieldId);
        IF (Enhanced) THEN
          EnhancedFieldDefinition.GET(TableId, FieldId);
      END;
    END;

    PROCEDURE SetSearchResult@1000000007(pRelationId@1000000001 : Integer;pRecordFound@1000000000 : Boolean);
    BEGIN
      //DOC NA2016.12 - Setting search result
      //  -> pRelationId: Relation ID
      //  -> pRecordFound: The result

      IF ((pRelationId <> 0) AND (pRelationId = RelationId)) THEN
        RecordFound := pRecordFound
      ELSE
        RecordFound := TRUE;
    END;

    PROCEDURE IsEnabled@1000000002() : Boolean;
    BEGIN
      //DOC NA2016.12 - Returning if the field is enabled
      //  <- The answer

      EXIT(Enabled);
    END;

    PROCEDURE IsEnhanced@1000000003() : Boolean;
    BEGIN
      //DOC NA2016.12 - Returning if the field is enhanced
      //  <- The answer

      EXIT(Enhanced);
    END;

    PROCEDURE GetTextValue@1000000000() : Text;
    VAR
      lTextValue@1000000000 : Text;
    BEGIN
      //DOC NA2016.14 - Returning text value
      //  <- The value

      IF (RecordFound) THEN
      BEGIN
        IF (Enhanced) THEN
          lTextValue := EnhancedFieldManagement.GetTextFieldValue(TableId, RecordReference.GETPOSITION(FALSE), FieldId)
        ELSE
        BEGIN
          IF (RecordRefManagement.FieldRefIsFlowField(FieldReference)) THEN
            FieldReference.CALCFIELD();
          lTextValue := FORMAT(FieldReference.VALUE(), 0, 1);
        END;
        lTextValue := COPYSTR(lTextValue, 1, 250);
      END;

      EXIT(lTextValue);
    END;

    PROCEDURE GetBlobValue@1000000001(VAR pBlob@1000000000 : Record 99008535);
    BEGIN
      //DOC NA2016.14 - Returning BLOB value
      //  <> pBlob: Target BLOB value

      IF (RecordFound) THEN
      BEGIN
        IF (Enhanced) THEN
          EnhancedFieldManagement.GetFieldValue_ImageBLOB(TableId, RecordReference.GETPOSITION(FALSE),
            EnhancedFieldDefinition.Name, pBlob)
        ELSE
        BEGIN
          IF (RecordRefManagement.FieldRefIsFlowField(FieldReference)) THEN
            FieldReference.CALCFIELD();
          pBlob.Blob := FieldReference.VALUE();
        END;
      END
      ELSE
        CLEAR(pBlob.Blob);
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 27/12/2015 - Created
      //DOC NA2016.14 JH 02/03/2016 - Calculating a flowfield in "Get..." functions
    }
    END.
  }
}

