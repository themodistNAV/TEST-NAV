OBJECT Table 9021601 Multi Label ItemCat ProdGrp
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:49:59 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnRename=BEGIN
               ERROR(Text004);
             END;

    CaptionML=[ENU=Multi Label ItemCat ProdGrp;
               ENG=Multi Label ItemCat ProdGrp];
    LookupPageID=Page9021601;
    DrillDownPageID=Page9021601;
  }
  FIELDS
  {
    { 1   ;   ;Item Category Code  ;Code10        ;TableRelation="Item Category";
                                                   CaptionML=[ENU=Item Category Code;
                                                              ENG=Item Category Code] }
    { 3   ;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   CaptionML=[ENU=Product Group Code;
                                                              ENG=Product Group Code] }
    { 5   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                IF "Label Code" <> '' THEN BEGIN
                                                                  Label.GET("Label Code");
                                                                  IF Label."Used-in Company" <> COMPANYNAME THEN
                                                                    ERROR(Text000,"Label Code",Label."Used-in Company");
                                                                END;
                                                                //DOC EC2009.2.68 -
                                                                //IF "Label Code" <> xRec."Label Code" THEN
                                                                //  "ePage GuID" := 0;
                                                                IF "Label Code" <> xRec."Label Code" THEN BEGIN
                                                                  "ePage GuID" := 0;
                                                                  "Auto Update Catalogue" := FALSE;
                                                                END;
                                                                //DOC EC2009.2.68 +
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code] }
    { 30  ;   ;ePage GuID          ;Integer       ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code));
                                                   OnValidate=BEGIN
                                                                IF "Item Category Code" = '' THEN
                                                                  ERROR(Text002,FIELDCAPTION("Item Category Code"));
                                                                IF "Label Code" = '' THEN
                                                                  ERROR(Text002,FIELDCAPTION("Label Code"));
                                                                IF "ePage GuID" = 0 THEN BEGIN
                                                                  "Auto Update Catalogue" := FALSE;
                                                                END ELSE BEGIN
                                                                  ePage.GET("Label Code","ePage GuID");
                                                                  IF ePage."Page Item Sort Code" = '' THEN BEGIN
                                                                    PageItemSort.RESET;
                                                                    PageItemSort.SETRANGE("Label Code","Label Code");
                                                                    PageItemSort.SETRANGE("Build By",PageItemSort."Build By"::"Item Cat/Prod Group");
                                                                    IF PageItemSort.COUNT = 1 THEN BEGIN
                                                                      IF CONFIRM(Text003,FALSE,"ePage GuID") THEN BEGIN
                                                                        PageItemSort.FINDFIRST;
                                                                        ePage."Page Item Sort Code" := PageItemSort.Code;
                                                                        ePage.MODIFY;
                                                                      END;
                                                                    END;
                                                                  END ELSE BEGIN
                                                                    PageItemSort.GET("Label Code",ePage."Page Item Sort Code");
                                                                    IF PageItemSort."Build By" <> PageItemSort."Build By"::"Item Cat/Prod Group" THEN
                                                                      MESSAGE(Text001);
                                                                  END;
                                                                END;

                                                                IF "Product Group Code" = '' THEN BEGIN
                                                                  IF "ePage GuID" <> 0 THEN
                                                                    ERROR(Text007);
                                                                END ELSE BEGIN
                                                                  ProdGroup.GET("Item Category Code","Product Group Code");
                                                                  IF ProdGroup."Label Code" = "Label Code" THEN BEGIN
                                                                    IF ProdGroup."ePage GuID" <> "ePage GuID" THEN
                                                                      IF CONFIRM (Text005,TRUE,FIELDCAPTION("ePage GuID"),ProdGroup."ePage GuID",
                                                                                  FIELDCAPTION("Product Group Code")) THEN BEGIN
                                                                        ProdGroup."ePage GuID" := "ePage GuID";
                                                                        ProdGroup.MODIFY;
                                                                      END ELSE
                                                                        ERROR(Text006);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=ePage GuID;
                                                              ENG=ePage GuID] }
    { 32  ;   ;Web Attribute Code  ;Code20        ;TableRelation="Web Attribute".Code WHERE (Table ID=CONST(27),
                                                                                             Label Code=FIELD(Label Code),
                                                                                             Data Type=CONST(Boolean));
                                                   OnValidate=BEGIN
                                                                IF "Item Category Code" = '' THEN
                                                                  ERROR(Text002,FIELDCAPTION("Item Category Code"));
                                                                IF "Label Code" = '' THEN
                                                                  ERROR(Text002,FIELDCAPTION("Label Code"));

                                                                IF "Product Group Code" = '' THEN BEGIN
                                                                  ItemCat.GET("Item Category Code");
                                                                  IF ItemCat."Label Code" = "Label Code" THEN BEGIN
                                                                    IF ItemCat."Web Attribute Code" <> "Web Attribute Code" THEN
                                                                      IF CONFIRM (Text005,TRUE,FIELDCAPTION("Web Attribute Code"),ItemCat."Web Attribute Code",
                                                                                  FIELDCAPTION("Item Category Code")) THEN BEGIN
                                                                        ItemCat."Web Attribute Code" := "Web Attribute Code";
                                                                        ItemCat.MODIFY;
                                                                      END ELSE
                                                                        ERROR(Text006);
                                                                  END;
                                                                END ELSE BEGIN
                                                                  ProdGroup.GET("Item Category Code","Product Group Code");
                                                                  IF ProdGroup."Label Code" = "Label Code" THEN BEGIN
                                                                    IF ProdGroup."Web Attribute Code" <> "Web Attribute Code" THEN
                                                                      IF CONFIRM (Text005,TRUE,FIELDCAPTION("Web Attribute Code"),ProdGroup."Web Attribute Code",
                                                                                  FIELDCAPTION("Product Group Code")) THEN BEGIN
                                                                        ProdGroup."Web Attribute Code" := "Web Attribute Code";
                                                                        ProdGroup.MODIFY;
                                                                      END ELSE
                                                                        ERROR(Text006);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Web Attribute Code;
                                                              ENG=Web Attribute Code] }
    { 34  ;   ;Auto Update Catalogue;Boolean      ;OnValidate=BEGIN
                                                                IF "ePage GuID" = 0 THEN
                                                                  ERROR(Text008);
                                                              END;

                                                   CaptionML=[ENU=Auto Update Catalogue;
                                                              ENG=Auto Update Catalogue] }
  }
  KEYS
  {
    {    ;Item Category Code,Product Group Code,Label Code;
                                                   Clustered=Yes }
    {    ;Label Code,ePage GuID                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Label@1190000005 : Record 9021618;
      Text000@1190000006 : TextConst 'ENU=You cannot use the Label %1 in this company as it is for company %2.;ENG=You cannot use the Label %1 in this company as it is for company %2.';
      ePage@1190000007 : Record 9021628;
      PageItemSort@1190000008 : Record 9021600;
      Text001@1190000009 : TextConst 'ENU=Warning... ePage %1 is linked to a Page Item Sort which does not build Page Items by Item Category / Product Group codes.  This may cause problems.;ENG=Warning... ePage %1 is linked to a Page Item Sort which does not build Page Items by Item Category / Product Group codes.  This may cause problems.';
      Text002@1190000010 : TextConst 'ENU=You must fill in the %1 first.;ENG=You must fill in the %1 first.';
      Text003@1190000011 : TextConst 'ENU=Since there is only one Page Item Sort that builds by Item Category/Product Group, would you like to assign this Page Item Sort Code to the ePage %1?;ENG=Since there is only one Page Item Sort that builds by Item Category/Product Group, would you like to assign this Page Item Sort Code to the ePage %1?';
      ItemCat@1190000016 : Record 5722;
      ProdGroup@1190000012 : Record 5723;
      Text004@1190000013 : TextConst 'ENU=Sorry, renaming is not allowed.  Please delete and reenter.;ENG=Sorry, renaming is not allowed.  Please delete and reenter.';
      Text005@1190000014 : TextConst 'ENU=You have already specified the %1 on the %3 which is %2.  Do you wish to change the %1 on the %3?;ENG=You have already specified the %1 on the %3 which is %2.  Do you wish to change the %1 on the %3?';
      Text006@1190000015 : TextConst 'ENU=Change Abandonned.;ENG=Change Abandonned.';
      Text007@1190000017 : TextConst 'ENU=You cannot have an ePage GuID if the Product Group Code is blank.;ENG=You cannot have an ePage GuID if the Product Group Code is blank.';
      Text008@1190000018 : TextConst 'ENU=You cannot tick Auto Update Catalogue if the ePage GuID is 0.;ENG=You cannot tick Auto Update Catalogue if the ePage GuID is 0.';

    BEGIN
    {
      //DOC EC2009.2.66 MQ 01/01/2012 - Add field 34 Auto Update Catalogue
      //DOC EC2009.2.67 MQ 01/01/2012 - Code in Auto Update Catalogue
      //DOC EC2009.2.68 MQ 03/08/2011 - GW110803 Change in Label Code OnValidate
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

