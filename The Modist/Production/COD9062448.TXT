OBJECT Codeunit 9062448 Sales Printout Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=23/09/15;
    Time=[ 1:41:32 PM];
    Version List=NH2016.4;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      mSalesHeader@1000000009 : Record 36;
      mSalesLine@1000000011 : Record 37;
      mDocumentPrintoutToolkit@1000000010 : Codeunit 9062447;
      tCopy@1000000006 : TextConst 'ENU=COPY;ENG=COPY';
      tAmountInWords@1000000003 : TextConst 'ENU=%1 %2;ENG=%1 %2';

    LOCAL PROCEDURE "--- Setting Document ---"@1000000052();
    BEGIN
    END;

    PROCEDURE SetSalesHeader@1000000060(pSalesHeader@1000000000 : Record 36);
    BEGIN
      //DOC NH2015.4 - Setting sales header
      //  -> pSalesHeader: The header

      mSalesHeader := pSalesHeader;
    END;

    PROCEDURE SetSalesShipmentHeader@1000000063(pSalesShipmentHeader@1000000000 : Record 110);
    BEGIN
      //DOC NH2015.4 - Setting sales shipment header
      //  -> pSalesShipmentHeader: The header

      CLEAR(mSalesHeader);
      mSalesHeader.TRANSFERFIELDS(pSalesShipmentHeader);
    END;

    PROCEDURE SetSalesInvoiceHeader@1000000062(pSalesInvoiceHeader@1000000000 : Record 112);
    BEGIN
      //DOC NH2015.4 - Setting sales invoice header
      //  -> pSalesInvoiceHeader: The header

      CLEAR(mSalesHeader);
      mSalesHeader.TRANSFERFIELDS(pSalesInvoiceHeader);
    END;

    PROCEDURE SetSalesCrMemoHeader@1000000061(pSalesCrMemoHeader@1000000000 : Record 114);
    BEGIN
      //DOC NH2015.4 - Setting sales credit memo header
      //  -> pSalesCrMemoHeader: The header

      CLEAR(mSalesHeader);
      mSalesHeader.TRANSFERFIELDS(pSalesCrMemoHeader);
    END;

    PROCEDURE SetReturnReceiptHeader@1000000026(pReturnReceiptHeader@1000000000 : Record 6660);
    BEGIN
      //DOC NH2015.4 - Setting return receipt header
      //  -> pReturnReceiptHeader: The header

      CLEAR(mSalesHeader);
      mSalesHeader.TRANSFERFIELDS(pReturnReceiptHeader);
    END;

    LOCAL PROCEDURE "--- Setting Line ---"@1000000029();
    BEGIN
    END;

    PROCEDURE SetSalesLine@1000000048(pSalesLine@1000000000 : Record 37);
    BEGIN
      //DOC NH2015.4 - Setting sales line
      //  -> pSalesLine: The line

      mSalesLine := pSalesLine;
    END;

    PROCEDURE SetSalesShipmentLine@1000000046(pSalesShipmentLine@1000000000 : Record 111);
    BEGIN
      //DOC NH2015.4 - Setting sales shipment line
      //  -> pSalesShipmentLine: The line

      CLEAR(mSalesLine);
      mSalesLine.TRANSFERFIELDS(pSalesShipmentLine);
    END;

    PROCEDURE SetSalesInvoiceLine@1000000038(pSalesInvoiceLine@1000000000 : Record 113);
    BEGIN
      //DOC NH2015.4 - Setting sales invoice line
      //  -> pSalesInvoiceLine: The line

      CLEAR(mSalesLine);
      mSalesLine.TRANSFERFIELDS(pSalesInvoiceLine);
    END;

    PROCEDURE SetSalesCrMemoLine@1000000030(pSalesCrMemoLine@1000000000 : Record 115);
    BEGIN
      //DOC NH2015.4 - Setting sales credit memo line
      //  -> pSalesCrMemoLine: The line

      CLEAR(mSalesLine);
      mSalesLine.TRANSFERFIELDS(pSalesCrMemoLine);
    END;

    PROCEDURE SetReturnReceiptLine@1000000028(pReturnReceiptLine@1000000000 : Record 6661);
    BEGIN
      //DOC NH2015.4 - Setting return receipt line
      //  -> pReturnReceiptLine: The line

      CLEAR(mSalesLine);
      mSalesLine.TRANSFERFIELDS(pReturnReceiptLine);
    END;

    LOCAL PROCEDURE "--- Getting Line ---"@1000000021();
    BEGIN
    END;

    PROCEDURE GetSalesLine@1000000013(pSalesLine@1000000000 : Record 37);
    BEGIN
      //DOC NH2015.4 - Getting sales line
      //  <> pSalesLine: Target line

      pSalesLine := mSalesLine;
    END;

    PROCEDURE GetSalesShipmentLine@1000000011(VAR pSalesShipmentLine@1000000000 : Record 111);
    BEGIN
      //DOC NH2015.4 - Getting sales shipment line
      //  <> pSalesShipmentLine: Target line

      pSalesShipmentLine.TRANSFERFIELDS(mSalesLine);
    END;

    PROCEDURE GetSalesInvoiceLine@1000000008(VAR pSalesInvoiceLine@1000000000 : Record 113);
    BEGIN
      //DOC NH2015.4 - Getting sales invoice line
      //  <> pSalesInvoiceLine: Target line

      pSalesInvoiceLine.TRANSFERFIELDS(mSalesLine);
    END;

    PROCEDURE GetSalesCrMemoLine@1000000006(VAR pSalesCrMemoLine@1000000000 : Record 115);
    BEGIN
      //DOC NH2015.4 - Getting sales credit memo line
      //  <> pSalesCrMemoLine: Target line

      pSalesCrMemoLine.TRANSFERFIELDS(mSalesLine);
    END;

    PROCEDURE GetReturnReceiptLine@1000000036(VAR pReturnReceiptLine@1000000000 : Record 6661);
    BEGIN
      //DOC NH2015.4 - Getting return receipt line
      //  <> pReturnReceiptLine: Target line

      pReturnReceiptLine.TRANSFERFIELDS(mSalesLine);
    END;

    PROCEDURE "--- System-Wide ---"@1000000005();
    BEGIN
    END;

    PROCEDURE CompanyShortAddress@1000000003(VAR pCompanyShortAddress@1000000000 : ARRAY [2] OF Text[250]);
    BEGIN
      //DOC TMPM1.04 - Creating company short address
      //  <> pCompanyShortAddress: Target array

      mDocumentPrintoutToolkit.CompanyShortAddress(pCompanyShortAddress);
    END;

    PROCEDURE ReportImages@1000000049(VAR pImage@1000000000 : Record 99008535;pAlternative@1000000002 : Boolean);
    BEGIN
      //DOC TMPM1.04 - Returning report images
      //  <> pImage: Target image
      //  -> pAlternative: Flag to load alternative images

      mDocumentPrintoutToolkit.ReportImages(pImage, pAlternative);
    END;

    PROCEDURE "--- General Information ---"@1000000017();
    BEGIN
    END;

    PROCEDURE CurrencyInformation@1000000022(VAR pCurrencyText@1000000004 : Text[100];VAR pCurrencyFactorText@1000000003 : Text[100];VAR pAmountCaption@1000000002 : Text[100];VAR pAmountInclVatCaption@1000000001 : Text[100]);
    BEGIN
      //DOC TMPM1.00 - Preparing currency information
      //  <> pCurrencyText: Target currency text
      //  <> pCurrencyFactorText: Target currency factor text
      //  <> pAmountCaption: Target amount caption (including currency)
      //  <> pAmountInclVatCaption: Target amount including vat caption (including currency)

      mDocumentPrintoutToolkit.CurrencyInformation(mSalesHeader."Currency Code", mSalesHeader."Currency Factor", pCurrencyText,
        pCurrencyFactorText, pAmountCaption, pAmountInclVatCaption);
    END;

    PROCEDURE ContactInformation@1000000020(VAR pContact@1000000000 : Record 5050);
    VAR
      lCustomer@1000000002 : Record 18;
      lCustomerContact@1000000001 : Record 5050;
    BEGIN
      //DOC NH2015.4 - Returning contact information
      //  <> pContact: Target contact record

      IF (lCustomer.GET(mSalesHeader."Sell-to Customer No.")) THEN
        lCustomerContact.TRANSFERFIELDS(lCustomer);

      mDocumentPrintoutToolkit.ContactInformation(mSalesHeader."Sell-to Contact No.", mSalesHeader."Sell-to Contact",
        lCustomerContact, pContact);
    END;

    LOCAL PROCEDURE "--- Texts ---"@1000000018();
    BEGIN
    END;

    PROCEDURE SalespersonPurchaserText@1000000023() : Text[100];
    BEGIN
      //DOC TMPM1.04 - Returning salesperson / purchaser text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.SalespersonPurchaserText(mSalesHeader."Salesperson Code"));
    END;

    PROCEDURE PaymentTermsText@1000000027() : Text[100];
    BEGIN
      //DOC TMPM1.04 - Returning payment terms text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.PaymentTermsText(mSalesHeader."Payment Terms Code"));
    END;

    PROCEDURE PaymentMethodText@1000000025() : Text[100];
    BEGIN
      //DOC TMPM1.04 - Returning payment method text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.PaymentMethodText(mSalesHeader."Payment Method Code"));
    END;

    PROCEDURE ShipmentMethodText@1000000024() : Text[100];
    BEGIN
      //DOC TMPM1.04 - Returning shipment method text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.ShipmentMethodText(mSalesHeader."Shipment Method Code"));
    END;

    PROCEDURE ShippingAgentText@1000000032() : Text[100];
    BEGIN
      //DOC TMPM1.04 - Returning shipping agent text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.ShippingAgentText(mSalesHeader."Shipping Agent Code"));
    END;

    PROCEDURE ShippingAgentServiceText@1000000034() : Text[100];
    VAR
      eText@1000000004 : TextConst 'ENU=%1 - %2;ENG=%1 - %2';
    BEGIN
      //DOC TMPM1.04 - Returning shipping agent service text
      //  <- The text

      EXIT(mDocumentPrintoutToolkit.ShippingAgentServiceText(mSalesHeader."Shipping Agent Code",
        mSalesHeader."Shipping Agent Service Code"));
    END;

    PROCEDURE "--- Copy Tools ---"@1000000012();
    BEGIN
    END;

    PROCEDURE FilterCopy@1000000016(pCopies@1000000000 : Integer;VAR pCopy@1000000001 : Record 2000000026);
    BEGIN
      //DOC TMPM1.00 - Filtering copies recordset (Integer)
      //  -> pCopies: Number of copies
      //  <> pCopy: Copies recordset (Integer) to filter

      mDocumentPrintoutToolkit.FilterCopy(pCopies, pCopy);
    END;

    PROCEDURE NextCopy@1000000019(pCopy@1000000000 : Record 2000000026;VAR pCopyText@1000000001 : Text[10]);
    BEGIN
      //DOC TMPM1.00 - Next copy
      //  -> pCopy: Copy record (Integer)
      //  <> pCopyText: Target copy text

      mDocumentPrintoutToolkit.NextCopy(pCopy, pCopyText);
    END;

    PROCEDURE "--- Line Tools ---"@1000000002();
    BEGIN
    END;

    PROCEDURE ClearLineIndex@1000000007();
    BEGIN
      //DOC TMPM1.00 - Clearing line index

      mDocumentPrintoutToolkit.ClearLineIndex();
    END;

    PROCEDURE NextLineIndex@1000000033(VAR pLineIndex@1000000002 : Integer;VAR pLineIndexText@1000000001 : Text[10]);
    BEGIN
      //DOC TMPM1.00 - Processing line index
      //  <> pLineIndex: Target line index
      //  <> pLineIndexText: Target line index text

      mDocumentPrintoutToolkit.NextLineIndex(mSalesLine.Type, pLineIndex, pLineIndexText);
    END;

    PROCEDURE LineDescription@1000000010() : Text;
    BEGIN
      //DOC NH2015.4 - Returning line description
      //  <- The description

      EXIT(mDocumentPrintoutToolkit.LineDescription(mSalesLine.Description, mSalesLine."Description 2"));
    END;

    PROCEDURE LineInternalInformation@1000000039(pShowInternalInformation@1000000001 : Boolean);
    VAR
      lSalesLine@1000000002 : Record 37;
    BEGIN
      //DOC NH2015.2 - Amending sales line internal information
      //  -> pShowInternalInformation: Show internal information flag

      mDocumentPrintoutToolkit.LineInternalInformation(pShowInternalInformation, mSalesLine.Type, mSalesLine."No.");
    END;

    PROCEDURE "--- Summary Tools ---"@1000000000();
    BEGIN
    END;

    PROCEDURE Summary@1000000031(VAR pAmount@1000000004 : Decimal;VAR pAmountInclVat@1000000005 : Decimal;VAR pAmountInWordsText@1000000006 : Text);
    VAR
      lSalesLineTmp@1000000000 : TEMPORARY Record 37;
      lSalesPost@1000000002 : Codeunit 80;
      lVatAmountLineTmp@1000000001 : TEMPORARY Record 290;
    BEGIN
      //DOC NH2015.2 - Calculating sales summary
      //  <> pAmount: Target amount
      //  <> pAmountInclVat: Target amount including VAT
      //  <> pVatAmountLine: Target VAT amount line recordset (for VAT lines)
      //  <> pAmountInWordsText: Target amount in words

      CLEAR(lSalesPost);
      lSalesPost.GetSalesLines(mSalesHeader, lSalesLineTmp, 0);
      lSalesLineTmp.CalcVATAmountLines(0, mSalesHeader, lSalesLineTmp, lVatAmountLineTmp);
      lSalesLineTmp.UpdateVATOnLines(0, mSalesHeader, lSalesLineTmp, lVatAmountLineTmp);

      pAmountInclVat := lVatAmountLineTmp.GetTotalAmountInclVAT();
      pAmount := pAmountInclVat - lVatAmountLineTmp.GetTotalVATAmount();
      pAmountInWordsText := mDocumentPrintoutToolkit.AmountInWords(pAmountInclVat);

      mDocumentPrintoutToolkit.SetSummary(lVatAmountLineTmp);
    END;

    PROCEDURE ClearSummary@1000000001();
    BEGIN
      //DOC TMPM1.00 - Clearing summary

      mDocumentPrintoutToolkit.ClearSummary();
    END;

    PROCEDURE AddLineToSummary@1000000004();
    BEGIN
      //DOC TMPM1.00 - Adding line to summary

      mDocumentPrintoutToolkit.AddLineToSummary(mSalesLine."VAT Identifier", mSalesLine."VAT Calculation Type",
        mSalesLine."Tax Group Code", mSalesLine."VAT %", mSalesLine.Amount, mSalesLine."Amount Including VAT",
        mSalesLine."Line Amount", mSalesLine."Allow Invoice Disc.", mSalesLine."Inv. Discount Amount",
        mSalesLine."VAT Clause Code");
    END;

    PROCEDURE GetSummaryAmounts@1000000009(VAR pSummaryAmount@1000000000 : Decimal;VAR pSummaryAmountInclVat@1000000001 : Decimal;VAR pAmountInWordsText@1000000002 : Text);
    BEGIN
      //DOC TMPM1.00 - Returning summary amounts
      //  <> pSummaryAmount: Target summary amount
      //  <> pSummaryAmountInclVat: Target summary amount including VAT
      //  <> pAmountInWordsText: Target summary amount in words

      mDocumentPrintoutToolkit.GetSummaryAmounts(pSummaryAmount, pSummaryAmountInclVat, pAmountInWordsText);
    END;

    PROCEDURE FilterSummary@1000000014(VAR pSummary@1000000000 : Record 2000000026);
    BEGIN
      //DOC TMPM1.00 - Filtering summary
      //  <> pSummary: Summary recordset (Integer)
      //  <- Result

      mDocumentPrintoutToolkit.FilterSummary(pSummary);
    END;

    PROCEDURE NextSummary@1000000015(pSummary@1000000000 : Record 2000000026;VAR pVatAmountLine@1000000001 : Record 290;VAR pVatIdentifierText@1000000003 : Text[100]) : Boolean;
    BEGIN
      //DOC TMPM1.00 - Dooing NEXT on buffer
      //  -> pSummary: Current summary record (Integer)
      //  <> pVatAmountLine: Target VAT amount line
      //  <- Result (TRUE = Next summary buffer retrieved, FALSE = No more records)

      EXIT(mDocumentPrintoutToolkit.NextSummary(pSummary, pVatAmountLine, pVatIdentifierText));
    END;

    LOCAL PROCEDURE "--- Formatting ---"@1000000042();
    BEGIN
    END;

    PROCEDURE FormatDate@1000000040(pDate@1000000000 : Date) : Text;
    BEGIN
      //DOC NH2015.4 - Formatting date
      //  -> pDate: Date to format
      //  <- Formatted date

      EXIT(mDocumentPrintoutToolkit.FormatDate(pDate));
    END;

    PROCEDURE FormatTime@1000000037(pTime@1000000000 : Time) : Text;
    BEGIN
      //DOC NH2015.4 - Formatting time
      //  -> pTime: Time to format
      //  <- Formatted time

      EXIT(mDocumentPrintoutToolkit.FormatTime(pTime));
    END;

    PROCEDURE FormatDateTime@1000000035(pDateTime@1000000000 : DateTime) : Text;
    BEGIN
      //DOC NH2015.4 - Formatting datetime
      //  -> pDateTime: Datetime to format
      //  <- Formatted datetime

      EXIT(mDocumentPrintoutToolkit.FormatDateTime(pDateTime));
    END;

    BEGIN
    {
      //DOC NH2015.1 JH 27/05/2014 - Created
      //DOC NH2015.4 JH 07/12/2014 - Purchase documents support added
      //DOC NH2016.4 JH 23/09/2015 - Upgraded to 2016
    }
    END.
  }
}

