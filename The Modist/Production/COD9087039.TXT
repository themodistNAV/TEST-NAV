OBJECT Codeunit 9087039 Replenishment Activity Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=21/09/16;
    Time=[ 5:28:40 PM];
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      tDefaultTemplateCode@1000000008 : TextConst 'ENU=DEFAULT;ENG=DEFAULT';
      tDefaultTemplateDescription@1000000009 : TextConst 'ENU=Default Replenishment Template;ENG=Default Replenishment Template';
      tDefaultBatchCode@1000000001 : TextConst 'ENU=DEFAULT;ENG=DEFAULT';
      tDefaultBatchDescription@1000000000 : TextConst 'ENU=Default Replenishment Batch;ENG=Default Replenishment Batch';
      tEmptyTemplate@1000000002 : TextConst 'ENU=Replenishment template code is empty;ENG=Replenishment template code is empty';
      tEmptyBatch@1000000003 : TextConst 'ENU=Replenishment batch code is empty;ENG=Replenishment batch code is empty';
      ReplenishmentTemplate@1000000005 : Record 9087041;
      ReplenishmentContext@1000000004 : Record 9087044;

    PROCEDURE SetReplenishmentTemplate@1000000000(pReplenishmentTemplate@1000000000 : Record 9087041);
    BEGIN
      //DOC NS2016.16 - Setting replenishment template
      //  -> pReplenishmentTemplate: The template

      ReplenishmentTemplate := pReplenishmentTemplate;
    END;

    LOCAL PROCEDURE FindReplenishmentTemplate@1000000001(pReplenishmentTemplateCode@1000000000 : Code[20]);
    BEGIN
      //DOC NS2016.16 - Trying to find replenishment template
      //  -> pReplenishmentTemplateCode: The template code

      IF ((ReplenishmentTemplate.Code = '') OR (ReplenishmentTemplate.Code <> pReplenishmentTemplateCode)) THEN
        ReplenishmentTemplate.GET(pReplenishmentTemplateCode);
    END;

    PROCEDURE UpdateParameters@1000000004(pReplenishmentTemplateCode@1000000002 : Code[20];pReplenishmentBatchCode@1000000001 : Code[20]);
    BEGIN
      //DOC NS2016.16 - Updating parameters for given replenishment template & batch
      //  -> pReplenishmentTemplateCode: Replenishment template code
      //  -> pReplenishmentBatchCode: Replenishment batch code

      RunActivity(ReplenishmentContext.Activity::"Update Parameters", pReplenishmentTemplateCode, pReplenishmentBatchCode);
    END;

    PROCEDURE CalculateRequired@1000000007(pReplenishmentTemplateCode@1000000000 : Code[20];pReplenishmentBatchCode@1000000001 : Code[20]);
    BEGIN
      //DOC NS2016.16 - Calculating required (quantities) for given replenishment template & batch
      //  -> pReplenishmentTemplateCode: Replenishment template code
      //  -> pReplenishmentBatchCode: Replenishment batch code

      RunActivity(ReplenishmentContext.Activity::"Calculate Required", pReplenishmentTemplateCode, pReplenishmentBatchCode);
    END;

    PROCEDURE CalculateAvailable@1000000006(pReplenishmentTemplateCode@1000000000 : Code[20];pReplenishmentBatchCode@1000000001 : Code[20]);
    BEGIN
      //DOC NS2016.16 - Calculating available (quantities) for given replenishment template & batch
      //  -> pReplenishmentTemplateCode: Replenishment template code
      //  -> pReplenishmentBatchCode: Replenishment batch code

      RunActivity(ReplenishmentContext.Activity::"Calculate Available", pReplenishmentTemplateCode, pReplenishmentBatchCode);
    END;

    LOCAL PROCEDURE RunActivity@1000000005(pActivity@1000000002 : Option;pReplenishmentTemplateCode@1000000000 : Code[20];pReplenishmentBatchCode@1000000001 : Code[20]);
    BEGIN
      //DOC NS2016.16 - Running specified activity for given replenishment template & batch
      //  -> pActivity: The activity
      //  -> pReplenishmentTemplateCode: Replenishment template code
      //  -> pReplenishmentBatchCode: Replenishment batch code

      FindReplenishmentTemplate(pReplenishmentTemplateCode);
      ReplenishmentTemplate.TESTFIELD("Replenishment Codeunit ID");

      CLEAR(ReplenishmentContext);
      ReplenishmentContext.VALIDATE(Activity, pActivity);
      ReplenishmentContext.VALIDATE("Replenishment Template Code", pReplenishmentTemplateCode);
      ReplenishmentContext.VALIDATE("Replenishment Batch Code", pReplenishmentBatchCode);

      CODEUNIT.RUN(ReplenishmentTemplate."Replenishment Codeunit ID", ReplenishmentContext);
    END;

    BEGIN
    {
      //DOC NS2016.16 JH 15/09/2016 - Created
    }
    END.
  }
}

