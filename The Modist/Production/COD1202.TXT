OBJECT Codeunit 1202 Import Payroll Transaction
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1220=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FileMgt@1000 : Codeunit 419;
      ImportPayrollTransCap@1002 : TextConst 'ENU=Select Payroll Transaction;ENG=Select Payroll Transaction';
      FileFilterTxt@1001 : TextConst 'ENU="Text Files(*.txt;*.csv)|*.txt;*.csv";ENG="Text Files(*.txt;*.csv)|*.txt;*.csv"';
      FileFilterExtensionTxt@1004 : TextConst '@@@={Locked};ENU=txt,csv;ENG=txt,csv';

    PROCEDURE SelectAndImportPayrollDataToGL@2(VAR GenJournalLine@1000 : Record 81;DataExchDefCode@1001 : Code[20]);
    VAR
      DataExchDef@1004 : Record 1222;
      TempBlob@1002 : Record 99008535;
      FileName@1003 : Text;
    BEGIN
      FileName := FileMgt.BLOBImportWithFilter(TempBlob,ImportPayrollTransCap,'',FileFilterTxt,FileFilterExtensionTxt);
      IF FileName <> '' THEN BEGIN
        DataExchDef.GET(DataExchDefCode);
        DataExchDef.TESTFIELD("Reading/Writing XMLport");
        ImportPayrollDataToGL(GenJournalLine,FileName,TempBlob,DataExchDefCode);
      END;
    END;

    PROCEDURE ImportPayrollDataToGL@18(GenJournalLine@1000 : Record 81;FileName@1005 : Text;TempBlob@1001 : Record 99008535;DataExchDefCode@1002 : Code[20]);
    VAR
      GenJournalLineTemplate@1060 : Record 81;
      DataExch@1003 : Record 1220;
    BEGIN
      PrepareGenJournalLine(GenJournalLineTemplate,GenJournalLine);
      CreateDataExch(DataExch,FileName,TempBlob,DataExchDefCode);
      ImportCSV(DataExch);
      MapDataToGenJournalLine(DataExch,GenJournalLineTemplate);
    END;

    LOCAL PROCEDURE CreateDataExch@9(VAR DataExch@1004 : Record 1220;FileName@1002 : Text;TempBlob@1000 : Record 99008535;DataExchDefCode@1001 : Code[20]);
    VAR
      Source@1003 : InStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(Source);
      DataExch.InsertRec(COPYSTR(FileName,1,MAXSTRLEN(DataExch."File Name")),Source,DataExchDefCode);
    END;

    LOCAL PROCEDURE ImportCSV@11(DataExch@1000 : Record 1220);
    VAR
      DataExchDef@1006 : Record 1222;
      Source@1002 : InStream;
    BEGIN
      DataExch."File Content".CREATEINSTREAM(Source);

      DataExch.SETRANGE("Entry No.",DataExch."Entry No.");

      DataExchDef.GET(DataExch."Data Exch. Def Code");

      XMLPORT.IMPORT(DataExchDef."Reading/Writing XMLport",Source,DataExch);
    END;

    LOCAL PROCEDURE MapDataToGenJournalLine@14(DataExch@1000 : Record 1220;GenJournalLineTemplate@1001 : Record 81);
    VAR
      ProcessDataExch@1003 : Codeunit 1201;
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(GenJournalLineTemplate);
      ProcessDataExch.ProcessAllLinesColumnMapping(DataExch,RecRef);
    END;

    LOCAL PROCEDURE PrepareGenJournalLine@1060(VAR GenJournalLineTemplate@1060 : Record 81;GenJournalLine@1061 : Record 81);
    BEGIN
      WITH GenJournalLineTemplate DO BEGIN
        "Journal Template Name" := GenJournalLine."Journal Template Name";
        "Journal Batch Name" := GenJournalLine."Journal Batch Name";
        SetUpNewLine(GenJournalLine,GenJournalLine."Balance (LCY)",TRUE);
        "Account Type" := "Account Type"::"G/L Account";

        GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
        GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
        IF GenJournalLine.FINDLAST THEN BEGIN
          "Line No." := GenJournalLine."Line No.";
          "Document No." := INCSTR(GenJournalLine."Document No.");
        END ELSE
          "Document No." := GenJournalLine."Document No.";
      END;
    END;

    BEGIN
    END.
  }
}

