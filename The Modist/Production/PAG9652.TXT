OBJECT Page 9652 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Report Layout Selection;
               ENG=Report Layout Selection];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table9651;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 SelectedCompany := COMPANYNAME;
               END;

    OnFindRecord=BEGIN
                   IF NOT IsInitialized THEN
                     InitializeData;
                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetRec;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 4       ;1   ;Action    ;
                      Name=Customizations;
                      CaptionML=[ENU=Custom Layouts;
                                 ENG=Custom Layouts];
                      RunObject=Page 9650;
                      RunPageLink=Report ID=FIELD(Report ID);
                      Promoted=Yes;
                      Image=Report;
                      PromotedCategory=Process }
      { 9       ;1   ;Action    ;
                      Name=RunReport;
                      CaptionML=[ENU=Run Report;
                                 ENG=Run Report];
                      Promoted=Yes;
                      Image=Report;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 REPORT.RUN("Report ID");
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=BulkUpdate;
                      CaptionML=[ENU=Update All layouts;
                                 ENG=Update All layouts];
                      Image=UpdateXML;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit 9651;
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(FALSE);
                               END;
                                }
      { 16      ;1   ;Action    ;
                      Name=TestUpdate;
                      CaptionML=[ENU=Test Layout Updates;
                                 ENG=Test Layout Updates];
                      Image=TestReport;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit 9651;
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 11  ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                CaptionML=[ENU=Company;
                           ENG=Company];
                GroupType=Group }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Company Name;
                           ENG=Company Name];
                SourceExpr=SelectedCompany;
                TableRelation=Company;
                Importance=Promoted;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                SourceExpr="Report ID";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Report Name";
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Selected Layout;
                           ENG=Selected Layout];
                SourceExpr=Type;
                OnValidate=BEGIN
                             UpdateRec;
                             COMMIT;
                             LookupCustomLayout;
                           END;
                            }

    { 13  ;2   ;Field     ;
                SourceExpr="Custom Report Layout ID";
                TableRelation="Custom Report Layout" WHERE (Report ID=FIELD(Report ID));
                Visible=FALSE;
                OnValidate=BEGIN
                             ReportLayoutSelection.VALIDATE("Custom Report Layout ID",ReportLayoutSelection."Custom Report Layout ID");
                             UpdateRec;
                           END;
                            }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Custom Layout Description;
                           ENG=Custom Layout Description];
                SourceExpr=CustomLayoutDescription;
                OnValidate=VAR
                             CustomReportLayout2@1000 : Record 9650;
                           BEGIN
                             CustomReportLayout2.SETCURRENTKEY("Report ID","Company Name",Type);
                             CustomReportLayout2.SETRANGE("Report ID",ReportLayoutSelection."Report ID");
                             CustomReportLayout2.SETFILTER("Company Name",'%1|%2','',SelectedCompany);
                             CustomReportLayout2.SETFILTER(
                               Description,STRSUBSTNO('@*%1*',CustomLayoutDescription));
                             CustomReportLayout2.FINDFIRST;
                             IF CustomReportLayout2.ID <> "Custom Report Layout ID" THEN BEGIN
                               VALIDATE("Custom Report Layout ID",CustomReportLayout2.ID);
                               UpdateRec;
                             END;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=BEGIN
                           LookupCustomLayout;
                         END;
                          }

    { 8   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 7   ;1   ;Part      ;
                CaptionML=[ENU=Custom Layouts;
                           ENG=Custom Layouts];
                SubPageLink=Report ID=FIELD(Report ID);
                PagePartID=Page9653;
                PartType=Page;
                ShowFilter=No;
                UpdatePropagation=Both }

  }
  CODE
  {
    VAR
      ReportLayoutSelection@1000 : Record 9651;
      SelectedCompany@1004 : Text[30];
      WrongCompanyErr@1005 : TextConst 'ENU=You cannot select a layout that is specific to another company.;ENG=You cannot select a layout that is specific to another company.';
      CustomLayoutDescription@1002 : Text;
      IsInitialized@1001 : Boolean;

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      IF ReportLayoutSelection.GET("Report ID",SelectedCompany) THEN BEGIN
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Company Name" := SelectedCompany;
        ReportLayoutSelection.MODIFY;
      END ELSE BEGIN
        CLEAR(ReportLayoutSelection);
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Company Name" := SelectedCompany;
        ReportLayoutSelection.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT GET("Report ID",'') THEN
        EXIT;
      IF NOT ReportLayoutSelection.GET("Report ID",SelectedCompany) THEN BEGIN
        ReportLayoutSelection.INIT;
        ReportLayoutSelection.Type := GetDefaultType("Report ID");
      END;
      Type := ReportLayoutSelection.Type;
      "Custom Report Layout ID" := ReportLayoutSelection."Custom Report Layout ID";
      CALCFIELDS("Report Layout Description");
      CustomLayoutDescription := "Report Layout Description";
      MODIFY;
    END;

    LOCAL PROCEDURE LookupCustomLayout@5();
    BEGIN
      IF Type <> Type::"Custom Layout" THEN
        EXIT;

      IF NOT SelectReportLayout THEN
        EXIT;

      GetRec;
      IF (Type = Type::"Custom Layout") AND
         ("Custom Report Layout ID" = 0)
      THEN BEGIN
        VALIDATE(Type,GetDefaultType("Report ID"));
        UpdateRec;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SelectReportLayout@3() : Boolean;
    VAR
      CustomReportLayout2@1000 : Record 9650;
      OK@1001 : Boolean;
    BEGIN
      CustomReportLayout2.FILTERGROUP(4);
      CustomReportLayout2.SETRANGE("Report ID","Report ID");
      CustomReportLayout2.FILTERGROUP(0);
      CustomReportLayout2.SETFILTER("Company Name",'%1|%2',SelectedCompany,'');
      OK := PAGE.RUNMODAL(PAGE::"Custom Report Layouts",CustomReportLayout2) = ACTION::LookupOK;
      IF OK THEN BEGIN
        IF CustomReportLayout2.FIND THEN BEGIN
          IF NOT (CustomReportLayout2."Company Name" IN [SelectedCompany,'']) THEN
            ERROR(WrongCompanyErr);
          "Custom Report Layout ID" := CustomReportLayout2.ID;
          Type := Type::"Custom Layout";
          UpdateRec;
        END
      END ELSE
        IF Type = Type::"Custom Layout" THEN
          IF CustomReportLayout2.ISEMPTY THEN BEGIN
            Type := GetDefaultType("Report ID");
            "Custom Report Layout ID" := 0;
            UpdateRec;
          END;
      EXIT(OK);
    END;

    LOCAL PROCEDURE InitializeData@1();
    VAR
      ReportMetadata@1000 : Record 2000000139;
    BEGIN
      ReportMetadata.SETRANGE(ProcessingOnly,FALSE);
      IF ReportMetadata.FINDSET THEN
        REPEAT
          INIT;
          "Report ID" := ReportMetadata.ID;
          INSERT;
        UNTIL ReportMetadata.NEXT = 0;
      IF FINDFIRST THEN;
      IsInitialized := TRUE;
    END;

    BEGIN
    END.
  }
}

