OBJECT Codeunit 41 TextManagement
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVGB9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CalendarSetup@1040000 : Record 10505;
      TodayText@1000 : TextConst '@@@=Must be uppercase hint reuse the transtaltion from cod1 for 2009 sp1;ENU=TODAY;ENG=TODAY';
      WorkdateText@1001 : TextConst '@@@=Must be uppercase hint reuse the transtaltion from cod1 for 2009 sp1;ENU=WORKDATE;ENG=WORKDATE';
      PeriodText@1004 : TextConst '@@@=Must be uppercase hint reuse the transtaltion from cod1 for 2009 sp1;ENU=PERIOD;ENG=PERIOD';
      YearText@1003 : TextConst '@@@=Must be uppercase hint reuse the transtaltion from cod1 for 2009 sp1;ENU=YEAR;ENG=YEAR';
      NumeralOutOfRangeError@1006 : TextConst 'ENU=When you specify periods and years, you can use numbers from 1 - 999, such as P-1, P1, Y2 or Y+3.;ENG=When you specify periods and years, you can use numbers from 1 - 999, such as P-1, P1, Y2 or Y+3.';
      AlphabetText@1002 : TextConst '@@@=Uppercase - translate into entire alphabet.;ENU=ABCDEFGHIJKLMNOPQRSTUVWXYZ;ENG=ABCDEFGHIJKLMNOPQRSTUVWXYZ';
      NowText@1007 : TextConst '@@@=Must be uppercase.;ENU=NOW;ENG=NOW';
      YesterdayText@1009 : TextConst '@@@=Must be uppercase;ENU=YESTERDAY;ENG=YESTERDAY';
      TomorrowText@1010 : TextConst '@@@=Must be uppercase;ENU=TOMORROW;ENG=TOMORROW';
      WeekText@1012 : TextConst '@@@=Must be uppercase;ENU=WEEK;ENG=WEEK';
      MonthText@1005 : TextConst '@@@=Must be uppercase;ENU=MONTH;ENG=MONTH';
      QuarterText@1018 : TextConst '@@@=Must be uppercase;ENU=QUARTER;ENG=QUARTER';
      UserText@1013 : TextConst '@@@=Must be uppercase;ENU=USER;ENG=USER';
      MeText@1011 : TextConst '@@@=Must be uppercase;ENU=ME;ENG=ME';
      MyCustomersText@1014 : TextConst '@@@=Must be uppercase;ENU=MYCUSTOMERS;ENG=MYCUSTOMERS';
      MyItemsText@1015 : TextConst '@@@=Must be uppercase;ENU=MYITEMS;ENG=MYITEMS';
      MyVendorsText@1016 : TextConst '@@@=Must be uppercase;ENU=MYVENDORS;ENG=MYVENDORS';
      CompanyText@1017 : TextConst '@@@=Must be uppercase;ENU=COMPANY;ENG=COMPANY';
      OverflowMsg@1008 : TextConst 'ENU=The filter contains more than 2000 numbers and has been truncated.;ENG=The filter contains more than 2000 numbers and has been truncated.';
      WeekIdentifier@1040006 : TextConst '@@@={Locked};ENU=K;ENG=K';
      CalendarSetupRead@1040002 : Boolean;
      UnincrementableStringErr@1019 : TextConst 'ENU=%1 contains no number and cannot be incremented.;ENG=%1 contains no number and cannot be incremented.';

    PROCEDURE MakeDateTimeText@1(VAR DateTimeText@1000 : Text[250]) : Integer;
    VAR
      Date@1001 : Date;
      Time@1002 : Time;
    BEGIN
      IF GetSeparateDateTime(DateTimeText,Date,Time) THEN BEGIN
        IF Date = 0D THEN
          EXIT(0);
        IF Time = 0T THEN
          Time := 000000T;
        DateTimeText := FORMAT(CREATEDATETIME(Date,Time));
      END;
      EXIT(0);
    END;

    PROCEDURE GetSeparateDateTime@2(DateTimeText@1000 : Text[250];VAR Date@1005 : Date;VAR Time@1006 : Time) : Boolean;
    VAR
      DateText@1001 : Text[250];
      TimeText@1002 : Text[250];
      Position@1008 : Integer;
      Length@1007 : Integer;
    BEGIN
      IF DateTimeText IN [NowText,'NOW'] THEN
        DateTimeText := FORMAT(CURRENTDATETIME);
      Date := 0D;
      Time := 0T;
      Position := 1;
      Length := STRLEN(DateTimeText);
      ReadCharacter(' ',DateTimeText,Position,Length);
      ReadUntilCharacter(' ',DateTimeText,Position,Length);
      DateText := DELCHR(COPYSTR(DateTimeText,1,Position - 1),'<>');
      TimeText := DELCHR(COPYSTR(DateTimeText,Position),'<>');
      IF DateText = '' THEN
        EXIT(TRUE);

      IF MakeDateText(DateText) = 0 THEN;
      IF NOT EVALUATE(Date,DateText) THEN
        EXIT(FALSE);

      IF TimeText = '' THEN
        EXIT(TRUE);

      IF MakeTimeText(TimeText) = 0 THEN;
      IF EVALUATE(Time,TimeText) THEN
        EXIT(TRUE);
    END;

    PROCEDURE MakeDateText@3(VAR DateText@1000 : Text[250]) : Integer;
    VAR
      Date@1002 : Date;
      PartOfText@1001 : Text[132];
      Position@1004 : Integer;
      Length@1003 : Integer;
    BEGIN
      Position := 1;
      Length := STRLEN(DateText);
      ReadCharacter(' ',DateText,Position,Length);
      IF NOT FindText(PartOfText,DateText,Position,Length) THEN
        EXIT(0);
      CASE PartOfText OF
        COPYSTR('TODAY',1,STRLEN(PartOfText)),COPYSTR(TodayText,1,STRLEN(PartOfText)):
          Date := TODAY;
        COPYSTR('WORKDATE',1,STRLEN(PartOfText)),COPYSTR(WorkdateText,1,STRLEN(PartOfText)):
          Date := WORKDATE;
        ELSE
          EXIT(0);
      END;
      Position := Position + STRLEN(PartOfText);
      ReadCharacter(' ',DateText,Position,Length);
      IF Position > Length THEN BEGIN
        DateText := FORMAT(Date);
        EXIT(0);
      END;
      EXIT(Position);
    END;

    PROCEDURE MakeTimeText@4(VAR TimeText@1000 : Text[250]) : Integer;
    VAR
      PartOfText@1001 : Text[132];
      Position@1004 : Integer;
      Length@1003 : Integer;
    BEGIN
      Position := 1;
      Length := STRLEN(TimeText);
      ReadCharacter(' ',TimeText,Position,Length);
      IF NOT FindText(PartOfText,TimeText,Position,Length) THEN
        EXIT(0);
      IF PartOfText <> COPYSTR(TimeText,1,STRLEN(PartOfText)) THEN
        EXIT(0);
      Position := Position + STRLEN(PartOfText);
      ReadCharacter(' ',TimeText,Position,Length);
      IF Position <= Length THEN
        EXIT(Position);
      TimeText := FORMAT(000000T + ROUND(TIME - 000000T,1000));
      EXIT(0);
    END;

    PROCEDURE MakeText@5(VAR Text@1000 : Text[250]) : Integer;
    VAR
      StandardText@1002 : Record 7;
      PartOfText@1001 : Text[132];
      Position@1004 : Integer;
      Length@1003 : Integer;
    BEGIN
      Position := 1;
      Length := STRLEN(Text);
      ReadCharacter(' ',Text,Position,Length);
      IF NOT ReadSymbol('?',Text,Position,Length) THEN
        EXIT(0);
      PartOfText := COPYSTR(Text,Position);
      IF PartOfText = '' THEN BEGIN
        IF PAGE.RUNMODAL(0,StandardText) = ACTION::LookupOK THEN
          Text := StandardText.Description;
        EXIT(0);
      END;
      StandardText.Code := COPYSTR(Text,Position,MAXSTRLEN(StandardText.Code));
      IF NOT StandardText.FIND('=>') OR
         (UPPERCASE(PartOfText) <> COPYSTR(StandardText.Code,1,STRLEN(PartOfText)))
      THEN
        EXIT(Position);
      Text := StandardText.Description;
      EXIT(0);
    END;

    LOCAL PROCEDURE GetCalendarSetup@1040005();
    BEGIN
      IF NOT CalendarSetupRead THEN
        IF NOT CalendarSetup.GET THEN
          CalendarSetup.INIT;
    END;

    PROCEDURE MakeTextFilter@17(VAR TextFilterText@1000 : Text) : Integer;
    VAR
      Position@1002 : Integer;
      Length@1001 : Integer;
      PartOfText@1003 : Text[250];
    BEGIN
      Position := 1;
      Length := STRLEN(TextFilterText);
      ReadCharacter(' ',TextFilterText,Position,Length);
      IF FindText(PartOfText,TextFilterText,Position,Length) THEN
        CASE PartOfText OF
          COPYSTR('ME',1,STRLEN(PartOfText)),COPYSTR(MeText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              TextFilterText := USERID;
            END;
          COPYSTR('USER',1,STRLEN(PartOfText)),COPYSTR(UserText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              TextFilterText := USERID;
            END;
          COPYSTR('COMPANY',1,STRLEN(PartOfText)),COPYSTR(CompanyText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              TextFilterText := COMPANYNAME;
            END;
          COPYSTR('MYCUSTOMERS',1,STRLEN(PartOfText)),COPYSTR(MyCustomersText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              GetMyFilterText(TextFilterText,DATABASE::"My Customer");
            END;
          COPYSTR('MYITEMS',1,STRLEN(PartOfText)),COPYSTR(MyItemsText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              GetMyFilterText(TextFilterText,DATABASE::"My Item");
            END;
          COPYSTR('MYVENDORS',1,STRLEN(PartOfText)),COPYSTR(MyVendorsText,1,STRLEN(PartOfText)):
            BEGIN
              Position := Position + STRLEN(PartOfText);
              GetMyFilterText(TextFilterText,DATABASE::"My Vendor");
            END;
          ELSE
            EXIT(Position);
        END;
      EXIT(0);
    END;

    PROCEDURE MakeDateTimeFilter@6(VAR DateTimeFilterText@1000 : Text[250]) : Integer;
    VAR
      Head@1006 : Text[250];
      Tail@1008 : Text[250];
      Position@1012 : Integer;
      Length@1011 : Integer;
    BEGIN
      DateTimeFilterText := DELCHR(DateTimeFilterText,'<>');
      Position := 1;
      Length := STRLEN(DateTimeFilterText);
      WHILE Length <> 0 DO BEGIN
        ReadCharacter(' |()',DateTimeFilterText,Position,Length);
        IF Position > 1 THEN BEGIN
          Head := Head + COPYSTR(DateTimeFilterText,1,Position - 1);
          DateTimeFilterText := COPYSTR(DateTimeFilterText,Position);
          Position := 1;
          Length := STRLEN(DateTimeFilterText);
        END;
        IF Length <> 0 THEN BEGIN
          ReadUntilCharacter('|()',DateTimeFilterText,Position,Length);
          IF Position > 1 THEN BEGIN
            Tail := COPYSTR(DateTimeFilterText,Position);
            DateTimeFilterText := COPYSTR(DateTimeFilterText,1,Position - 1);
            MakeDateTimeFilter2(DateTimeFilterText);
            EVALUATE(Head,Head + DateTimeFilterText);
            DateTimeFilterText := Tail;
            Position := 1;
            Length := STRLEN(DateTimeFilterText);
          END;
        END;
      END;
      DateTimeFilterText := Head;
      EXIT(0);
    END;

    LOCAL PROCEDURE MakeDateTimeFilter2@7(VAR DateTimeFilterText@1000 : Text[250]);
    VAR
      DateTime1@1002 : DateTime;
      DateTime2@1005 : DateTime;
      Date1@1007 : Date;
      Date2@1003 : Date;
      Time1@1004 : Time;
      Time2@1001 : Time;
      StringPosition@1011 : Integer;
    BEGIN
      StringPosition := STRPOS(DateTimeFilterText,'..');
      IF StringPosition = 0 THEN BEGIN
        IF NOT GetSeparateDateTime(DateTimeFilterText,Date1,Time1) THEN
          EXIT;
        IF Date1 = 0D THEN
          EXIT;
        IF Time1 = 0T THEN BEGIN
          DateTimeFilterText := FORMAT(CREATEDATETIME(Date1,000000T)) + '..' + FORMAT(CREATEDATETIME(Date1,235959.995T));
          EXIT;
        END;
        DateTimeFilterText := FORMAT(CREATEDATETIME(Date1,Time1));
        EXIT;
      END;

      IF NOT GetSeparateDateTime(COPYSTR(DateTimeFilterText,1,StringPosition - 1),Date1,Time1) THEN
        EXIT;
      IF NOT GetSeparateDateTime(COPYSTR(DateTimeFilterText,StringPosition + 2),Date2,Time2) THEN
        EXIT;

      IF (Date1 = 0D) AND (Date2 = 0D) THEN
        EXIT;

      IF Date1 <> 0D THEN BEGIN
        IF Time1 = 0T THEN
          Time1 := 000000T;
        DateTime1 := CREATEDATETIME(Date1,Time1);
      END;
      IF Date2 <> 0D THEN BEGIN
        IF Time2 = 0T THEN
          Time2 := 235959T;
        DateTime2 := CREATEDATETIME(Date2,Time2);
      END;
      DateTimeFilterText := FORMAT(DateTime1) + '..' + FORMAT(DateTime2);
    END;

    PROCEDURE MakeDateFilter@8(VAR DateFilterText@1000 : Text[250]) : Integer;
    VAR
      Date1@1005 : Date;
      Date2@1004 : Date;
      Text1@1007 : Text[30];
      Text2@1006 : Text[30];
      StringPosition@1001 : Integer;
      i@1002 : Integer;
      OK@1003 : Boolean;
    BEGIN
      DateFilterText := DELCHR(DateFilterText,'<>');
      IF DateFilterText = '' THEN
        EXIT(0);
      StringPosition := STRPOS(DateFilterText,'..');
      IF StringPosition = 0 THEN BEGIN
        i := MakeDateFilter2(OK,Date1,Date2,DateFilterText);
        IF i <> 0 THEN
          EXIT(i);
        IF OK THEN
          IF Date1 = Date2 THEN
            DateFilterText := FORMAT(Date1)
          ELSE
            DateFilterText := STRSUBSTNO('%1..%2',Date1,Date2);
        EXIT(0);
      END;

      Text1 := COPYSTR(DateFilterText,1,StringPosition - 1);
      i := MakeDateFilter2(OK,Date1,Date2,Text1);
      IF i <> 0 THEN
        EXIT(i);
      IF OK THEN
        Text1 := FORMAT(Date1);

      ReadCharacter('.',DateFilterText,StringPosition,STRLEN(DateFilterText));

      Text2 := COPYSTR(DateFilterText,StringPosition);
      i := MakeDateFilter2(OK,Date1,Date2,Text2);
      IF i <> 0 THEN
        EXIT(StringPosition + i - 1);
      IF OK THEN
        Text2 := FORMAT(Date2);

      DateFilterText := Text1 + '..' + Text2;
      EXIT(0);
    END;

    LOCAL PROCEDURE MakeDateFilter2@9(VAR OK@1000 : Boolean;VAR Date1@1001 : Date;VAR Date2@1002 : Date;VAR DateFilterText@1003 : Text[1024]) : Integer;
    VAR
      Calendar@1040005 : Record 2000000007;
      ScopeIdentifier@1040004 : ARRAY [5] OF Text[30];
      PartOfText@1004 : Text[250];
      RemainderOfText@1009 : Text[1024];
      i@1040003 : Integer;
      Position@1007 : Integer;
      Length@1006 : Integer;
      DateFormula@1005 : DateFormula;
    BEGIN
      IF EVALUATE(DateFormula,DateFilterText) THEN BEGIN
        RemainderOfText := DateFilterText;
        DateFilterText := '';
      END ELSE BEGIN
        Position := STRPOS(DateFilterText,'+');
        IF Position = 0 THEN
          Position := STRPOS(DateFilterText,'-');

        IF Position > 0 THEN BEGIN
          RemainderOfText := DELCHR(COPYSTR(DateFilterText,Position));
          IF EVALUATE(DateFormula,RemainderOfText) THEN
            DateFilterText := DELCHR(COPYSTR(DateFilterText,1,Position - 1))
          ELSE
            RemainderOfText := '';
        END;
      END;

      Position := 1;
      Length := STRLEN(DateFilterText);
      ReadCharacter(' ',DateFilterText,Position,Length);

      FindText(PartOfText,DateFilterText,Position,Length);
      IF PartOfText <> '' THEN BEGIN
        WITH Calendar DO BEGIN
          FOR i := 1 TO 4 DO BEGIN
            "Period Type" := i;
            ScopeIdentifier[i] := UPPERCASE((FORMAT("Period Type")));
          END;
        END;
        CASE PartOfText OF
          COPYSTR('TODAY',1,STRLEN(PartOfText)),COPYSTR(TodayText,1,STRLEN(PartOfText)):
            OK := FindDate(TODAY,Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('WORKDATE',1,STRLEN(PartOfText)),COPYSTR(WorkdateText,1,STRLEN(PartOfText)):
            OK := FindDate(WORKDATE,Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('NOW',1,STRLEN(PartOfText)),COPYSTR(NowText,1,STRLEN(PartOfText)):
            OK := FindDate(DT2DATE(CURRENTDATETIME),Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('YESTERDAY',1,STRLEN(PartOfText)),COPYSTR(YesterdayText,1,STRLEN(PartOfText)):
            OK := FindDate(CALCDATE('<-1D>'),Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('TOMORROW',1,STRLEN(PartOfText)),COPYSTR(TomorrowText,1,STRLEN(PartOfText)):
            OK := FindDate(CALCDATE('<1D>'),Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('WEEK',1,STRLEN(PartOfText)),COPYSTR(WeekText,1,STRLEN(PartOfText)):
            OK := FindDates('<-CW>','<CW>',Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('MONTH',1,STRLEN(PartOfText)),COPYSTR(MonthText,1,STRLEN(PartOfText)):
            OK := FindDates('<-CM>','<CM>',Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR('QUARTER',1,STRLEN(PartOfText)),COPYSTR(QuarterText,1,STRLEN(PartOfText)):
            OK := FindDates('<-CQ>','<CQ>',Date1,Date2,DateFilterText,PartOfText,Position,Length);
          COPYSTR(ScopeIdentifier[1],1,STRLEN(PartOfText)),
          WeekIdentifier:
            OK := FindPeriod(Date1,Date2,Calendar."Period Type"::Week,DateFilterText,PartOfText,Position,Length);
          COPYSTR(ScopeIdentifier[2],1,STRLEN(PartOfText)),
          COPYSTR('PERIOD',1,STRLEN(PartOfText)),COPYSTR(PeriodText,1,STRLEN(PartOfText)):
            OK := FindPeriod(Date1,Date2,Calendar."Period Type"::Month,DateFilterText,PartOfText,Position,Length);
          COPYSTR(ScopeIdentifier[3],1,STRLEN(PartOfText)):
            OK := FindPeriod(Date1,Date2,Calendar."Period Type"::Quarter,DateFilterText,PartOfText,Position,Length);
          COPYSTR(ScopeIdentifier[4],1,STRLEN(PartOfText)),
          COPYSTR('YEAR',1,STRLEN(PartOfText)),COPYSTR(YearText,1,STRLEN(PartOfText)):
            OK := FindPeriod(Date1,Date2,Calendar."Period Type"::Year,DateFilterText,PartOfText,Position,Length);
        END
      END
        ELSE
          IF (DateFilterText <> '') AND EVALUATE(Date1,DateFilterText) THEN BEGIN
            Date2 := Date1;
            OK := TRUE;
            Position := 0;
          END ELSE
            IF RemainderOfText <> '' THEN BEGIN
              Date1 := TODAY;
              Date2 := Date1;
              OK := TRUE;
              Position := 0;
            END ELSE
              OK := FALSE;

      IF OK AND (RemainderOfText <> '') THEN BEGIN
        Date1 := CALCDATE(DateFormula,Date1);
        Date2 := CALCDATE(DateFormula,Date2);
      END;
      EXIT(Position);
    END;

    PROCEDURE MakeTimeFilter@22(VAR TimeFilterText@1000 : Text[250]) : Integer;
    VAR
      Head@1006 : Text[250];
      Tail@1008 : Text[250];
      Position@1012 : Integer;
      Length@1011 : Integer;
    BEGIN
      TimeFilterText := DELCHR(TimeFilterText,'<>');
      Position := 1;
      Length := STRLEN(TimeFilterText);
      WHILE Length <> 0 DO BEGIN
        ReadCharacter(' |()',TimeFilterText,Position,Length);
        IF Position > 1 THEN BEGIN
          Head := Head + COPYSTR(TimeFilterText,1,Position - 1);
          TimeFilterText := COPYSTR(TimeFilterText,Position);
          Position := 1;
          Length := STRLEN(TimeFilterText);
        END;
        IF Length <> 0 THEN BEGIN
          ReadUntilCharacter('|()',TimeFilterText,Position,Length);
          IF Position > 1 THEN BEGIN
            Tail := COPYSTR(TimeFilterText,Position);
            TimeFilterText := COPYSTR(TimeFilterText,1,Position - 1);
            MakeTimeFilter2(TimeFilterText);
            EVALUATE(Head,Head + TimeFilterText);
            TimeFilterText := Tail;
            Position := 1;
            Length := STRLEN(TimeFilterText);
          END;
        END;
      END;
      TimeFilterText := Head;
      EXIT(0);
    END;

    LOCAL PROCEDURE MakeTimeFilter2@21(VAR TimeFilterText@1000 : Text[250]);
    VAR
      Time1@1004 : Time;
      Time2@1001 : Time;
      StringPosition@1011 : Integer;
    BEGIN
      StringPosition := STRPOS(TimeFilterText,'..');
      IF StringPosition = 0 THEN BEGIN
        IF NOT GetTime(Time1,TimeFilterText) THEN
          EXIT;
        IF Time1 = 0T THEN BEGIN
          TimeFilterText := FORMAT(000000T) + '..' + FORMAT(235959.995T);
          EXIT;
        END;
        TimeFilterText := FORMAT(Time1);
        EXIT;
      END;
      IF NOT GetTime(Time1,COPYSTR(TimeFilterText,1,StringPosition - 1)) THEN
        EXIT;
      IF NOT GetTime(Time2,COPYSTR(TimeFilterText,StringPosition + 2)) THEN
        EXIT;

      IF Time1 = 0T THEN
        Time1 := 000000T;
      IF Time2 = 0T THEN
        Time2 := 235959T;

      TimeFilterText := FORMAT(Time1) + '..' + FORMAT(Time2);
    END;

    PROCEDURE EvaluateIncStr@20(StringToIncrement@1000 : Code[20];ErrorHint@1001 : Text);
    BEGIN
      IF INCSTR(StringToIncrement) = '' THEN
        ERROR(UnincrementableStringError,ErrorHint);
    END;

    PROCEDURE UnincrementableStringError@19() : Text;
    BEGIN
      EXIT(UnincrementableStringErr)
    END;

    LOCAL PROCEDURE GetTime@16(VAR Time0@1000 : Time;FilterText@1001 : Text[250]) : Boolean;
    BEGIN
      FilterText := DELCHR(FilterText);
      IF FilterText IN [NowText,'NOW'] THEN BEGIN
        Time0 := TIME;
        EXIT(TRUE);
      END;
      EXIT(EVALUATE(Time0,FilterText));
    END;

    LOCAL PROCEDURE FindPeriod@10(VAR Date1@1000 : Date;VAR Date2@1001 : Date;PeriodType@1040010 : 'Day,Week,Month,Quarter,Year';VAR DateFilterText@1003 : Text;PartOfText@1040000 : Text;VAR Position@1004 : Integer;Length@1005 : Integer) : Boolean;
    VAR
      Calendar@1040001 : Record 2000000007;
      AccountingPeriod@104000 : Record 10560;
      Sign@1007 : Text[1];
      Numeral@1008 : Integer;
      UseAccountingPeriod@1040004 : Boolean;
    BEGIN
      Position := Position + STRLEN(PartOfText);
      ReadCharacter(' ',DateFilterText,Position,Length);
      Calendar.RESET;
      Calendar.SETRANGE("Period Type",PeriodType);
      Calendar."Period Type" := PeriodType;
      AccountingPeriod.RESET;
      AccountingPeriod.SETRANGE("Period Type",PeriodType);
      AccountingPeriod."Period Type" := PeriodType;
      UseAccountingPeriod := AccountingPeriod.FIND('-');
      GetCalendarSetup;
      CASE PeriodType OF
        PeriodType::Week:
          IF CalendarSetup."Use System For Week Period" THEN
            UseAccountingPeriod := FALSE;
        PeriodType::Month:
          IF CalendarSetup."Use System For Month Period" THEN
            UseAccountingPeriod := FALSE;
        PeriodType::Quarter:
          IF CalendarSetup."Use System For Quarter Period" THEN
            UseAccountingPeriod := FALSE;
      END;
      Sign := '';
      IF ReadSymbol('+',DateFilterText,Position,Length) THEN
        Sign := '+'
      ELSE
        IF ReadSymbol('-',DateFilterText,Position,Length) THEN
          Sign := '-';
      IF Sign = '' THEN
        IF ReadNumeral(Numeral,DateFilterText,Position,Length) THEN BEGIN
          IF AccountingPeriod."Period Type" = AccountingPeriod."Period Type"::Year THEN BEGIN
            AccountingPeriod.FINDFIRST;
            AccountingPeriod.NEXT(Numeral - 1);
        END ELSE BEGIN
            IF UseAccountingPeriod THEN BEGIN
              WITH AccountingPeriod DO BEGIN
                SETRANGE("Period Type","Period Type"::Year);
                SETRANGE("Period Start",0D,WORKDATE);
                FIND('+');
                SETRANGE("Period Type",PeriodType);
                SETRANGE("Period Start","Period Start","Period End");
                FIND('-');
                IF Numeral = 1 THEN
                  SETRANGE("Period Start");
                NEXT(Numeral - "Period No.");
              END;
            END ELSE BEGIN
              WITH Calendar DO BEGIN
                SETRANGE("Period Type","Period Type"::Year);
                SETRANGE("Period Start",0D,WORKDATE);
                FIND('+');
                SETRANGE("Period Type",PeriodType);
                SETRANGE("Period Start","Period Start","Period End");
                FIND('-');
                IF Numeral = 1 THEN
                  SETRANGE("Period Start");
                NEXT(Numeral - "Period No.");
              END;
            END;
          END;
        END ELSE BEGIN
          IF UseAccountingPeriod THEN BEGIN
            WITH AccountingPeriod DO BEGIN
              "Period Start" := WORKDATE;
              FIND('=<');
            END;
          END ELSE BEGIN
            WITH Calendar DO BEGIN
              "Period Start" := WORKDATE;
              FIND('=<');
            END;
          END;
        END
      ELSE BEGIN
        IF NOT ReadNumeral(Numeral,DateFilterText,Position,Length) THEN
          EXIT(TRUE);
        IF Sign = '-' THEN
          Numeral := -Numeral;
        IF UseAccountingPeriod THEN BEGIN
          WITH AccountingPeriod DO BEGIN
            "Period Start" := WORKDATE;
            FIND('=<');
            NEXT(Numeral);
          END;
        END ELSE BEGIN
          WITH Calendar DO BEGIN
            "Period Start" := WORKDATE;
            FIND('=<');
            NEXT(Numeral);
          END;
        END;
      END;
      IF NOT UseAccountingPeriod THEN
        AccountingPeriod.TRANSFERFIELDS(Calendar);
      Date1 := AccountingPeriod."Period Start";
      Date2 := NORMALDATE(AccountingPeriod."Period End");
      ReadCharacter(' ',DateFilterText,Position,Length);
      IF Position > Length THEN
        Position := 0;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FindDate@18(Date1Input@1000 : Date;VAR Date1@1002 : Date;VAR Date2@1001 : Date;DateFilterText@1003 : Text;PartOfText@1006 : Text;VAR Position@1004 : Integer;Length@1005 : Integer) : Boolean;
    BEGIN
      Position := Position + STRLEN(PartOfText);
      ReadCharacter(' ',DateFilterText,Position,Length);
      Date1 := Date1Input;
      Date2 := Date1;
      IF Position > Length THEN
        Position := 0;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FindDates@27(DateFormulaText1@1000 : Text;DateFormulaText2@1006 : Text;VAR Date1@1002 : Date;VAR Date2@1001 : Date;DateFilterText@1003 : Text[1024];PartOfText@1007 : Text;VAR Position@1004 : Integer;Length@1005 : Integer) : Boolean;
    BEGIN
      Position := Position + STRLEN(PartOfText);
      ReadCharacter(' ',DateFilterText,Position,Length);
      Date1 := CALCDATE(DateFormulaText1);
      Date2 := CALCDATE(DateFormulaText2);
      IF Position > Length THEN
        Position := 0;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FindText@11(VAR PartOfText@1000 : Text[250];Text@1001 : Text;Position@1002 : Integer;Length@1003 : Integer) : Boolean;
    VAR
      Position2@1005 : Integer;
    BEGIN
      Position2 := Position;
      ReadCharacter(AlphabetText,Text,Position,Length);
      IF Position = Position2 THEN
        EXIT(FALSE);
      PartOfText := UPPERCASE(COPYSTR(Text,Position2,Position - Position2));
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ReadSymbol@12(Token@1000 : Text[30];Text@1001 : Text[1024];VAR Position@1002 : Integer;Length@1003 : Integer) : Boolean;
    BEGIN
      IF Token <> COPYSTR(Text,Position,STRLEN(Token)) THEN
        EXIT(FALSE);
      Position := Position + STRLEN(Token);
      ReadCharacter(' ',Text,Position,Length);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ReadNumeral@13(VAR Numeral@1000 : Integer;Text@1001 : Text[1024];VAR Position@1002 : Integer;Length@1003 : Integer) : Boolean;
    VAR
      Position2@1005 : Integer;
      i@1004 : Integer;
    BEGIN
      Position2 := Position;
      ReadCharacter('0123456789',Text,Position,Length);
      IF Position2 = Position THEN
        EXIT(FALSE);
      Numeral := 0;
      FOR i := Position2 TO Position - 1 DO
        IF Numeral < 1000 THEN
          Numeral := Numeral * 10 + STRPOS('0123456789',COPYSTR(Text,i,1)) - 1;
      IF (Numeral < 1) OR (Numeral > 999) THEN
        ERROR(NumeralOutOfRangeError);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ReadCharacter@14(Character@1000 : Text[50];Text@1001 : Text;VAR Position@1002 : Integer;Length@1003 : Integer);
    BEGIN
      WHILE (Position <= Length) AND (STRPOS(Character,UPPERCASE(COPYSTR(Text,Position,1))) <> 0) DO
        Position := Position + 1;
    END;

    LOCAL PROCEDURE ReadUntilCharacter@15(Character@1000 : Text[50];Text@1001 : Text[250];VAR Position@1002 : Integer;Length@1003 : Integer);
    BEGIN
      WHILE (Position <= Length) AND (STRPOS(Character,UPPERCASE(COPYSTR(Text,Position,1))) = 0) DO
        Position := Position + 1;
    END;

    LOCAL PROCEDURE GetMyFilterText@23(VAR TextFilterText@1001 : Text;MyTableNo@1000 : Integer);
    VAR
      RecRef@1002 : RecordRef;
      FieldRef@1004 : FieldRef;
      NoOfValues@1003 : Integer;
    BEGIN
      IF NOT (MyTableNo IN [DATABASE::"My Customer",DATABASE::"My Vendor",DATABASE::"My Item"]) THEN
        EXIT;

      TextFilterText := '';
      NoOfValues := 0;
      RecRef.OPEN(MyTableNo);
      FieldRef := RecRef.FIELD(1);
      FieldRef.SETRANGE(USERID);
      IF RecRef.FIND('-') THEN
        REPEAT
          FieldRef := RecRef.FIELD(2);
          AddToFilter(TextFilterText,FORMAT(FieldRef.VALUE));
          NoOfValues += 1;
        UNTIL (RecRef.NEXT = 0) OR (NoOfValues > 2000);
      RecRef.CLOSE;

      IF NoOfValues > 2000 THEN
        MESSAGE(OverflowMsg);
    END;

    LOCAL PROCEDURE AddToFilter@37(VAR FilterString@1001 : Text;MyNo@1000 : Code[20]);
    BEGIN
      IF FilterString = '' THEN
        FilterString := MyNo
      ELSE
        FilterString += '|' + MyNo;
    END;

    PROCEDURE RemoveMessageTrailingDots@24(Message@1000 : Text) : Text;
    BEGIN
      EXIT(DELCHR(Message,'>','.'));
    END;

    BEGIN
    END.
  }
}

