OBJECT Codeunit 841 Cash Flow Management
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst '@@@=Source data doesn''t exist for G/L Account: 8210.;ENU=Source data does not exist for %1: %2.;ENG=Source data does not exist for %1: %2.';
      Text001@1001 : TextConst '@@@=Source data doesn''t exist in Vendor Ledger Entry for Document No.: PO000123.;ENU=Source data does not exist in %1 for %2: %3.;ENG=Source data does not exist in %1 for %2: %3.';
      Text002@1002 : TextConst 'ENU=Source type is not supported.;ENG=Source type is not supported.';

    PROCEDURE ShowSource@1(SourceType@1009 : Integer;SourceNo@1010 : Code[20];BudgetName@1000 : Code[10]);
    VAR
      CFWorksheetLine@1008 : Record 846;
    BEGIN
      CASE SourceType OF
        CFWorksheetLine."Source Type"::"Liquid Funds":
          ShowLiquidFunds(SourceNo);
        CFWorksheetLine."Source Type"::Receivables:
          ShowCustomer(SourceNo);
        CFWorksheetLine."Source Type"::Payables:
          ShowVendor(SourceNo);
        CFWorksheetLine."Source Type"::"Sales Orders":
          ShowSO(SourceNo);
        CFWorksheetLine."Source Type"::"Purchase Orders":
          ShowPO(SourceNo);
        CFWorksheetLine."Source Type"::"Service Orders":
          ShowServO(SourceNo);
        CFWorksheetLine."Source Type"::"Cash Flow Manual Revenue":
          ShowManualRevenue(SourceNo);
        CFWorksheetLine."Source Type"::"Cash Flow Manual Expense":
          ShowManualExpense(SourceNo);
        CFWorksheetLine."Source Type"::"Fixed Assets Budget",
        CFWorksheetLine."Source Type"::"Fixed Assets Disposal":
          ShowFA(SourceNo);
        CFWorksheetLine."Source Type"::"G/L Budget":
          ShowGLBudget(BudgetName,SourceNo);
        ELSE
          ERROR(Text002);
      END;
    END;

    LOCAL PROCEDURE ShowLiquidFunds@4(SourceNo@1000 : Code[20]);
    VAR
      GLAccount@1007 : Record 15;
    BEGIN
      GLAccount.SETRANGE("No.",SourceNo);
      IF NOT GLAccount.FINDFIRST THEN
        ERROR(Text000,GLAccount.TABLECAPTION,SourceNo);
      PAGE.RUN(PAGE::"Chart of Accounts",GLAccount);
    END;

    LOCAL PROCEDURE ShowCustomer@5(SourceNo@1000 : Code[20]);
    VAR
      CustLedgEntry@1012 : Record 21;
    BEGIN
      CustLedgEntry.SETRANGE("Document No.",SourceNo);
      IF NOT CustLedgEntry.FINDFIRST THEN
        ERROR(Text001,CustLedgEntry.TABLECAPTION,CustLedgEntry.FIELDCAPTION("Document No."),SourceNo);
      PAGE.RUN(0,CustLedgEntry);
    END;

    LOCAL PROCEDURE ShowVendor@6(SourceNo@1000 : Code[20]);
    VAR
      VendLedgEntry@1011 : Record 25;
    BEGIN
      VendLedgEntry.SETRANGE("Document No.",SourceNo);
      IF NOT VendLedgEntry.FINDFIRST THEN
        ERROR(Text001,VendLedgEntry.TABLECAPTION,VendLedgEntry.FIELDCAPTION("Document No."),SourceNo);
      PAGE.RUN(0,VendLedgEntry);
    END;

    LOCAL PROCEDURE ShowSO@7(SourceNo@1000 : Code[20]);
    VAR
      SalesHeader@1010 : Record 36;
      SalesOrder@1005 : Page 42;
    BEGIN
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Order);
      SalesHeader.SETRANGE("No.",SourceNo);
      IF NOT SalesHeader.FINDFIRST THEN
        ERROR(Text000,SalesOrder.CAPTION,SourceNo);
      SalesOrder.SETTABLEVIEW(SalesHeader);
      SalesOrder.RUN;
    END;

    LOCAL PROCEDURE ShowPO@8(SourceNo@1000 : Code[20]);
    VAR
      PurchaseHeader@1009 : Record 38;
      PurchaseOrder@1004 : Page 50;
    BEGIN
      PurchaseHeader.SETRANGE("Document Type",PurchaseHeader."Document Type"::Order);
      PurchaseHeader.SETRANGE("No.",SourceNo);
      IF NOT PurchaseHeader.FINDFIRST THEN
        ERROR(Text000,PurchaseOrder.CAPTION,SourceNo);
      PurchaseOrder.SETTABLEVIEW(PurchaseHeader);
      PurchaseOrder.RUN;
    END;

    LOCAL PROCEDURE ShowServO@9(SourceNo@1000 : Code[20]);
    VAR
      ServiceHeader@1008 : Record 5900;
      ServiceOrder@1003 : Page 5900;
    BEGIN
      ServiceHeader.SETRANGE("Document Type",ServiceHeader."Document Type"::Order);
      ServiceHeader.SETRANGE("No.",SourceNo);
      IF NOT ServiceHeader.FINDFIRST THEN
        ERROR(Text000,ServiceOrder.CAPTION,SourceNo);
      ServiceOrder.SETTABLEVIEW(ServiceHeader);
      ServiceOrder.RUN;
    END;

    LOCAL PROCEDURE ShowManualRevenue@10(SourceNo@1000 : Code[20]);
    VAR
      CFManualRevenue@1006 : Record 849;
      CFManualRevenues@1001 : Page 857;
    BEGIN
      CFManualRevenue.SETRANGE(Code,SourceNo);
      IF NOT CFManualRevenue.FINDFIRST THEN
        ERROR(Text000,CFManualRevenues.CAPTION,SourceNo);
      CFManualRevenues.SETTABLEVIEW(CFManualRevenue);
      CFManualRevenues.RUN;
    END;

    LOCAL PROCEDURE ShowManualExpense@11(SourceNo@1000 : Code[20]);
    VAR
      CFManualExpense@1006 : Record 850;
      CFManualExpenses@1001 : Page 859;
    BEGIN
      CFManualExpense.SETRANGE(Code,SourceNo);
      IF NOT CFManualExpense.FINDFIRST THEN
        ERROR(Text000,CFManualExpenses.CAPTION,SourceNo);
      CFManualExpenses.SETTABLEVIEW(CFManualExpense);
      CFManualExpenses.RUN;
    END;

    LOCAL PROCEDURE ShowFA@2(SourceNo@1000 : Code[20]);
    VAR
      FixedAsset@1006 : Record 5600;
    BEGIN
      FixedAsset.SETRANGE("No.",SourceNo);
      IF NOT FixedAsset.FINDFIRST THEN
        ERROR(Text001,FixedAsset.TABLECAPTION,FixedAsset.FIELDCAPTION("No."),SourceNo);
      PAGE.RUN(PAGE::"Fixed Asset Card",FixedAsset);
    END;

    LOCAL PROCEDURE ShowGLBudget@3(BudgetName@1000 : Code[10];SourceNo@1001 : Code[20]);
    VAR
      GLBudgetName@1003 : Record 95;
      GLAccount@1004 : Record 15;
      Budget@1002 : Page 113;
    BEGIN
      IF NOT GLAccount.GET(SourceNo) THEN
        ERROR(Text000,GLAccount.TABLECAPTION,SourceNo);
      IF NOT GLBudgetName.GET(BudgetName) THEN
        ERROR(Text000,GLBudgetName.TABLECAPTION,BudgetName);
      Budget.SetBudgetName(BudgetName);
      Budget.SetGLAccountFilter(SourceNo);
      Budget.RUN;
    END;

    PROCEDURE CashFlowName@12(CashFlowNo@1001 : Code[20]) : Text[50];
    VAR
      CashFlowForecast@1000 : Record 840;
    BEGIN
      IF CashFlowForecast.GET(CashFlowNo) THEN
        EXIT(CashFlowForecast.Description);
      EXIT('')
    END;

    PROCEDURE CashFlowAccountName@13(CashFlowAccountNo@1000 : Code[20]) : Text[50];
    VAR
      CashFlowAccount@1001 : Record 841;
    BEGIN
      IF CashFlowAccount.GET(CashFlowAccountNo) THEN
        EXIT(CashFlowAccount.Name);
      EXIT('')
    END;

    BEGIN
    END.
  }
}

