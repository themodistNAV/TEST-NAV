OBJECT Codeunit 880 OCR - Send to Service
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            SendAllReadyToOcr;
          END;

  }
  CODE
  {
    VAR
      SendMsg@1000 : TextConst 'ENU=Sending to the OCR Service @1@@@@@@@@@@@@@@@@@@@.;ENG=Sending to the OCR Service @1@@@@@@@@@@@@@@@@@@@.';
      SendDoneMsg@1001 : TextConst '@@@=%1 is a number, e.g. 1;ENU=%1 documents have been sent to the OCR service.;ENG=%1 documents have been sent to the OCR service.';

    PROCEDURE SendAllReadyToOcr@1();
    VAR
      IncomingDocument@1001 : Record 130;
      IncomingDocumentAttachment@1002 : Record 133;
      TempIncomingDocumentAttachment@1006 : TEMPORARY Record 133;
      OCRServiceMgt@1003 : Codeunit 1294;
      IncDocAttsReadyforOCR@1000 : Query 133;
      Window@1008 : Dialog;
      NoOfDocuments@1005 : Integer;
      i@1007 : Integer;
    BEGIN
      IF NOT IncDocAttsReadyforOCR.OPEN THEN
        EXIT;  // empty

      Window.OPEN(SendMsg);

      // Find Document Count and lock records
      IncomingDocument.LOCKTABLE;
      IncomingDocumentAttachment.LOCKTABLE;
      WHILE IncDocAttsReadyforOCR.READ DO BEGIN
        NoOfDocuments += 1;
        IncomingDocumentAttachment.GET(IncDocAttsReadyforOCR.Incoming_Document_Entry_No,IncDocAttsReadyforOCR.Line_No);
        IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");  // lock
        TempIncomingDocumentAttachment := IncomingDocumentAttachment;
        TempIncomingDocumentAttachment.INSERT;
      END;
      IncDocAttsReadyforOCR.CLOSE;
      IF NoOfDocuments = 0 THEN
        EXIT;

      OCRServiceMgt.StartUpload(NoOfDocuments);

      TempIncomingDocumentAttachment.FINDSET;
      REPEAT
        i += 1;
        Window.UPDATE(1,10000 * i DIV NoOfDocuments);
        WITH IncomingDocumentAttachment DO BEGIN
          IncomingDocumentAttachment := TempIncomingDocumentAttachment;
          FIND;
          IncomingDocument.GET("Incoming Document Entry No.");
          IncomingDocument.SendToOCR(FALSE);
        END;
      UNTIL TempIncomingDocumentAttachment.NEXT = 0;

      COMMIT;
      Window.CLOSE;
      MESSAGE(SendDoneMsg,NoOfDocuments);
    END;

    BEGIN
    END.
  }
}

