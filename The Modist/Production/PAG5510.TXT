OBJECT Page 5510 Production Journal
{
  OBJECT-PROPERTIES
  {
    Date=23/03/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45480;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Production Journal;
               ENG=Production Journal];
    InsertAllowed=No;
    SourceTable=Table83;
    DataCaptionExpr=GetCaption;
    PageType=Worksheet;
    OnInit=BEGIN
             "Applies-from EntryEditable" := TRUE;
             QuantityEditable := TRUE;
             "Output QuantityEditable" := TRUE;
             "Scrap QuantityEditable" := TRUE;
             "Scrap CodeEditable" := TRUE;
             FinishedEditable := TRUE;
             "Work Shift CodeEditable" := TRUE;
             "Run TimeEditable" := TRUE;
             "Setup TimeEditable" := TRUE;
             CapUnitofMeasureCodeEditable := TRUE;
             "Concurrent CapacityEditable" := TRUE;
             "Ending TimeEditable" := TRUE;
             "Starting TimeEditable" := TRUE;
           END;

    OnOpenPage=BEGIN
                 SetFilterGroup;

                 IF ProdOrderLineNo <> 0 THEN
                   ProdOrderLine.GET(ProdOrder.Status,ProdOrder."No.",ProdOrderLineNo);
               END;

    OnAfterGetRecord=BEGIN
                       ActualScrapQtyHideValue := FALSE;
                       ActualOutputQtyHideValue := FALSE;
                       ActualRunTimeHideValue := FALSE;
                       ActualSetupTimeHideValue := FALSE;
                       ActualConsumpQtyHideValue := FALSE;
                       "Scrap QuantityHideValue" := FALSE;
                       "Output QuantityHideValue" := FALSE;
                       "Run TimeHideValue" := FALSE;
                       "Setup TimeHideValue" := FALSE;
                       QuantityHideValue := FALSE;
                       DescriptionIndent := 0;
                       ShowShortcutDimCode(ShortcutDimCode);
                       DescriptionOnFormat;
                       QuantityOnFormat;
                       SetupTimeOnFormat;
                       RunTimeOnFormat;
                       OutputQuantityOnFormat;
                       ScrapQuantityOnFormat;
                       ActualConsumpQtyOnFormat;
                       ActualSetupTimeOnFormat;
                       ActualRunTimeOnFormat;
                       ActualOutputQtyOnFormat;
                       ActualScrapQtyOnFormat;
                     END;

    OnModifyRecord=BEGIN
                     "Changed by User" := TRUE;
                   END;

    OnDeleteRecord=VAR
                     ReserveItemJnlLine@1000 : Codeunit 99000835;
                   BEGIN
                     COMMIT;
                     IF NOT ReserveItemJnlLine.DeleteLineConfirm(Rec) THEN
                       EXIT(FALSE);
                     ReserveItemJnlLine.DeleteLine(Rec);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           GetActTimeAndQtyBase;

                           ControlsMngt;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 35      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Line;
                                 ENG=&Line];
                      Image=Line }
      { 44      ;2   ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 ENG=Dimensions];
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 45      ;2   ;Action    ;
                      Name=ItemTrackingLines;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=[ENU=Item &Tracking Lines;
                                 ENG=Item &Tracking Lines];
                      Image=ItemTrackingLines;
                      OnAction=BEGIN
                                 OpenItemTrackingLines(FALSE);
                               END;
                                }
      { 46      ;2   ;Action    ;
                      CaptionML=[ENU=Bin Contents;
                                 ENG=Bin Contents];
                      RunObject=Page 7305;
                      RunPageView=SORTING(Location Code,Bin Code,Item No.,Variant Code);
                      RunPageLink=Location Code=FIELD(Location Code),
                                  Item No.=FIELD(Item No.),
                                  Variant Code=FIELD(Variant Code);
                      Image=BinContent }
      { 18      ;1   ;ActionGroup;
                      CaptionML=[ENU=Pro&d. Order;
                                 ENG=Pro&d. Order];
                      Image=Order }
      { 19      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Card;
                                 ENG=Card];
                      RunObject=Page 99000831;
                      RunPageLink=No.=FIELD(Order No.);
                      Image=EditLines }
      { 20      ;2   ;ActionGroup;
                      CaptionML=[ENU=Ledger E&ntries;
                                 ENG=Ledger E&ntries];
                      Image=Entries }
      { 21      ;3   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Item Ledger E&ntries;
                                 ENG=Item Ledger E&ntries];
                      RunObject=Page 38;
                      RunPageView=SORTING(Order Type,Order No.);
                      RunPageLink=Order Type=CONST(Production),
                                  Order No.=FIELD(Order No.);
                      Image=ItemLedger }
      { 22      ;3   ;Action    ;
                      CaptionML=[ENU=Capacity Ledger Entries;
                                 ENG=Capacity Ledger Entries];
                      RunObject=Page 5832;
                      RunPageView=SORTING(Order Type,Order No.);
                      RunPageLink=Order Type=CONST(Production),
                                  Order No.=FIELD(Order No.);
                      Image=CapacityLedger }
      { 25      ;3   ;Action    ;
                      CaptionML=[ENU=Value Entries;
                                 ENG=Value Entries];
                      RunObject=Page 5802;
                      RunPageView=SORTING(Order Type,Order No.);
                      RunPageLink=Order Type=CONST(Production),
                                  Order No.=FIELD(Order No.);
                      Image=ValueLedger }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 29      ;1   ;ActionGroup;
                      CaptionML=[ENU=P&osting;
                                 ENG=P&osting];
                      Image=Post }
      { 41      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Test Report;
                                 ENG=Test Report];
                      Image=TestReport;
                      OnAction=BEGIN
                                 ReportPrint.PrintItemJnlLine(Rec);
                               END;
                                }
      { 56      ;2   ;Action    ;
                      Name=Post;
                      ShortCutKey=F9;
                      CaptionML=[ENU=P&ost;
                                 ENG=P&ost];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PostOrder;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DeleteRecTemp;

                                 PostingItemJnlFromProduction(FALSE);

                                 InsertTempRec;

                                 SetFilterGroup;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 57      ;2   ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=[ENU=Post and &Print;
                                 ENG=Post and &Print];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PostPrint;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DeleteRecTemp;

                                 PostingItemJnlFromProduction(TRUE);

                                 InsertTempRec;

                                 SetFilterGroup;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 31      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Print;
                                 ENG=&Print];
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ItemJnlLine@1001 : Record 83;
                               BEGIN
                                 ItemJnlLine.COPY(Rec);
                                 ItemJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                 ItemJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                 REPORT.RUNMODAL(REPORT::"Inventory Movement",TRUE,TRUE,ItemJnlLine);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 61  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 48  ;2   ;Field     ;
                CaptionML=[ENU=Posting Date;
                           ENG=Posting Date];
                SourceExpr=PostingDate;
                OnValidate=BEGIN
                             PostingDateOnAfterValidate;
                           END;
                            }

    { 60  ;2   ;Field     ;
                CaptionML=[ENU=Flushing Method Filter;
                           ENG=Flushing Method Filter];
                OptionCaptionML=[ENU=Manual,Forward,Backward,Pick + Forward,Pick + Backward,All Methods;
                                 ENG=Manual,Forward,Backward,Pick + Forward,Pick + Backward,All Methods];
                SourceExpr=FlushingFilter;
                OnValidate=BEGIN
                             FlushingFilterOnAfterValidate;
                           END;
                            }

    { 1   ;1   ;Group     ;
                IndentationColumnName=DescriptionIndent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 43  ;2   ;Field     ;
                SourceExpr="Entry Type";
                Editable=FALSE }

    { 38  ;2   ;Field     ;
                SourceExpr="Posting Date";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Order Line No.";
                Visible=FALSE;
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Item No.";
                Editable=FALSE;
                OnLookup=BEGIN
                           IF Item.GET("Item No.") THEN
                             PAGE.RUNMODAL(PAGE::"Item List",Item);
                         END;
                          }

    { 64  ;2   ;Field     ;
                SourceExpr="Operation No.";
                Editable=FALSE }

    { 80  ;2   ;Field     ;
                OptionCaptionML=[ENU="Work Center,Machine Center, ";
                                 ENG="Work Centre,Machine Centre, "];
                SourceExpr=Type;
                Visible=TRUE }

    { 39  ;2   ;Field     ;
                SourceExpr="Flushing Method";
                Visible=FALSE }

    { 82  ;2   ;Field     ;
                SourceExpr="No." }

    { 5   ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 84  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE;
                StyleExpr=DescriptionEmphasize }

    { 23  ;2   ;Field     ;
                CaptionML=[ENU=Consumption Quantity;
                           ENG=Consumption Quantity];
                SourceExpr=Quantity;
                Editable=QuantityEditable;
                HideValue=QuantityHideValue }

    { 26  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code";
                Visible=FALSE }

    { 100 ;2   ;Field     ;
                SourceExpr="Location Code";
                Visible=FALSE }

    { 28  ;2   ;Field     ;
                SourceExpr="Bin Code";
                Visible=FALSE }

    { 114 ;2   ;Field     ;
                SourceExpr="Work Shift Code";
                Visible=FALSE;
                Editable="Work Shift CodeEditable" }

    { 118 ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Gen. Bus. Posting Group";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr="Gen. Prod. Posting Group";
                Visible=FALSE }

    { 120 ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 300 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[3];
                CaptionClass='1,2,3';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(3),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(3,ShortcutDimCode[3]);
                           END;
                            }

    { 302 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[4];
                CaptionClass='1,2,4';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(4),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(4,ShortcutDimCode[4]);
                           END;
                            }

    { 304 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[5];
                CaptionClass='1,2,5';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(5),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(5,ShortcutDimCode[5]);
                           END;
                            }

    { 306 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[6];
                CaptionClass='1,2,6';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(6),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(6,ShortcutDimCode[6]);
                           END;
                            }

    { 308 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[7];
                CaptionClass='1,2,7';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(7),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(7,ShortcutDimCode[7]);
                           END;
                            }

    { 310 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[8];
                CaptionClass='1,2,8';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(8),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(8,ShortcutDimCode[8]);
                           END;
                            }

    { 94  ;2   ;Field     ;
                SourceExpr="Starting Time";
                Visible=FALSE;
                Editable="Starting TimeEditable" }

    { 96  ;2   ;Field     ;
                SourceExpr="Ending Time";
                Visible=FALSE;
                Editable="Ending TimeEditable" }

    { 98  ;2   ;Field     ;
                SourceExpr="Concurrent Capacity";
                Visible=FALSE;
                Editable="Concurrent CapacityEditable" }

    { 8   ;2   ;Field     ;
                SourceExpr="Setup Time";
                Editable="Setup TimeEditable";
                HideValue="Setup TimeHideValue" }

    { 86  ;2   ;Field     ;
                SourceExpr="Run Time";
                Editable="Run TimeEditable";
                HideValue="Run TimeHideValue" }

    { 3   ;2   ;Field     ;
                SourceExpr="Cap. Unit of Measure Code";
                Visible=FALSE;
                Editable=CapUnitofMeasureCodeEditable }

    { 112 ;2   ;Field     ;
                SourceExpr="Scrap Code";
                Visible=FALSE;
                Editable="Scrap CodeEditable" }

    { 126 ;2   ;Field     ;
                SourceExpr="Output Quantity";
                Editable="Output QuantityEditable";
                HideValue="Output QuantityHideValue" }

    { 128 ;2   ;Field     ;
                SourceExpr="Scrap Quantity";
                Editable="Scrap QuantityEditable";
                HideValue="Scrap QuantityHideValue" }

    { 122 ;2   ;Field     ;
                SourceExpr=Finished;
                Editable=FinishedEditable }

    { 33  ;2   ;Field     ;
                SourceExpr="Applies-to Entry";
                Visible=FALSE }

    { 32  ;2   ;Field     ;
                SourceExpr="Applies-from Entry";
                Visible=FALSE;
                Editable="Applies-from EntryEditable" }

    { 134 ;2   ;Field     ;
                SourceExpr="Document Date";
                Visible=FALSE;
                Editable=FALSE }

    { 136 ;2   ;Field     ;
                SourceExpr="External Document No.";
                Visible=FALSE }

    { 73  ;1   ;Group     ;
                CaptionML=[ENU=Actual;
                           ENG=Actual] }

    { 1902114901;2;Group  ;
                GroupType=FixedLayout }

    { 1901742001;3;Group  ;
                CaptionML=[ENU=Consump. Qty.;
                           ENG=Consump. Qty.] }

    { 55  ;4   ;Field     ;
                DecimalPlaces=0:5;
                SourceExpr=ActualConsumpQty;
                Editable=FALSE;
                HideValue=ActualConsumpQtyHideValue }

    { 1901741901;3;Group  ;
                CaptionML=[ENU=Setup Time;
                           ENG=Setup Time] }

    { 50  ;4   ;Field     ;
                CaptionML=[ENU=Setup Time;
                           ENG=Setup Time];
                DecimalPlaces=0:5;
                SourceExpr=ActualSetupTime;
                Editable=FALSE;
                HideValue=ActualSetupTimeHideValue }

    { 1902759401;3;Group  ;
                CaptionML=[ENU=Run Time;
                           ENG=Run Time] }

    { 52  ;4   ;Field     ;
                CaptionML=[ENU=Run Time;
                           ENG=Run Time];
                DecimalPlaces=0:5;
                SourceExpr=ActualRunTime;
                Editable=FALSE;
                HideValue=ActualRunTimeHideValue }

    { 1900205801;3;Group  ;
                CaptionML=[ENU=Output Qty.;
                           ENG=Output Qty.] }

    { 54  ;4   ;Field     ;
                CaptionML=[ENU=Output Qty.;
                           ENG=Output Qty.];
                DecimalPlaces=0:5;
                SourceExpr=ActualOutputQty;
                Editable=FALSE;
                HideValue=ActualOutputQtyHideValue }

    { 1900205901;3;Group  ;
                CaptionML=[ENU=Scrap Qty.;
                           ENG=Scrap Qty.] }

    { 58  ;4   ;Field     ;
                CaptionML=[ENU=Scrap Qty.;
                           ENG=Scrap Qty.];
                DecimalPlaces=0:5;
                SourceExpr=ActualScrapQty;
                Editable=FALSE;
                HideValue=ActualScrapQtyHideValue }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Item@1019 : Record 27;
      ProdOrder@1009 : Record 5405;
      ProdOrderLine@1012 : Record 5406;
      ProdOrderComp@1017 : Record 5407;
      TempItemJrnlLine@1002 : TEMPORARY Record 83;
      CostCalcMgt@1021 : Codeunit 5836;
      ReportPrint@1001 : Codeunit 228;
      PostingDate@1008 : Date;
      xPostingDate@1007 : Date;
      ProdOrderLineNo@1006 : Integer;
      ShortcutDimCode@1005 : ARRAY [8] OF Code[20];
      ToTemplateName@1011 : Code[10];
      ToBatchName@1010 : Code[10];
      ActualRunTime@1013 : Decimal;
      ActualSetupTime@1014 : Decimal;
      ActualOutputQty@1015 : Decimal;
      ActualScrapQty@1020 : Decimal;
      ActualConsumpQty@1016 : Decimal;
      FlushingFilter@1003 : 'Manual,Forward,Backward,Pick + Forward,Pick + Backward,All Methods';
      DescriptionIndent@19057867 : Integer INDATASET;
      QuantityHideValue@19060207 : Boolean INDATASET;
      "Setup TimeHideValue"@19037416 : Boolean INDATASET;
      "Run TimeHideValue"@19073369 : Boolean INDATASET;
      "Output QuantityHideValue"@19005396 : Boolean INDATASET;
      "Scrap QuantityHideValue"@19076632 : Boolean INDATASET;
      ActualConsumpQtyHideValue@19061616 : Boolean INDATASET;
      ActualSetupTimeHideValue@19068503 : Boolean INDATASET;
      ActualRunTimeHideValue@19010265 : Boolean INDATASET;
      ActualOutputQtyHideValue@19030556 : Boolean INDATASET;
      ActualScrapQtyHideValue@19001037 : Boolean INDATASET;
      "Starting TimeEditable"@19033885 : Boolean INDATASET;
      "Ending TimeEditable"@19072097 : Boolean INDATASET;
      "Concurrent CapacityEditable"@19033053 : Boolean INDATASET;
      CapUnitofMeasureCodeEditable@19008712 : Boolean INDATASET;
      "Setup TimeEditable"@19005706 : Boolean INDATASET;
      "Run TimeEditable"@19034139 : Boolean INDATASET;
      "Work Shift CodeEditable"@19055238 : Boolean INDATASET;
      FinishedEditable@19037742 : Boolean INDATASET;
      "Scrap CodeEditable"@19021735 : Boolean INDATASET;
      "Scrap QuantityEditable"@19061944 : Boolean INDATASET;
      "Output QuantityEditable"@19056726 : Boolean INDATASET;
      QuantityEditable@19013162 : Boolean INDATASET;
      "Applies-from EntryEditable"@19013746 : Boolean INDATASET;
      DescriptionEmphasize@19013747 : Text INDATASET;

    PROCEDURE Setup@2(TemplateName@1003 : Code[10];BatchName@1002 : Code[10];ProductionOrder@1001 : Record 5405;ProdLineNo@1000 : Integer;PostDate@1004 : Date);
    BEGIN
      ToTemplateName := TemplateName;
      ToBatchName := BatchName;
      ProdOrder := ProductionOrder;
      ProdOrderLineNo := ProdLineNo;
      PostingDate := PostDate;
      xPostingDate := PostingDate;

      FlushingFilter := FlushingFilter::Manual;
    END;

    LOCAL PROCEDURE GetActTimeAndQtyBase@4();
    BEGIN
      ActualSetupTime := 0;
      ActualRunTime := 0;
      ActualOutputQty := 0;
      ActualScrapQty := 0;
      ActualConsumpQty := 0;

      IF "Qty. per Unit of Measure" = 0 THEN
        "Qty. per Unit of Measure" := 1;
      IF "Qty. per Cap. Unit of Measure" = 0 THEN
        "Qty. per Cap. Unit of Measure" := 1;

      IF Item.GET("Item No.") THEN
        CASE "Entry Type" OF
          "Entry Type"::Consumption:
            IF ProdOrderComp.GET(
                 ProdOrder.Status,
                 "Order No.",
                 "Order Line No.",
                 "Prod. Order Comp. Line No.")
            THEN BEGIN
              ProdOrderComp.CALCFIELDS("Act. Consumption (Qty)"); // Base Unit
              ActualConsumpQty :=
                ProdOrderComp."Act. Consumption (Qty)" / "Qty. per Unit of Measure";
              IF Item."Rounding Precision" > 0 THEN
                ActualConsumpQty := ROUND(ActualConsumpQty,Item."Rounding Precision",'>')
              ELSE
                ActualConsumpQty := ROUND(ActualConsumpQty,0.00001);
            END;
          "Entry Type"::Output:
            BEGIN
              IF ProdOrderLineNo = 0 THEN
                IF NOT ProdOrderLine.GET(ProdOrder.Status,ProdOrder."No.","Order Line No.") THEN
                  CLEAR(ProdOrderLine);
              IF ProdOrderLine."Prod. Order No." <> '' THEN BEGIN
                CostCalcMgt.CalcActTimeAndQtyBase(
                  ProdOrderLine,"Operation No.",ActualRunTime,ActualSetupTime,ActualOutputQty,ActualScrapQty);
                ActualSetupTime :=
                  ROUND(ActualSetupTime / "Qty. per Cap. Unit of Measure",0.00001);
                ActualRunTime :=
                  ROUND(ActualRunTime / "Qty. per Cap. Unit of Measure",0.00001);

                ActualOutputQty := ActualOutputQty / "Qty. per Unit of Measure";
                ActualScrapQty := ActualScrapQty / "Qty. per Unit of Measure";
                IF Item."Rounding Precision" > 0 THEN BEGIN
                  ActualOutputQty := ROUND(ActualOutputQty,Item."Rounding Precision",'>');
                  ActualScrapQty := ROUND(ActualScrapQty,Item."Rounding Precision",'>');
                END ELSE BEGIN
                  ActualOutputQty := ROUND(ActualOutputQty,0.00001);
                  ActualScrapQty := ROUND(ActualScrapQty,0.00001);
                END;
              END;
            END;
        END;
    END;

    LOCAL PROCEDURE ControlsMngt@1();
    VAR
      OperationExist@1000 : Boolean;
    BEGIN
      IF ("Entry Type" = "Entry Type"::Output) AND
         ("Operation No." <> '')
      THEN
        OperationExist := TRUE
      ELSE
        OperationExist := FALSE;

      "Starting TimeEditable" := OperationExist;
      "Ending TimeEditable" := OperationExist;
      "Concurrent CapacityEditable" := OperationExist;
      CapUnitofMeasureCodeEditable := OperationExist;
      "Setup TimeEditable" := OperationExist;
      "Run TimeEditable" := OperationExist;
      "Work Shift CodeEditable" := OperationExist;

      FinishedEditable := "Entry Type" = "Entry Type"::Output;
      "Scrap CodeEditable" := "Entry Type" = "Entry Type"::Output;
      "Scrap QuantityEditable" := "Entry Type" = "Entry Type"::Output;
      "Output QuantityEditable" := "Entry Type" = "Entry Type"::Output;

      QuantityEditable := "Entry Type" = "Entry Type"::Consumption;
      "Applies-from EntryEditable" := "Entry Type" = "Entry Type"::Consumption;
    END;

    LOCAL PROCEDURE DeleteRecTemp@6();
    BEGIN
      TempItemJrnlLine.DELETEALL;

      IF FIND('-') THEN
        REPEAT
          CASE "Entry Type" OF
            "Entry Type"::Consumption:
              IF "Quantity (Base)" = 0 THEN BEGIN
                TempItemJrnlLine := Rec;
                TempItemJrnlLine.INSERT;

                DELETE;
              END;
            "Entry Type"::Output:
              IF TimeIsEmpty AND
                 ("Output Quantity (Base)" = 0) AND ("Scrap Quantity (Base)" = 0)
              THEN BEGIN
                TempItemJrnlLine := Rec;
                TempItemJrnlLine.INSERT;

                DELETE;
              END;
          END;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE InsertTempRec@7();
    BEGIN
      IF TempItemJrnlLine.FIND('-') THEN
        REPEAT
          Rec := TempItemJrnlLine;
          "Changed by User" := FALSE;
          INSERT;
        UNTIL TempItemJrnlLine.NEXT = 0;
      TempItemJrnlLine.DELETEALL;
    END;

    PROCEDURE SetFilterGroup@5();
    BEGIN
      FILTERGROUP(2);
      SETRANGE("Journal Template Name",ToTemplateName);
      SETRANGE("Journal Batch Name",ToBatchName);
      SETRANGE("Order Type","Order Type"::Production);
      SETRANGE("Order No.",ProdOrder."No.");
      IF ProdOrderLineNo <> 0 THEN
        SETRANGE("Order Line No.",ProdOrderLineNo);
      SetFlushingFilter;
      FILTERGROUP(0);
    END;

    PROCEDURE SetFlushingFilter@8();
    BEGIN
      IF FlushingFilter <> FlushingFilter::"All Methods" THEN
        SETRANGE("Flushing Method",FlushingFilter)
      ELSE
        SETRANGE("Flushing Method");
    END;

    LOCAL PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      SourceTableName@1002 : Text[100];
      Descrip@1001 : Text[100];
    BEGIN
      SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,5405);
      IF ProdOrderLineNo <> 0 THEN
        Descrip := ProdOrderLine.Description
      ELSE
        Descrip := ProdOrder.Description;

      EXIT(STRSUBSTNO('%1 %2 %3',SourceTableName,ProdOrder."No.",Descrip));
    END;

    LOCAL PROCEDURE PostingDateOnAfterValidate@19003005();
    BEGIN
      IF PostingDate = 0D THEN
        PostingDate := xPostingDate;

      IF PostingDate <> xPostingDate THEN BEGIN
        MODIFYALL("Posting Date",PostingDate);
        xPostingDate := PostingDate;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE FlushingFilterOnAfterValidate@19064520();
    BEGIN
      SetFilterGroup;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      DescriptionIndent := Level;
      IF "Entry Type" = "Entry Type"::Output THEN
        DescriptionEmphasize := 'Strong'
      ELSE
        DescriptionEmphasize := '';
    END;

    LOCAL PROCEDURE QuantityOnFormat@19071269();
    BEGIN
      IF "Entry Type" = "Entry Type"::Output THEN
        QuantityHideValue := TRUE;
    END;

    LOCAL PROCEDURE SetupTimeOnFormat@19007490();
    BEGIN
      IF ("Entry Type" = "Entry Type"::Consumption) OR
         ("Operation No." = '')
      THEN
        "Setup TimeHideValue" := TRUE;
    END;

    LOCAL PROCEDURE RunTimeOnFormat@19059514();
    BEGIN
      IF ("Entry Type" = "Entry Type"::Consumption) OR
         ("Operation No." = '')
      THEN
        "Run TimeHideValue" := TRUE;
    END;

    LOCAL PROCEDURE OutputQuantityOnFormat@19003029();
    BEGIN
      IF "Entry Type" = "Entry Type"::Consumption THEN
        "Output QuantityHideValue" := TRUE;
    END;

    LOCAL PROCEDURE ScrapQuantityOnFormat@19017313();
    BEGIN
      IF "Entry Type" = "Entry Type"::Consumption THEN
        "Scrap QuantityHideValue" := TRUE;
    END;

    LOCAL PROCEDURE ActualConsumpQtyOnFormat@19012702();
    BEGIN
      IF "Entry Type" = "Entry Type"::Output THEN
        ActualConsumpQtyHideValue := TRUE;
    END;

    LOCAL PROCEDURE ActualSetupTimeOnFormat@19031665();
    BEGIN
      IF ("Entry Type" = "Entry Type"::Consumption) OR
         ("Operation No." = '')
      THEN
        ActualSetupTimeHideValue := TRUE;
    END;

    LOCAL PROCEDURE ActualRunTimeOnFormat@19024131();
    BEGIN
      IF ("Entry Type" = "Entry Type"::Consumption) OR
         ("Operation No." = '')
      THEN
        ActualRunTimeHideValue := TRUE;
    END;

    LOCAL PROCEDURE ActualOutputQtyOnFormat@19059076();
    BEGIN
      IF "Entry Type" = "Entry Type"::Consumption THEN
        ActualOutputQtyHideValue := TRUE;
    END;

    LOCAL PROCEDURE ActualScrapQtyOnFormat@19036240();
    BEGIN
      IF "Entry Type" = "Entry Type"::Consumption THEN
        ActualScrapQtyHideValue := TRUE;
    END;

    BEGIN
    END.
  }
}

