OBJECT Table 50031 Gate Pass Line
{
  OBJECT-PROPERTIES
  {
    Date=07/10/18;
    Time=11:39:43 AM;
    Modified=Yes;
    Version List=TM0001;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatusOpen;
             END;

    OnModify=BEGIN
               TestStatusOpen;
             END;

    OnDelete=BEGIN
               TestStatusOpen;
             END;

  }
  FIELDS
  {
    { 5   ;   ;Document No.        ;Code20        ;TableRelation="Gate Pass Header";
                                                   Editable=No }
    { 10  ;   ;Line No.            ;Integer       ;Editable=No }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;

                                                                TmpGatePassLine := Rec;
                                                                INIT;
                                                                "Item No." := TmpGatePassLine."Item No.";
                                                                IF "Item No." = '' THEN
                                                                  EXIT;

                                                                GetItem;
                                                                CLEAR(ItemHierarchy);
                                                                ItemHierarchy.RESET;
                                                                ItemHierarchy.SETRANGE(Type,ItemHierarchy.Type::Department);
                                                                ItemHierarchy.SETRANGE("Division Code",Item."Division Code");
                                                                ItemHierarchy.SETRANGE("Department Code",Item."Department Code");
                                                                IF ItemHierarchy.FINDFIRST THEN;


                                                                Description := Item.Description;
                                                                "Unit Price (LCY)" := GetItemUnitCost;
                                                                "Country/Region of Origin Code" := Item."Country/Region of Origin Code";
                                                                "Item Category" := Item."Department Code";
                                                                //HSCODE := Item."Tariff No.";
                                                                HSCODE := ItemHierarchy.HSCODE;
                                                                VALIDATE(Quantity,1);
                                                              END;
                                                               }
    { 30  ;   ;Description         ;Text50         }
    { 40  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmounts;
                                                              END;
                                                               }
    { 50  ;   ;Unit Price (LCY)    ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmounts;
                                                              END;
                                                               }
    { 60  ;   ;Amount (LCY)        ;Decimal        }
    { 70  ;   ;Country/Region of Origin Code;Code10;
                                                   TableRelation=Country/Region }
    { 80  ;   ;HSCODE              ;Code20         }
    { 90  ;   ;Item Category       ;Code20         }
    { 100 ;   ;Ref. Document No.   ;Code20         }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GatePassHeader@1000000001 : Record 50033;
      TmpGatePassLine@1000000002 : TEMPORARY Record 50031;
      Item@1000000000 : Record 27;
      GLSetup@1000000004 : Record 98;
      ItemHierarchy@1000000006 : Record 9087005;
      SalesPriceCalcMgt@1000000003 : Codeunit 7000;
      GLSetupOK@1000000005 : Boolean;

    LOCAL PROCEDURE GetItem@4();
    BEGIN
      TESTFIELD("Item No.");
      IF Item."No." <> "Item No." THEN
        Item.GET("Item No.");
    END;

    LOCAL PROCEDURE GetItemUnitPrice@1000000000() : Decimal;
    VAR
      TmpSalesPrice@1000000000 : TEMPORARY Record 7002;
    BEGIN
      TmpSalesPrice.RESET;
      TmpSalesPrice.DELETEALL;
      GetItem;
      GetGLSetup;
      SalesPriceCalcMgt.FindSalesPrice(TmpSalesPrice,'','','','',Item."No.",'',Item."Sales Unit of Measure",GLSetup."LCY Code",WORKDATE,FALSE);
      IF TmpSalesPrice.FINDLAST THEN
        EXIT(TmpSalesPrice."Unit Price");
      EXIT(0);
    END;

    LOCAL PROCEDURE GetItemUnitCost@1000000003() : Decimal;
    BEGIN
      GetItem;
      EXIT(Item."Unit Cost");
    END;

    LOCAL PROCEDURE UpdateAmounts@1000000002();
    BEGIN
      "Amount (LCY)" := Quantity * "Unit Price (LCY)";
    END;

    LOCAL PROCEDURE GetHeader@1();
    BEGIN
      TESTFIELD("Document No.");
      IF "Document No." <> GatePassHeader."No." THEN
        GatePassHeader.GET("Document No.");
    END;

    LOCAL PROCEDURE TestStatusOpen@37();
    BEGIN
      GetHeader;
      GatePassHeader.TESTFIELD(Status,GatePassHeader.Status::Open);
    END;

    LOCAL PROCEDURE GetGLSetup@1000000001();
    BEGIN
      IF NOT GLSetupOK THEN BEGIN
        GLSetup.GET;
        GLSetupOK := TRUE;
      END;
    END;

    BEGIN
    END.
  }
}

