OBJECT Codeunit 11102105 OM - Check New Transport
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CASE RunType OF
              RunType::Check :
                CheckRun;

              RunType::Process :
                ProcessRun;

            END;
          END;

  }
  CODE
  {
    VAR
      ServerFileName@1000 : Text[1024];
      RunType@1001 : 'Check,Process';

    LOCAL PROCEDURE CheckRun@3();
    VAR
      Setup@1000 : Record 11102035;
      TmpFile@1001 : TEMPORARY Record 11102094;
      FileHandler@1002 : Codeunit 11102097;
      CheckNewTransport@1003 : Codeunit 11102105;
    BEGIN
      Setup.CustomGet;

      IF Setup."Transport Import Folder" = '' THEN
        EXIT;

      FileHandler.GetServerDirectoryContent(TmpFile, Setup."Transport Import Folder");
      TmpFile.SETFILTER(Name, '*.fib|*.FIB');
      IF TmpFile.FINDSET THEN
        REPEAT
          CheckNewTransport.SetServerFileName(FileHandler.GetFileName(TmpFile.Name));
          IF CheckNewTransport.RUN THEN
            ;
        UNTIL TmpFile.NEXT = 0;
    END;

    PROCEDURE SetServerFileName@5(NewServerFileName@1000 : Text[1024]);
    BEGIN
      ServerFileName := NewServerFileName;
      RunType := RunType::Process;
    END;

    LOCAL PROCEDURE ProcessRun@4();
    VAR
      Setup@1000 : Record 11102035;
      ImportTransport@1001 : Codeunit 11102087;
      ServerFileNameBusy@1002 : Text[1024];
      ServerFileNameReady@1003 : Text[1024];
    BEGIN
      Setup.CustomGet;

      ServerFileNameBusy := Setup."Transport Import Folder" + ServerFileName + '_busy';
      ServerFileNameReady := Setup."Transport Archive Folder" + ServerFileName;

      RenameFile(Setup."Transport Import Folder" + ServerFileName, ServerFileNameBusy);

      ImportTransport.SetServerFileName(ServerFileNameBusy);
      IF ImportTransport.RUN THEN
        ;

      RenameFile(ServerFileNameBusy, ServerFileNameReady);
    END;

    LOCAL PROCEDURE RenameFile@1(FromServerFileName@1000 : Text[1024];ToServerFileName@1001 : Text[1024]);
    BEGIN
      IF EXISTS(ToServerFileName) THEN
        ERASE(ToServerFileName);
      RENAME(FromServerFileName, ToServerFileName);
    END;

    BEGIN
    END.
  }
}

