OBJECT Codeunit 9062665 Universal Html Ctl. Msg. Proxy
{
  OBJECT-PROPERTIES
  {
    Date=20/06/16;
    Time=[ 3:01:14 PM];
    Version List=NH2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EnablementSetup@1000000004 : Record 9062229;
      EnablementSetupLoaded@1000000007 : Boolean;
      ControlRequestEvent@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessageEvent";
      HtmlResourceLinksRequest@1000000003 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.ResourceLinksRequest";
      RazorEngineMgt@1000000001 : Codeunit 9062584;
      TemplateEngineInitialized@1000000002 : Boolean;
      TemplateCode@1000000006 : Code[50];
      ViewCode@1000000005 : Code[50];

    PROCEDURE SetRequestEvent@1000000000(VAR RequestEvent@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessageEvent");
    BEGIN
      ControlRequestEvent := RequestEvent;
    END;

    PROCEDURE SendRequestMessage@1000000001(RequestMessage@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessage");
    BEGIN
      IF (NOT(ISNULL(ControlRequestEvent))) THEN
        ControlRequestEvent.Invoke(RequestMessage);
    END;

    PROCEDURE SendRequest@1000000002(Request@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.Request");
    BEGIN
      SendRequestMessage(Request.CreateMessage());
    END;

    PROCEDURE SendHtmlRequest@1000000003(TargetSelector@1000000000 : Text;Html@1000000001 : Text;Append@1000000002 : Boolean);
    VAR
      HtmlRequest@1000000003 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.HtmlRequest";
    BEGIN
      HtmlRequest := HtmlRequest.HtmlRequest();
      HtmlRequest.Append := Append;
      HtmlRequest.TargetSelector := TargetSelector;
      HtmlRequest.Html := Html;
      SendRequest(HtmlRequest);
    END;

    PROCEDURE SendJavaScriptRequest@1000000004(Script@1000000001 : Text);
    VAR
      JavaScriptRequest@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.JavaScriptRequest";
    BEGIN
      JavaScriptRequest := JavaScriptRequest.JavaScriptRequest();
      JavaScriptRequest.Script := Script;
      SendRequest(JavaScriptRequest);
    END;

    PROCEDURE SendStyleSheetRequest@1000000005(StyleSheet@1000000000 : Text);
    VAR
      StyleSheetRequest@1000000001 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.StyleSheetRequest";
    BEGIN
      StyleSheetRequest := StyleSheetRequest.StyleSheetRequest();
      StyleSheetRequest.Styles := StyleSheet;
      SendRequest(StyleSheetRequest);
    END;

    PROCEDURE SendCommandRequest@1000000006(CommandName@1000000001 : Text;Parameters@1000000002 : Text);
    VAR
      CommandRequest@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.CommandRequest";
    BEGIN
      CommandRequest := CommandRequest.CommandRequest();
      CommandRequest.Command := CommandName;
      CommandRequest.Parameters := Parameters;
      SendRequest(CommandRequest);
    END;

    PROCEDURE SendResizeRequest@1000000007(Width@1000000001 : Text;Height@1000000002 : Text);
    VAR
      ResizeRequest@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.ResizeRequest";
    BEGIN
      ResizeRequest := ResizeRequest.ResizeRequest();
      ResizeRequest.Width := Width;
      ResizeRequest.Height := Height;
      SendRequest(ResizeRequest);
    END;

    PROCEDURE SendResourceLinkRequest@1000000009();
    BEGIN
      IF (NOT(ISNULL(HtmlResourceLinksRequest))) THEN BEGIN
        SendRequest(HtmlResourceLinksRequest);
        CLEAR(HtmlResourceLinksRequest);
      END;
    END;

    PROCEDURE SendStaticHtmlResource@1000000008(HtmlResourceCode@1000000000 : Code[50];TargetSelector@1000000002 : Text);
    VAR
      HtmlResource@1000000001 : Record 9062792;
    BEGIN
      HtmlResource.GET(HtmlResourceCode);
      CASE (HtmlResource.Type) OF
        HtmlResource.Type::Css:
          SendStyleSheetRequest(HtmlResource.GetText());
        HtmlResource.Type::Html:
          SendHtmlRequest(TargetSelector, HtmlResource.GetText(), FALSE);
        HtmlResource.Type::JavaScript:
          SendJavaScriptRequest(HtmlResource.GetText());
      END;
    END;

    LOCAL PROCEDURE "-- Resource Links --"@1000000016();
    BEGIN
    END;

    LOCAL PROCEDURE AddResourceLink@1000000015(type@1000000000 : Text;source@1000000001 : Text);
    VAR
      HtmlResourceLink@1000000003 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.Requests.ResourceLink";
    BEGIN
      source := CheckUrl(source);
      IF (ISNULL(HtmlResourceLinksRequest)) THEN
        HtmlResourceLinksRequest := HtmlResourceLinksRequest.ResourceLinksRequest();
      HtmlResourceLink := HtmlResourceLink.ResourceLink('', type, source);
      HtmlResourceLinksRequest.Resources.Add(HtmlResourceLink);
    END;

    PROCEDURE AddScriptLink@1000000018(source@1000000000 : Text);
    BEGIN
      AddResourceLink('Script', source);
    END;

    PROCEDURE AddStyleSheetLink@1000000019(source@1000000000 : Text);
    BEGIN
      AddResourceLink('StyleSheet', source);
    END;

    LOCAL PROCEDURE "--- Template Processing ---"@1000000011();
    BEGIN
    END;

    PROCEDURE LoadView@1000000012(NewTemplateCode@1000000000 : Code[50];NewViewCode@1000000001 : Code[50]);
    VAR
      HtmlControlResource@1000000002 : Record 9062801;
      CacheName@1000000003 : Text;
      NewTemplateCreated@1000000004 : Boolean;
    BEGIN
      //DOC NA2016.16 >>
      IF (TemplateCode = NewTemplateCode) AND (ViewCode = NewViewCode) THEN
        EXIT;

      TemplateCode := NewTemplateCode;
      ViewCode := NewViewCode;

      IF (TemplateCode = '') OR (ViewCode = '') THEN
        EXIT;
      //DOC NA2016.16 <<

      //NA2016.15 >>
      TemplateEngineInitialized := TRUE;
      CacheName := NewTemplateCode + '::' + NewViewCode;
      RazorEngineMgt.InitFromTemplCache(TemplateCode, ViewCode, NewTemplateCreated);

      //IF (TemplateEngineInitialized) THEN
      //  ClearTemplateEngineCache()
      //ELSE
      //  InitializeTemplateEngine();
      //NA2016.15 <<

      HtmlControlResource.RESET();
      HtmlControlResource.SETCURRENTKEY("Template Code", "View Code", Type);
      HtmlControlResource.SETRANGE("Template Code", TemplateCode);
      HtmlControlResource.SETRANGE("View Code", ViewCode);
      IF (HtmlControlResource.FINDSET()) THEN BEGIN
        REPEAT
          IF (HtmlControlResource.Static) THEN
            SendStaticHtmlResource(HtmlControlResource."Html Resource Code", HtmlControlResource."Target Part Name")
          ELSE
            //DOC NA2016.15 >>
            IF (NewTemplateCreated) THEN
              RazorEngineMgt.CompileTemplate(HtmlControlResource."Html Resource Code", HtmlControlResource."Html Template Name");
            //DOC NA2016.15 <<
        UNTIL (HtmlControlResource.NEXT() = 0);
      END;
    END;

    PROCEDURE RenderViewData@1000000023(VAR DataSource@1000000002 : Record 9062810);
    VAR
      HtmlControlResource@1000000003 : Record 9062801;
      TargetSelector@1000000004 : Text;
    BEGIN
      IF (DataSource.FINDSET()) THEN BEGIN
        REPEAT
          DataSource.CALCFIELDS(Data);

          HtmlControlResource.RESET();
          HtmlControlResource.SETCURRENTKEY("Template Code", "View Code", "Data Source Name");
          HtmlControlResource.SETRANGE("Template Code", TemplateCode);
          HtmlControlResource.SETRANGE("View Code", ViewCode);
          HtmlControlResource.SETRANGE("Data Source Name", DataSource.Name);
          HtmlControlResource.SETRANGE(Static, FALSE);
          IF (HtmlControlResource.FINDSET()) THEN BEGIN
            RazorEngineMgt.SetXmlTextData(DataSource.GetDataText());
            REPEAT
              IF (DataSource."Target Part Name" <> '') THEN
                TargetSelector := DataSource."Target Part Name"
              ELSE
                TargetSelector := HtmlControlResource."Target Part Name";
              SendHtmlRequest(TargetSelector, RazorEngineMgt.RunTemplate(HtmlControlResource."Html Resource Code", HtmlControlResource."Html Template Name"), FALSE);
            UNTIL (HtmlControlResource.NEXT() = 0);
          END;
        UNTIL (DataSource.NEXT() = 0);
      END;
    END;

    LOCAL PROCEDURE InitializeTemplateEngine@1000000010();
    BEGIN
      RazorEngineMgt.Initialize();
      TemplateEngineInitialized := TRUE;
    END;

    LOCAL PROCEDURE ClearTemplateEngineCache@1000000013();
    BEGIN
      RazorEngineMgt.ClearCache();
    END;

    LOCAL PROCEDURE "--- Internal Functions ---"@1000000021();
    BEGIN
    END;

    LOCAL PROCEDURE GetContentServerUrl@1000000022() : Text;
    BEGIN
      GetEnablementSetup();
      EXIT(EnablementSetup.GetContentServerUrl());
    END;

    LOCAL PROCEDURE CheckUrl@1000000020(url@1000000000 : Text) : Text;
    BEGIN
      IF (url = '') THEN
        EXIT('');
      IF (STRPOS(url, '://') > 0) THEN
        EXIT(url);

      IF (COPYSTR(url, 1, 1) <> '/') THEN
        url := '/' + url;

      EXIT(GetContentServerUrl() + url);
    END;

    LOCAL PROCEDURE GetEnablementSetup@1000000014();
    BEGIN
      IF (NOT(EnablementSetupLoaded)) THEN BEGIN
        EnablementSetup.GET();
        EnablementSetupLoaded := TRUE;
      END;
    END;

    EVENT ControlRequestEvent@1000000000::OnEvent@8(message@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication.9.0.0.0, Version=9.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessage");
    BEGIN
    END;

    BEGIN
    {
      //DOC NA2016.12 AZ 21/01/2016 - Created
      //DOC NA2016.15 AZ 14/04/2016 - Razor Template Engine Cache Implementation
      //DOC NA2016.16 AZ 24/05/2016 - CfMD amendments
      //DOC NA2016.16 AZ 20/06/2016 - View loading bugfixes
    }
    END.
  }
}

