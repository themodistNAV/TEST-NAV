OBJECT Table 9087006 Brand
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=12:28:42 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(Code);
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CheckOnDeletion();

               BrandVendor.RESET();
               BrandVendor.SETRANGE("Brand Code", Rec.Code);
               BrandVendor.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Brand;
               ENG=Brand];
    LookupPageID=Page9087019;
    DrillDownPageID=Page9087019;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                TnpToolkit.CodeToName(Rec.Code, Rec.Name);
                                                                Rec.VALIDATE(Name);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 10  ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 20  ;   ;Vendors             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Brand Vendor" WHERE (Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Vendors;
                                                              ENG=Vendors];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 30  ;   ;Styles              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Style WHERE (Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Styles;
                                                              ENG=Styles];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 31  ;   ;Items               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Item WHERE (Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Items;
                                                              ENG=Items];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 50  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ENG=Date Filter] }
    { 60  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              ENG=Global Dimension 1 Filter];
                                                   CaptionClass='1,3,1' }
    { 61  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              ENG=Global Dimension 2 Filter];
                                                   CaptionClass='1,3,2' }
    { 70  ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              ENG=Location Filter] }
    { 81  ;   ;Purchase Order Lines;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Line" WHERE (Document Type=CONST(Order),
                                                                                            Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Purchase Order Lines;
                                                              ENG=Purchase Order Lines];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 82  ;   ;Purchases (LCY)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Purchase Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Purchase),
                                                                                                                   Brand Code=FIELD(Code),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Location Code=FIELD(Location Filter),
                                                                                                                   Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Purchases (LCY);
                                                              ENG=Purchases (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 90  ;   ;Sales (LCY)         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Sales Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Sale),
                                                                                                                Brand Code=FIELD(Code),
                                                                                                                Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                Location Code=FIELD(Location Filter),
                                                                                                                Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (LCY);
                                                              ENG=Sales (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 91  ;   ;Sales Order Lines   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Line" WHERE (Document Type=CONST(Order),
                                                                                         Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Sales Order Lines;
                                                              ENG=Sales Order Lines];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 100 ;   ;Item Ledger Entries ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Item Ledger Entry" WHERE (Brand Code=FIELD(Code)));
                                                   CaptionML=[ENU=Item Ledger Entries;
                                                              ENG=Item Ledger Entries];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 110 ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ENG=Blocked] }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BrandVendor@1000000001 : Record 9087007;
      TnpToolkit@1000000000 : Codeunit 9062230;

    LOCAL PROCEDURE CheckOnDeletion@1000000006();
    VAR
      lDeletionCheck@1000000000 : Codeunit 9062384;
      eBrandCode@1000000001 : TextConst 'ENU=Brand Code;ENG=Brand Code';
    BEGIN
      //DOC NS2015.1 - Checking on deletion

      lDeletionCheck.AddTable(DATABASE::Style);
      lDeletionCheck.AddTable(DATABASE::Item);
      lDeletionCheck.AddTable(DATABASE::"Item Variant");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Summary Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Line");
      lDeletionCheck.AddTable(DATABASE::"Sales Line");
      lDeletionCheck.AddTable(DATABASE::"Purchase Header");
      lDeletionCheck.AddTable(DATABASE::"Purchase Line");
      lDeletionCheck.AddTable(DATABASE::"Vendor Ledger Entry");
      lDeletionCheck.AddTable(DATABASE::"Item Ledger Entry");
      lDeletionCheck.AddTable(DATABASE::"Value Entry");
      lDeletionCheck.AddTable(DATABASE::"Sales Shipment Line");
      lDeletionCheck.AddTable(DATABASE::"Sales Invoice Line");
      lDeletionCheck.AddTable(DATABASE::"Sales Cr.Memo Line");
      lDeletionCheck.AddTable(DATABASE::"Purch. Rcpt. Header");
      lDeletionCheck.AddTable(DATABASE::"Purch. Rcpt. Line");
      lDeletionCheck.AddTable(DATABASE::"Purch. Inv. Header");
      lDeletionCheck.AddTable(DATABASE::"Purch. Inv. Line");
      lDeletionCheck.AddTable(DATABASE::"Purch. Cr. Memo Hdr.");
      lDeletionCheck.AddTable(DATABASE::"Purch. Cr. Memo Line");
      lDeletionCheck.AddTable(DATABASE::"Return Shipment Line");
      lDeletionCheck.AddTable(DATABASE::"Return Receipt Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Arch. Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Staging Arch. Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Arch. Header");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Arch. Sum. Line");
      lDeletionCheck.AddTable(DATABASE::"Buying Sheet Arch. Line");

      lDeletionCheck.CheckMultipleTables(DATABASE::Brand, Rec.Code, eBrandCode);
    END;

    PROCEDURE Edit@1000000003();
    BEGIN
      PAGE.RUN(PAGE::"Brand List", Rec);
    END;

    PROCEDURE ShowVendors@1000000001();
    BEGIN
      //DOC NS2015.1 - Showing vendors related to the brand

      BrandVendor.RESET();
      BrandVendor.FILTERGROUP(2);
      BrandVendor.SETRANGE("Brand Code", Rec.Code);
      BrandVendor.FILTERGROUP(0);
      PAGE.RUN(0, BrandVendor);
    END;

    PROCEDURE ShowStyles@1000000002();
    VAR
      lStyle@1000000000 : Record 9087012;
    BEGIN
      //DOC NS2015.1 - Showing styles related to the brand

      lStyle.RESET();
      lStyle.SETRANGE("Brand Code", Rec.Code);
      PAGE.RUN(0, lStyle);
    END;

    PROCEDURE ShowItems@1000000000();
    VAR
      lItem@1000000000 : Record 27;
    BEGIN
      //DOC NS2015.1 - Showing items related to the brand

      lItem.RESET();
      lItem.SETRANGE("Brand Code", Rec.Code);
      PAGE.RUN(0, lItem);
    END;

    PROCEDURE ShowPurchaseOrderLines@1000000007();
    VAR
      lPurchaseLine@1000000000 : Record 39;
    BEGIN
      //DOC NS2015.1 - Showing purchase order lines related to the brand

      lPurchaseLine.RESET();
      lPurchaseLine.SETCURRENTKEY("Brand Code");
      lPurchaseLine.SETRANGE("Document Type", lPurchaseLine."Document Type"::Order);
      lPurchaseLine.SETRANGE("Brand Code", Rec.Code);
      PAGE.RUN(0, lPurchaseLine);
    END;

    PROCEDURE ShowItemLedgerEntries@1000000008();
    VAR
      lItemLedgerEntry@1000000000 : Record 32;
    BEGIN
      //DOC NS2015.1 - Showing item ledger entries related to the brand

      lItemLedgerEntry.RESET();
      lItemLedgerEntry.SETCURRENTKEY("Brand Code");
      lItemLedgerEntry.SETRANGE("Brand Code", Rec.Code);
      PAGE.RUN(0, lItemLedgerEntry);
    END;

    PROCEDURE ShowSalesOrderLines@1000000005();
    VAR
      lSalesLine@1000000000 : Record 37;
    BEGIN
      //DOC NS2015.1 - Showing purchase order lines related to the brand

      lSalesLine.RESET();
      lSalesLine.SETCURRENTKEY("Brand Code");
      lSalesLine.SETRANGE("Document Type", lSalesLine."Document Type"::Order);
      lSalesLine.SETRANGE("Brand Code", Rec.Code);
      PAGE.RUN(0, lSalesLine);
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 04/11/2013 - Created
      //DOC NS2015.3  JH 30/07/2014 - "Blocked" added
      //DOC NS2016.16 JH 05/09/2016 - nSemble 2016
    }
    END.
  }
}

