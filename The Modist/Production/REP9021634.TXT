OBJECT Report 9021634 Create Item Label Attributes
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:06 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Item Label Attributes;
               ENG=Create Item Label Attributes];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF LabelCodeSet = '' THEN
                    ERROR(Text000);

                  LabelLookup.RESET;
                  LabelLookup.SETRANGE("Attribute Type",TxtProductAttributeType);
                  IF NOT AllValidLabelAttributes THEN
                    LabelLookup.SETRANGE(Required,TRUE);

                  LabelAttributeRec.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference");  //DOC EC2013.3.00
                  LabelAttributeRec.SETRANGE("Label Code",LabelCodeSet);
                  LabelAttributeRec.SETRANGE("Attribute Type",TxtProductAttributeType); //DOC EC2013.3.00

                  LangRec.RESET;
                  LangRec.SETRANGE("Windows Language ID",GLOBALLANGUAGE);
                  LangRec.SETFILTER(Code,'<>%1','CPL');
                  IF LangRec.FINDFIRST THEN
                    LanguageCode := LangRec.Code
                  ELSE
                    LanguageCode := '';
                END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               OnAfterGetRecord=BEGIN
                                  IF LabelLookup.FIND('-') THEN
                                  REPEAT
                                    LabelAttributeRec.SETCURRENTKEY("Label Code","Attribute Type",
                                                 "Attribute Owner Reference","Type Code");  //DOC EC2013.3.00 new line
                                    LabelAttributeRec.SETRANGE("Attribute Owner Reference","No.");
                                    LabelAttributeRec.SETRANGE("Type Code",LabelLookup.Code);
                                    IF NOT LabelAttributeRec.FINDFIRST THEN BEGIN
                                      LabelAttributeRec1.INIT;
                                      LabelAttributeRec1."Label Code" := LabelCodeSet;
                                      LabelAttributeRec1."Attribute Type" := TxtProductAttributeType; //DOC EC2013.3.00 add 'PRODUCT'
                                      LabelAttributeRec1."Attribute Owner Reference" := "No."; //DOC EC2013.3.00
                                      LabelAttributeRec1."Type Code" := LabelLookup.Code;
                                      LabelAttributeRec1."Allow Blank Language Code" := LabelLookup."Allow Blank Language Code";
                                      LabelAttributeRec1."Allow HTML" := LabelLookup."Allow HTML";
                                      LabelAttributeRec1.INSERT(TRUE);
                                      IF (LanguageCode <> '') AND
                                         (LabelAttributeRec1."Allow Blank Language Code" = FALSE) THEN BEGIN
                                        LabelAttTValue.INIT;
                                        LabelAttTValue."Label Code" := LabelCodeSet;
                                        LabelAttTValue."Label Attribute Entry No." := LabelAttributeRec1."Entry No.";
                                        LabelAttTValue."Language Code" := LanguageCode;
                                        //LabelAttTValue.Value := ;
                                        LabelAttTValue.INSERT;
                                      END;
                                    END;
                                  UNTIL LabelLookup.NEXT = 0;
                                END;

               ReqFilterFields=No.,Item Category Code,Product Group Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      LabelAttTValue@1040000 : Record 9021624;
      LabelLookup@1040001 : Record 9021622;
      LabelAttributeRec@1040002 : Record 9021623;
      LabelAttributeRec1@1040005 : Record 9021623;
      LangRec@1040006 : Record 8;
      LabelCodeSet@1040003 : Code[10];
      Text000@1040004 : TextConst 'ENU=The set label code must not be blank.;ENG=The set label code must not be blank.';
      LanguageCode@1040007 : Code[20];
      AllValidLabelAttributes@1040008 : Boolean;
      TxtProductAttributeType@1000000000 : TextConst 'ENU=PRODUCT;ENG=PRODUCT';

    PROCEDURE SetLabelCode@1040000(LabelCodeIn@1040000 : Code[10]);
    BEGIN
      LabelCodeSet := LabelCodeIn;
    END;

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - Created
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

