OBJECT Page 11102109 OM - Renumber Fields Wizard
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Renumber Fields Wizard;
               ENU=Renumber Fields Wizard;
               ESP=Renumber Fields Wizard;
               FRA=Renumber Fields Wizard;
               NLD=Velden omnummeren wizard;
               DEA=Renumber Fields Wizard];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    PageType=NavigatePage;
    OnInit=BEGIN
             NextEnable := TRUE;
           END;

    OnOpenPage=VAR
                 FieldToRenumber@1000 : Record 11102074;
                 RenumberFieldMgt@1001 : Codeunit 11102054;
                 i@1002 : Integer;
               BEGIN
                 RenumberFieldMgt.GetInvolvedObjects(FieldToRenumber, TmpObject, TRUE, UpdateReferencingObjects);

                 IF FieldToRenumber.FINDSET THEN
                   REPEAT
                     TmpFieldToRenumber := FieldToRenumber;
                     TmpFieldToRenumber.INSERT;
                     IF NOT TmpObjectToRenumber.GET(TmpObjectToRenumber.Type::Table, '', FieldToRenumber."Table No.") THEN BEGIN
                       TmpObjectToRenumber.Type := TmpObjectToRenumber.Type::Table;
                       TmpObjectToRenumber.ID := FieldToRenumber."Table No.";
                       TmpObjectToRenumber.INSERT;
                     END;
                   UNTIL FieldToRenumber.NEXT = 0;

                 AddStep(StepType::"Check Objects");
                 AddStep(StepType::"Add Fields");
                 AddStep(StepType::"Transfer Data");
                 AddStep(StepType::"Update Objects");
                 AddStep(StepType::"Compile Objects");
                 AddStep(StepType::"Delete Data");
                 AddStep(StepType::"Delete Old Fields");
                 IF SetModifyFlag THEN
                   AddStep(StepType::"Set Datetime Objects");
                 AddStep(StepType::"Update OM Data");

                 NoOfPages := NoOfSteps + 2;

                 FOR i := 1 TO 10 DO
                   SetCheckBox(i, i <= NoOfSteps, FALSE);

                 ActivatePage(1);
               END;

    OnQueryClosePage=VAR
                       ctConfirm@1000 : TextConst 'DEU=Renumbering of the fields is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;ENU=Renumbering of the fields is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;ESP=Renumbering of the fields is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;FRA=Renumbering of the fields is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?;NLD=Het omnummeren van velden is nog niet afgerond.\Het is ten zeerste aan te raden dit proces af te ronden.\Weet u het zeker dat u wilt stoppen?;DEA=Renumbering of the fields is not finished.\It is highly recommended to finish this process.\Are you sure you want to stop?';
                     BEGIN
                       IF ActivePage IN [1, NoOfPages] THEN
                         EXIT(TRUE);

                       EXIT(CONFIRM(ctConfirm, TRUE));
                     END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=&Previous;
                                 ENU=&Previous;
                                 ESP=&Previous;
                                 FRA=&Previous;
                                 NLD=&Vorige;
                                 DEA=&Previous];
                      Enabled=FALSE;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 ActivatePage(ActivePage - 1);

                                 IF (ActivePage = 8 + 1) AND NOT SetModifyFlag THEN
                                   ActivatePage(ActivePage - 1);
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Next;
                                 ENU=&Next;
                                 ESP=&Next;
                                 FRA=&Next;
                                 NLD=Vo&lgende;
                                 DEA=&Next];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 IF (ActivePage > 1) AND (ActivePage - 1 <= NoOfSteps) THEN
                                   ExecuteStep(ActivePage - 1);

                                 ActivatePage(ActivePage + 1);

                                 IF (ActivePage = 8 + 1) AND NOT SetModifyFlag THEN
                                   ActivatePage(ActivePage + 1);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=&Finish;
                                 ENU=&Finish;
                                 ESP=&Finish;
                                 FRA=&Finish;
                                 NLD=Voltooie&n;
                                 DEA=&Finish];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=Step1;
                Visible=Step1Visible }

    { 7   ;2   ;Field     ;
                CaptionML=[DEU=This wizard will help you with renumbering fields.;
                           ENU=This wizard will help you with renumbering fields.;
                           ESP=This wizard will help you with renumbering fields.;
                           FRA=This wizard will help you with renumbering fields.;
                           NLD=Deze wizard helpt u bij het omnummeren van velden.;
                           DEA=This wizard will help you with renumbering fields.];
                MultiLine=Yes }

    { 8   ;2   ;Field     ;
                CaptionML=[DEU=Press next to go to the first step.;
                           ENU=Press next to go to the first step.;
                           ESP=Press next to go to the first step.;
                           FRA=Press next to go to the first step.;
                           NLD=Druk op volgende om naar de eerste stap te gaan.;
                           DEA=Press next to go to the first step.];
                MultiLine=Yes }

    { 9   ;1   ;Group     ;
                Name=Step2;
                Visible=Step2Visible }

    { 10  ;2   ;Field     ;
                CaptionML=[DEU=The following steps have to be executed.;
                           ENU=The following steps have to be executed.;
                           ESP=The following steps have to be executed.;
                           FRA=The following steps have to be executed.;
                           NLD=De volgende stappen moeten worden uitgevoerd.;
                           DEA=The following steps have to be executed.];
                MultiLine=Yes }

    { 11  ;2   ;Field     ;
                SourceExpr=StepExecuted[1];
                CaptionClass=DisplayStepDescription[1];
                Visible=cChecked1Visible;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=StepExecuted[2];
                CaptionClass=DisplayStepDescription[2];
                Visible=cChecked2Visible;
                Editable=FALSE }

    { 13  ;2   ;Field     ;
                SourceExpr=StepExecuted[3];
                CaptionClass=DisplayStepDescription[3];
                Visible=cChecked3Visible;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr=StepExecuted[4];
                CaptionClass=DisplayStepDescription[4];
                Visible=cChecked4Visible;
                Editable=FALSE }

    { 15  ;2   ;Field     ;
                SourceExpr=StepExecuted[5];
                CaptionClass=DisplayStepDescription[5];
                Visible=cChecked5Visible;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr=StepExecuted[6];
                CaptionClass=DisplayStepDescription[6];
                Visible=cChecked6Visible;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                SourceExpr=StepExecuted[7];
                CaptionClass=DisplayStepDescription[7];
                Visible=cChecked7Visible;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr=StepExecuted[8];
                CaptionClass=DisplayStepDescription[8];
                Visible=cChecked8Visible;
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr=StepExecuted[9];
                CaptionClass=DisplayStepDescription[9];
                Visible=cChecked9Visible;
                Editable=FALSE }

    { 21  ;1   ;Group     ;
                Name=Step3;
                Visible=Step3Visible }

    { 22  ;2   ;Field     ;
                CaptionML=[DEU=The fields are renumbered;
                           ENU=The fields are renumbered;
                           ESP=The fields are renumbered;
                           FRA=The fields are renumbered;
                           NLD=De velden zijn omgenummerd;
                           DEA=The fields are renumbered];
                MultiLine=Yes }

    { 23  ;2   ;Field     ;
                CaptionML=[DEU=Press finish to close the wizard.;
                           ENU=Press finish to close the wizard.;
                           ESP=Press finish to close the wizard.;
                           FRA=Press finish to close the wizard.;
                           NLD=Druk op voltooien om de wizard te sluiten.;
                           DEA=Press finish to close the wizard.];
                MultiLine=Yes }

  }
  CODE
  {
    VAR
      TmpFieldToRenumber@1000 : TEMPORARY Record 11102074;
      TmpObject@1001 : TEMPORARY Record 2000000001;
      TmpObjectToRenumber@1002 : TEMPORARY Record 2000000001;
      DisplayStepDescription@1003 : ARRAY [10] OF Text[1024];
      StepDescription@1004 : ARRAY [10] OF Text[1024];
      ActivePage@1005 : Integer;
      NoOfPages@1006 : Integer;
      NoOfSteps@1007 : Integer;
      Step@1008 : ARRAY [10] OF Integer;
      StepType@1009 : ' ,Check Objects,Add Fields,Transfer Data,Update Objects,Compile Objects,Delete Data,Delete Old Fields,Set Datetime Objects,Update OM Data';
      cChecked10Visible@1010 : Boolean INDATASET;
      cChecked1Visible@1011 : Boolean INDATASET;
      cChecked2Visible@1012 : Boolean INDATASET;
      cChecked3Visible@1013 : Boolean INDATASET;
      cChecked4Visible@1014 : Boolean INDATASET;
      cChecked5Visible@1015 : Boolean INDATASET;
      cChecked6Visible@1016 : Boolean INDATASET;
      cChecked7Visible@1017 : Boolean INDATASET;
      cChecked8Visible@1018 : Boolean INDATASET;
      cChecked9Visible@1019 : Boolean INDATASET;
      FinishEnable@1020 : Boolean INDATASET;
      NextEnable@1021 : Boolean INDATASET;
      SetModifyFlag@1022 : Boolean INDATASET;
      Step1Visible@1023 : Boolean INDATASET;
      Step2Visible@1024 : Boolean INDATASET;
      Step3Visible@1025 : Boolean INDATASET;
      StepExecuted@1026 : ARRAY [12] OF Boolean;
      UpdateReferencingObjects@1027 : Boolean;

    LOCAL PROCEDURE ActivatePage@1(PageNo@1000 : Integer);
    VAR
      ActiveStep@1001 : Integer;
      i@1002 : Integer;
      ScreenNo@1003 : Integer;
    BEGIN
      CASE TRUE OF
        PageNo = 1 :
          ScreenNo := 1;

        PageNo - 1 <= NoOfSteps :
          ScreenNo := 2;

        ELSE
          ScreenNo := 3;
      END;

      Step1Visible := ScreenNo = 1;
      Step2Visible := ScreenNo = 2;
      Step3Visible := ScreenNo = 3;

      ActivePage := PageNo;
      NextEnable := ActivePage < NoOfPages;
      FinishEnable := ActivePage = NoOfPages;

      IF (PageNo > 1) AND (PageNo - 1 <= NoOfSteps) THEN
        ActiveStep := PageNo - 1;

      FOR i := 1 TO 10 DO
        IF i = ActiveStep THEN
          DisplayStepDescription[i] := ' > ' + StepDescription[i]
        ELSE
          IF StepDescription[i] = '' THEN
            DisplayStepDescription[i] := ''
          ELSE
            DisplayStepDescription[i] := '    ' + StepDescription[i];
    END;

    LOCAL PROCEDURE SetCheckBox@2(Number@1000 : Integer;Visible@1001 : Boolean;Checked@1002 : Boolean);
    BEGIN
      CASE Number OF
        1 : cChecked1Visible := Visible;
        2 : cChecked2Visible := Visible;
        3 : cChecked3Visible := Visible;
        4 : cChecked4Visible := Visible;
        5 : cChecked5Visible := Visible;
        6 : cChecked6Visible := Visible;
        7 : cChecked7Visible := Visible;
        8 : cChecked8Visible := Visible;
        9 : cChecked9Visible := Visible;
        10 : cChecked10Visible := Visible;
      END;

      StepExecuted[Number] := Checked;
    END;

    LOCAL PROCEDURE ExecuteStep@3(Step@1000 : Integer);
    VAR
      Progressbar@1001 : Codeunit 11102044;
      RenumberFieldMgt@1002 : Codeunit 11102054;
      StartDate@1003 : Date;
      StartTime@1004 : Time;
      MaxBar@1005 : Integer;
      ctUpdatingObjects@1006 : TextConst 'DEU=Update Objects...;ENU=Update Objects...;ESP=Update Objects...;FRA=Update Objects...;NLD=Objecten bijwerken...;DEA=Update Objects...';
    BEGIN
      StartDate := TODAY;
      StartTime := TIME;

      CASE Step OF
        StepType::"Check Objects" : MaxBar := TmpObject.COUNT * 4;
        StepType::"Add Fields" : MaxBar := TmpFieldToRenumber.COUNT;
        StepType::"Transfer Data" : MaxBar := TmpObjectToRenumber.COUNT;
        StepType::"Update Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Compile Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Delete Data" : MaxBar := TmpObjectToRenumber.COUNT;
        StepType::"Delete Old Fields" : MaxBar := TmpFieldToRenumber.COUNT;
        StepType::"Set Datetime Objects" : MaxBar := TmpObject.COUNT;
        StepType::"Update OM Data" : MaxBar := (TmpObject.COUNT * 3) + TmpFieldToRenumber.COUNT;
      END;

      Progressbar.Open(ctUpdatingObjects, 1);
      Progressbar.BarSize := MaxBar;

      CASE Step OF

        StepType::"Check Objects" :
          RenumberFieldMgt.CheckObjects(Progressbar, TmpObject);

        StepType::"Add Fields" :
          RenumberFieldMgt.AddNewFields(Progressbar, TmpObjectToRenumber, TmpFieldToRenumber, TRUE);

        StepType::"Transfer Data" :
          RenumberFieldMgt.TransferData(Progressbar, TmpObjectToRenumber, TmpFieldToRenumber);

        StepType::"Update Objects" :
          RenumberFieldMgt.UpdateObjects(Progressbar, TmpObject, TmpFieldToRenumber, TRUE, TRUE);

        StepType::"Compile Objects" :
          RenumberFieldMgt.CompileObjects(Progressbar, TmpObject);

        StepType::"Delete Data" :
          RenumberFieldMgt.DeleteData(Progressbar, TmpObjectToRenumber, TmpFieldToRenumber);

        StepType::"Delete Old Fields" :
          RenumberFieldMgt.DeleteOldFields(Progressbar, TmpObjectToRenumber, TmpFieldToRenumber, TRUE);

        StepType::"Set Datetime Objects" :
          RenumberFieldMgt.SetDatetimeStamp(Progressbar, TmpObject, StartDate, StartTime);

        StepType::"Update OM Data" :
          BEGIN
            RenumberFieldMgt.UpdateOMData(Progressbar, TmpFieldToRenumber);
            RenumberFieldMgt.UpdateCALHistory(Progressbar, TmpObject);
            RenumberFieldMgt.UpdateWhereUsedObjects(Progressbar, TmpObject);
            RenumberFieldMgt.UpdateWhereUsedIn(Progressbar, TmpObject);
          END;

      END;

      StepExecuted[Step] := TRUE;
    END;

    PROCEDURE Finished@4() : Boolean;
    BEGIN
      EXIT(StepExecuted[StepType::"Update OM Data"]);
    END;

    PROCEDURE SetUpdateReferencingObjects@32(NewValue@1000 : Boolean);
    BEGIN
      UpdateReferencingObjects := NewValue;
    END;

    PROCEDURE SetSetModifyFlag@26(NewValue@1000 : Boolean);
    BEGIN
      SetModifyFlag := NewValue;
    END;

    LOCAL PROCEDURE AddStep@5(StepType2@1000 : Integer);
    VAR
      ctAddFields@1001 : TextConst 'DEU=Addg new fields;ENU=Addg new fields;ESP=Addg new fields;FRA=Addg new fields;NLD=Nieuwe velden toevoegen;DEA=Addg new fields';
      ctCheckObjects@1002 : TextConst 'DEU=Check existing objects;ENU=Check existing objects;ESP=Check existing objects;FRA=Check existing objects;NLD=Bestaande objecten controleren;DEA=Check existing objects';
      ctCompileObjects@1003 : TextConst 'DEU=Compile objects;ENU=Compile objects;ESP=Compile objects;FRA=Compile objects;NLD=Objecten compileren;DEA=Compile objects';
      ctDeleteData@1004 : TextConst 'DEU=Delete data from old fields;ENU=Delete data from old fields;ESP=Delete data from old fields;FRA=Delete data from old fields;NLD=Data verwijderen uit oude velden;DEA=Delete data from old fields';
      ctDeleteOldFields@1005 : TextConst 'DEU=Delete old fields;ENU=Delete old fields;ESP=Delete old fields;FRA=Delete old fields;NLD=Oude velden verwijderen;DEA=Delete old fields';
      ctSetDatetimeObjects@1006 : TextConst 'DEU=Set new datetime stamp on objects;ENU=Set new datetime stamp on objects;ESP=Set new datetime stamp on objects;FRA=Set new datetime stamp on objects;NLD=Nieuwe datumstempel op objecten;DEA=Set new datetime stamp on objects';
      ctTransferData@1007 : TextConst 'DEU=Copy data to new fields;ENU=Copy data to new fields;ESP=Copy data to new fields;FRA=Copy data to new fields;NLD=Data naar nieuwe velden kopieren;DEA=Copy data to new fields';
      ctUpdateObjects@1008 : TextConst 'DEU=Update Objects;ENU=Update Objects;ESP=Update Objects;FRA=Update Objects;NLD=Objecten bijwerken;DEA=Update Objects';
      ctUpdateOMData@1009 : TextConst 'DEU=Update Object Manager data;ENU=Update Object Manager data;ESP=Update Object Manager data;FRA=Update Object Manager data;NLD=Object Manager data bijwerken;DEA=Update Object Manager data';
    BEGIN
      NoOfSteps += 1;
      Step[NoOfSteps] := StepType2;

      CASE StepType2 OF

        StepType::"Check Objects" :
          StepDescription[NoOfSteps] := ctCheckObjects;

        StepType::"Add Fields" :
          StepDescription[NoOfSteps] := ctAddFields;

        StepType::"Transfer Data" :
          StepDescription[NoOfSteps] := ctTransferData;

        StepType::"Update Objects" :
          StepDescription[NoOfSteps] := ctUpdateObjects;

        StepType::"Compile Objects" :
          StepDescription[NoOfSteps] := ctCompileObjects;

        StepType::"Delete Data" :
          StepDescription[NoOfSteps] := ctDeleteData;

        StepType::"Delete Old Fields" :
          StepDescription[NoOfSteps] := ctDeleteOldFields;

        StepType::"Set Datetime Objects" :
          StepDescription[NoOfSteps] := ctSetDatetimeObjects;

        StepType::"Update OM Data" :
          StepDescription[NoOfSteps] := ctUpdateOMData;

      END;
    END;

    BEGIN
    END.
  }
}

