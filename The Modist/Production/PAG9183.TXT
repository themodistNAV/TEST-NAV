OBJECT Page 9183 Generic Chart Setup
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Generic Chart Setup;
               ENG=Generic Chart Setup];
    SourceTable=Table2000000078;
    PageType=Card;
    OnAfterGetRecord=BEGIN
                       SetFieldValues;
                       EnableControls;
                     END;

    ActionList=ACTIONS
    {
      { 17      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 38      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Import Chart;
                                 ENG=Import Chart];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Import;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ImportChartDefinition;
                               END;
                                }
      { 37      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=E&xport Chart;
                                 ENG=E&xport Chart];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Export;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ExportChartDefinition;
                               END;
                                }
      { 41      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Copy Chart;
                                 ENG=Copy Chart];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Copy;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CopyGenericChart@1034 : Page 9187;
                               BEGIN
                                 CopyGenericChart.SetSourceChart(Rec);
                                 CopyGenericChart.RUNMODAL;
                               END;
                                }
      { 14      ;    ;ActionContainer;
                      Name=Action;
                      CaptionML=[ENU=Actions;
                                 ENG=Actions];
                      ActionContainerType=RelatedInformation }
      { 15      ;1   ;Action    ;
                      Name=Filters;
                      CaptionML=[ENU=Filters;
                                 ENG=Filters];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditFilter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowChartFiltersPage;
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=[ENU=General;
                           ENG=General];
                GroupType=Group }

    { 3   ;2   ;Field     ;
                NotBlank=Yes;
                SourceExpr=ID }

    { 4   ;2   ;Field     ;
                SourceExpr=Name }

    { 7   ;2   ;Field     ;
                Name=ChartExists;
                CaptionML=[ENU=Chart Exists;
                           ENG=Chart Exists];
                SourceExpr=BLOB.HASVALUE;
                Visible=FALSE }

    { 8   ;1   ;Group     ;
                Name=Data Source;
                CaptionML=[ENU=Data Source;
                           ENG=Data Source];
                GroupType=Group }

    { 11  ;2   ;Field     ;
                Name=Source Type;
                CaptionML=[ENU=Source Type;
                           ENG=Source Type];
                SourceExpr=TempGenericChartSetup."Source Type";
                OnValidate=VAR
                             CalledFrom@1001 : 'Source Type,Source ID';
                           BEGIN
                             ResetFields(CalledFrom::"Source Type");
                             UpdateChartDefinition;
                             StoreTempXRecValues
                           END;

                OnLookup=VAR
                           CalledFrom@1002 : 'Source Type,Source ID';
                         BEGIN
                           ResetFields(CalledFrom::"Source Type");
                           UpdateChartDefinition;
                           StoreTempXRecValues
                         END;
                          }

    { 9   ;2   ;Field     ;
                Name=Source ID;
                CaptionML=[ENU=Source ID;
                           ENG=Source ID];
                SourceExpr=TempGenericChartSetup."Source ID";
                OnValidate=VAR
                             GenericChartMgt@1000 : Codeunit 9180;
                             CalledFrom@1002 : 'Source Type,Source ID';
                           BEGIN
                             ResetFields(CalledFrom::"Source ID");
                             GenericChartMgt.ValidateObjectID(
                               TempGenericChartSetup."Source Type",TempGenericChartSetup."Source ID",TempGenericChartSetup."Object Name");
                             UpdateChartDefinition;
                             StoreTempXRecValues
                           END;

                OnLookup=VAR
                           GenericChartMgt@1000 : Codeunit 9180;
                           CalledFrom@1003 : 'Source Type,Source ID';
                         BEGIN
                           GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,FALSE);
                           GenericChartMgt.LookUpObjectId(
                             TempGenericChartSetup."Source Type",TempGenericChartSetup."Source ID",TempGenericChartSetup."Object Name");
                           ResetFields(CalledFrom::"Source ID");
                           GenericChartMgt.ValidateObjectID(
                             TempGenericChartSetup."Source Type",TempGenericChartSetup."Source ID",TempGenericChartSetup."Object Name");
                           UpdateChartDefinition;
                           StoreTempXRecValues
                         END;
                          }

    { 10  ;2   ;Field     ;
                Name=Source Name;
                CaptionML=[ENU=Source Name;
                           ENG=Source Name];
                SourceExpr=TempGenericChartSetup."Object Name";
                Editable=FALSE;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                           END;
                            }

    { 43  ;2   ;Field     ;
                CaptionML=[ENU=Filters;
                           ENG=Filters];
                SourceExpr=TempGenericChartSetup."Filter Text";
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               ShowChartFiltersPage;
                               CurrPage.UPDATE;
                             END;
                              }

    { 18  ;1   ;Group     ;
                Name=Measures (Y-Axis);
                CaptionML=[ENU=Measures (Y-Axis);
                           ENG=Measures (Y-Axis)];
                GroupType=Group;
                Layout=Rows }

    { 25  ;2   ;Group     ;
                GroupType=GridLayout;
                Layout=Rows }

    { 19  ;3   ;Group     ;
                Name=Required Measure;
                CaptionML=[ENU=Required Measure;
                           ENG=Required Measure];
                GroupType=Group }

    { 20  ;4   ;Field     ;
                Name=RequiredMeasureColumn;
                CaptionML=[ENU=Data Column;
                           ENG=Data Column];
                SourceExpr=DataColumn[1];
                Enabled=DataColumn1Enabled;
                Editable=DataColumn1Enabled;
                RowSpan=1;
                ColumnSpan=1;
                OnValidate=BEGIN
                             ValidateDataColumn(1);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[1],DummyCaption,2,FALSE);
                           ValidateDataColumn(1);
                         END;
                          }

    { 21  ;4   ;Field     ;
                Name=RequiredMeasureAggregation;
                CaptionML=[ENU=Aggregation;
                           ENG=Aggregation];
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[1];
                RowSpan=1;
                ColumnSpan=1;
                OnValidate=BEGIN
                             CheckAggregation(1);
                             UpdateChartDefinition;
                           END;
                            }

    { 22  ;4   ;Field     ;
                Name=RequiredMeasureType;
                CaptionML=[ENU=Graph Type;
                           ENG=Graph Type];
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,Pie,Doughnut,Range,Radar,Funnel;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,Pie,Doughnut,Range,Radar,Funnel];
                SourceExpr=ChartType;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;
                            }

    { 54  ;4   ;Field     ;
                Name=RequiredMeasureCaption;
                CaptionML=[ENU=Data Point Label;
                           ENG=Data Point Label];
                SourceExpr=MeasureCaption[1];
                Enabled=DataColumn1Enabled;
                Editable=DataColumn1Enabled;
                RowSpan=1;
                ColumnSpan=1;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.RequiredMeasureCode,GenericChartMgt.GetUserLanguage,MeasureCaption[1]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[1] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.RequiredMeasureCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 23  ;3   ;Group     ;
                Name=Optional Measure 1;
                CaptionML=[ENU=Optional Measure;
                           ENG=Optional Measure];
                GroupType=Group }

    { 24  ;4   ;Field     ;
                Name=OptionalMeasureColumn1;
                SourceExpr=DataColumn[2];
                Enabled=OptionalMeasuresEnabled AND DataColumn2Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             ValidateDataColumn(2);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[2],DummyCaption,2,FALSE);
                           ValidateDataColumn(2);
                         END;

                ShowCaption=No }

    { 26  ;4   ;Field     ;
                Name=OptionalMeasureAggregation1;
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[2];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             CheckAggregation(2);
                             UpdateChartDefinition;
                           END;

                ShowCaption=No }

    { 27  ;4   ;Field     ;
                Name=OptionalMeasureType1;
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range];
                SourceExpr=ChartTypeReduced[2];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 55  ;4   ;Field     ;
                Name=OptionalMeasureCaption1;
                SourceExpr=MeasureCaption[2];
                Enabled=OptionalMeasuresEnabled AND DataColumn2Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.OptionalMeasure1Code,GenericChartMgt.GetUserLanguage,MeasureCaption[2]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[2] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.OptionalMeasure1Code);
                               UpdateChartDefinition;
                             END;

                ShowCaption=No }

    { 28  ;3   ;Group     ;
                Name=Optional Measure 2;
                CaptionML=[ENU=Optional Measure;
                           ENG=Optional Measure];
                GroupType=Group }

    { 29  ;4   ;Field     ;
                Name=OptionalMeasureColumn2;
                SourceExpr=DataColumn[3];
                Enabled=OptionalMeasuresEnabled AND DataColumn3Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             ValidateDataColumn(3);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[3],DummyCaption,2,FALSE);
                           ValidateDataColumn(3);
                         END;

                ShowCaption=No }

    { 30  ;4   ;Field     ;
                Name=OptionalMeasureAggregation2;
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[3];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             CheckAggregation(3);
                             UpdateChartDefinition;
                           END;

                ShowCaption=No }

    { 31  ;4   ;Field     ;
                Name=OptionalMeasureType2;
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range];
                SourceExpr=ChartTypeReduced[3];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 56  ;4   ;Field     ;
                Name=OptionalMeasureCaption2;
                SourceExpr=MeasureCaption[3];
                Enabled=OptionalMeasuresEnabled AND DataColumn3Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.OptionalMeasure2Code,GenericChartMgt.GetUserLanguage,MeasureCaption[3]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[3] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.OptionalMeasure2Code);
                               UpdateChartDefinition;
                             END;

                ShowCaption=No }

    { 32  ;3   ;Group     ;
                Name=Optional Measure 3;
                CaptionML=[ENU=Optional Measure;
                           ENG=Optional Measure];
                GroupType=Group }

    { 33  ;4   ;Field     ;
                Name=OptionalMeasureColumn3;
                SourceExpr=DataColumn[4];
                Enabled=OptionalMeasuresEnabled AND DataColumn4Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             ValidateDataColumn(4);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[4],DummyCaption,2,FALSE);
                           ValidateDataColumn(4);
                         END;

                ShowCaption=No }

    { 34  ;4   ;Field     ;
                Name=OptionalMeasureAggregation3;
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[4];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             CheckAggregation(4);
                             UpdateChartDefinition;
                           END;

                ShowCaption=No }

    { 35  ;4   ;Field     ;
                Name=OptionalMeasureType3;
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range];
                SourceExpr=ChartTypeReduced[4];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 57  ;4   ;Field     ;
                Name=OptionalMeasureCaption3;
                SourceExpr=MeasureCaption[4];
                Enabled=OptionalMeasuresEnabled AND DataColumn4Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.OptionalMeasure3Code,GenericChartMgt.GetUserLanguage,MeasureCaption[4]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[4] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.OptionalMeasure3Code);
                               UpdateChartDefinition;
                             END;

                ShowCaption=No }

    { 47  ;3   ;Group     ;
                Name=Optional Measure 4;
                CaptionML=[ENU=Optional Measure;
                           ENG=Optional Measure];
                GroupType=Group }

    { 46  ;4   ;Field     ;
                Name=OptionalMeasureColumn4;
                SourceExpr=DataColumn[5];
                Enabled=OptionalMeasuresEnabled AND DataColumn5Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             ValidateDataColumn(5);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[5],DummyCaption,2,FALSE);
                           ValidateDataColumn(5);
                         END;

                ShowCaption=No }

    { 45  ;4   ;Field     ;
                Name=OptionalMeasureAggregation4;
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[5];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             CheckAggregation(5);
                             UpdateChartDefinition;
                           END;

                ShowCaption=No }

    { 40  ;4   ;Field     ;
                Name=OptionalMeasureType4;
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range];
                SourceExpr=ChartTypeReduced[5];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 58  ;4   ;Field     ;
                Name=OptionalMeasureCaption4;
                SourceExpr=MeasureCaption[5];
                Enabled=OptionalMeasuresEnabled AND DataColumn5Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.OptionalMeasure4Code,GenericChartMgt.GetUserLanguage,MeasureCaption[5]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[5] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.OptionalMeasure4Code);
                               UpdateChartDefinition;
                             END;

                ShowCaption=No }

    { 51  ;3   ;Group     ;
                Name=Optional Measure 5;
                CaptionML=[ENU=Optional Measure;
                           ENG=Optional Measure];
                GroupType=Group }

    { 50  ;4   ;Field     ;
                Name=OptionalMeasureColumn5;
                SourceExpr=DataColumn[6];
                Enabled=OptionalMeasuresEnabled AND DataColumn6Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             ValidateDataColumn(6);
                           END;

                OnLookup=BEGIN
                           GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[6],DummyCaption,2,FALSE);
                           ValidateDataColumn(6);
                         END;

                ShowCaption=No }

    { 49  ;4   ;Field     ;
                Name=OptionalMeasureAggregation5;
                OptionCaptionML=[ENU=None,Count,Sum,Min,Max,Avg;
                                 ENG=None,Count,Sum,Min,Max,Avg];
                SourceExpr=Aggregation[6];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             CheckAggregation(6);
                             UpdateChartDefinition;
                           END;

                ShowCaption=No }

    { 48  ;4   ;Field     ;
                Name=OptionalMeasureType5;
                OptionCaptionML=[ENU=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range;
                                 ENG=Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range];
                SourceExpr=ChartTypeReduced[6];
                Enabled=OptionalMeasuresEnabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             UpdateChartDefinition;
                             EnableControls;
                           END;

                ShowCaption=No }

    { 59  ;4   ;Field     ;
                Name=OptionalMeasureCaption5;
                SourceExpr=MeasureCaption[6];
                Enabled=OptionalMeasuresEnabled AND DataColumn6Enabled;
                Editable=OptionalMeasuresEnabled;
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.OptionalMeasure5Code,GenericChartMgt.GetUserLanguage,MeasureCaption[6]);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               MeasureCaption[6] := GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.OptionalMeasure5Code);
                               UpdateChartDefinition;
                             END;

                ShowCaption=No }

    { 53  ;2   ;Field     ;
                Name=Y-Axis Title;
                CaptionML=[ENU=Y-Axis Title;
                           ENG=Y-Axis Title];
                SourceExpr=TempGenericChartSetup."Y-Axis Title";
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.YAxisTitleCode,GenericChartMgt.GetUserLanguage,
                               TempGenericChartSetup."Y-Axis Title");
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               TempGenericChartSetup."Y-Axis Title" :=
                                 GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.YAxisTitleCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 52  ;2   ;Field     ;
                Name=Show Y-Axis Title;
                CaptionML=[ENU=Show Y-Axis Title;
                           ENG=Show Y-Axis Title];
                SourceExpr=TempGenericChartSetup."Y-Axis Show Title";
                OnValidate=VAR
                             GenericChartMgt@1001 : Codeunit 9180;
                           BEGIN
                             GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,TRUE);
                             UpdateChartDefinition;
                           END;
                            }

    { 12  ;1   ;Group     ;
                Name=Dimensions;
                CaptionML=[ENU=Dimensions (X- and Z-Axes);
                           ENG=Dimensions (X- and Z-Axes)];
                GroupType=Group }

    { 5   ;2   ;Field     ;
                Name=X-Axis Field;
                CaptionML=[ENU=X-Axis Field;
                           ENG=X-Axis Field];
                SourceExpr=TempGenericChartSetup."X-Axis Field Name";
                OnValidate=BEGIN
                             ValidateDimension(1);
                           END;

                OnLookup=VAR
                           GenericChartMgt@1000 : Codeunit 9180;
                         BEGIN
                           WITH TempGenericChartSetup DO
                             GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,"X-Axis Field ID","X-Axis Field Name","X-Axis Title",0,FALSE);
                           TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.XAxisTitleCode,GenericChartMgt.GetUserLanguage,
                             TempGenericChartSetup."X-Axis Title");
                           ValidateDimension(1);
                         END;
                          }

    { 61  ;2   ;Field     ;
                Name=X-Axis Title;
                CaptionML=[ENU=X-Axis Title;
                           ENG=X-Axis Title];
                SourceExpr=TempGenericChartSetup."X-Axis Title";
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.XAxisTitleCode,GenericChartMgt.GetUserLanguage,
                               TempGenericChartSetup."X-Axis Title");
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               TempGenericChartSetup."X-Axis Title" :=
                                 GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.XAxisTitleCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 42  ;2   ;Field     ;
                Name=Show X-Axis Title;
                CaptionML=[ENU=Show X-Axis Title;
                           ENG=Show X-Axis Title];
                SourceExpr=TempGenericChartSetup."X-Axis Show Title";
                OnValidate=VAR
                             GenericChartMgt@1001 : Codeunit 9180;
                           BEGIN
                             GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,TRUE);
                             UpdateChartDefinition;
                           END;
                            }

    { 39  ;2   ;Field     ;
                Name=Data Point X Label;
                CaptionML=[ENU=Data Point X Label;
                           ENG=Data Point X Label];
                SourceExpr=TempGenericChartSetup."X-Axis Field Caption";
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.XAxisCaptionCode,GenericChartMgt.GetUserLanguage,
                               TempGenericChartSetup."X-Axis Field Caption");
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               TempGenericChartSetup."X-Axis Field Caption" :=
                                 GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.XAxisCaptionCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 13  ;2   ;Field     ;
                Name=Z-Axis Field;
                CaptionML=[ENU=Z-Axis Field;
                           ENG=Z-Axis Field];
                SourceExpr=TempGenericChartSetup."Z-Axis Field Name";
                Enabled=ZAxisEnabled;
                Editable=ZAxisEnabled;
                OnValidate=BEGIN
                             ValidateDimension(2);
                           END;

                OnLookup=VAR
                           GenericChartMgt@1000 : Codeunit 9180;
                         BEGIN
                           WITH TempGenericChartSetup DO
                             GenericChartMgt.RetrieveFieldColumn(TempGenericChartSetup,"Z-Axis Field ID","Z-Axis Field Name","Z-Axis Title",0,FALSE);
                           ValidateDimension(2);
                         END;
                          }

    { 60  ;2   ;Field     ;
                Name=Data Point Z Label;
                CaptionML=[ENU=Data Point Z Label;
                           ENG=Data Point Z Label];
                SourceExpr=TempGenericChartSetup."Z-Axis Field Caption";
                OnValidate=BEGIN
                             TempGenericChartCaptionsBuf.SetCaption(GenericChartMgt.ZAxisCaptionCode,GenericChartMgt.GetUserLanguage,
                               TempGenericChartSetup."Z-Axis Field Caption");
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               TempGenericChartSetup."Z-Axis Field Caption" :=
                                 GenericChartMgt.TextMLAssistEdit(TempGenericChartCaptionsBuf,GenericChartMgt.ZAxisCaptionCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 44  ;1   ;Group     ;
                Name=Chart Description;
                CaptionML=[ENU=Chart Description;
                           ENG=Chart Description];
                GroupType=Group }

    { 36  ;2   ;Field     ;
                Name=Description;
                CaptionML=[ENU=Description;
                           ENG=Description];
                SourceExpr=ChartDescription;
                Editable=TitleEnabled;
                MultiLine=Yes;
                OnValidate=BEGIN
                             TempGenericChartMemoBuf.SetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage,ChartDescription);
                             UpdateChartDefinition;
                           END;

                OnAssistEdit=BEGIN
                               ChartDescription := GenericChartMgt.MemoMLAssistEdit(TempGenericChartMemoBuf,GenericChartMgt.DescriptionCode);
                               UpdateChartDefinition;
                             END;
                              }

    { 68  ;1   ;Part      ;
                Name=PreviewPart;
                CaptionML=[ENU=Preview Part;
                           ENG=Preview Part];
                PagePartID=Page9184;
                PartType=Page }

  }
  CODE
  {
    VAR
      TempGenericChartSetup@1004 : TEMPORARY Record 9180;
      TempGenericChartFilter@1020 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1001 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1029 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1028 : TEMPORARY Record 9186;
      GenericChartMgt@1016 : Codeunit 9180;
      DataColumn@1013 : ARRAY [6] OF Text[50];
      MeasureCaption@1025 : ARRAY [6] OF Text[250];
      Aggregation@1014 : ARRAY [6] OF 'None,Count,Sum,Min,Max,Avg';
      ChartType@1015 : 'Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,Pie,Doughnut,Range,Radar,Funnel';
      ChartTypeReduced@1003 : ARRAY [6] OF 'Column,Point,Line,ColumnStacked,ColumnStacked100,Area,AreaStacked,AreaStacked100,StepLine,,,Range';
      ChartDescription@1011 : Text;
      xRecSourceType@1009 : ' ,Table,Query';
      xRecSourceID@1010 : Integer;
      DummyInt@1017 : Integer;
      DummyCaption@1030 : Text[50];
      Text001@1000 : TextConst 'ENU=All fields will be reset. Are you sure that you want to change the %1?;ENG=All fields will be reset. Are you sure that you want to change the %1?';
      OptionalMeasuresEnabled@1005 : Boolean INDATASET;
      Text002@1006 : TextConst 'ENU=Field %1 is already assigned to a measure or dimension. Select a different field.;ENG=Field %1 is already assigned to a measure or dimension. Select a different field.';
      Text003@1007 : TextConst '@@@=Do you want to replace the existing definition for Chart 36-06?;ENU=Do you want to replace the existing definition for %1 %2?;ENG=Do you want to replace the existing definition for %1 %2?';
      DataColumn1Enabled@1012 : Boolean INDATASET;
      DataColumn2Enabled@1022 : Boolean INDATASET;
      DataColumn3Enabled@1023 : Boolean INDATASET;
      DataColumn4Enabled@1024 : Boolean INDATASET;
      DataColumn5Enabled@1026 : Boolean INDATASET;
      DataColumn6Enabled@1027 : Boolean INDATASET;
      ZAxisEnabled@1008 : Boolean INDATASET;
      Text004@1018 : TextConst 'ENU=You can only specify one Measure with aggregation type Count.;ENG=You can only specify one Measure with aggregation type Count.';
      Text005@1019 : TextConst 'ENU=If you select Aggregation Count, the Data Column will be cleared for this measure. Do you want to continue?;ENG=If you select Aggregation Count, the Data Column will be cleared for this measure. Do you want to continue?';
      TitleEnabled@1002 : Boolean;

    LOCAL PROCEDURE SetFieldValues@6();
    BEGIN
      ClearAllVariables;
      GenericChartMgt.RetrieveXML(
        Rec,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
      ChartDescription := TempGenericChartMemoBuf.GetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage);
      TempGenericChartSetup."Y-Axis Title" :=
        TempGenericChartCaptionsBuf.GetCaption(GenericChartMgt.YAxisTitleCode,GenericChartMgt.GetUserLanguage);
      TempGenericChartSetup."X-Axis Field Caption" :=
        TempGenericChartCaptionsBuf.GetCaption(GenericChartMgt.XAxisCaptionCode,GenericChartMgt.GetUserLanguage);
      TempGenericChartSetup."X-Axis Title" :=
        TempGenericChartCaptionsBuf.GetCaption(GenericChartMgt.XAxisTitleCode,GenericChartMgt.GetUserLanguage);
      TempGenericChartSetup."Z-Axis Field Caption" :=
        TempGenericChartCaptionsBuf.GetCaption(GenericChartMgt.ZAxisCaptionCode,GenericChartMgt.GetUserLanguage);
      TempGenericChartSetup.INSERT;
      StoreTempXRecValues;
      FillMatrixWhenOpenPage;
      UpdateTempGenericChartYAXis;
      RefreshPreview;
    END;

    LOCAL PROCEDURE ValidateDataColumn@13(Index@1000 : Integer);
    VAR
      FieldAlreadyExists@1002 : Boolean;
      i@1001 : Integer;
    BEGIN
      GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,TRUE);
      FOR i := 1 TO ARRAYLEN(DataColumn) DO BEGIN
        IF (DataColumn[i] <> '') AND (Index <> i) THEN
          FieldAlreadyExists := (DataColumn[Index] = DataColumn[i]) OR FieldAlreadyExists;
      END;

      IF FieldAlreadyExists THEN
        ERROR(Text002,DataColumn[Index]);

      GenericChartMgt.CheckDataTypeAggregationCompliance(TempGenericChartSetup,DataColumn[Index],Aggregation[Index]);
      GenericChartMgt.ValidateFieldColumn(TempGenericChartSetup,DummyInt,DataColumn[Index],DummyCaption,2,FALSE,Aggregation[Index]);
      UpdateChartDefinition;
      EnableControls;
    END;

    LOCAL PROCEDURE ValidateDimension@14(Index@1000 : Integer);
    VAR
      DummyAggregation@1002 : 'None,Count,Sum,Min,Max,Avg';
    BEGIN
      GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,TRUE);
      WITH TempGenericChartSetup DO BEGIN
        IF ("X-Axis Field Name" <> '') AND ("Z-Axis Field Name" <> '') AND ("X-Axis Field Name" = "Z-Axis Field Name") THEN
          ERROR(Text002,DataColumn[Index]);
      END;
      WITH TempGenericChartSetup DO
        CASE Index OF
          1:
            GenericChartMgt.ValidateFieldColumn(
              TempGenericChartSetup,"X-Axis Field ID","X-Axis Field Name","X-Axis Title",0,FALSE,DummyAggregation);
          2:
            GenericChartMgt.ValidateFieldColumn(
              TempGenericChartSetup,"Z-Axis Field ID","Z-Axis Field Name","Z-Axis Title",0,FALSE,DummyAggregation);
        END;
      UpdateChartDefinition;
      EnableControls;
    END;

    LOCAL PROCEDURE UpdateTempGenericChartYAXis@2();
    VAR
      i@1000 : Integer;
      Count@1001 : Integer;
    BEGIN
      TempGenericChartYAxis.DELETEALL;

      IF ChartCapableOfOptionalMeasures THEN
        Count := ARRAYLEN(DataColumn)
      ELSE
        Count := 1;

      FOR i := 1 TO Count DO
        IF AddYAxisMeasure(i) THEN BEGIN
          CLEAR(TempGenericChartYAxis);
          TempGenericChartYAxis.ID := ID;
          TempGenericChartYAxis."Line No." := i * 10000;
          TempGenericChartYAxis."Y-Axis Measure Field Name" := DataColumn[i];
          GenericChartMgt.RetrieveFieldColumnIDFromName(
            TempGenericChartSetup."Source Type",TempGenericChartSetup."Source ID",TempGenericChartYAxis."Y-Axis Measure Field ID",
            DataColumn[i]);
          TempGenericChartYAxis."Y-Axis Measure Field Caption" := MeasureCaption[i];
          TempGenericChartYAxis.Aggregation := Aggregation[i];
          IF i = 1 THEN
            TempGenericChartYAxis."Chart Type" := ChartType
          ELSE
            TempGenericChartYAxis."Chart Type" := ChartTypeReduced[i];
          TempGenericChartYAxis.INSERT;
        END;
    END;

    LOCAL PROCEDURE FillMatrixWhenOpenPage@1();
    VAR
      i@1001 : Integer;
      CurrentCaptionCode@1002 : Code[10];
    BEGIN
      CLEAR(DataColumn);
      CLEAR(MeasureCaption);
      CLEAR(Aggregation);
      CLEAR(ChartType);

      i := 0;
      IF TempGenericChartYAxis.FINDSET THEN BEGIN
        CurrentCaptionCode := GenericChartMgt.RequiredMeasureCode;
        REPEAT
          i += 1;
          DataColumn[i] := TempGenericChartYAxis."Y-Axis Measure Field Name";
          MeasureCaption[i] := TempGenericChartCaptionsBuf.GetCaption(CurrentCaptionCode,GenericChartMgt.GetUserLanguage);
          Aggregation[i] := TempGenericChartYAxis.Aggregation;
          IF i = 1 THEN
            ChartType := TempGenericChartYAxis."Chart Type"
          ELSE
            ChartTypeReduced[i] := TempGenericChartYAxis."Chart Type";

          IF CurrentCaptionCode = GenericChartMgt.RequiredMeasureCode THEN
            CurrentCaptionCode := GenericChartMgt.OptionalMeasure1Code
          ELSE
            CurrentCaptionCode := INCSTR(CurrentCaptionCode)
        UNTIL TempGenericChartYAxis.NEXT = 0;
      END
    END;

    LOCAL PROCEDURE UpdateChartDefinition@5();
    BEGIN
      IF ChartDefinitionCreationIsPossible THEN BEGIN
        UpdateTempGenericChartYAXis;
        GenericChartMgt.SaveChanges(Rec,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
          TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);
      END;
      EnableControls;
      RefreshPreview;
    END;

    LOCAL PROCEDURE ChartDefinitionCreationIsPossible@4() : Boolean;
    BEGIN
      WITH TempGenericChartSetup DO
        EXIT(("Source Type" <> "Source Type"::" ") AND ("Source ID" <> 0));
    END;

    LOCAL PROCEDURE RefreshPreview@8();
    VAR
      chartBuilder@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
    BEGIN
      IF ChartDefinitionCreationIsPossible THEN BEGIN
        chartBuilder := chartBuilder.Empty;
        GenericChartMgt.FillChartHelper(chartBuilder,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
          TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);

        CurrPage.PreviewPart.PAGE.SetChartDefinition(chartBuilder);
      END;
    END;

    LOCAL PROCEDURE ShowChartFiltersPage@11();
    VAR
      TempGenericChartFilter2@1001 : TEMPORARY Record 9181;
      GenericChartFiltersPage@1000 : Page 9181;
      FilterText@1002 : Text[250];
    BEGIN
      GenericChartMgt.CheckSourceTypeID(TempGenericChartSetup,TRUE);
      GenericChartFiltersPage.SetTempGenericChart(TempGenericChartSetup);
      GenericChartFiltersPage.SetFilters(TempGenericChartFilter);
      GenericChartFiltersPage.SETTABLEVIEW(TempGenericChartFilter);
      COMMIT;
      GenericChartFiltersPage.RUNMODAL;
      TempGenericChartFilter.DELETEALL;
      CLEAR(FilterText);
      GenericChartFiltersPage.GetFilters(TempGenericChartFilter2);
      IF TempGenericChartFilter2.FINDSET THEN
        REPEAT
          TempGenericChartFilter := TempGenericChartFilter2;
          TempGenericChartFilter.INSERT;
          IF TempGenericChartFilter."Filter Value" <> '' THEN
            GenericChartMgt.BuildFilterText(FilterText,
              COPYSTR(
                TempGenericChartFilter."Filter Field Name" + ' : ' + TempGenericChartFilter."Filter Value",1,MAXSTRLEN(FilterText)));
        UNTIL TempGenericChartFilter2.NEXT = 0;
      GenericChartMgt.FinalizeFilterText(FilterText);
      TempGenericChartSetup."Filter Text" := FilterText;
      IF TempGenericChartSetup.MODIFY THEN;
      UpdateChartDefinition;
    END;

    LOCAL PROCEDURE ResetFields@7(CalledFrom@1000 : 'Source Type,Source ID');
    VAR
      ID@1001 : Code[20];
      SourceType@1002 : Option;
      SourceID@1003 : Integer;
    BEGIN
      ID := TempGenericChartSetup.ID;
      WITH TempGenericChartSetup DO BEGIN
        SourceType := "Source Type";
        SourceID := "Source ID";
        CASE CalledFrom OF
          CalledFrom::"Source Type":
            BEGIN
              IF "Source Type" = xRecSourceType THEN
                EXIT;
              IF xRecSourceType <> xRecSourceType::" " THEN
                IF NOT CONFIRM(Text001,TRUE,FIELDCAPTION("Source Type")) THEN
                  ERROR('');
            END;
          CalledFrom::"Source ID":
            BEGIN
              IF "Source ID" = xRecSourceID THEN
                EXIT;
              IF xRecSourceID <> 0 THEN
                IF NOT CONFIRM(Text001,TRUE,FIELDCAPTION("Source ID")) THEN
                  ERROR('');
            END;
        END;
      END;
      ClearAllVariables;
      TempGenericChartSetup.ID := ID;
      WITH TempGenericChartSetup DO BEGIN
        "Source Type" := SourceType;
        IF CalledFrom = CalledFrom::"Source ID" THEN
          "Source ID" := SourceID;
      END;
    END;

    LOCAL PROCEDURE ClearAllVariables@9();
    BEGIN
      TempGenericChartSetup.DELETEALL;
      TempGenericChartFilter.DELETEALL;
      TempGenericChartYAxis.DELETEALL;
      TempGenericChartCaptionsBuf.DELETEALL;
      TempGenericChartMemoBuf.DELETEALL;

      CLEAR(TempGenericChartFilter);
      CLEAR(TempGenericChartYAxis);
      CLEAR(TempGenericChartSetup);
      CLEAR(TempGenericChartCaptionsBuf);
      CLEAR(TempGenericChartMemoBuf);
      CLEAR(GenericChartMgt);
      CLEAR(DataColumn);
      CLEAR(MeasureCaption);
      CLEAR(Aggregation);
      CLEAR(ChartType);
      CLEAR(ChartTypeReduced);
      CLEAR(DummyInt);
      CLEAR(DummyCaption);
    END;

    LOCAL PROCEDURE ChartCapableOfOptionalMeasures@12() : Boolean;
    BEGIN
      EXIT(NOT (ChartType IN [ChartType::Pie,ChartType::Doughnut,ChartType::Funnel,ChartType::Radar]));
    END;

    LOCAL PROCEDURE StoreTempXRecValues@15();
    BEGIN
      xRecSourceType := TempGenericChartSetup."Source Type";
      xRecSourceID := TempGenericChartSetup."Source ID";
    END;

    LOCAL PROCEDURE ImportChartDefinition@3();
    VAR
      TempBLOB@1000 : Record 99008535;
      FileMgt@1001 : Codeunit 419;
    BEGIN
      IF FileMgt.BLOBImport(TempBLOB,'*.xml') = '' THEN
        EXIT;

      IF BLOB.HASVALUE THEN
        IF NOT CONFIRM(Text003,FALSE,TABLECAPTION,ID) THEN
          EXIT;

      BLOB := TempBLOB.Blob;
      CurrPage.SAVERECORD;
    END;

    LOCAL PROCEDURE ExportChartDefinition@10();
    VAR
      TempBlob@1000 : Record 99008535;
      FileMgt@1001 : Codeunit 419;
    BEGIN
      CALCFIELDS(BLOB);
      IF BLOB.HASVALUE THEN BEGIN
        TempBlob.Blob := BLOB;
        FileMgt.BLOBExport(TempBlob,'*.xml',TRUE);
      END;
    END;

    LOCAL PROCEDURE EnableControls@17();
    BEGIN
      OptionalMeasuresEnabled := ChartCapableOfOptionalMeasures AND (TempGenericChartSetup."Z-Axis Field Name" = '');
      ZAxisEnabled :=
        ChartCapableOfOptionalMeasures AND
        (DataColumn[2] = '') AND (DataColumn[3] = '') AND (DataColumn[4] = '') AND (DataColumn[5] = '') AND (DataColumn[6] = '');
      TitleEnabled := ChartDefinitionCreationIsPossible;
      DataColumn1Enabled := Aggregation[1] <> Aggregation[1]::Count;
      DataColumn2Enabled := Aggregation[2] <> Aggregation[2]::Count;
      DataColumn3Enabled := Aggregation[3] <> Aggregation[3]::Count;
      DataColumn4Enabled := Aggregation[4] <> Aggregation[4]::Count;
      DataColumn5Enabled := Aggregation[5] <> Aggregation[5]::Count;
      DataColumn6Enabled := Aggregation[6] <> Aggregation[6]::Count;
    END;

    LOCAL PROCEDURE AddYAxisMeasure@19(index@1000 : Integer) : Boolean;
    BEGIN
      IF Aggregation[index] = Aggregation[index]::Count THEN
        EXIT(TRUE);
      EXIT(DataColumn[index] <> '');
    END;

    LOCAL PROCEDURE CheckAggregation@30(index@1000 : Integer);
    VAR
      FieldOfTypeCountAlreadyExists@1002 : Boolean;
      i@1001 : Integer;
      CountColumnName@1003 : Text[50];
      ReplaceDataColumn@1004 : Boolean;
    BEGIN
      FOR i := 1 TO ARRAYLEN(Aggregation) DO BEGIN
        IF (Aggregation[i] = Aggregation[i]::Count) AND (index <> i) THEN
          FieldOfTypeCountAlreadyExists := (Aggregation[index] = Aggregation[i]) OR FieldOfTypeCountAlreadyExists;
      END;

      IF FieldOfTypeCountAlreadyExists THEN
        ERROR(Text004);

      GenericChartMgt.CheckDataTypeAggregationCompliance(TempGenericChartSetup,DataColumn[index],Aggregation[index]);
      IF Aggregation[index] = Aggregation[index]::Count THEN BEGIN
        CountColumnName := GenericChartMgt.GetQueryCountColumnName(TempGenericChartSetup);
        IF DataColumn[index] <> CountColumnName THEN BEGIN
          ReplaceDataColumn := TRUE;
          IF DataColumn[index] <> '' THEN
            ReplaceDataColumn := CONFIRM(Text005,FALSE);
          IF ReplaceDataColumn THEN
            DataColumn[index] := CountColumnName
          ELSE
            ERROR('');
        END;
      END;
    END;

    BEGIN
    END.
  }
}

