OBJECT Codeunit 9062640 Event Sub Document Posting FA
{
  OBJECT-PROPERTIES
  {
    Date=12/11/15;
    Time=[ 4:50:44 PM];
    Version List=NA2016.11;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DontAutoDelete@1000000002 : Boolean;
      DontAutoDeleteTableIDFilter@1000000003 : Text;

    [EventSubscriber(Codeunit,80,OnBeforePostSalesDoc)]
    LOCAL PROCEDURE SalesPostDontDeleteFileAttachments@1000000000(VAR SalesHeader@1000000000 : Record 36);
    BEGIN
      DontAutoDelete := TRUE;
      DontAutoDeleteTableIDFilter := '36';
    END;

    [EventSubscriber(Codeunit,80,OnAfterPostSalesDoc,"",Skip,Skip)]
    LOCAL PROCEDURE SalesPostTransferFileAttachment@9062231(VAR SalesHeader@9062230 : Record 36;VAR GenJnlPostLine@9062231 : Codeunit 12;SalesShptHdrNo@9062232 : Code[20];RetRcpHdrNo@9062233 : Code[20];SalesInvHdrNo@9062234 : Code[20];SalesCrMemoHdrNo@9062235 : Code[20]);
    VAR
      FileAttachCU@9062236 : Codeunit 9062267;
      RecRef@9062237 : RecordRef;
      FldRef1@9062238 : FieldRef;
      FldRef2@9062239 : FieldRef;
      FileAttachment@1000000001 : Record 9062272;
    BEGIN
      DontAutoDelete := FALSE;
      DontAutoDeleteTableIDFilter := '';

      IF SalesShptHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Sales Shipment Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := SalesShptHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Sales Shipment Header",RecRef.GETPOSITION,36,SalesHeader.GETPOSITION);
            END;
          RecRef.CLOSE;
        END;

      IF RetRcpHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Return Receipt Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := SalesInvHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Return Receipt Header",RecRef.GETPOSITION,36,SalesHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
        END;


      IF SalesInvHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Sales Invoice Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := SalesInvHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Sales Invoice Header",RecRef.GETPOSITION,36,SalesHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
        END;


      IF SalesCrMemoHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Sales Cr.Memo Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := SalesCrMemoHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Sales Cr.Memo Header",RecRef.GETPOSITION,36,SalesHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
        END;

      //Delete Sales Header File Attachments
      FileAttachment.SETRANGE(FileAttachment."Table ID",DATABASE::"Sales Header");
      FileAttachment.SETRANGE(FileAttachment."Primary Key Value 1",FORMAT(SalesHeader."Document Type",0,2));
      FileAttachment.SETRANGE(FileAttachment."Primary Key Value 2",SalesHeader."No.");
      IF FileAttachment.FINDSET THEN
        FileAttachment.DELETEALL(TRUE);
    END;

    [EventSubscriber(Codeunit,90,OnBeforePostPurchaseDoc)]
    LOCAL PROCEDURE PurchPostDontDeleteFileAttachments@1000000001(VAR PurchaseHeader@1000000000 : Record 38);
    BEGIN
      DontAutoDelete := TRUE;
      DontAutoDeleteTableIDFilter := '38';
    END;

    [EventSubscriber(Codeunit,90,OnAfterPostPurchaseDoc,"",Skip,Skip)]
    LOCAL PROCEDURE PurchPostTransferFileAttachment@9062230(VAR PurchaseHeader@9062230 : Record 38;VAR GenJnlPostLine@9062231 : Codeunit 12;PurchRcpHdrNo@9062232 : Code[20];RetShptHdrNo@9062233 : Code[20];PurchInvHdrNo@9062234 : Code[20];PurchCrMemoHdrNo@9062235 : Code[20]);
    VAR
      FileAttachCU@9062239 : Codeunit 9062267;
      RecRef@9062238 : RecordRef;
      FldRef1@9062237 : FieldRef;
      FldRef2@9062236 : FieldRef;
      FileAttachment@1000000000 : Record 9062272;
    BEGIN
      DontAutoDelete := FALSE;
      DontAutoDeleteTableIDFilter := '';

      IF PurchRcpHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Purch. Rcpt. Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := PurchRcpHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Purch. Rcpt. Header",RecRef.GETPOSITION,38,PurchaseHeader.GETPOSITION);
            END;
          RecRef.CLOSE;
        END;


      IF RetShptHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Return Shipment Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := RetShptHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Return Shipment Header",RecRef.GETPOSITION,38,PurchaseHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
        END;


      IF PurchInvHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Purch. Inv. Header");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := PurchInvHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Purch. Inv. Header",RecRef.GETPOSITION,38,PurchaseHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
      END;

      IF PurchCrMemoHdrNo <>'' THEN
        BEGIN

          RecRef.OPEN(DATABASE::"Purch. Cr. Memo Hdr.");
          FldRef1:= RecRef.FIELD(3);
          FldRef1.VALUE := PurchCrMemoHdrNo;

          IF RecRef.FIND('=') THEN
            BEGIN
              FileAttachCU.CopyToRecord(DATABASE::"Purch. Cr. Memo Hdr.",RecRef.GETPOSITION,38,PurchaseHeader.GETPOSITION);
            END;
            RecRef.CLOSE;
      END;

      FileAttachment.SETRANGE(FileAttachment."Table ID",DATABASE::"Purchase Header");
      FileAttachment.SETRANGE(FileAttachment."Primary Key Value 1",FORMAT(PurchaseHeader."Document Type",0,2));
      FileAttachment.SETRANGE(FileAttachment."Primary Key Value 2",PurchaseHeader."No.");
      IF FileAttachment.FINDSET THEN
        FileAttachment.DELETEALL(TRUE);
    END;

    [EventSubscriber(Codeunit,1,OnAfterGetDatabaseTableTriggerSetup)]
    LOCAL PROCEDURE OnAfterGetDatabaseTableTriggerSetup@9062234(TableId@9062229 : Integer;VAR OnDatabaseInsert@9062230 : Boolean;VAR OnDatabaseModify@9062231 : Boolean;VAR OnDatabaseDelete@9062232 : Boolean;VAR OnDatabaseRename@9062233 : Boolean);
    BEGIN
      GetDatabaseTableTriggerSetup(TableId, OnDatabaseInsert, OnDatabaseModify, OnDatabaseDelete, OnDatabaseRename);
    END;

    PROCEDURE GetDatabaseTableTriggerSetup@9062229(pTableId@9062229 : Integer;VAR pLogInsert@9062230 : Boolean;VAR pLogModify@9062231 : Boolean;VAR pLogDelete@9062232 : Boolean;VAR pLogRename@9062233 : Boolean);
    BEGIN
      IF (COMPANYNAME() = '') THEN
        EXIT;

      pLogDelete := TRUE;
      pLogRename := TRUE;
    END;

    [EventSubscriber(Codeunit,1,OnAfterOnDatabaseDelete)]
    LOCAL PROCEDURE DeleteFAOnAfterOnDatabaseDelete@9062233(RecRef@9062230 : RecordRef);
    VAR
      FileAttachCU@9062234 : Codeunit 9062267;
      Object@1000000000 : Record 2000000001;
      ExitFunction@1000000001 : Boolean;
    BEGIN
      //This needs to call the File attachement delete function for the delete record.
      OnBeforeDatabaseTriggerDeleteFileAttachment(DontAutoDelete,DontAutoDeleteTableIDFilter);

      IF DontAutoDelete THEN BEGIN
        Object.FILTERGROUP(11);
        Object.SETRANGE(Object.Type,Object.Type::Table);
        Object.SETFILTER(Object.ID,DontAutoDeleteTableIDFilter);
        Object.FILTERGROUP(12);
        Object.SETRANGE(Object.ID,RecRef.NUMBER);
        ExitFunction := Object.FINDFIRST;

        Object.RESET;
        IF ExitFunction THEN BEGIN
          EXIT;
        END;
      END;

      FileAttachCU.RecordDelete(RecRef);
    END;

    [EventSubscriber(Codeunit,1,OnAfterOnDatabaseRename)]
    LOCAL PROCEDURE RenameFAOnAfterOnDatabaseRename@1000000006(RecRef@1000000000 : RecordRef;xRecRef@1000000001 : RecordRef);
    VAR
      FileAttachCU@9062234 : Codeunit 9062267;
    BEGIN
      //This needs to call the File attachement Rename function for the renamed record.
      FileAttachCU.RecordRename(RecRef,xRecRef);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDatabaseTriggerDeleteFileAttachment@1000000002(VAR PreventAutoDelete@1000000000 : Boolean;VAR PreventAutoDeleteTableIDFilter@1000000001 : Text);
    BEGIN
      //DOC PC NA2016.11 12/11/2015 - Added to allow developers to prevent File Attachments being automatically deleted
      // - Set "PreventAutoDelete" to TRUE to stop related File Attachments being deleted OnDelete of the Record
      // - Set "PreventAutoDeleteTableIDFilter" to a Filter String containing the ID of the tables to enable this function for (if blank, the function will be enabled for all Tables)
    END;

    BEGIN
    {
      //DOC NA2016.11 BRM/PC 12/11/2015 - Used to handle events from posting documents and moving them posted documents
    }
    END.
  }
}

