OBJECT Codeunit 9021798 WS: Order Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:21 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnRun=VAR
            xmlOut@1000000000 : BigText;
            fileName@1000000001 : Text[100];
            customerRec@1000000002 : Record 18;
          BEGIN
            ApproveQuote('103',xmlOut);
          END;

  }
  CODE
  {

    PROCEDURE ApproveQuote@1000000000(DocumentNo@1000000000 : Code[20];VAR ResponseXml@1000000014 : BigText);
    VAR
      SalesHeader@1000000004 : Record 36;
      "** from Make Order **"@1000000003 : Integer;
      PurchaseHeader@1000000002 : Record 38;
      "___ XML STUFF ___"@1000000015 : Integer;
      errorText@1000000013 : Text[250];
      msgText@1000000012 : Text[250];
      outputXml@1000000011 : BigText;
      xmlDoc@1000000010 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      xmlErrorNode@1000000009 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      xmlMsgNode@1000000008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      xmlNode@1000000007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      xmlRootNode@1000000006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      xmlTextNode@1000000005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TxtNoQuote@1000000001 : TextConst 'ENU=I''m not happy with that there DocumentNo parameter [%1] since there doesn''t seem to be a Quote Document that matches it.;ENG=I''m not happy with that there DocumentNo parameter [%1] since there doesn''t seem to be a Quote Document that matches it.';
      TxtQuoteToOrderError@1000000016 : TextConst 'ENU=Something went wrong trying to convert the Quote to an Order.;ENG=Something went wrong trying to convert the Quote to an Order.';
      TxtQuoteToOrderDone@1000000017 : TextConst 'ENU=Lovely - Quote %1 is approved, and has been converted to an Order.;ENG=Lovely - Quote %1 is approved, and has been converted to an Order.';
    BEGIN

      //prep the xml output doc...
      xmlDoc:=xmlDoc.XmlDocument();

      //add the root node...
      xmlRootNode:=xmlDoc.CreateNode('element','response','');
      xmlDoc.AppendChild(xmlRootNode);

      //add an error node (always present but only populated with a value if there's an error...
      xmlErrorNode:=xmlDoc.CreateNode('element','error','');
      xmlRootNode.AppendChild(xmlErrorNode);
      errorText:='';

      //add an error node (always present but only populated with a value if there's an error...
      xmlMsgNode:=xmlDoc.CreateNode('element','message','');
      xmlRootNode.AppendChild(xmlMsgNode);
      msgText:='';


      IF NOT SalesHeader.GET(0,DocumentNo) THEN BEGIN
        errorText:=STRSUBSTNO(TxtNoQuote, DocumentNo);
      END ELSE BEGIN
        IF NOT CODEUNIT.RUN(CODEUNIT::"Sales-Quote to Order (Yes/No)",SalesHeader) THEN BEGIN
          errorText:=TxtQuoteToOrderError;
        END ELSE BEGIN
          msgText:=STRSUBSTNO(TxtQuoteToOrderDone, DocumentNo);
        END;
      END;


      //add any error text collected along the way...
      xmlTextNode:=xmlDoc.CreateTextNode(errorText);
      xmlErrorNode.AppendChild(xmlTextNode);

      //add any message text collected along the way...
      xmlTextNode:=xmlDoc.CreateTextNode(msgText);
      xmlMsgNode.AppendChild(xmlTextNode);

      //MESSAGE(xmlDoc.InnerXml);

      //stuff the xml into the output...
      ResponseXml.ADDTEXT(xmlDoc.InnerXml);
    END;

    BEGIN
    {
      //DOC EC2009.2 MQ 01/01/2012 - NAV web service POC for TD quote approval & Derm My Docs for NAV2013 based on various research:
      //DOC EC2009.2 MQ 01/01/2012 -   http://blogs.msdn.com/b/freddyk/archive/2008/11/23/search-in-nav-2009-part-1-out-of-3.aspx
      //DOC EC2009.2 MQ 01/01/2012 -   http://gotcal.com/index.php/2010/11/send-dynamics-nav-rtc-reports-and-documents-as-pdf/
      //DOC EC2016.6 JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

