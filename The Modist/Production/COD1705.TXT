OBJECT Codeunit 1705 Exp. Mapping Det Pos. Pay
{
  OBJECT-PROPERTIES
  {
    Date=29/01/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44974;
  }
  PROPERTIES
  {
    TableNo=1220;
    OnRun=VAR
            PositivePayDetail@1010 : Record 1241;
            DataExch@1009 : Record 1220;
            DataExchLineDef@1006 : Record 1227;
            PositivePayExportMgt@1001 : Codeunit 1711;
            RecordRef@1004 : RecordRef;
            Window@1000 : Dialog;
            LineNo@1002 : Integer;
          BEGIN
            Window.OPEN(ProgressMsg);

            // Range through the line types, Look at details...
            LineNo := 1;
            DataExchLineDef.INIT;
            DataExchLineDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
            DataExchLineDef.SETRANGE("Line Type",DataExchLineDef."Line Type"::Detail);
            IF DataExchLineDef.FINDFIRST THEN BEGIN
              PositivePayDetail.INIT;
              PositivePayDetail.SETRANGE("Data Exch. Entry No.","Entry No.");
              IF PositivePayDetail.FINDSET THEN BEGIN
                REPEAT
                  Window.UPDATE(1,LineNo);
                  IF HandlePositivePayDetails(PositivePayDetail) THEN BEGIN
                    DataExch.SETRANGE("Entry No.","Entry No.");
                    IF DataExch.FINDFIRST THEN BEGIN
                      RecordRef.GETTABLE(PositivePayDetail);
                      PositivePayExportMgt.InsertDataExchLineForFlatFile(
                        DataExch,
                        LineNo,
                        RecordRef);
                      LineNo := LineNo + 1;
                    END;
                  END;
                UNTIL  PositivePayDetail.NEXT = 0;
              END;
            END;
            Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      ProgressMsg@1000 : TextConst 'ENU=Processing line no. #1######.;ENG=Processing line no. #1######.';

    LOCAL PROCEDURE HandlePositivePayDetails@1(PositivePayDetail@1000 : Record 1241) : Boolean;
    VAR
      CheckLedgEntry@1001 : Record 272;
    BEGIN
      IF PositivePayDetail.Payee = '' THEN BEGIN
        CheckLedgEntry.SETRANGE("Positive Pay Exported",FALSE);
        CheckLedgEntry.SETRANGE("Data Exch. Voided Entry No.",PositivePayDetail."Data Exch. Entry No.");
        CheckLedgEntry.SETRANGE("Check No.",PositivePayDetail."Check Number");
        IF CheckLedgEntry.FINDLAST THEN
          EXIT(CheckLedgEntry."Entry Status" <> CheckLedgEntry."Entry Status"::"Test Print");
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

