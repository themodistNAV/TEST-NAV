OBJECT Codeunit 408 DimensionManagement
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 80=imd,
                TableData 232=imd,
                TableData 355=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Dimensions %1 and %2 can''t be used concurrently.;ENG=Dimensions %1 and %2 can''t be used concurrently.';
      Text001@1001 : TextConst 'ENU=Dimension combinations %1 - %2 and %3 - %4 can''t be used concurrently.;ENG=Dimension combinations %1 - %2 and %3 - %4 can''t be used concurrently.';
      Text002@1002 : TextConst 'ENU=This Shortcut Dimension is not defined in the %1.;ENG=This Shortcut Dimension is not defined in the %1.';
      Text003@1003 : TextConst 'ENU=%1 is not an available %2 for that dimension.;ENG=%1 is not an available %2 for that dimension.';
      Text004@1004 : TextConst 'ENU=Select a %1 for the %2 %3.;ENG=Select a %1 for the %2 %3.';
      Text005@1005 : TextConst 'ENU=Select a %1 for the %2 %3 for %4 %5.;ENG=Select a %1 for the %2 %3 for %4 %5.';
      Text006@1006 : TextConst 'ENU=Select %1 %2 for the %3 %4.;ENG=Select %1 %2 for the %3 %4.';
      Text007@1007 : TextConst 'ENU=Select %1 %2 for the %3 %4 for %5 %6.;ENG=Select %1 %2 for the %3 %4 for %5 %6.';
      Text008@1008 : TextConst 'ENU=%1 %2 must be blank.;ENG=%1 %2 must be blank.';
      Text009@1009 : TextConst 'ENU=%1 %2 must be blank for %3 %4.;ENG=%1 %2 must be blank for %3 %4.';
      Text010@1010 : TextConst 'ENU=%1 %2 must not be mentioned.;ENG=%1 %2 must not be mentioned.';
      Text011@1011 : TextConst 'ENU=%1 %2 must not be mentioned for %3 %4.;ENG=%1 %2 must not be mentioned for %3 %4.';
      Text012@1012 : TextConst 'ENU=A %1 used in %2 has not been used in %3.;ENG=A %1 used in %2 has not been used in %3.';
      Text013@1013 : TextConst 'ENU=%1 for %2 %3 is not the same in %4 and %5.;ENG=%1 for %2 %3 is not the same in %4 and %5.';
      Text014@1014 : TextConst 'ENU=%1 %2 is blocked.;ENG=%1 %2 is blocked.';
      Text015@1015 : TextConst 'ENU=%1 %2 can''t be found.;ENG=%1 %2 can''t be found.';
      Text016@1016 : TextConst 'ENU=%1 %2 - %3 is blocked.;ENG=%1 %2 - %3 is blocked.';
      Text017@1017 : TextConst 'ENU=%1 for %2 %3 - %4 must not be %5.;ENG=%1 for %2 %3 - %4 must not be %5.';
      Text018@1018 : TextConst 'ENU=%1 for %2 is missing.;ENG=%1 for %2 is missing.';
      Text019@1028 : TextConst 'ENU=You have changed a dimension.\\Do you want to update the lines?;ENG=You have changed a dimension.\\Do you want to update the lines?';
      TempDimBuf1@1019 : TEMPORARY Record 360;
      TempDimBuf2@1020 : TEMPORARY Record 360;
      ObjTransl@1027 : Record 377;
      DimValComb@1032 : Record 351;
      JobTaskDimTemp@1033 : TEMPORARY Record 1002;
      DefaultDim@1035 : Record 352;
      DimSetEntry@1034 : Record 480;
      TempDimSetEntry2@1029 : TEMPORARY Record 480;
      TempDimCombInitialized@1031 : Boolean;
      TempDimCombEmpty@1030 : Boolean;
      DimCombErr@1021 : Text[250];
      DimValuePostingErr@1022 : Text[250];
      DimErr@1023 : Text[250];
      DocDimConsistencyErr@1024 : Text[250];
      HasGotGLSetup@1025 : Boolean;
      GLSetupShortcutDimCode@1026 : ARRAY [8] OF Code[20];
      DimSetFilterCtr@1036 : Integer;
      LastDimSetIDInFilter@1037 : Integer;

    PROCEDURE GetDimensionSetID@123(VAR DimSetEntry2@1000 : Record 480) : Integer;
    BEGIN
      EXIT(DimSetEntry.GetDimensionSetID(DimSetEntry2));
    END;

    PROCEDURE GetDimensionSet@124(VAR TempDimSetEntry@1000 : TEMPORARY Record 480;DimSetID@1001 : Integer);
    VAR
      DimSetEntry2@1002 : Record 480;
    BEGIN
      TempDimSetEntry.DELETEALL;
      WITH DimSetEntry2 DO BEGIN
        SETRANGE("Dimension Set ID",DimSetID);
        IF FINDSET THEN
          REPEAT
            TempDimSetEntry := DimSetEntry2;
            TempDimSetEntry.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE ShowDimensionSet@125(DimSetID@1000 : Integer;NewCaption@1003 : Text[250]);
    VAR
      DimSetEntries@1002 : Page 479;
    BEGIN
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      DimSetEntries.SETTABLEVIEW(DimSetEntry);
      DimSetEntries.SetFormCaption(NewCaption);
      DimSetEntry.RESET;
      DimSetEntries.RUNMODAL;
    END;

    PROCEDURE EditDimensionSet@128(DimSetID@1000 : Integer;NewCaption@1003 : Text[250]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
    BEGIN
      NewDimSetID := DimSetID;
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      EditDimSetEntries.SETTABLEVIEW(DimSetEntry);
      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    PROCEDURE EditDimensionSet2@131(DimSetID@1000 : Integer;NewCaption@1003 : Text[250];VAR GlobalDimVal1@1006 : Code[20];VAR GlobalDimVal2@1005 : Code[20]) : Integer;
    VAR
      EditDimSetEntries@1002 : Page 480;
      NewDimSetID@1004 : Integer;
    BEGIN
      NewDimSetID := DimSetID;
      DimSetEntry.RESET;
      DimSetEntry.FILTERGROUP(2);
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      DimSetEntry.FILTERGROUP(0);
      EditDimSetEntries.SETTABLEVIEW(DimSetEntry);
      EditDimSetEntries.SetFormCaption(NewCaption);
      EditDimSetEntries.RUNMODAL;
      NewDimSetID := EditDimSetEntries.GetDimensionID;
      UpdateGlobalDimFromDimSetID(NewDimSetID,GlobalDimVal1,GlobalDimVal2);
      DimSetEntry.RESET;
      EXIT(NewDimSetID);
    END;

    LOCAL PROCEDURE EditReclasDimensionSet@126(VAR DimSetID@1001 : Integer;VAR NewDimSetID@1002 : Integer;NewCaption@1000 : Text[250]);
    VAR
      EditReclasDimensions@1004 : Page 484;
    BEGIN
      EditReclasDimensions.SetDimensionIDs(DimSetID,NewDimSetID);
      EditReclasDimensions.SetFormCaption(NewCaption);
      EditReclasDimensions.RUNMODAL;
      EditReclasDimensions.GetDimensionIDs(DimSetID,NewDimSetID);
    END;

    PROCEDURE EditReclasDimensionSet2@133(VAR DimSetID@1001 : Integer;VAR NewDimSetID@1002 : Integer;NewCaption@1000 : Text[250];VAR GlobalDimVal1@1005 : Code[20];VAR GlobalDimVal2@1003 : Code[20];VAR NewGlobalDimVal1@1007 : Code[20];VAR NewGlobalDimVal2@1006 : Code[20]);
    VAR
      EditReclasDimensions@1004 : Page 484;
    BEGIN
      EditReclasDimensions.SetDimensionIDs(DimSetID,NewDimSetID);
      EditReclasDimensions.SetFormCaption(NewCaption);
      EditReclasDimensions.RUNMODAL;
      EditReclasDimensions.GetDimensionIDs(DimSetID,NewDimSetID);
      UpdateGlobalDimFromDimSetID(DimSetID,GlobalDimVal1,GlobalDimVal2);
      UpdateGlobalDimFromDimSetID(NewDimSetID,NewGlobalDimVal1,NewGlobalDimVal2);
    END;

    PROCEDURE UpdateGlobalDimFromDimSetID@130(DimSetID@1000 : Integer;VAR GlobalDimVal1@1001 : Code[20];VAR GlobalDimVal2@1002 : Code[20]);
    BEGIN
      GetGLSetup;
      GlobalDimVal1 := '';
      GlobalDimVal2 := '';
      IF GLSetupShortcutDimCode[1] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[1]) THEN
          GlobalDimVal1 := DimSetEntry."Dimension Value Code";
      IF GLSetupShortcutDimCode[2] <> '' THEN
        IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[2]) THEN
          GlobalDimVal2 := DimSetEntry."Dimension Value Code";
    END;

    PROCEDURE GetCombinedDimensionSetID@132(DimensionSetIDArr@1000 : ARRAY [10] OF Integer;VAR GlobalDimVal1@1004 : Code[20];VAR GlobalDimVal2@1005 : Code[20]) : Integer;
    VAR
      TempDimSetEntry@1003 : TEMPORARY Record 480;
      i@1001 : Integer;
    BEGIN
      GetGLSetup;
      GlobalDimVal1 := '';
      GlobalDimVal2 := '';
      DimSetEntry.RESET;
      FOR i := 1 TO 10 DO
        IF DimensionSetIDArr[i] <> 0 THEN BEGIN
          DimSetEntry.SETRANGE("Dimension Set ID",DimensionSetIDArr[i]);
          IF DimSetEntry.FINDSET THEN
            REPEAT
              IF TempDimSetEntry.GET(0,DimSetEntry."Dimension Code") THEN
                TempDimSetEntry.DELETE;
              TempDimSetEntry := DimSetEntry;
              TempDimSetEntry."Dimension Set ID" := 0;
              TempDimSetEntry.INSERT;
              IF GLSetupShortcutDimCode[1] = TempDimSetEntry."Dimension Code" THEN
                GlobalDimVal1 := TempDimSetEntry."Dimension Value Code";
              IF GLSetupShortcutDimCode[2] = TempDimSetEntry."Dimension Code" THEN
                GlobalDimVal2 := TempDimSetEntry."Dimension Value Code";
            UNTIL DimSetEntry.NEXT = 0;
        END;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE GetDeltaDimSetID@137(DimSetID@1005 : Integer;NewParentDimSetID@1000 : Integer;OldParentDimSetID@1001 : Integer) : Integer;
    VAR
      TempDimSetEntry@1004 : TEMPORARY Record 480;
      TempDimSetEntryNew@1003 : TEMPORARY Record 480;
      TempDimSetEntryDeleted@1002 : TEMPORARY Record 480;
    BEGIN
      // Returns an updated DimSetID based on parent's old and new DimSetID
      IF NewParentDimSetID = OldParentDimSetID THEN
        EXIT(DimSetID);
      GetDimensionSet(TempDimSetEntry,DimSetID);
      GetDimensionSet(TempDimSetEntryNew,NewParentDimSetID);
      GetDimensionSet(TempDimSetEntryDeleted,OldParentDimSetID);
      IF TempDimSetEntryDeleted.FINDSET THEN
        REPEAT
          IF TempDimSetEntryNew.GET(NewParentDimSetID,TempDimSetEntryDeleted."Dimension Code") THEN BEGIN
            IF TempDimSetEntryNew."Dimension Value Code" = TempDimSetEntryDeleted."Dimension Value Code" THEN
              TempDimSetEntryNew.DELETE;
            TempDimSetEntryDeleted.DELETE;
          END;
        UNTIL TempDimSetEntryDeleted.NEXT = 0;

      IF TempDimSetEntryDeleted.FINDSET THEN
        REPEAT
          IF TempDimSetEntry.GET(DimSetID,TempDimSetEntryDeleted."Dimension Code") THEN
            TempDimSetEntry.DELETE;
        UNTIL TempDimSetEntryDeleted.NEXT = 0;

      IF TempDimSetEntryNew.FINDSET THEN
        REPEAT
          IF TempDimSetEntry.GET(DimSetID,TempDimSetEntryNew."Dimension Code") THEN BEGIN
            IF TempDimSetEntry."Dimension Value Code" <> TempDimSetEntryNew."Dimension Value Code" THEN BEGIN
              TempDimSetEntry."Dimension Value Code" := TempDimSetEntryNew."Dimension Value Code";
              TempDimSetEntry."Dimension Value ID" := TempDimSetEntryNew."Dimension Value ID";
              TempDimSetEntry.MODIFY;
            END;
          END ELSE BEGIN
            TempDimSetEntry := TempDimSetEntryNew;
            TempDimSetEntry."Dimension Set ID" := DimSetID;
            TempDimSetEntry.INSERT;
          END;
        UNTIL TempDimSetEntryNew.NEXT = 0;

      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      IF NOT HasGotGLSetup THEN BEGIN
        GLSetup.GET;
        GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
        GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
        GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
        GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
        GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
        GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
        GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
        GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";
        HasGotGLSetup := TRUE;
      END;
    END;

    PROCEDURE CheckDimIDComb@138(DimSetID@1002 : Integer) : Boolean;
    BEGIN
      TempDimBuf1.RESET;
      TempDimBuf1.DELETEALL;
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          TempDimBuf1.INIT;
          TempDimBuf1."Table ID" := DATABASE::"Dimension Buffer";
          TempDimBuf1."Entry No." := 0;
          TempDimBuf1."Dimension Code" := DimSetEntry."Dimension Code";
          TempDimBuf1."Dimension Value Code" := DimSetEntry."Dimension Value Code";
          TempDimBuf1.INSERT;
        UNTIL DimSetEntry.NEXT = 0;

      DimSetEntry.RESET;
      EXIT(CheckDimComb);
    END;

    PROCEDURE CheckDimValuePosting@14(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20];DimSetID@1003 : Integer) : Boolean;
    VAR
      i@1004 : Integer;
      j@1005 : Integer;
      NoFilter@1006 : ARRAY [2] OF Text[250];
    BEGIN
      IF NOT CheckBlockedDimAndValues(DimSetID) THEN
        EXIT(FALSE);
      DefaultDim.SETFILTER("Value Posting",'<>%1',DefaultDim."Value Posting"::" ");
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN
              REPEAT
                DimSetEntry.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                CASE DefaultDim."Value Posting" OF
                  DefaultDim."Value Posting"::"Code Mandatory":
                    BEGIN
                      IF NOT DimSetEntry.FINDFIRST OR (DimSetEntry."Dimension Value Code" = '') THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text004,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text005,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),
                              DefaultDim."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                  DefaultDim."Value Posting"::"Same Code":
                    BEGIN
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        IF NOT DimSetEntry.FINDFIRST OR
                           (DefaultDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code")
                        THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text006,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text007,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),
                                DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),
                                DefaultDim."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END ELSE BEGIN
                        IF DimSetEntry.FINDFIRST THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text008,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text009,
                                DimSetEntry.FIELDCAPTION("Dimension Code"),
                                DimSetEntry."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END;
                    END;
                  DefaultDim."Value Posting"::"No Code":
                    BEGIN
                      IF DimSetEntry.FINDFIRST THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text010,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),DimSetEntry."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text011,
                              DimSetEntry.FIELDCAPTION("Dimension Code"),
                              DimSetEntry."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                END;
              UNTIL DefaultDim.NEXT = 0;
          END;
        END;
      END;
      DimSetEntry.RESET;
      EXIT(TRUE);
    END;

    PROCEDURE CheckDimBuffer@64(VAR DimBuffer@1000 : Record 360) : Boolean;
    VAR
      i@1001 : Integer;
    BEGIN
      TempDimBuf1.RESET;
      TempDimBuf1.DELETEALL;
      IF DimBuffer.FINDSET THEN BEGIN
        i := 1;
        REPEAT
          TempDimBuf1.INIT;
          TempDimBuf1."Table ID" := DATABASE::"Dimension Buffer";
          TempDimBuf1."Entry No." := i;
          TempDimBuf1."Dimension Code" := DimBuffer."Dimension Code";
          TempDimBuf1."Dimension Value Code" := DimBuffer."Dimension Value Code";
          TempDimBuf1.INSERT;
          i := i + 1;
        UNTIL DimBuffer.NEXT = 0;
      END;
      EXIT(CheckDimComb);
    END;

    LOCAL PROCEDURE CheckDimComb@3() : Boolean;
    VAR
      DimComb@1000 : Record 350;
      CurrentDimCode@1002 : Code[20];
      CurrentDimValCode@1003 : Code[20];
      DimFilter@1004 : Text[1024];
      FilterTooLong@1005 : Boolean;
    BEGIN
      IF NOT TempDimCombInitialized THEN BEGIN
        TempDimCombInitialized := TRUE;
        IF DimComb.ISEMPTY THEN
          TempDimCombEmpty := TRUE;
      END;

      IF TempDimCombEmpty THEN
        EXIT(TRUE);

      IF NOT TempDimBuf1.FINDSET THEN
        EXIT(TRUE);

      REPEAT
        IF STRLEN(DimFilter) + 1 + STRLEN(TempDimBuf1."Dimension Code") > MAXSTRLEN(DimFilter) THEN
          FilterTooLong := TRUE
        ELSE
          IF DimFilter = '' THEN
            DimFilter := TempDimBuf1."Dimension Code"
          ELSE
            DimFilter := DimFilter + '|' + TempDimBuf1."Dimension Code";
      UNTIL FilterTooLong OR (TempDimBuf1.NEXT = 0);

      IF NOT FilterTooLong THEN BEGIN
        DimComb.SETFILTER("Dimension 1 Code",DimFilter);
        DimComb.SETFILTER("Dimension 2 Code",DimFilter);
        IF DimComb.FINDSET THEN
          REPEAT
            IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
              DimCombErr := STRSUBSTNO(Text000,DimComb."Dimension 1 Code",DimComb."Dimension 2 Code");
              EXIT(FALSE);
            END ELSE BEGIN
              TempDimBuf1.SETRANGE("Dimension Code",DimComb."Dimension 1 Code");
              TempDimBuf1.FINDFIRST;
              CurrentDimCode := TempDimBuf1."Dimension Code";
              CurrentDimValCode := TempDimBuf1."Dimension Value Code";
              TempDimBuf1.SETRANGE("Dimension Code",DimComb."Dimension 2 Code");
              TempDimBuf1.FINDFIRST;
              IF NOT
                 CheckDimValueComb(
                   TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code",
                   CurrentDimCode,CurrentDimValCode)
              THEN
                EXIT(FALSE);
              IF NOT
                 CheckDimValueComb(
                   CurrentDimCode,CurrentDimValCode,
                   TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code")
              THEN
                EXIT(FALSE);
            END;
          UNTIL DimComb.NEXT = 0;
        EXIT(TRUE);
      END;

      WHILE TempDimBuf1.FINDFIRST DO BEGIN
        CurrentDimCode := TempDimBuf1."Dimension Code";
        CurrentDimValCode := TempDimBuf1."Dimension Value Code";
        TempDimBuf1.DELETE;
        IF TempDimBuf1.FINDSET THEN
          REPEAT
            IF CurrentDimCode > TempDimBuf1."Dimension Code" THEN BEGIN
              IF DimComb.GET(TempDimBuf1."Dimension Code",CurrentDimCode) THEN BEGIN
                IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
                  DimCombErr :=
                    STRSUBSTNO(
                      Text000,
                      TempDimBuf1."Dimension Code",CurrentDimCode);
                  EXIT(FALSE);
                END;
                IF NOT
                   CheckDimValueComb(
                     TempDimBuf1."Dimension Code",TempDimBuf1."Dimension Value Code",
                     CurrentDimCode,CurrentDimValCode)
                THEN
                  EXIT(FALSE);
              END;
            END ELSE BEGIN
              IF DimComb.GET(CurrentDimCode,TempDimBuf1."Dimension Code") THEN BEGIN
                IF DimComb."Combination Restriction" = DimComb."Combination Restriction"::Blocked THEN BEGIN
                  DimCombErr :=
                    STRSUBSTNO(
                      Text000,
                      CurrentDimCode,TempDimBuf1."Dimension Code");
                  EXIT(FALSE);
                END;
                IF NOT
                   CheckDimValueComb(
                     CurrentDimCode,CurrentDimValCode,TempDimBuf1."Dimension Code",
                     TempDimBuf1."Dimension Value Code")
                THEN
                  EXIT(FALSE);
              END;
            END;
          UNTIL TempDimBuf1.NEXT = 0;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckDimValueComb@9(Dim1@1000 : Code[20];Dim1Value@1001 : Code[20];Dim2@1002 : Code[20];Dim2Value@1003 : Code[20]) : Boolean;
    BEGIN
      IF DimValComb.GET(Dim1,Dim1Value,Dim2,Dim2Value) THEN BEGIN
        DimCombErr :=
          STRSUBSTNO(Text001,
            Dim1,Dim1Value,Dim2,Dim2Value);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimCombErr@41() : Text[250];
    BEGIN
      EXIT(DimCombErr);
    END;

    PROCEDURE UpdateDefaultDim@59(TableID@1000 : Integer;No@1001 : Code[20];VAR GlobalDim1Code@1002 : Code[20];VAR GlobalDim2Code@1003 : Code[20]);
    VAR
      DefaultDim@1004 : Record 352;
    BEGIN
      GetGLSetup;
      IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[1]) THEN
        GlobalDim1Code := DefaultDim."Dimension Value Code"
      ELSE
        GlobalDim1Code := '';
      IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[2]) THEN
        GlobalDim2Code := DefaultDim."Dimension Value Code"
      ELSE
        GlobalDim2Code := '';
    END;

    PROCEDURE GetDefaultDimID@8(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20];SourceCode@1002 : Code[20];VAR GlobalDim1Code@1003 : Code[20];VAR GlobalDim2Code@1004 : Code[20];InheritFromDimSetID@1014 : Integer;InheritFromTableNo@1015 : Integer) : Integer;
    VAR
      DimVal@1012 : Record 349;
      DefaultDimPriority1@1005 : Record 354;
      DefaultDimPriority2@1006 : Record 354;
      DefaultDim@1007 : Record 352;
      TempDimSetEntry@1011 : TEMPORARY Record 480;
      TempDimSetEntry0@1016 : TEMPORARY Record 480;
      i@1010 : Integer;
      j@1009 : Integer;
      NoFilter@1008 : ARRAY [2] OF Code[20];
      NewDimSetID@1013 : Integer;
    BEGIN
      GetGLSetup;
      IF InheritFromDimSetID > 0 THEN
        GetDimensionSet(TempDimSetEntry0,InheritFromDimSetID);
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
      IF TempDimSetEntry0.FINDSET THEN
        REPEAT
          TempDimBuf2.INIT;
          TempDimBuf2."Table ID" := InheritFromTableNo;
          TempDimBuf2."Entry No." := 0;
          TempDimBuf2."Dimension Code" := TempDimSetEntry0."Dimension Code";
          TempDimBuf2."Dimension Value Code" := TempDimSetEntry0."Dimension Value Code";
          TempDimBuf2.INSERT;
        UNTIL TempDimSetEntry0.NEXT = 0;

      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN
              REPEAT
                IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                  TempDimBuf2.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                  IF NOT TempDimBuf2.FINDFIRST THEN BEGIN
                    TempDimBuf2.INIT;
                    TempDimBuf2."Table ID" := DefaultDim."Table ID";
                    TempDimBuf2."Entry No." := 0;
                    TempDimBuf2."Dimension Code" := DefaultDim."Dimension Code";
                    TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                    TempDimBuf2.INSERT;
                  END ELSE BEGIN
                    IF DefaultDimPriority1.GET(SourceCode,DefaultDim."Table ID") THEN BEGIN
                      IF DefaultDimPriority2.GET(SourceCode,TempDimBuf2."Table ID") THEN BEGIN
                        IF DefaultDimPriority1.Priority < DefaultDimPriority2.Priority THEN BEGIN
                          TempDimBuf2.DELETE;
                          TempDimBuf2."Table ID" := DefaultDim."Table ID";
                          TempDimBuf2."Entry No." := 0;
                          TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                          TempDimBuf2.INSERT;
                        END;
                      END ELSE BEGIN
                        TempDimBuf2.DELETE;
                        TempDimBuf2."Table ID" := DefaultDim."Table ID";
                        TempDimBuf2."Entry No." := 0;
                        TempDimBuf2."Dimension Value Code" := DefaultDim."Dimension Value Code";
                        TempDimBuf2.INSERT;
                      END;
                    END;
                  END;
                  IF GLSetupShortcutDimCode[1] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim1Code := TempDimBuf2."Dimension Value Code";
                  IF GLSetupShortcutDimCode[2] = TempDimBuf2."Dimension Code" THEN
                    GlobalDim2Code := TempDimBuf2."Dimension Value Code";
                END;
              UNTIL DefaultDim.NEXT = 0;
          END;
        END;
      END;
      TempDimBuf2.RESET;
      IF TempDimBuf2.FINDSET THEN BEGIN
        REPEAT
          DimVal.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code");
          TempDimSetEntry."Dimension Code" := TempDimBuf2."Dimension Code";
          TempDimSetEntry."Dimension Value Code" := TempDimBuf2."Dimension Value Code";
          TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
          TempDimSetEntry.INSERT;
        UNTIL TempDimBuf2.NEXT = 0;
        NewDimSetID := GetDimensionSetID(TempDimSetEntry);
      END;
      EXIT(NewDimSetID);
    END;

    PROCEDURE TypeToTableID1@11(Type@1000 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner') : Integer;
    BEGIN
      CASE Type OF
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
        Type::Customer:
          EXIT(DATABASE::Customer);
        Type::Vendor:
          EXIT(DATABASE::Vendor);
        Type::"Bank Account":
          EXIT(DATABASE::"Bank Account");
        Type::"Fixed Asset":
          EXIT(DATABASE::"Fixed Asset");
        Type::"IC Partner":
          EXIT(DATABASE::"IC Partner");
      END;
    END;

    PROCEDURE TypeToTableID2@13(Type@1000 : 'Resource,Item,G/L Account') : Integer;
    BEGIN
      CASE Type OF
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
      END;
    END;

    PROCEDURE TypeToTableID3@16(Type@1000 : ' ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::"Fixed Asset":
          EXIT(DATABASE::"Fixed Asset");
        Type::"Charge (Item)":
          EXIT(DATABASE::"Item Charge");
      END;
    END;

    PROCEDURE TypeToTableID4@1(Type@1000 : ' ,Item,Resource') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
      END;
    END;

    PROCEDURE TypeToTableID5@119(Type@1000 : ' ,Item,Resource,Cost,G/L Account') : Integer;
    BEGIN
      CASE Type OF
        Type::" ":
          EXIT(0);
        Type::Item:
          EXIT(DATABASE::Item);
        Type::Resource:
          EXIT(DATABASE::Resource);
        Type::Cost:
          EXIT(DATABASE::"Service Cost");
        Type::"G/L Account":
          EXIT(DATABASE::"G/L Account");
      END;
    END;

    PROCEDURE DeleteDefaultDim@58(TableID@1000 : Integer;No@1001 : Code[20]);
    VAR
      DefaultDim@1002 : Record 352;
    BEGIN
      DefaultDim.SETRANGE("Table ID",TableID);
      DefaultDim.SETRANGE("No.",No);
      IF NOT DefaultDim.ISEMPTY THEN
        DefaultDim.DELETEALL;
    END;

    PROCEDURE LookupDimValueCode@21(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimVal@1002 : Record 349;
      GLSetup@1003 : Record 98;
    BEGIN
      GetGLSetup;
      IF GLSetupShortcutDimCode[FieldNumber] = '' THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      DimVal."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
      DimVal.Code := ShortcutDimCode;
      IF PAGE.RUNMODAL(0,DimVal) = ACTION::LookupOK THEN BEGIN
        CheckDim(DimVal."Dimension Code");
        CheckDimValue(DimVal."Dimension Code",DimVal.Code);
        ShortcutDimCode := DimVal.Code;
      END;
    END;

    PROCEDURE ValidateDimValueCode@22(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimVal@1002 : Record 349;
      GLSetup@1003 : Record 98;
    BEGIN
      GetGLSetup;
      IF (GLSetupShortcutDimCode[FieldNumber] = '') AND (ShortcutDimCode <> '') THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      IF ShortcutDimCode <> '' THEN BEGIN
        DimVal.SETRANGE(Code,ShortcutDimCode);
        IF NOT DimVal.FINDFIRST THEN BEGIN
          DimVal.SETFILTER(Code,STRSUBSTNO('%1*',ShortcutDimCode));
          IF DimVal.FINDFIRST THEN
            ShortcutDimCode := DimVal.Code
          ELSE
            ERROR(
              STRSUBSTNO(Text003,
                ShortcutDimCode,DimVal.FIELDCAPTION(Code)));
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimValues@127(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20];VAR DimSetID@1004 : Integer);
    VAR
      DimVal@1002 : Record 349;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimVal."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
      IF ShortcutDimCode <> '' THEN BEGIN
        DimVal.GET(DimVal."Dimension Code",ShortcutDimCode);
        IF NOT CheckDim(DimVal."Dimension Code") THEN
          ERROR(GetDimErr);
        IF NOT CheckDimValue(DimVal."Dimension Code",ShortcutDimCode) THEN
          ERROR(GetDimErr);
      END;
      GetDimensionSet(TempDimSetEntry,DimSetID);
      IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimVal."Dimension Code") THEN
        IF TempDimSetEntry."Dimension Value Code" <> ShortcutDimCode THEN
          TempDimSetEntry.DELETE;
      IF ShortcutDimCode <> '' THEN BEGIN
        TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
        TempDimSetEntry."Dimension Value Code" := DimVal.Code;
        TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
        IF TempDimSetEntry.INSERT THEN;
      END;
      DimSetID := GetDimensionSetID(TempDimSetEntry);
    END;

    PROCEDURE SaveDefaultDim@53(TableID@1000 : Integer;No@1001 : Code[20];FieldNumber@1002 : Integer;ShortcutDimCode@1003 : Code[20]);
    VAR
      DefaultDim@1004 : Record 352;
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          DefaultDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          DefaultDim.MODIFY;
        END ELSE BEGIN
          DefaultDim.INIT;
          DefaultDim.VALIDATE("Table ID",TableID);
          DefaultDim.VALIDATE("No.",No);
          DefaultDim.VALIDATE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
          DefaultDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          DefaultDim.INSERT;
        END;
      END ELSE
        IF DefaultDim.GET(TableID,No,GLSetupShortcutDimCode[FieldNumber]) THEN
          DefaultDim.DELETE;
    END;

    PROCEDURE GetShortcutDimensions@129(DimSetID@1000 : Integer;VAR ShortcutDimCode@1004 : ARRAY [8] OF Code[20]);
    VAR
      i@1006 : Integer;
    BEGIN
      GetGLSetup;
      FOR i := 3 TO 8 DO BEGIN
        ShortcutDimCode[i] := '';
        IF GLSetupShortcutDimCode[i] <> '' THEN
          IF DimSetEntry.GET(DimSetID,GLSetupShortcutDimCode[i]) THEN
            ShortcutDimCode[i] := DimSetEntry."Dimension Value Code";
      END;
    END;

    PROCEDURE CheckDimBufferValuePosting@68(VAR DimBuffer@1000 : Record 360;TableID@1001 : ARRAY [10] OF Integer;No@1002 : ARRAY [10] OF Code[20]) : Boolean;
    VAR
      i@1005 : Integer;
    BEGIN
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
      IF DimBuffer.FINDSET THEN BEGIN
        i := 1;
        REPEAT
          IF (NOT CheckDimValue(
                DimBuffer."Dimension Code",DimBuffer."Dimension Value Code")) OR
             (NOT CheckDim(DimBuffer."Dimension Code"))
          THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;
          TempDimBuf2.INIT;
          TempDimBuf2."Entry No." := i;
          TempDimBuf2."Dimension Code" := DimBuffer."Dimension Code";
          TempDimBuf2."Dimension Value Code" := DimBuffer."Dimension Value Code";
          TempDimBuf2.INSERT;
          i := i + 1;
        UNTIL DimBuffer.NEXT = 0;
      END;
      EXIT(CheckValuePosting(TableID,No));
    END;

    LOCAL PROCEDURE CheckValuePosting@36(TableID@1000 : ARRAY [10] OF Integer;No@1001 : ARRAY [10] OF Code[20]) : Boolean;
    VAR
      DefaultDim@1002 : Record 352;
      i@1004 : Integer;
      j@1005 : Integer;
      NoFilter@1006 : ARRAY [2] OF Text[250];
    BEGIN
      DefaultDim.SETFILTER("Value Posting",'<>%1',DefaultDim."Value Posting"::" ");
      NoFilter[2] := '';
      FOR i := 1 TO ARRAYLEN(TableID) DO BEGIN
        IF (TableID[i] <> 0) AND (No[i] <> '') THEN BEGIN
          DefaultDim.SETRANGE("Table ID",TableID[i]);
          NoFilter[1] := No[i];
          FOR j := 1 TO 2 DO BEGIN
            DefaultDim.SETRANGE("No.",NoFilter[j]);
            IF DefaultDim.FINDSET THEN BEGIN
              REPEAT
                TempDimBuf2.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
                CASE DefaultDim."Value Posting" OF
                  DefaultDim."Value Posting"::"Code Mandatory":
                    BEGIN
                      IF (NOT TempDimBuf2.FINDFIRST) OR
                         (TempDimBuf2."Dimension Value Code" = '')
                      THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text004,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text005,
                              DefaultDim.FIELDCAPTION("Dimension Value Code"),
                              DefaultDim.FIELDCAPTION("Dimension Code"),
                              DefaultDim."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                  DefaultDim."Value Posting"::"Same Code":
                    BEGIN
                      IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
                        IF (NOT TempDimBuf2.FINDFIRST) OR
                           (DefaultDim."Dimension Value Code" <> TempDimBuf2."Dimension Value Code")
                        THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text006,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),DefaultDim."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text007,
                                DefaultDim.FIELDCAPTION("Dimension Value Code"),
                                DefaultDim."Dimension Value Code",
                                DefaultDim.FIELDCAPTION("Dimension Code"),
                                DefaultDim."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END ELSE BEGIN
                        IF TempDimBuf2.FINDFIRST THEN BEGIN
                          IF DefaultDim."No." = '' THEN
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text008,
                                TempDimBuf2.FIELDCAPTION("Dimension Code"),TempDimBuf2."Dimension Code")
                          ELSE
                            DimValuePostingErr :=
                              STRSUBSTNO(
                                Text009,
                                TempDimBuf2.FIELDCAPTION("Dimension Code"),
                                TempDimBuf2."Dimension Code",
                                ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                                DefaultDim."No.");
                          EXIT(FALSE);
                        END;
                      END;
                    END;
                  DefaultDim."Value Posting"::"No Code":
                    BEGIN
                      IF TempDimBuf2.FINDFIRST THEN BEGIN
                        IF DefaultDim."No." = '' THEN
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text010,
                              TempDimBuf2.FIELDCAPTION("Dimension Code"),TempDimBuf2."Dimension Code")
                        ELSE
                          DimValuePostingErr :=
                            STRSUBSTNO(
                              Text011,
                              TempDimBuf2.FIELDCAPTION("Dimension Code"),
                              TempDimBuf2."Dimension Code",
                              ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DefaultDim."Table ID"),
                              DefaultDim."No.");
                        EXIT(FALSE);
                      END;
                    END;
                END;
              UNTIL DefaultDim.NEXT = 0;
              TempDimBuf2.RESET;
            END;
          END;
        END;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimValuePostingErr@98() : Text[250];
    BEGIN
      EXIT(DimValuePostingErr);
    END;

    PROCEDURE SetupObjectNoList@40(VAR TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058);
    BEGIN
      InsertObject(TempAllObjWithCaption,DATABASE::"Salesperson/Purchaser");
      InsertObject(TempAllObjWithCaption,DATABASE::"G/L Account");
      InsertObject(TempAllObjWithCaption,DATABASE::Customer);
      InsertObject(TempAllObjWithCaption,DATABASE::Vendor);
      InsertObject(TempAllObjWithCaption,DATABASE::Item);
      InsertObject(TempAllObjWithCaption,DATABASE::"Resource Group");
      InsertObject(TempAllObjWithCaption,DATABASE::Resource);
      InsertObject(TempAllObjWithCaption,DATABASE::Job);
      InsertObject(TempAllObjWithCaption,DATABASE::"Bank Account");
      InsertObject(TempAllObjWithCaption,DATABASE::Campaign);
      InsertObject(TempAllObjWithCaption,DATABASE::Employee);
      InsertObject(TempAllObjWithCaption,DATABASE::"Fixed Asset");
      InsertObject(TempAllObjWithCaption,DATABASE::Insurance);
      InsertObject(TempAllObjWithCaption,DATABASE::"Responsibility Center");
      InsertObject(TempAllObjWithCaption,DATABASE::"Item Charge");
      InsertObject(TempAllObjWithCaption,DATABASE::"Work Center");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Contract Header");
      InsertObject(TempAllObjWithCaption,DATABASE::"Customer Template");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Contract Template");
      InsertObject(TempAllObjWithCaption,DATABASE::"IC Partner");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Order Type");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Item Group");
      InsertObject(TempAllObjWithCaption,DATABASE::"Service Item");
      InsertObject(TempAllObjWithCaption,DATABASE::"Cash Flow Manual Expense");
      InsertObject(TempAllObjWithCaption,DATABASE::"Cash Flow Manual Revenue");
    END;

    PROCEDURE GetDocDimConsistencyErr@56() : Text[250];
    BEGIN
      EXIT(DocDimConsistencyErr);
    END;

    PROCEDURE CheckDim@60(DimCode@1000 : Code[20]) : Boolean;
    VAR
      Dim@1001 : Record 348;
    BEGIN
      IF Dim.GET(DimCode) THEN BEGIN
        IF Dim.Blocked THEN BEGIN
          DimErr :=
            STRSUBSTNO(Text014,Dim.TABLECAPTION,DimCode);
          EXIT(FALSE);
        END;
      END ELSE BEGIN
        DimErr :=
          STRSUBSTNO(Text015,Dim.TABLECAPTION,DimCode);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CheckDimValue@61(DimCode@1000 : Code[20];DimValCode@1001 : Code[20]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      IF (DimCode <> '') AND (DimValCode <> '') THEN BEGIN
        IF DimVal.GET(DimCode,DimValCode) THEN BEGIN
          IF DimVal.Blocked THEN BEGIN
            DimErr :=
              STRSUBSTNO(
                Text016,DimVal.TABLECAPTION,DimCode,DimValCode);
            EXIT(FALSE);
          END;
          IF NOT (DimVal."Dimension Value Type" IN
                  [DimVal."Dimension Value Type"::Standard,
                   DimVal."Dimension Value Type"::"Begin-Total"])
          THEN BEGIN
            DimErr :=
              STRSUBSTNO(Text017,DimVal.FIELDCAPTION("Dimension Value Type"),
                DimVal.TABLECAPTION,DimCode,DimValCode,FORMAT(DimVal."Dimension Value Type"));
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          DimErr :=
            STRSUBSTNO(
              Text018,DimVal.TABLECAPTION,DimCode);
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckBlockedDimAndValues@6(DimSetID@1000 : Integer) : Boolean;
    VAR
      DimSetEntry@1001 : Record 480;
    BEGIN
      IF DimSetID = 0 THEN
        EXIT(TRUE);
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF NOT CheckDim(DimSetEntry."Dimension Code") OR
             NOT CheckDimValue(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code")
          THEN BEGIN
            DimValuePostingErr := DimErr;
            EXIT(FALSE);
          END;
        UNTIL DimSetEntry.NEXT = 0;
      EXIT(TRUE);
    END;

    PROCEDURE GetDimErr@71() : Text[250];
    BEGIN
      EXIT(DimErr);
    END;

    PROCEDURE LookupDimValueCodeNoUpdate@20(FieldNumber@1000 : Integer);
    VAR
      DimVal@1001 : Record 349;
      GLSetup@1002 : Record 98;
    BEGIN
      GetGLSetup;
      IF GLSetupShortcutDimCode[FieldNumber] = '' THEN
        ERROR(Text002,GLSetup.TABLECAPTION);
      DimVal.SETRANGE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
      IF PAGE.RUNMODAL(0,DimVal) = ACTION::LookupOK THEN;
    END;

    LOCAL PROCEDURE GlobalDimNo@2(DimensionCode@1001 : Code[20]) : Integer;
    VAR
      Index@1000 : Integer;
    BEGIN
      GetGLSetup;

      FOR Index := 1 TO ARRAYLEN(GLSetupShortcutDimCode) DO
        IF GLSetupShortcutDimCode[Index] = DimensionCode THEN
          EXIT(Index);

      EXIT(0);
    END;

    PROCEDURE CopyJnlLineDimToICJnlDim@93(TableID@1000 : Integer;TransactionNo@1001 : Integer;PartnerCode@1002 : Code[20];TransactionSource@1008 : 'Rejected,Created';LineNo@1003 : Integer;DimSetID@1004 : Integer);
    VAR
      InOutBoxJnlLineDim@1005 : Record 423;
      DimSetEntry@1009 : Record 480;
      ICDim@1006 : Code[20];
      ICDimValue@1007 : Code[20];
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          ICDim := ConvertDimtoICDim(DimSetEntry."Dimension Code");
          ICDimValue := ConvertDimValuetoICDimVal(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
          IF (ICDim <> '') AND (ICDimValue <> '') THEN BEGIN
            InOutBoxJnlLineDim."Table ID" := TableID;
            InOutBoxJnlLineDim."IC Partner Code" := PartnerCode;
            InOutBoxJnlLineDim."Transaction No." := TransactionNo;
            InOutBoxJnlLineDim."Transaction Source" := TransactionSource;
            InOutBoxJnlLineDim."Line No." := LineNo;
            InOutBoxJnlLineDim."Dimension Code" := ICDim;
            InOutBoxJnlLineDim."Dimension Value Code" := ICDimValue;
            InOutBoxJnlLineDim.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE DefaultDimOnInsert@17(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,FALSE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnInsert);
    END;

    PROCEDURE DefaultDimOnModify@18(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,FALSE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnModify);
    END;

    PROCEDURE DefaultDimOnDelete@19(DefaultDimension@1000 : Record 352);
    VAR
      CallingTrigger@1001 : 'OnInsert,OnModify,OnDelete';
    BEGIN
      IF DefaultDimension."Table ID" = DATABASE::Job THEN
        UpdateJobTaskDim(DefaultDimension,TRUE);

      UpdateCostType(DefaultDimension,CallingTrigger::OnDelete);
    END;

    PROCEDURE CopyICJnlDimToICJnlDim@97(VAR FromInOutBoxLineDim@1001 : Record 423;VAR ToInOutBoxlineDim@1000 : Record 423);
    BEGIN
      IF FromInOutBoxLineDim.FINDSET THEN
        REPEAT
          ToInOutBoxlineDim := FromInOutBoxLineDim;
          ToInOutBoxlineDim.INSERT;
        UNTIL FromInOutBoxLineDim.NEXT = 0;
    END;

    PROCEDURE CopyDocDimtoICDocDim@107(TableID@1005 : Integer;TransactionNo@1004 : Integer;PartnerCode@1003 : Code[20];TransactionSource@1002 : 'Rejected,Created';LineNo@1001 : Integer;DimSetEntryID@1000 : Integer);
    VAR
      InOutBoxDocDim@1008 : Record 442;
      DimSetEntry@1009 : Record 480;
      ICDim@1007 : Code[20];
      ICDimValue@1006 : Code[20];
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetEntryID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          ICDim := ConvertDimtoICDim(DimSetEntry."Dimension Code");
          ICDimValue := ConvertDimValuetoICDimVal(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
          IF (ICDim <> '') AND (ICDimValue <> '') THEN BEGIN
            InOutBoxDocDim."Table ID" := TableID;
            InOutBoxDocDim."IC Partner Code" := PartnerCode;
            InOutBoxDocDim."Transaction No." := TransactionNo;
            InOutBoxDocDim."Transaction Source" := TransactionSource;
            InOutBoxDocDim."Line No." := LineNo;
            InOutBoxDocDim."Dimension Code" := ICDim;
            InOutBoxDocDim."Dimension Value Code" := ICDimValue;
            InOutBoxDocDim.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE CopyICDocDimtoICDocDim@108(FromSourceICDocDim@1003 : Record 442;VAR ToSourceICDocDim@1002 : Record 442;ToTableID@1001 : Integer;ToTransactionSource@1000 : Integer);
    BEGIN
      WITH FromSourceICDocDim DO BEGIN
        SetICDocDimFilters(FromSourceICDocDim,"Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.");
        IF FINDSET THEN
          REPEAT
            ToSourceICDocDim := FromSourceICDocDim;
            ToSourceICDocDim."Table ID" := ToTableID;
            ToSourceICDocDim."Transaction Source" := ToTransactionSource;
            ToSourceICDocDim.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE MoveICDocDimtoICDocDim@112(FromSourceICDocDim@1003 : Record 442;VAR ToSourceICDocDim@1002 : Record 442;ToTableID@1001 : Integer;ToTransactionSource@1000 : Integer);
    BEGIN
      WITH FromSourceICDocDim DO BEGIN
        SetICDocDimFilters(FromSourceICDocDim,"Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.");
        IF FINDSET THEN
          REPEAT
            ToSourceICDocDim := FromSourceICDocDim;
            ToSourceICDocDim."Table ID" := ToTableID;
            ToSourceICDocDim."Transaction Source" := ToTransactionSource;
            ToSourceICDocDim.INSERT;
            DELETE;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE SetICDocDimFilters@110(VAR ICDocDim@1000 : Record 442;TableID@1001 : Integer;TransactionNo@1002 : Integer;PartnerCode@1003 : Code[20];TransactionSource@1004 : Integer;LineNo@1005 : Integer);
    BEGIN
      ICDocDim.RESET;
      ICDocDim.SETRANGE("Table ID",TableID);
      ICDocDim.SETRANGE("Transaction No.",TransactionNo);
      ICDocDim.SETRANGE("IC Partner Code",PartnerCode);
      ICDocDim.SETRANGE("Transaction Source",TransactionSource);
      ICDocDim.SETRANGE("Line No.",LineNo);
    END;

    PROCEDURE DeleteICDocDim@109("Table ID"@1000 : Integer;"IC Transaction No."@1001 : Integer;"IC Partner Code"@1002 : Code[20];"Transaction Source"@1003 : 'Rejected,Created';LineNo@1005 : Integer);
    VAR
      ICDocDim@1004 : Record 442;
    BEGIN
      SetICDocDimFilters(ICDocDim,"Table ID","IC Transaction No.","IC Partner Code","Transaction Source",LineNo);
      IF NOT ICDocDim.ISEMPTY THEN
        ICDocDim.DELETEALL;
    END;

    PROCEDURE DeleteICJnlDim@116("Table ID"@1000 : Integer;"IC Transaction No."@1001 : Integer;"IC Partner Code"@1002 : Code[20];"Transaction Source"@1003 : 'Rejected,Created';LineNo@1005 : Integer);
    VAR
      ICJnlDim@1004 : Record 423;
    BEGIN
      ICJnlDim.SETRANGE("Table ID","Table ID");
      ICJnlDim.SETRANGE("Transaction No.","IC Transaction No.");
      ICJnlDim.SETRANGE("IC Partner Code","IC Partner Code");
      ICJnlDim.SETRANGE("Transaction Source","Transaction Source");
      ICJnlDim.SETRANGE("Line No.",LineNo);
      IF NOT ICJnlDim.ISEMPTY THEN
        ICJnlDim.DELETEALL;
    END;

    LOCAL PROCEDURE ConvertICDimtoDim@99(FromICDim@1000 : Code[20]) DimCode : Code[20];
    VAR
      ICDim@1002 : Record 411;
    BEGIN
      IF ICDim.GET(FromICDim) THEN
        DimCode := ICDim."Map-to Dimension Code";
    END;

    LOCAL PROCEDURE ConvertICDimValuetoDimValue@100(FromICDim@1000 : Code[20];FromICDimValue@1001 : Code[20]) DimValueCode : Code[20];
    VAR
      ICDimValue@1002 : Record 412;
    BEGIN
      IF ICDimValue.GET(FromICDim,FromICDimValue) THEN
        DimValueCode := ICDimValue."Map-to Dimension Value Code";
    END;

    PROCEDURE ConvertDimtoICDim@102(FromDim@1001 : Code[20]) ICDimCode : Code[20];
    VAR
      Dim@1000 : Record 348;
    BEGIN
      IF Dim.GET(FromDim) THEN
        ICDimCode := Dim."Map-to IC Dimension Code";
    END;

    PROCEDURE ConvertDimValuetoICDimVal@103(FromDim@1000 : Code[20];FromDimValue@1001 : Code[20]) ICDimValueCode : Code[20];
    VAR
      DimValue@1002 : Record 349;
    BEGIN
      IF DimValue.GET(FromDim,FromDimValue) THEN
        ICDimValueCode := DimValue."Map-to IC Dimension Value Code";
    END;

    PROCEDURE CheckICDimValue@113(ICDimCode@1001 : Code[20];ICDimValCode@1000 : Code[20]) : Boolean;
    VAR
      ICDimVal@1002 : Record 412;
    BEGIN
      IF (ICDimCode <> '') AND (ICDimValCode <> '') THEN BEGIN
        IF ICDimVal.GET(ICDimCode,ICDimValCode) THEN BEGIN
          IF ICDimVal.Blocked THEN BEGIN
            DimErr :=
              STRSUBSTNO(
                Text016,ICDimVal.TABLECAPTION,ICDimCode,ICDimValCode);
            EXIT(FALSE);
          END;
          IF NOT (ICDimVal."Dimension Value Type" IN
                  [ICDimVal."Dimension Value Type"::Standard,
                   ICDimVal."Dimension Value Type"::"Begin-Total"])
          THEN BEGIN
            DimErr :=
              STRSUBSTNO(Text017,ICDimVal.FIELDCAPTION("Dimension Value Type"),
                ICDimVal.TABLECAPTION,ICDimCode,ICDimValCode,FORMAT(ICDimVal."Dimension Value Type"));
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          DimErr :=
            STRSUBSTNO(
              Text018,ICDimVal.TABLECAPTION,ICDimCode);
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CheckICDim@114(ICDimCode@1000 : Code[20]) : Boolean;
    VAR
      ICDim@1001 : Record 411;
    BEGIN
      IF ICDim.GET(ICDimCode) THEN BEGIN
        IF ICDim.Blocked THEN BEGIN
          DimErr :=
            STRSUBSTNO(Text014,ICDim.TABLECAPTION,ICDimCode);
          EXIT(FALSE);
        END;
      END ELSE BEGIN
        DimErr :=
          STRSUBSTNO(Text015,ICDim.TABLECAPTION,ICDimCode);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE SaveJobTaskDim@39("Job No."@1000 : Code[20];"Job Task No."@1001 : Code[20];FieldNumber@1003 : Integer;ShortcutDimCode@1002 : Code[20]);
    VAR
      JobTaskDim@1004 : Record 1002;
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF JobTaskDim.GET("Job No.","Job Task No.",GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          JobTaskDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          JobTaskDim.MODIFY;
        END ELSE BEGIN
          JobTaskDim.INIT;
          JobTaskDim.VALIDATE("Job No.","Job No.");
          JobTaskDim.VALIDATE("Job Task No.","Job Task No.");
          JobTaskDim.VALIDATE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
          JobTaskDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          JobTaskDim.INSERT;
        END;
      END ELSE
        IF JobTaskDim.GET("Job No.","Job Task No.",GLSetupShortcutDimCode[FieldNumber]) THEN
          JobTaskDim.DELETE;
    END;

    PROCEDURE SaveJobTaskTempDim@46(FieldNumber@1001 : Integer;ShortcutDimCode@1000 : Code[20]);
    BEGIN
      GetGLSetup;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF JobTaskDimTemp.GET('','',GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          JobTaskDimTemp."Dimension Value Code" := ShortcutDimCode;
          JobTaskDimTemp.MODIFY;
        END ELSE BEGIN
          JobTaskDimTemp.INIT;
          JobTaskDimTemp."Dimension Code" := GLSetupShortcutDimCode[FieldNumber];
          JobTaskDimTemp."Dimension Value Code" := ShortcutDimCode;
          JobTaskDimTemp.INSERT;
        END;
      END ELSE
        IF JobTaskDimTemp.GET('','',GLSetupShortcutDimCode[FieldNumber]) THEN
          JobTaskDimTemp.DELETE;
    END;

    PROCEDURE InsertJobTaskDim@54("Job No."@1000 : Code[20];"Job Task No."@1001 : Code[20];VAR GlobalDim1Code@1005 : Code[20];VAR GlobalDim2Code@1004 : Code[20]);
    VAR
      DefaultDim@1002 : Record 352;
      JobTaskDim@1003 : Record 1002;
    BEGIN
      GetGLSetup;
      DefaultDim.SETRANGE("Table ID",DATABASE::Job);
      DefaultDim.SETRANGE("No.","Job No.");
      IF DefaultDim.FINDSET(FALSE,FALSE) THEN
        REPEAT
          IF DefaultDim."Dimension Value Code" <> '' THEN BEGIN
            JobTaskDim.INIT;
            JobTaskDim."Job No." := "Job No.";
            JobTaskDim."Job Task No." := "Job Task No.";
            JobTaskDim."Dimension Code" := DefaultDim."Dimension Code";
            JobTaskDim."Dimension Value Code" := DefaultDim."Dimension Value Code";
            JobTaskDim.INSERT;
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[1] THEN
              GlobalDim1Code := JobTaskDim."Dimension Value Code";
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[2] THEN
              GlobalDim2Code := JobTaskDim."Dimension Value Code";
          END;
        UNTIL DefaultDim.NEXT = 0;

      JobTaskDimTemp.RESET;
      IF JobTaskDimTemp.FINDSET THEN
        REPEAT
          IF NOT JobTaskDim.GET("Job No.","Job Task No.",JobTaskDimTemp."Dimension Code") THEN BEGIN
            JobTaskDim.INIT;
            JobTaskDim."Job No." := "Job No.";
            JobTaskDim."Job Task No." := "Job Task No.";
            JobTaskDim."Dimension Code" := JobTaskDimTemp."Dimension Code";
            JobTaskDim."Dimension Value Code" := JobTaskDimTemp."Dimension Value Code";
            JobTaskDim.INSERT;
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[1] THEN
              GlobalDim1Code := JobTaskDim."Dimension Value Code";
            IF JobTaskDim."Dimension Code" = GLSetupShortcutDimCode[2] THEN
              GlobalDim2Code := JobTaskDim."Dimension Value Code";
          END;
        UNTIL JobTaskDimTemp.NEXT = 0;
      JobTaskDimTemp.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateJobTaskDim@15(DefaultDimension@1000 : Record 352;FromOnDelete@1003 : Boolean);
    VAR
      JobTaskDimension@1001 : Record 1002;
      JobTask@1002 : Record 1001;
    BEGIN
      IF DefaultDimension."Table ID" <> DATABASE::Job THEN
        EXIT;

      JobTask.SETRANGE("Job No.",DefaultDimension."No.");
      IF JobTask.ISEMPTY THEN
        EXIT;

      IF NOT CONFIRM(Text019,TRUE) THEN
        EXIT;

      JobTaskDimension.SETRANGE("Job No.",DefaultDimension."No.");
      JobTaskDimension.SETRANGE("Dimension Code",DefaultDimension."Dimension Code");
      JobTaskDimension.DELETEALL(TRUE);

      IF FromOnDelete OR
         (DefaultDimension."Value Posting" = DefaultDimension."Value Posting"::"No Code") OR
         (DefaultDimension."Dimension Value Code" = '')
      THEN
        EXIT;

      IF JobTask.FINDSET THEN
        REPEAT
          CLEAR(JobTaskDimension);
          JobTaskDimension."Job No." := JobTask."Job No.";
          JobTaskDimension."Job Task No." := JobTask."Job Task No.";
          JobTaskDimension."Dimension Code" := DefaultDimension."Dimension Code";
          JobTaskDimension."Dimension Value Code" := DefaultDimension."Dimension Value Code";
          JobTaskDimension.INSERT(TRUE);
        UNTIL JobTask.NEXT = 0;
    END;

    PROCEDURE DeleteJobTaskTempDim@76();
    BEGIN
      JobTaskDimTemp.RESET;
      JobTaskDimTemp.DELETEALL;
    END;

    PROCEDURE CopyJobTaskDimToJobTaskDim@77(JobNo@1000 : Code[20];JobTaskNo@1001 : Code[20];NewJobNo@1002 : Code[20];NewJobTaskNo@1003 : Code[20]);
    VAR
      JobTaskDimension@1004 : Record 1002;
      JobTaskDimension2@1005 : Record 1002;
    BEGIN
      JobTaskDimension.RESET;
      JobTaskDimension.SETRANGE("Job No.",JobNo);
      JobTaskDimension.SETRANGE("Job Task No.",JobTaskNo);
      IF JobTaskDimension.FINDSET THEN
        REPEAT
          IF NOT JobTaskDimension2.GET(NewJobNo,NewJobTaskNo,JobTaskDimension."Dimension Code") THEN BEGIN
            JobTaskDimension2.INIT;
            JobTaskDimension2."Job No." := NewJobNo;
            JobTaskDimension2."Job Task No." := NewJobTaskNo;
            JobTaskDimension2."Dimension Code" := JobTaskDimension."Dimension Code";
            JobTaskDimension2."Dimension Value Code" := JobTaskDimension."Dimension Value Code";
            JobTaskDimension2.INSERT(TRUE);
          END;
        UNTIL JobTaskDimension.NEXT = 0;
    END;

    PROCEDURE CheckDimIDConsistency@10(VAR DimSetEntry@1000 : Record 480;VAR PostedDimSetEntry@1001 : Record 480;DocTableID@1002 : Integer;PostedDocTableID@1003 : Integer) : Boolean;
    BEGIN
      IF DimSetEntry.FINDSET THEN;
      IF PostedDimSetEntry.FINDSET THEN;
      REPEAT
        CASE TRUE OF
          DimSetEntry."Dimension Code" > PostedDimSetEntry."Dimension Code":
            BEGIN
              DocDimConsistencyErr :=
                STRSUBSTNO(
                  Text012,
                  DimSetEntry.FIELDCAPTION("Dimension Code"),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID));
              EXIT(FALSE);
            END;
          DimSetEntry."Dimension Code" < PostedDimSetEntry."Dimension Code":
            BEGIN
              DocDimConsistencyErr :=
                STRSUBSTNO(
                  Text012,
                  PostedDimSetEntry.FIELDCAPTION("Dimension Code"),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID),
                  ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID));
              EXIT(FALSE);
            END;
          DimSetEntry."Dimension Code" = PostedDimSetEntry."Dimension Code":
            BEGIN
              IF DimSetEntry."Dimension Value Code" <> PostedDimSetEntry."Dimension Value Code" THEN BEGIN
                DocDimConsistencyErr :=
                  STRSUBSTNO(
                    Text013,
                    DimSetEntry.FIELDCAPTION("Dimension Value Code"),
                    DimSetEntry.FIELDCAPTION("Dimension Code"),
                    DimSetEntry."Dimension Code",
                    ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DocTableID),
                    ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,PostedDocTableID));
                EXIT(FALSE);
              END;
            END;
        END;
      UNTIL (DimSetEntry.NEXT = 0) AND (PostedDimSetEntry.NEXT = 0);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateDimSetEntryFromDimValue@4(DimValue@1000 : Record 349;VAR TempDimSetEntry@1001 : TEMPORARY Record 480);
    BEGIN
      TempDimSetEntry."Dimension Code" := DimValue."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimValue.Code;
      TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
      TempDimSetEntry.INSERT;
    END;

    PROCEDURE CreateDimSetIDFromICDocDim@5(VAR ICDocDim@1000 : Record 442) : Integer;
    VAR
      DimValue@1004 : Record 349;
      TempDimSetEntry@1003 : TEMPORARY Record 480;
    BEGIN
      IF ICDocDim.FIND('-') THEN
        REPEAT
          DimValue.GET(
            ConvertICDimtoDim(ICDocDim."Dimension Code"),
            ConvertICDimValuetoDimValue(ICDocDim."Dimension Code",ICDocDim."Dimension Value Code"));
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL ICDocDim.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE CreateDimSetIDFromICJnlLineDim@7(VAR ICInboxOutboxJnlLineDim@1000 : Record 423) : Integer;
    VAR
      DimValue@1001 : Record 349;
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      IF ICInboxOutboxJnlLineDim.FIND('-') THEN
        REPEAT
          DimValue.GET(
            ConvertICDimtoDim(ICInboxOutboxJnlLineDim."Dimension Code"),
            ConvertICDimValuetoDimValue(
              ICInboxOutboxJnlLineDim."Dimension Code",ICInboxOutboxJnlLineDim."Dimension Value Code"));
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL ICInboxOutboxJnlLineDim.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE CopyDimBufToDimSetEntry@65(VAR FromDimBuf@1000 : Record 360;VAR DimSetEntry@1001 : Record 480);
    VAR
      DimValue@1005 : Record 349;
    BEGIN
      WITH FromDimBuf DO
        IF FINDSET THEN
          REPEAT
            DimValue.GET("Dimension Code","Dimension Value Code");
            DimSetEntry."Dimension Code" := "Dimension Code";
            DimSetEntry."Dimension Value Code" := "Dimension Value Code";
            DimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
            DimSetEntry.INSERT;
          UNTIL NEXT = 0;
    END;

    PROCEDURE CreateDimSetIDFromDimBuf@12(VAR DimBuf@1000 : Record 360) : Integer;
    VAR
      DimValue@1001 : Record 349;
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      IF DimBuf.FINDSET THEN
        REPEAT
          DimValue.GET(DimBuf."Dimension Code",DimBuf."Dimension Value Code");
          CreateDimSetEntryFromDimValue(DimValue,TempDimSetEntry);
        UNTIL DimBuf.NEXT = 0;
      EXIT(GetDimensionSetID(TempDimSetEntry));
    END;

    PROCEDURE GetDimSetIDsForFilter@23(DimCode@1000 : Code[20];DimValueFilter@1001 : Text[250]);
    VAR
      DimSetEntry2@1002 : Record 480;
    BEGIN
      DimSetEntry2.SETFILTER("Dimension Code",'%1',DimCode);
      DimSetEntry2.SETFILTER("Dimension Value Code",DimValueFilter);
      IF DimSetEntry2.FINDSET THEN
        REPEAT
          AddDimSetIDtoTempEntry(TempDimSetEntry2,DimSetEntry2."Dimension Set ID");
        UNTIL DimSetEntry2.NEXT = 0;
      IF FilterIncludesBlank(DimCode,DimValueFilter) THEN
        GetDimSetIDsForBlank(DimCode);
      DimSetFilterCtr += 1;
    END;

    LOCAL PROCEDURE GetDimSetIDsForBlank@27(DimCode@1000 : Code[20]);
    VAR
      TempDimSetEntry@1001 : TEMPORARY Record 480;
      DimSetEntry2@1002 : Record 480;
      PrevDimSetID@1004 : Integer;
      i@1003 : Integer;
    BEGIN
      AddDimSetIDtoTempEntry(TempDimSetEntry,0);
      FOR i := 1 TO 2 DO BEGIN
        IF i = 2 THEN
          DimSetEntry2.SETFILTER("Dimension Code",'%1',DimCode);
        IF DimSetEntry2.FINDSET THEN BEGIN
          PrevDimSetID := 0;
          REPEAT
            IF DimSetEntry2."Dimension Set ID" <> PrevDimSetID THEN BEGIN
              AddDimSetIDtoTempEntry(TempDimSetEntry,DimSetEntry2."Dimension Set ID");
              PrevDimSetID := DimSetEntry2."Dimension Set ID";
            END;
          UNTIL DimSetEntry2.NEXT = 0;
        END;
      END;
      TempDimSetEntry.SETFILTER("Dimension Value ID",'%1',1);
      IF TempDimSetEntry.FINDSET THEN
        REPEAT
          AddDimSetIDtoTempEntry(TempDimSetEntry2,TempDimSetEntry."Dimension Set ID");
        UNTIL TempDimSetEntry.NEXT = 0;
    END;

    PROCEDURE GetNextDimSetFilterChunk@24(Length@1000 : Integer) DimSetFilterChunk : Text[1024];
    VAR
      EndLoop@1002 : Boolean;
    BEGIN
      IF Length > MAXSTRLEN(DimSetFilterChunk) THEN
        Length := MAXSTRLEN(DimSetFilterChunk);
      TempDimSetEntry2.SETFILTER("Dimension Value ID",'%1',DimSetFilterCtr);
      TempDimSetEntry2.SETFILTER("Dimension Set ID",'>%1',LastDimSetIDInFilter);
      IF TempDimSetEntry2.FINDSET THEN BEGIN
        DimSetFilterChunk := FORMAT(TempDimSetEntry2."Dimension Set ID");
        LastDimSetIDInFilter := TempDimSetEntry2."Dimension Set ID";
        WHILE (STRLEN(DimSetFilterChunk) < Length) AND (NOT EndLoop) DO BEGIN
          IF TempDimSetEntry2.NEXT <> 0 THEN BEGIN
            IF STRLEN(DimSetFilterChunk + '|' + FORMAT(TempDimSetEntry2."Dimension Set ID")) <= Length THEN BEGIN
              DimSetFilterChunk += '|' + FORMAT(TempDimSetEntry2."Dimension Set ID");
              LastDimSetIDInFilter := TempDimSetEntry2."Dimension Set ID";
            END ELSE
              EndLoop := TRUE;
          END ELSE
            EndLoop := TRUE;
        END;
      END;
    END;

    LOCAL PROCEDURE FilterIncludesBlank@25(DimCode@1001 : Code[20];DimValueFilter@1000 : Text[250]) : Boolean;
    VAR
      TempDimSetEntry@1002 : TEMPORARY Record 480;
    BEGIN
      TempDimSetEntry."Dimension Code" := DimCode;
      TempDimSetEntry.INSERT;
      TempDimSetEntry.SETFILTER("Dimension Value Code",DimValueFilter);
      EXIT(NOT TempDimSetEntry.ISEMPTY);
    END;

    LOCAL PROCEDURE AddDimSetIDtoTempEntry@30(VAR TempDimSetEntry@1001 : TEMPORARY Record 480;DimSetID@1000 : Integer);
    BEGIN
      IF TempDimSetEntry.GET(DimSetID,'') THEN BEGIN
        TempDimSetEntry."Dimension Value ID" += 1;
        TempDimSetEntry.MODIFY;
      END ELSE BEGIN
        TempDimSetEntry."Dimension Set ID" := DimSetID;
        TempDimSetEntry."Dimension Value ID" := 1;
        TempDimSetEntry.INSERT
      END;
    END;

    PROCEDURE ClearDimSetFilter@26();
    BEGIN
      TempDimSetEntry2.RESET;
      TempDimSetEntry2.DELETEALL;
      DimSetFilterCtr := 0;
      LastDimSetIDInFilter := 0;
    END;

    PROCEDURE GetTempDimSetEntry@31(VAR TempDimSetEntry@1000 : TEMPORARY Record 480);
    BEGIN
      TempDimSetEntry.COPY(TempDimSetEntry2,TRUE);
    END;

    LOCAL PROCEDURE UpdateCostType@28(DefaultDimension@1000 : Record 352;CallingTrigger@1004 : 'OnInsert,OnModify,OnDelete');
    VAR
      GLAcc@1003 : Record 15;
      CostAccSetup@1002 : Record 1108;
      CostAccMgt@1001 : Codeunit 1100;
    BEGIN
      IF CostAccSetup.GET AND (DefaultDimension."Table ID" = DATABASE::"G/L Account") THEN
        IF GLAcc.GET(DefaultDimension."No.") THEN
          CostAccMgt.UpdateCostTypeFromDefaultDimension(DefaultDimension,GLAcc,CallingTrigger);
    END;

    PROCEDURE CreateDimSetFromJobTaskDim@32(JobNo@1006 : Code[20];JobTaskNo@1001 : Code[20];VAR GlobalDimVal1@1005 : Code[20];VAR GlobalDimVal2@1004 : Code[20]) NewDimSetID : Integer;
    VAR
      JobTaskDimension@1000 : Record 1002;
      DimValue@1002 : Record 349;
      TempDimSetEntry@1003 : TEMPORARY Record 480;
    BEGIN
      WITH JobTaskDimension DO BEGIN
        SETRANGE("Job No.",JobNo);
        SETRANGE("Job Task No.",JobTaskNo);
        IF FINDSET THEN BEGIN
          REPEAT
            DimValue.GET("Dimension Code","Dimension Value Code");
            TempDimSetEntry."Dimension Code" := "Dimension Code";
            TempDimSetEntry."Dimension Value Code" := "Dimension Value Code";
            TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
            TempDimSetEntry.INSERT(TRUE);
          UNTIL NEXT = 0;
          NewDimSetID := GetDimensionSetID(TempDimSetEntry);
          UpdateGlobalDimFromDimSetID(NewDimSetID,GlobalDimVal1,GlobalDimVal2);
        END;
      END;
    END;

    PROCEDURE UpdateGenJnlLineDim@34(VAR GenJnlLine@1000 : Record 81;DimSetID@1001 : Integer);
    BEGIN
      GenJnlLine."Dimension Set ID" := DimSetID;
      UpdateGlobalDimFromDimSetID(
        GenJnlLine."Dimension Set ID",
        GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
    END;

    PROCEDURE UpdateGenJnlLineDimFromCustLedgEntry@33(VAR GenJnlLine@1000 : Record 81;DtldCustLedgEntry@1001 : Record 379);
    VAR
      CustLedgEntry@1002 : Record 21;
    BEGIN
      IF DtldCustLedgEntry."Cust. Ledger Entry No." <> 0 THEN BEGIN
        CustLedgEntry.GET(DtldCustLedgEntry."Cust. Ledger Entry No.");
        UpdateGenJnlLineDim(GenJnlLine,CustLedgEntry."Dimension Set ID");
      END;
    END;

    PROCEDURE UpdateGenJnlLineDimFromVendLedgEntry@29(VAR GenJnlLine@1001 : Record 81;DtldVendLedgEntry@1000 : Record 380);
    VAR
      VendLedgEntry@1002 : Record 25;
    BEGIN
      IF DtldVendLedgEntry."Vendor Ledger Entry No." <> 0 THEN BEGIN
        VendLedgEntry.GET(DtldVendLedgEntry."Vendor Ledger Entry No.");
        UpdateGenJnlLineDim(GenJnlLine,VendLedgEntry."Dimension Set ID");
      END;
    END;

    PROCEDURE GetDimSetEntryDefaultDim@45(VAR DimSetEntry2@1001 : Record 480);
    VAR
      DimValue@1000 : Record 349;
    BEGIN
      IF NOT DimSetEntry2.ISEMPTY THEN
        DimSetEntry2.DELETEALL;
      IF TempDimBuf2.FINDSET THEN
        REPEAT
          DimValue.GET(TempDimBuf2."Dimension Code",TempDimBuf2."Dimension Value Code");
          DimSetEntry2."Dimension Code" := TempDimBuf2."Dimension Code";
          DimSetEntry2."Dimension Value Code" := TempDimBuf2."Dimension Value Code";
          DimSetEntry2."Dimension Value ID" := DimValue."Dimension Value ID";
          DimSetEntry2.INSERT;
        UNTIL TempDimBuf2.NEXT = 0;
      TempDimBuf2.RESET;
      TempDimBuf2.DELETEALL;
    END;

    LOCAL PROCEDURE InsertObject@35(VAR TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058;TableID@1001 : Integer);
    VAR
      AllObjWithCaption@1002 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID",TableID);
      IF AllObjWithCaption.FINDFIRST THEN BEGIN
        TempAllObjWithCaption := AllObjWithCaption;
        TempAllObjWithCaption.INSERT;
      END;
    END;

    PROCEDURE GetConsolidatedDimFilterByDimFilter@37(VAR Dimension@1000 : Record 348;DimFilter@1001 : Text) ConsolidatedDimFilter : Text;
    BEGIN
      Dimension.SETFILTER("Consolidation Code",DimFilter);
      ConsolidatedDimFilter += DimFilter;
      IF Dimension.FINDSET THEN
        REPEAT
          ConsolidatedDimFilter += '|' + Dimension.Code;
        UNTIL Dimension.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

