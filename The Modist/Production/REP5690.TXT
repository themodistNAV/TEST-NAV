OBJECT Report 5690 Index Fixed Assets
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Index Fixed Assets;
               ENG=Index Fixed Assets];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF FAPostingDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF FAPostingDate <> NORMALDATE(FAPostingDate) THEN
                    ERROR(Text001);

                  IF PostingDate = 0D THEN
                    PostingDate := FAPostingDate;
                  DeprBook.GET(DeprBookCode);
                  DeprBook.TESTFIELD("Allow Indexation",TRUE);
                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (FAPostingDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  DeprBook.IndexGLIntegration(GLIntegration);
                  FirstGenJnl := TRUE;
                  FirstFAJnl := TRUE;

                  FALedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","FA Posting Date");
                  FALedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  FALedgEntry.SETRANGE("FA Posting Category",FALedgEntry."FA Posting Category"::" ");
                  FALedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  MaintenanceLedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Date");
                  MaintenanceLedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  MaintenanceLedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  Window.OPEN(Text003);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnAfterGetRecord=BEGIN
                                  IF NOT FADeprBook.GET("No.",DeprBookCode) THEN
                                    CurrReport.SKIP;
                                  Window.UPDATE(1,"No.");

                                  IF Inactive OR
                                     Blocked OR
                                     (FADeprBook."Disposal Date" > 0D) OR
                                     (FADeprBook."Acquisition Date" = 0D) OR
                                     (FADeprBook."Acquisition Date" > FAPostingDate)
                                  THEN
                                    CurrReport.SKIP;

                                  FALedgEntry.SETRANGE("FA No.","No.");
                                  MaintenanceLedgEntry.SETRANGE("FA No.","No.");

                                  i := 0;
                                  WHILE i < 9 DO BEGIN
                                    i := i + 1;
                                    IF i = 7 THEN
                                      i := 8;
                                    IndexAmount := 0;
                                    IF IndexChoices[i] THEN BEGIN
                                      IF i = 1 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Acquisition Cost");
                                      IF i = 2 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Depreciation);
                                      IF i = 3 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Write-Down");
                                      IF i = 4 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Appreciation);
                                      IF i = 5 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 1");
                                      IF i = 6 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 2");
                                      IF i = 9 THEN
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Salvage Value");
                                      IF i = 8 THEN BEGIN
                                        MaintenanceLedgEntry.CALCSUMS(Amount);
                                        IndexAmount := MaintenanceLedgEntry.Amount;
                                      END ELSE BEGIN
                                        FALedgEntry.CALCSUMS(Amount);
                                        IndexAmount := FALedgEntry.Amount;
                                      END;
                                      IndexAmount :=
                                        DepreciationCalc.CalcRounding(DeprBookCode,IndexAmount * (IndexFigure / 100 - 1));
                                      IF NOT GLIntegration[i] OR "Budgeted Asset" THEN
                                        InsertFAJnlLine("No.",IndexAmount,i - 1)
                                      ELSE
                                        InsertGenJnlLine("No.",IndexAmount,i - 1);
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=[ENU=Depreciation Book;
                             ENG=Depreciation Book];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 5   ;2   ;Field     ;
                  Name=IndexFigure;
                  CaptionML=[ENU=Index Figure;
                             ENG=Index Figure];
                  SourceExpr=IndexFigure;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=[ENU=FA Posting Date;
                             ENG=FA Posting Date];
                  SourceExpr=FAPostingDate }

      { 11  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ENG=Posting Date];
                  SourceExpr=PostingDate }

      { 23  ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ENG=Document No.];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ENG=Posting Description];
                  SourceExpr=PostingDescription }

      { 20  ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=[ENU=Insert Bal. Account;
                             ENG=Insert Bal. Account];
                  SourceExpr=BalAccount }

      { 4   ;2   ;Group     ;
                  CaptionML=[ENU=Index;
                             ENG=Index] }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Acquisition Cost;
                             ENG=Acquisition Cost];
                  SourceExpr=IndexChoices[1] }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Depreciation;
                             ENG=Depreciation];
                  SourceExpr=IndexChoices[2] }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Write-Down;
                             ENG=Write-Down];
                  SourceExpr=IndexChoices[3] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Appreciation;
                             ENG=Appreciation];
                  SourceExpr=IndexChoices[4] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Custom 1;
                             ENG=Custom 1];
                  SourceExpr=IndexChoices[5] }

      { 17  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 2;
                             ENG=Custom 2];
                  SourceExpr=IndexChoices[6] }

      { 18  ;3   ;Field     ;
                  CaptionML=[ENU=Salvage Value;
                             ENG=Salvage Value];
                  SourceExpr=IndexChoices[9] }

      { 19  ;3   ;Field     ;
                  CaptionML=[ENU=Maintenance;
                             ENG=Maintenance];
                  SourceExpr=IndexChoices[8] }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;ENG=You must specify %1.';
      Text001@1001 : TextConst 'ENU=FA Posting Date must not be a closing date.;ENG=FA Posting Date must not be a closing date.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";ENG="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Indexing fixed asset   #1##########;ENG=Indexing fixed asset   #1##########';
      GenJnlLine@1004 : Record 81;
      FASetup@1005 : Record 5603;
      FAJnlLine@1006 : Record 5621;
      DeprBook@1007 : Record 5611;
      FADeprBook@1008 : Record 5612;
      FALedgEntry@1009 : Record 5601;
      MaintenanceLedgEntry@1010 : Record 5625;
      FAJnlSetup@1011 : Record 5605;
      DepreciationCalc@1012 : Codeunit 5616;
      Window@1013 : Dialog;
      FAPostingDate@1014 : Date;
      IndexFigure@1015 : Decimal;
      IndexChoices@1016 : ARRAY [9] OF Boolean;
      IndexAmount@1017 : Decimal;
      GLIntegration@1018 : ARRAY [9] OF Boolean;
      FirstGenJnl@1019 : Boolean;
      FirstFAJnl@1020 : Boolean;
      PostingDate@1021 : Date;
      DocumentNo@1022 : Code[20];
      DocumentNo2@1023 : Code[20];
      DocumentNo3@1024 : Code[20];
      NoSeries2@1025 : Code[10];
      Noseries3@1026 : Code[10];
      PostingDescription@1027 : Text[50];
      DeprBookCode@1028 : Code[10];
      BalAccount@1029 : Boolean;
      FAJnlNextLineNo@1030 : Integer;
      GenJnlNextLineNo@1031 : Integer;
      i@1032 : Integer;

    LOCAL PROCEDURE InsertGenJnlLine@2(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value');
    VAR
      FAInsertGLAcc@1003 : Codeunit 5601;
    BEGIN
      IF IndexAmount = 0 THEN
        EXIT;
      IF FirstGenJnl THEN BEGIN
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        NoSeries2 := FAJnlSetup.GetGenNoSeries(GenJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FAPostingDate,TRUE)
        ELSE
          DocumentNo2 := DocumentNo;
        FirstGenJnl := FALSE;
      END;
      WITH GenJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
        "FA Posting Date" := FAPostingDate;
        "Posting Date" := PostingDate;
        IF "Posting Date" = "FA Posting Date" THEN
          "FA Posting Date" := 0D;
        "FA Posting Type" := PostingType + 1;
        "Account Type" := "Account Type"::"Fixed Asset";
        VALIDATE("Account No.",FANo);
        "Document No." := DocumentNo2;
        "Posting No. Series" := NoSeries2;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := TRUE;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;
        INSERT(TRUE);
        IF BalAccount THEN BEGIN
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          IF FIND('+') THEN ;
          GenJnlNextLineNo := "Line No.";
        END;
      END;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@3(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value');
    BEGIN
      IF IndexAmount = 0 THEN
        EXIT;
      IF FirstFAJnl THEN BEGIN
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        Noseries3 := FAJnlSetup.GetFANoSeries(FAJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo3 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FAPostingDate,TRUE)
        ELSE
          DocumentNo3 := DocumentNo;
        FirstFAJnl := FALSE;
      END;
      WITH FAJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
        "Posting Date" := PostingDate;
        "FA Posting Date" := FAPostingDate;
        IF "Posting Date" = "FA Posting Date" THEN
          "Posting Date" := 0D;
        "FA Posting Type" := PostingType;
        VALIDATE("FA No.",FANo);
        "Document No." := DocumentNo3;
        "Posting No. Series" := Noseries3;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := TRUE;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;
        INSERT(TRUE);
      END;
    END;

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1000 : Code[10];IndexFigureFrom@1001 : Decimal;FAPostingDateFrom@1002 : Date;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[50];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      IndexFigure := IndexFigureFrom;
      FAPostingDate := FAPostingDateFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    PROCEDURE SetIndexAcquisitionCost@4(IndexChoice@1000 : Boolean);
    BEGIN
      IndexChoices[1] := IndexChoice;
    END;

    PROCEDURE SetIndexDepreciation@5(IndexChoice@1000 : Boolean);
    BEGIN
      IndexChoices[2] := IndexChoice;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

