OBJECT Codeunit 9062266 File Attachment Auto Process.
{
  OBJECT-PROPERTIES
  {
    Date=28/09/15;
    Time=[ 4:07:34 PM];
    Version List=NA2016.10;
  }
  PROPERTIES
  {
    TableNo=9062734;
    OnRun=BEGIN
            Code();
          END;

  }
  CODE
  {
    VAR
      FAQ@1000000007 : Record 9062270;
      FAQEntry@1000000006 : Record 9062271;
      WFlowIn@1000000000 : Codeunit 9062262;
      WorkflowManagement@9062229 : Codeunit 9062258;
      SysB@1000000001 : Record 9062230;
      TNPUtils@1000000002 : Codeunit 9062229;

    PROCEDURE Code@1000000001();
    VAR
      FAQProcessEntry@1000000000 : Codeunit 9062381;
      WorkflowNo@9062229 : Code[20];
      ContactNo@9062230 : Code[20];
      InformationText@9062231 : Text;
      eInformationText@9062232 : TextConst 'ENU=%1. E-Mail subject: %2. Received %3.;ENG=%1. E-Mail subject: %2. Received %3.';
    BEGIN
      IF FAQ.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
            FAQ.RefreshQueueEntries;
            IF FAQ."Process Automatically" THEN BEGIN
              FAQEntry.SETRANGE("Queue Code",FAQ.Code);
              FAQEntry.SETRANGE("AutoProcess Errored",FALSE);
              IF FAQEntry.FINDSET THEN BEGIN
                REPEAT
                  TNPUtils.SetCurrentContact(FAQEntry."Sender Contact No.");
                  COMMIT;
                  IF NOT FAQProcessEntry.RUN(FAQEntry) THEN BEGIN
                    FAQEntry."AutoProcess Error Count" += 1;
                    FAQEntry.MODIFY;
                    IF FAQEntry."AutoProcess Error Count" >= SysB.GetDecimal('F_ATT_RETRIES') THEN BEGIN
                      FAQEntry.VALIDATE("AutoProcess Errored",TRUE);
                      FAQEntry.VALIDATE("AutoProcess Error Message",COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(FAQEntry."AutoProcess Error Message")));
                      FAQEntry.MODIFY(TRUE);
                    END;
                  END;
                  IF FAQEntry."AutoProcess Errored" THEN BEGIN
                    WorkflowNo := SysB.GetText('F_ATT_ERROR_WORKFLOW');
                    ContactNo := FAQEntry."Sender Contact No.";
                    IF (ContactNo = '') AND (FAQEntry."Sender Email Address" <> '') THEN
                      ContactNo := TNPUtils.GetContactFromEmail(FAQEntry."Sender Email Address");
                    InformationText := STRSUBSTNO(eInformationText, FAQEntry."AutoProcess Error Message", FAQEntry.Description,
                      FAQEntry."DateTime Created");
                    WorkflowManagement.CreateGroupWFlow(ContactNo, TRUE, WorkflowNo, InformationText, FAQEntry.GETPOSITION,
                      0, '', '', TRUE);
                  END;
                UNTIL FAQEntry.NEXT = 0;
              END;
            END;
        UNTIL FAQ.NEXT = 0;
      END;
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 02/09/2012 - Migrated to 2013
      //DOC NA2015.1  PC 30/01/2014 - Changed AutoProcess function to be it's own codeunit and therefore errors can be caught and handled
      //DOC NA2015.1  PC 26/03/2014 - Added "AutoProcess Error Count" incrementing, so that entries will be processed multiple times before actually failing
      //DOC NA2015.3  PC 18/08/2014 - Called TNPUtils.SetCurrentContact so that actions are taken as correct Contact for File Attachment
      //DOC NA2015.4  JH 28/12/2014 - System behaviours renamed
      //DOC NA2015.9  JH 04/09/2015 - Improved error workflow
      //DOC NA2015.9  JH 08/09/2015 - Clearing current contact
      //DOC NA2016.10 JH 26/08/2015 - Upgraded to 2016
    }
    END.
  }
}

