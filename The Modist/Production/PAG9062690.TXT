OBJECT Page 9062690 Tag Selection
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tag Selection;
               ENG=Tag Selection];
    MultipleNewLines=No;
    SourceTable=Table9062492;
    PageType=StandardDialog;
    ShowFilter=No;
    OnQueryClosePage=VAR
                       TagFound@9062230 : Boolean;
                       ConfirmMessage@9062231 : Text;
                     BEGIN
                       IF (CloseAction IN [ACTION::OK, ACTION::LookupOK]) THEN BEGIN
                         TagFound := FindTag();
                         IF (NOT(TagFound)) THEN BEGIN

                           IF (TagVisibility = TagVisibility::Public) THEN
                             ConfirmMessage := cnfCreatePublicTag
                           ELSE
                             ConfirmMessage := cnfCreatePrivateTag;

                           IF (CONFIRM(STRSUBSTNO(ConfirmMessage, TagName))) THEN BEGIN
                             Tag.INIT();
                             Tag.Code := '';
                             Tag."Table No." := TagTableNo;
                             Tag.VALIDATE(Name, TagName);
                             Tag.VALIDATE(Visibility, TagVisibility);
                             Tag.INSERT(TRUE);
                             EXIT(TRUE);
                           END ELSE
                             EXIT(FALSE);
                         END;
                         EXIT(TRUE);

                       END ELSE
                         EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 9062229;1;Field     ;
                CaptionML=[ENU=Tag Name;
                           ENG=Tag Name];
                SourceExpr=TagName;
                OnLookup=VAR
                           TagList@9062229 : Page 9062688;
                         BEGIN
                           Tag.RESET();
                           Tag.SETRANGE("Table No.", TagTableNo);
                           Tag.SETFILTER("User ID", '%1|%2', '', USERID);
                           IF (TagName <> '') THEN
                             Tag.SETRANGE(Name, TagName);
                           IF (Tag.FINDFIRST()) THEN ;
                           Tag.SETRANGE(Name);
                           TagList.LOOKUPMODE := TRUE;
                           TagList.SETTABLEVIEW(Tag);
                           TagList.SETRECORD(Tag);
                           IF (TagList.RUNMODAL() = ACTION::LookupOK) THEN BEGIN
                             TagList.GETRECORD(Tag);
                             TagName := Tag.Name;
                             TagVisibility := Tag.Visibility;
                           END;
                         END;
                          }

    { 9062230;1;Field     ;
                CaptionML=ENU=Visibility;
                OptionCaptionML=[ENU=Private,Public;
                                 ENG=Private,Public];
                SourceExpr=TagVisibility }

  }
  CODE
  {
    VAR
      Tag@9062230 : Record 9062492;
      TagName@1000000000 : Text;
      TagVisibility@9062229 : 'Private,Public';
      TagTableNo@9062231 : Integer;
      cnfCreatePublicTag@9062232 : TextConst 'ENU=Public tag %1 does not exist. Do you want to create it?;ENG=Public tag %1 does not exist. Do you want to create it?';
      cnfCreatePrivateTag@9062233 : TextConst 'ENU=Private tag %1 does not exist. Do you want to create it?;ENG=Private tag %1 does not exist. Do you want to create it?';

    PROCEDURE SetTableNo@9062232(NewValue@9062229 : Integer);
    BEGIN
      TagTableNo := NewValue;
    END;

    LOCAL PROCEDURE FindTag@9062230() : Boolean;
    BEGIN
      Tag.RESET();
      Tag.SETRANGE(Name, TagName);
      Tag.SETRANGE("Table No.", TagTableNo);
      IF (TagVisibility = TagVisibility::Public) THEN
        Tag.SETRANGE("User ID", '')
      ELSE
        Tag.SETRANGE("User ID", USERID);
      EXIT(Tag.FINDFIRST());
    END;

    PROCEDURE GetSelectedTag@9062229(VAR SelectedTag@9062229 : Record 9062492);
    BEGIN
      SelectedTag.GET(Tag.Code, Tag."Table No.", Tag."User ID");
    END;

    BEGIN
    {
      //DOC NA2015.5  IH 10/03/2015 - Updated
      //DOC NA2015.8  IH 13/07/2015 - Amendments
      //DOC NA2016.10 JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

