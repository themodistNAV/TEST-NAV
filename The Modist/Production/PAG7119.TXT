OBJECT Page 7119 Inventory Analysis Report
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVGB9.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Inventory Analysis Report;
               ENG=Inventory Analysis Report];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table7114;
    DataCaptionExpr=GetCaption;
    SourceTableView=WHERE(Analysis Area=CONST(Inventory));
    PageType=Card;
    OnOpenPage=VAR
                 AnalysisLineTemplate@1001 : Record 7112;
               BEGIN
                 IF (NewCurrentReportName <> '') AND (NewCurrentReportName <> CurrentReportName) THEN BEGIN
                   CurrentReportName := NewCurrentReportName;
                   AnalysisReportMgt.CheckReportName(CurrentReportName,Rec);
                   ValidateReportName;
                   AnalysisReportMgt.SetAnalysisLineTemplName(CurrentLineTemplate,Rec);
                   ValidateAnalysisTemplateName;
                 END;

                 AnalysisReportMgt.OpenAnalysisLines(CurrentLineTemplate,Rec);
                 AnalysisReportMgt.OpenColumns(CurrentColumnTemplate,Rec,AnalysisColumn);

                 AnalysisReportMgt.CopyColumnsToTemp(Rec,CurrentColumnTemplate,AnalysisColumn);
                 AnalysisReportMgt.SetSourceType(Rec,CurrentSourceTypeFilter);
                 AnalysisReportMgt.SetSourceNo(Rec,CurrentSourceTypeNoFilter);

                 GLSetup.GET;

                 IF AnalysisLineTemplate.GET(GETRANGEMAX("Analysis Area"),CurrentLineTemplate) THEN
                   IF AnalysisLineTemplate."Item Analysis View Code" <> '' THEN
                     ItemAnalysisView.GET(GETRANGEMAX("Analysis Area"),AnalysisLineTemplate."Item Analysis View Code")
                   ELSE BEGIN
                     CLEAR(ItemAnalysisView);
                     ItemAnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
                     ItemAnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
                   END;

                 PeriodFormMgt.SetCalendarSource(CalendarSource);
                 FindPeriod('');

                 NoOfColumns := InvtAnalysisMatrix.GetMatrixDimension;
                 Direction := Direction::Forward;

                 ClearPoints;
                 SetPointsAnalysisColumn;
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 17      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Actions;
                                 ENG=&Actions];
                      Image=Action }
      { 18      ;2   ;Action    ;
                      Name=SetUpLines;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Set Up &Lines;
                                 ENG=Set Up &Lines];
                      Image=SetupLines;
                      OnAction=BEGIN
                                 AnalysisReportMgt.OpenAnalysisLinesForm(Rec,CurrentLineTemplate);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Set Up &Columns;
                                 ENG=Set Up &Columns];
                      Image=SetupColumns;
                      OnAction=BEGIN
                                 AnalysisReportMgt.OpenAnalysisColumnsForm(Rec,CurrentColumnTemplate);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 20      ;2   ;Separator  }
      { 2       ;2   ;ActionGroup;
                      CaptionML=[ENU=Export to Excel;
                                 ENG=Export to Excel];
                      Image=ExportToExcel }
      { 21      ;3   ;Action    ;
                      CaptionML=[ENU=Create New Document;
                                 ENG=Create New Document];
                      Image=ExportToExcel;
                      OnAction=VAR
                                 ExportAnalysisRepToExcel@1000 : Report 7113;
                               BEGIN
                                 ExportAnalysisRepToExcel.SetOptions(Rec,CurrentColumnTemplate,CurrentLineTemplate,PeriodType,CalendarSource);
                                 ExportAnalysisRepToExcel.RUN;
                               END;
                                }
      { 4       ;3   ;Action    ;
                      CaptionML=[ENU=Update Existing Document;
                                 ENG=Update Existing Document];
                      Image=ExportToExcel;
                      OnAction=VAR
                                 ExportAnalysisRepToExcel@1000 : Report 7113;
                               BEGIN
                                 ExportAnalysisRepToExcel.SetOptions(Rec,CurrentColumnTemplate,CurrentLineTemplate,PeriodType,CalendarSource);
                                 ExportAnalysisRepToExcel.SetUpdateExistingWorksheet(TRUE);
                                 ExportAnalysisRepToExcel.RUN;
                               END;
                                }
      { 14      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Reports;
                                 ENG=&Reports];
                      Image=Report }
      { 23      ;2   ;Action    ;
                      CaptionML=[ENU=Print;
                                 ENG=Print];
                      Image=Print;
                      OnAction=VAR
                                 AnalysisReport@1006 : Report 7112;
                                 DateFilter@1005 : Text[30];
                                 ItemBudgetFilter@1004 : Text[30];
                                 LocationFilter@1003 : Text[30];
                                 Dim1Filter@1002 : Text[250];
                                 Dim2Filter@1001 : Text[250];
                                 Dim3Filter@1000 : Text[250];
                               BEGIN
                                 AnalysisReport.SetParams(GETRANGEMAX("Analysis Area"),CurrentReportName,CurrentLineTemplate,CurrentColumnTemplate);
                                 DateFilter := GETFILTER("Date Filter");
                                 ItemBudgetFilter := GETFILTER("Item Budget Filter");
                                 LocationFilter := GETFILTER("Location Filter");
                                 Dim1Filter := GETFILTER("Dimension 1 Filter");
                                 Dim2Filter := GETFILTER("Dimension 2 Filter");
                                 Dim3Filter := GETFILTER("Dimension 3 Filter");
                                 AnalysisReport.SetFilters(
                                   DateFilter,ItemBudgetFilter,LocationFilter,Dim1Filter,Dim2Filter,Dim3Filter,
                                   CurrentSourceTypeFilter,CurrentSourceTypeNoFilter);
                                 AnalysisReport.RUN;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1       ;1   ;Action    ;
                      Name=ShowMatrix;
                      CaptionML=[ENU=&Show Matrix;
                                 ENG=&Show Matrix];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ShowMatrix;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetFilters;

                                 CLEAR(MatrixColumnCaptions);

                                 FillMatrixColumns;

                                 CLEAR(InvtAnalysisMatrix);
                                 InvtAnalysisMatrix.Load(AnalysisColumn,MatrixColumnCaptions,FirstLineNo,LastLineNo);
                                 InvtAnalysisMatrix.SETTABLEVIEW(AnalysisLine);
                                 InvtAnalysisMatrix.SetCalendarSource(CalendarSource,PeriodType);
                                 InvtAnalysisMatrix.RUNMODAL;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 ENG=Next Set];
                      ToolTipML=[ENU=Next Set;
                                 ENG=Next Set];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Direction := Direction::Forward;
                                 SetPointsAnalysisColumn;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 ENG=Previous Set];
                      ToolTipML=[ENU=Previous Set;
                                 ENG=Previous Set];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Direction := Direction::Backward;
                                 SetPointsAnalysisColumn;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 29  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 40  ;2   ;Field     ;
                CaptionML=[ENU=Analysis Report Name;
                           ENG=Analysis Report Name];
                SourceExpr=CurrentReportName;
                OnValidate=BEGIN
                             AnalysisReportMgt.CheckReportName(CurrentReportName,Rec);
                             CurrentReportNameOnAfterValida;
                           END;

                OnLookup=BEGIN
                           IF AnalysisReportMgt.LookupReportName(GETRANGEMAX("Analysis Area"),CurrentReportName) THEN BEGIN
                             Text := CurrentReportName;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Analysis Line Template;
                           ENG=Analysis Line Template];
                SourceExpr=CurrentLineTemplate;
                OnValidate=BEGIN
                             AnalysisReportMgt.CheckAnalysisLineTemplName(CurrentLineTemplate,Rec);
                             CurrentLineTemplateOnAfterVali;
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           AnalysisReportMgt.LookupAnalysisLineTemplName(CurrentLineTemplate,Rec);
                           ValidateAnalysisTemplateName;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Analysis Column Template;
                           ENG=Analysis Column Template];
                SourceExpr=CurrentColumnTemplate;
                OnValidate=BEGIN
                             AnalysisReportMgt.GetColumnTemplate(GETRANGEMAX("Analysis Area"),CurrentColumnTemplate);
                             CurrentColumnTemplateOnAfterVa;
                           END;

                OnLookup=BEGIN
                           IF AnalysisReportMgt.LookupColumnName(GETRANGEMAX("Analysis Area"),CurrentColumnTemplate) THEN BEGIN
                             Text := CurrentColumnTemplate;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1907524401;1;Group  ;
                CaptionML=[ENU=Filters;
                           ENG=Filters] }

    { 27  ;2   ;Field     ;
                CaptionML=[ENU=Source Type Filter;
                           ENG=Source Type Filter];
                OptionCaptionML=[ENU=" ,Customer,Vendor,Item";
                                 ENG=" ,Customer,Vendor,Item"];
                SourceExpr=CurrentSourceTypeFilter;
                OnValidate=BEGIN
                             SETRANGE("Source Type Filter",CurrentSourceTypeFilter);
                             CurrentSourceTypeNoFilter := '';
                             AnalysisReportMgt.SetSourceNo(Rec,CurrentSourceTypeNoFilter);
                             CurrentSourceTypeFilterOnAfter;
                           END;
                            }

    { 25  ;2   ;Field     ;
                CaptionML=[ENU=Source No. Filter;
                           ENG=Source No. Filter];
                SourceExpr=CurrentSourceTypeNoFilter;
                OnValidate=BEGIN
                             CurrentSourceTypeNoFilterOnAft;
                           END;

                OnLookup=BEGIN
                           AnalysisReportMgt.LookupSourceNo(Rec,CurrentSourceTypeFilter,CurrentSourceTypeNoFilter);
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1906098301;1;Group  ;
                CaptionML=[ENU=Matrix Options;
                           ENG=Matrix Options] }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=View by;
                           ENG=View by];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 ENG=Day,Week,Month,Quarter,Year,Accounting Period];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                           END;
                            }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Column Set;
                           ENG=Column Set];
                SourceExpr=ColumnsSet }

    { 1040008;2;Field     ;
                Name=Calendar Source;
                OptionCaptionML=[ENU=" ,System Calendar,Acc. Period Calendar";
                                 ENG=" ,System Calendar,Acc. Period Calendar"];
                SourceExpr=CalendarSource;
                OnValidate=BEGIN
                             PeriodFormMgt.SetCalendarSource(CalendarSource);
                             FindPeriod('');
                           END;
                            }

  }
  CODE
  {
    VAR
      GLSetup@1017 : Record 98;
      AnalysisColumn@1003 : TEMPORARY Record 7118;
      ItemAnalysisView@1016 : Record 7152;
      AnalysisLine@1020 : Record 7114;
      InvtAnalysisMatrix@1004 : Page 9201;
      AnalysisReportMgt@1000 : Codeunit 7110;
      CurrentReportName@1005 : Code[10];
      CurrentLineTemplate@1001 : Code[10];
      CurrentColumnTemplate@1002 : Code[10];
      NewCurrentReportName@1010 : Code[10];
      CurrentSourceTypeNoFilter@1015 : Code[1024];
      CurrentSourceTypeFilter@1014 : ' ,Customer,Vendor,Item';
      PeriodType@1006 : 'Day,Week,Month,Quarter,Year';
      Direction@1012 : 'Backward,Forward';
      NoOfColumns@1013 : Integer;
      FirstLineNo@1018 : Integer;
      LastLineNo@1019 : Integer;
      FirstColumn@1021 : Text[1024];
      LastColumn@1022 : Text[1024];
      MatrixColumnCaptions@1009 : ARRAY [32] OF Text[1024];
      PeriodFormMgt@1040001 : Codeunit 359;
      CalendarSource@1040000 : ' ,System,AccPeriod';

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Calendar@1001 : Record 2000000007;
    BEGIN
      AnalysisReportMgt.SetCalendarSource(CalendarSource,PeriodType);
      IF GETFILTER("Date Filter") <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",GETFILTER("Date Filter"));
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      END;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
      IF GETRANGEMIN("Date Filter") = GETRANGEMAX("Date Filter") THEN
        SETRANGE("Date Filter",GETRANGEMIN("Date Filter"));
    END;

    LOCAL PROCEDURE ValidateAnalysisTemplateName@3();
    VAR
      AnalysisLineTemplate@1001 : Record 7112;
      PrevItemAnalysisView@1000 : Record 7152;
    BEGIN
      IF AnalysisLineTemplate.GET(GETRANGEMAX("Analysis Area"),CurrentLineTemplate) THEN
        IF (AnalysisLineTemplate."Default Column Template Name" <> '') AND
           (CurrentColumnTemplate <> AnalysisLineTemplate."Default Column Template Name")
        THEN BEGIN
          CurrentColumnTemplate := AnalysisLineTemplate."Default Column Template Name";
          AnalysisReportMgt.OpenColumns(CurrentColumnTemplate,Rec,AnalysisColumn);
          AnalysisReportMgt.CopyColumnsToTemp(Rec,CurrentColumnTemplate,AnalysisColumn);
        END;

      IF AnalysisLineTemplate."Item Analysis View Code" <> ItemAnalysisView.Code THEN BEGIN
        PrevItemAnalysisView := ItemAnalysisView;
        IF AnalysisLineTemplate."Item Analysis View Code" <> '' THEN
          ItemAnalysisView.GET(GETRANGEMAX("Analysis Area"),AnalysisLineTemplate."Item Analysis View Code")
        ELSE BEGIN
          CLEAR(ItemAnalysisView);
          ItemAnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
          ItemAnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
        END;
        IF PrevItemAnalysisView."Dimension 1 Code" <> ItemAnalysisView."Dimension 1 Code" THEN
          SETRANGE("Dimension 1 Filter");
        IF PrevItemAnalysisView."Dimension 2 Code" <> ItemAnalysisView."Dimension 2 Code" THEN
          SETRANGE("Dimension 2 Filter");
        IF PrevItemAnalysisView."Dimension 3 Code" <> ItemAnalysisView."Dimension 3 Code" THEN
          SETRANGE("Dimension 3 Filter");
      END;
    END;

    LOCAL PROCEDURE ValidateReportName@1();
    VAR
      AnalysisReportName@1000 : Record 7111;
    BEGIN
      IF AnalysisReportName.GET(GETRANGEMAX("Analysis Area"),CurrentReportName) THEN BEGIN
        IF AnalysisReportName."Analysis Line Template Name" <> '' THEN
          CurrentLineTemplate := AnalysisReportName."Analysis Line Template Name";
        IF AnalysisReportName."Analysis Column Template Name" <> '' THEN
          CurrentColumnTemplate := AnalysisReportName."Analysis Column Template Name";
      END;
    END;

    LOCAL PROCEDURE GetCaption@6() : Text[250];
    VAR
      AnalysisReportName@1000 : Record 7111;
    BEGIN
      IF CurrentReportName <> '' THEN
        IF AnalysisReportName.GET("Analysis Area"::Inventory,CurrentReportName) THEN
          EXIT(AnalysisReportName.Name + ' ' + AnalysisReportName.Description);
    END;

    PROCEDURE SetFilters@4();
    BEGIN
      AnalysisColumn.RESET;
      AnalysisColumn.SETRANGE("Analysis Area","Analysis Area"::Inventory);
      AnalysisColumn.SETRANGE("Analysis Column Template",CurrentColumnTemplate);

      AnalysisLine.COPY(Rec);
      AnalysisLine.SETRANGE("Analysis Area","Analysis Area"::Inventory);
      AnalysisLine.SETRANGE("Analysis Line Template Name",CurrentLineTemplate);
    END;

    LOCAL PROCEDURE ColumnsSet@7() : Text[80];
    BEGIN
      IF FirstColumn = LastColumn THEN
        EXIT(FirstColumn);

      EXIT(FirstColumn + '..' + LastColumn);
    END;

    LOCAL PROCEDURE SetPointsAnalysisColumn@22();
    VAR
      AnalysisColumn2@1013 : Record 7118;
      tmpFirstColumn@1000 : Text[80];
      tmpLastColumn@1001 : Text[80];
      tmpFirstLineNo@1003 : Integer;
      tmpLastLineNo@1002 : Integer;
    BEGIN
      AnalysisColumn2.SETRANGE("Analysis Area",AnalysisColumn2."Analysis Area"::Inventory);
      AnalysisColumn2.SETRANGE("Analysis Column Template",CurrentColumnTemplate);

      IF (Direction = Direction::Forward) OR
         (FirstColumn = '')
      THEN BEGIN
        IF LastColumn = '' THEN BEGIN
          AnalysisColumn2.FIND('-');
          tmpFirstColumn := AnalysisColumn2."Column No.";
          tmpFirstLineNo := AnalysisColumn2."Line No.";
          AnalysisColumn2.NEXT(NoOfColumns - 1);
          tmpLastColumn := AnalysisColumn2."Column No.";
          tmpLastLineNo := AnalysisColumn2."Line No.";
        END ELSE BEGIN
          IF AnalysisColumn2.GET(AnalysisColumn2."Analysis Area"::Inventory,CurrentColumnTemplate,LastLineNo) THEN;
          AnalysisColumn2.NEXT(1);
          tmpFirstColumn := AnalysisColumn2."Column No.";
          tmpFirstLineNo := AnalysisColumn2."Line No.";
          AnalysisColumn2.NEXT(NoOfColumns - 1);
          tmpLastColumn := AnalysisColumn2."Column No.";
          tmpLastLineNo := AnalysisColumn2."Line No.";
        END;
      END ELSE BEGIN
        IF AnalysisColumn2.GET(AnalysisColumn2."Analysis Area"::Inventory,CurrentColumnTemplate,FirstLineNo) THEN;
        AnalysisColumn2.NEXT(-1);
        tmpLastColumn := AnalysisColumn2."Column No.";
        tmpLastLineNo := AnalysisColumn2."Line No.";
        AnalysisColumn2.NEXT(-NoOfColumns + 1);
        tmpFirstColumn := AnalysisColumn2."Column No.";
        tmpFirstLineNo := AnalysisColumn2."Line No.";
      END;

      IF (tmpFirstColumn = tmpLastColumn) AND
         ((tmpLastColumn = LastColumn) OR (tmpFirstColumn = FirstColumn))
      THEN
        EXIT;

      FirstColumn := tmpFirstColumn;
      LastColumn := tmpLastColumn;
      FirstLineNo := tmpFirstLineNo;
      LastLineNo := tmpLastLineNo;
    END;

    LOCAL PROCEDURE FillMatrixColumns@8();
    VAR
      AnalysisColumn2@1013 : Record 7118;
      i@1004 : Integer;
    BEGIN
      AnalysisColumn2.SETRANGE("Analysis Area",AnalysisColumn2."Analysis Area"::Inventory);
      AnalysisColumn2.SETRANGE("Analysis Column Template",CurrentColumnTemplate);
      AnalysisColumn2.SETRANGE("Line No.",FirstLineNo,LastLineNo);
      i := 1;

      IF AnalysisColumn2.FIND('-') THEN
        REPEAT
          MatrixColumnCaptions[i] := AnalysisColumn2."Column Header";
          i := i + 1;
        UNTIL (AnalysisColumn2.NEXT = 0) OR (i > ARRAYLEN(MatrixColumnCaptions));
    END;

    LOCAL PROCEDURE ClearPoints@5();
    BEGIN
      CLEAR(FirstColumn);
      CLEAR(LastColumn);
    END;

    PROCEDURE SetReportName@10(NewReportName@1000 : Code[10]);
    BEGIN
      NewCurrentReportName := NewReportName;
    END;

    LOCAL PROCEDURE CurrentReportNameOnAfterValida@19043622();
    BEGIN
      CurrPage.SAVERECORD;
      ValidateReportName;
      AnalysisReportMgt.SetAnalysisLineTemplName(CurrentLineTemplate,Rec);
      ValidateAnalysisTemplateName;
      AnalysisReportMgt.CopyColumnsToTemp(Rec,CurrentColumnTemplate,AnalysisColumn);
      CurrPage.UPDATE(FALSE);
      ClearPoints;
      SetPointsAnalysisColumn;
    END;

    LOCAL PROCEDURE CurrentLineTemplateOnAfterVali@19002239();
    BEGIN
      CurrPage.SAVERECORD;
      AnalysisReportMgt.SetAnalysisLineTemplName(CurrentLineTemplate,Rec);
      ValidateAnalysisTemplateName;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CurrentColumnTemplateOnAfterVa@19007060();
    BEGIN
      AnalysisReportMgt.CopyColumnsToTemp(Rec,CurrentColumnTemplate,AnalysisColumn);
      CurrPage.UPDATE(FALSE);
      ClearPoints;
      SetPointsAnalysisColumn;
    END;

    LOCAL PROCEDURE CurrentSourceTypeNoFilterOnAft@19027013();
    BEGIN
      AnalysisReportMgt.SetSourceNo(Rec,CurrentSourceTypeNoFilter);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CurrentSourceTypeFilterOnAfter@19072332();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

