OBJECT Codeunit 9087007 Buying Sheet Status Management
{
  OBJECT-PROPERTIES
  {
    Date=14/09/16;
    Time=10:34:33 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    TableNo=9087020;
    OnRun=BEGIN
            Release(Rec);
          END;

  }
  CODE
  {
    VAR
      nSembleSetup@1000000006 : Record 9087000;
      BuyingSheetHeader@1000000000 : Record 9087020;
      EnhancedWorkflowInterface@1000000005 : Codeunit 9062262;
      nSembleWorkflowManagement@1000000004 : Codeunit 9087004;
      TnpToolkit@1000000003 : Codeunit 9062230;

    PROCEDURE Release@1000000002(VAR pBuyingSheetHeader@1000000000 : Record 9087020) : Boolean;
    VAR
      lApprovalEnabled@1000000003 : Boolean;
      lStatus@1000000002 : Option;
      lResult@1000000001 : Boolean;
    BEGIN
      //DOC NS2016.16 - Releaseing buying sheet
      //  <> pBuyingSheetHeader: The buying sheet header
      //  <- TRUE = Released, FALSE = Already released

      lResult := FALSE;

      BuyingSheetHeader := pBuyingSheetHeader;

      IF (BuyingSheetHeader.Status <> BuyingSheetHeader.Status::Released) THEN
      BEGIN
        BuyingSheetHeader.TESTFIELD("Vendor No.");
        BuyingSheetHeader.TESTFIELD("Location Code");
        lApprovalEnabled := nSembleWorkflowManagement.BuyingSheetApprovalEnabled();
        lStatus := TnpToolkit.IIFInteger(lApprovalEnabled, BuyingSheetHeader.Status::"Pending Approval", BuyingSheetHeader.Status::Released);
        BuyingSheetHeader.AllocateOverhead();
        BuyingSheetHeader.VALIDATE(Status, lStatus);
        BuyingSheetHeader.MODIFY(TRUE);
        IF (lStatus = BuyingSheetHeader.Status::"Pending Approval") THEN
        BEGIN
          nSembleSetup.VerifyAndGet();
          EnhancedWorkflowInterface.Simple(nSembleSetup."Buying Sheet Appr. Workf. No.", BuyingSheetHeader.GETPOSITION(FALSE));
        END;
        lResult := TRUE;
      END;

      pBuyingSheetHeader := BuyingSheetHeader;

      EXIT(lResult);
    END;

    PROCEDURE Reopen@1000000004(VAR pBuyingSheetHeader@1000000000 : Record 9087020) : Boolean;
    VAR
      lApprovalEnabled@1000000002 : Boolean;
      lResult@1000000001 : Boolean;
    BEGIN
      //DOC NS2016.16 - Reopening buying sheet
      //  <> pBuyingSheetHeader: The buying sheet header
      //  <- TRUE = Reopened, FALSE = Already opened

      lResult := FALSE;

      BuyingSheetHeader := pBuyingSheetHeader;
      IF (BuyingSheetHeader.Status <> BuyingSheetHeader.Status::Open) THEN
      BEGIN
        IF (BuyingSheetHeader.Status <> BuyingSheetHeader.Status::"Pending Approval") THEN
        BEGIN
          lApprovalEnabled := nSembleWorkflowManagement.BuyingSheetApprovalEnabled();
          IF (lApprovalEnabled) THEN
          BEGIN
            nSembleSetup.VerifyAndGet();
            EnhancedWorkflowInterface.CancelWorkflow(nSembleSetup."Buying Sheet Appr. Workf. No.", BuyingSheetHeader.GETPOSITION(FALSE));
          END;
        END;
        BuyingSheetHeader.GET(BuyingSheetHeader."No.");
        BuyingSheetHeader.VALIDATE(Status, BuyingSheetHeader.Status::Open);
        BuyingSheetHeader.MODIFY(TRUE);
        lResult := TRUE;
      END;
      pBuyingSheetHeader := BuyingSheetHeader;

      EXIT(lResult);
    END;

    BEGIN
    {
      //DOC NS2016.16 JH 06/09/2016 - Created
    }
    END.
  }
}

