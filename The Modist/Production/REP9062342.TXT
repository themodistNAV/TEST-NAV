OBJECT Report 9062342 Add Migration XMLports
{
  OBJECT-PROPERTIES
  {
    Date=25/11/15;
    Time=[ 3:45:04 PM];
    Version List=NA2016.11;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Add Migration XMLports;
               ENG=Add Migration XMLports];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Ok := FALSE;
                 END;

    OnPreReport=BEGIN
                  Ok := TRUE;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      InsertAllowed=No;
      DeleteAllowed=No;
      SourceTable=Table9062730;
      SourceTableTemporary=Yes;
      OnInit=BEGIN
               OnlyMigrationXmlPorts := TRUE;
             END;

      OnOpenPage=BEGIN
                   UpdateRequestPage();
                 END;

      OnClosePage=BEGIN
                    RequestOptionsPage.SETSELECTIONFILTER(Rec);
                    IF (Rec.FINDSET()) THEN
                    REPEAT
                      SelectedXmlPortBuffer := Rec;
                      SelectedXmlPortBuffer.INSERT();
                    UNTIL (Rec.NEXT() = 0);
                  END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  ContainerType=ContentArea }

      { 1000000008;1;Group  ;
                  Name=XmlPortsGroup;
                  CaptionML=[ENU=XMLports;
                             ENG=XMLports];
                  GroupType=Group }

      { 1000000001;2;Group  ;
                  Name=XmlPortsRepeater;
                  CaptionML=[ENU=XMLports;
                             ENG=XMLports];
                  GroupType=Repeater }

      { 1000000002;3;Field  ;
                  Name=IdField;
                  CaptionML=[ENU=ID;
                             ENG=ID];
                  SourceExpr=ID;
                  Editable=FALSE }

      { 1000000003;3;Field  ;
                  Name=NameField;
                  CaptionML=[ENU=Name;
                             ENG=Name];
                  SourceExpr=Name;
                  Editable=FALSE }

      { 1000000005;1;Group  ;
                  Name=OptionsGroup;
                  CaptionML=[ENU=Options;
                             ENG=Options];
                  GroupType=Group }

      { 1000000007;2;Field  ;
                  Name=OnlyMigrationXmlPortsField;
                  CaptionML=[ENU=Only Migration XMLports;
                             ENG=Only Migration XMLports];
                  SourceExpr=OnlyMigrationXmlPorts;
                  OnValidate=BEGIN
                               UpdateRequestPage();
                             END;
                              }

      { 1000000006;2;Field  ;
                  Name=AddField;
                  CaptionML=[ENU=Add;
                             ENG=Add];
                  SourceExpr=Add }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ParentMigrationLine@1000000001 : Record 9062374;
      SelectedXmlPortBuffer@1000000007 : TEMPORARY Record 9062730;
      ParentMigrationLinePresent@1000000002 : Boolean;
      OnlyMigrationXmlPorts@1000000000 : Boolean;
      Add@1000000003 : ',To the Current Line,At Root Level';
      Ok@1000000004 : Boolean;

    PROCEDURE Initialise@1000000006(pParentMigrationLine@1000000001 : Record 9062374;pOnlyMigrationXmlPorts@1000000000 : Boolean);
    BEGIN
      ParentMigrationLine := pParentMigrationLine;
      OnlyMigrationXmlPorts := pOnlyMigrationXmlPorts;

      IF (ParentMigrationLine.ID <> 0) THEN
        CASE (ParentMigrationLine.Type) OF
          ParentMigrationLine.Type::Group:
            ParentMigrationLinePresent := TRUE;
          ParentMigrationLine.Type::XMLport:
            ParentMigrationLinePresent := (ParentMigrationLine."Parent ID" <> 0);
          ELSE
            ParentMigrationLinePresent := FALSE;
        END
      ELSE
        ParentMigrationLinePresent := FALSE;

      IF (ParentMigrationLinePresent) THEN
        Add := Add::"To the Current Line"
      ELSE
        Add := Add::"At Root Level";
    END;

    PROCEDURE GetOk@1000000000() : Boolean;
    BEGIN
      EXIT(Ok);
    END;

    PROCEDURE GetSelectedXmlPorts@1000000001(VAR pNameValueBuffer@1000000000 : Record 9062730);
    BEGIN
      pNameValueBuffer.COPY(SelectedXmlPortBuffer, TRUE);
    END;

    PROCEDURE GetAdd@1000000002() : Integer;
    BEGIN
      EXIT(Add);
    END;

    LOCAL PROCEDURE UpdateRequestPage@1000000003();
    VAR
      lAllObjWithCaption@1000000001 : Record 2000000058;
      lSelectedBuffer@1000000000 : TEMPORARY Record 2000000026;
    BEGIN
      Rec.RESET();
      Rec.SETRANGE(Selected, TRUE);
      IF (Rec.FINDSET()) THEN
      REPEAT
        lSelectedBuffer.Number := Rec.ID;
        lSelectedBuffer.INSERT();
      UNTIL (Rec.NEXT() = 0);

      Rec.RESET();
      Rec.DELETEALL();

      lAllObjWithCaption.RESET();
      lAllObjWithCaption.SETRANGE("Object Type", lAllObjWithCaption."Object Type"::XMLport);
      IF (OnlyMigrationXmlPorts) THEN
        lAllObjWithCaption.SETFILTER("Object Name", 'Migrate*');
      IF (lAllObjWithCaption.FINDSET()) THEN
      REPEAT
        CLEAR(Rec);
        Rec.INIT();
        Rec.ID := lAllObjWithCaption."Object ID";
        Rec.Name := lAllObjWithCaption."Object Caption";
        Rec.Selected := (lSelectedBuffer.GET(lAllObjWithCaption."Object ID"));
        Rec.INSERT();
      UNTIL (lAllObjWithCaption.NEXT() = 0);
      IF (Rec.FINDFIRST()) THEN;
    END;

    LOCAL PROCEDURE Select@1000000008(pSelect@1000000000 : Boolean);
    VAR
      lId@1000000001 : Integer;
    BEGIN
      lId := Rec.ID;
      IF (Rec.FINDSET()) THEN
      REPEAT
        Rec.Selected := pSelect;
        Rec.MODIFY();
      UNTIL (Rec.NEXT() = 0);
      IF (Rec.GET(lId)) THEN;
    END;

    BEGIN
    {
      //DOC NA2016.11 JH 24/11/2015 - Created
    }
    END.
  }
  RDLDATA
  {
  }
}

