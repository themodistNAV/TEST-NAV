OBJECT Report 9062230 Create Demonstration Campaigns
{
  OBJECT-PROPERTIES
  {
    Date=06/04/16;
    Time=12:00:00 PM;
    Version List=NA2016.14;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Demonstration Campaigns;
               ENG=Create Demonstration Campaigns];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  InitialiseInteraction();

                  CheckRequestData();
                  GenerateSalespeopleBuffer();
                  GenerateItemBuffer();
                  GenerateCampaignStatusBuffer();
                  CreateCampaigns();

                  FinaliseInteraction();
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 9062230; ;Container ;
                  ContainerType=ContentArea }

      { 9062231;1;Field     ;
                  Name=NoOfCampaignsField;
                  CaptionML=[ENU=No. of Campaigns;
                             ENG=No. of Campaigns];
                  SourceExpr=NoOfCampaigns;
                  MinValue=0;
                  MaxValue=10000 }

      { 9062232;1;Field     ;
                  Name=StartDateField;
                  CaptionML=[ENU=Start Date;
                             ENG=Start Date];
                  SourceExpr=StartDate }

      { 9062233;1;Field     ;
                  Name=EndDateField;
                  CaptionML=[ENU=End Date;
                             ENG=End Date];
                  SourceExpr=EndDate }

      { 9062234;1;Field     ;
                  Name=MaximumLengthField;
                  CaptionML=[ENU=Maximum Length;
                             ENG=Maximum Length];
                  SourceExpr=MaximumLength;
                  MinValue=0 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Salesperson@9062237 : Record 13;
      SalespersonBuffer@9062238 : TEMPORARY Record 9062730;
      SalespersonBufferCount@9062239 : Integer;
      Campaign@9062233 : Record 5071;
      Item@9062235 : Record 27;
      ItemBuffer@9062234 : TEMPORARY Record 9062730;
      ItemBufferCount@9062236 : Integer;
      CampaignStatus@9062240 : Record 5073;
      CampaignStatusBuffer@9062241 : TEMPORARY Record 9062730;
      CampaignStatusBufferCount@9062242 : Integer;
      NoOfCampaigns@9062230 : Integer;
      StartDate@9062231 : Date;
      EndDate@9062232 : Date;
      MaximumLength@9062244 : Integer;
      DialogManagement@9062243 : Codeunit 9062249;

    LOCAL PROCEDURE CheckRequestData@9062233();
    VAR
      eEmptyNoOfCampaigns@9062233 : TextConst 'ENU=Please provide "%1";ENG=Please provide "%1"';
      eNoOfCampaigns@9062232 : TextConst 'ENU=No. of Campaigns;ENG=No. of Campaigns';
      eStartDate@9062231 : TextConst 'ENU=Start Date;ENG=Start Date';
      eEndDate@9062230 : TextConst 'ENU=End Date;ENG=End Date';
      lDateBuffer@9062234 : Date;
    BEGIN
      IF (NoOfCampaigns = 0) THEN
        ERROR(eEmptyNoOfCampaigns, eNoOfCampaigns);
      IF (StartDate = 0D) THEN
        ERROR(eEmptyNoOfCampaigns, eStartDate);
      IF (EndDate = 0D) THEN
        ERROR(eEmptyNoOfCampaigns, eEndDate);

      IF (EndDate < StartDate) THEN
      BEGIN
        lDateBuffer := EndDate;
        EndDate := StartDate;
        StartDate := lDateBuffer;
      END;
    END;

    LOCAL PROCEDURE GenerateSalespeopleBuffer@9062247();
    VAR
      eNoSalespeople@9062230 : TextConst 'ENU=There are no salespeople in the system.;ENG=There are no salespeople in the system.';
    BEGIN
      SalespersonBuffer.RESET();
      SalespersonBuffer.DELETEALL();
      SalespersonBufferCount := 0;

      Salesperson.RESET();
      IF (Salesperson.ISEMPTY()) THEN
        ERROR(eNoSalespeople);
      IF (Salesperson.FINDSET()) THEN
      REPEAT
        SalespersonBufferCount += 1;
        SalespersonBuffer.ID := SalespersonBufferCount;
        SalespersonBuffer.Value := Salesperson.Code;
        SalespersonBuffer.INSERT();
      UNTIL (Salesperson.NEXT() = 0);
    END;

    LOCAL PROCEDURE GenerateItemBuffer@9062240();
    BEGIN
      ItemBuffer.RESET();
      ItemBuffer.DELETEALL();
      ItemBufferCount := 0;

      Item.RESET();
      IF (Item.FINDSET()) THEN
      REPEAT
        ItemBufferCount += 1;
        ItemBuffer.ID := ItemBufferCount;
        ItemBuffer.Name := Item.Description;
        ItemBuffer.Value := Item."No.";
        ItemBuffer.INSERT();
      UNTIL (Item.NEXT() = 0);
    END;

    LOCAL PROCEDURE GenerateCampaignStatusBuffer@9062230();
    VAR
      eNoCampaignStatuses@9062230 : TextConst 'ENU=There are no campaign statuses in the system.;ENG=There are no campaign statuses in the system.';
    BEGIN
      CampaignStatusBuffer.RESET();
      CampaignStatusBuffer.DELETEALL();
      CampaignStatusBufferCount := 0;

      CampaignStatus.RESET();
      IF (CampaignStatus.ISEMPTY()) THEN
        ERROR(eNoCampaignStatuses);
      IF (CampaignStatus.FINDSET()) THEN
      REPEAT
        CampaignStatusBufferCount += 1;
        CampaignStatusBuffer.ID := CampaignStatusBufferCount;
        CampaignStatusBuffer.Name := CampaignStatus.Description;
        CampaignStatusBuffer.Value := CampaignStatus.Code;
        CampaignStatusBuffer.INSERT();
      UNTIL (CampaignStatus.NEXT() = 0);
    END;

    LOCAL PROCEDURE CreateCampaigns@9062236();
    VAR
      lIndex@9062230 : Integer;
      lPeriodLength@9062232 : Integer;
      lPeriodStartOffset@9062231 : Integer;
      lPeriodEndOffset@9062233 : Integer;
      lMaximumEndOffset@9062234 : Integer;
    BEGIN
      RANDOMIZE;
      lPeriodLength := (EndDate - StartDate);
      FOR lIndex := 1 TO NoOfCampaigns DO
      BEGIN
        DialogManagement.StepCounter(1);

        SalespersonBuffer.GET(RANDOM(SalespersonBufferCount));
        ItemBuffer.GET(RANDOM(ItemBufferCount));
        CampaignStatusBuffer.GET(RANDOM(CampaignStatusBufferCount));

        lPeriodStartOffset := RANDOM(lPeriodLength);
        lMaximumEndOffset := lPeriodLength - lPeriodStartOffset;
        IF (lMaximumEndOffset > MaximumLength) THEN
          lMaximumEndOffset := MaximumLength;
        lPeriodEndOffset := RANDOM(lMaximumEndOffset);

        CLEAR(Campaign);
        Campaign.INIT();
        Campaign.INSERT(TRUE);
        Campaign.VALIDATE(Description, ItemBuffer.Name);
        Campaign.VALIDATE("Starting Date", StartDate + lPeriodStartOffset);
        Campaign.VALIDATE("Ending Date", Campaign."Starting Date" + lPeriodEndOffset);
        Campaign.VALIDATE("Salesperson Code", SalespersonBuffer.Value);
        Campaign.VALIDATE("Status Code", CampaignStatusBuffer.Value);
        Campaign.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE InitialiseInteraction@9062235();
    VAR
      eTitle@9062231 : TextConst 'ENU=Creating campaigns...;ENG=Creating campaigns...';
      eProgressCounter@9062230 : TextConst 'ENU=Progress;ENG=Progress';
    BEGIN
      DialogManagement.Add(eProgressCounter, 10, 1, 30, TRUE, TRUE);
      DialogManagement.Open(eTitle);
      DialogManagement.SetCounterCount(1, NoOfCampaigns);
    END;

    LOCAL PROCEDURE FinaliseInteraction@9062237();
    BEGIN
      DialogManagement.Close();
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 24/10/2015 - Created
      //DOC NA2016.12 JH 27/12/2015 - Modified, new version
      //DOC NA2016.12 JH 27/01/2016 - Bugfixes
      //DOC NA2016.14 JH 06/04/2016 - Bugfixes
    }
    END.
  }
  RDLDATA
  {
  }
}

