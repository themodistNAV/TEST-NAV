OBJECT Codeunit 5651 Insurance Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5635;
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;ENG=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text001@1001 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;ENG=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      GLSetup@1002 : Record 98;
      FASetup@1003 : Record 5603;
      DimMgt@1004 : Codeunit 408;
      CallNo@1005 : Integer;

    PROCEDURE RunCheck@1(VAR InsuranceJnlLine@1000 : Record 5635);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        IF "Insurance No." = '' THEN
          EXIT;
        TESTFIELD("Insurance No.");
        TESTFIELD("Document No.");
        TESTFIELD("Posting Date");
        TESTFIELD("FA No.");
        IF CallNo = 0 THEN BEGIN
          FASetup.GET;
          FASetup.TESTFIELD("Insurance Depr. Book");
        END;
        CallNo := 1;

        IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
          ERROR(
            Text000,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Insurance;
        No[1] := "Insurance No.";
        IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
          IF "Line No." <> 0 THEN
            ERROR(
              Text001,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          ELSE
            ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    BEGIN
    END.
  }
}

