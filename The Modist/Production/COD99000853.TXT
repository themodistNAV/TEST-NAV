OBJECT Codeunit 99000853 Calc. Low-level code
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            FindTopLevel;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Calculate low-level code;ENG=Calculate low-level code';
      Text001@1001 : TextConst 'ENU=No. #2################## @3@@@@@@@@@@@@@;ENG=No. #2################## @3@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=Top-Level Items;ENG=Top-Level Items';
      Text003@1003 : TextConst 'ENU=BOMs;ENG=BOMs';
      HideDialogs@1007 : Boolean;

    LOCAL PROCEDURE FindTopLevel@1();
    VAR
      ProdBOMLine@1000 : Record 99000772;
      BOMComp@1006 : Record 90;
      Item@1001 : Record 27;
      ProdBOMHeader@1002 : Record 99000771;
      ProdBOMHeader2@1009 : Record 99000771;
      CalcLowLevelCode@1005 : Codeunit 99000793;
      Window@1004 : Dialog;
      WindowUpdateDateTime@1007 : DateTime;
      NoofItems@1003 : Integer;
      CountOfRecords@1008 : Integer;
      HasProductionBOM@1010 : Boolean;
    BEGIN
      NoofItems := 0;
      IF NOT HideDialogs THEN
        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;
      Window.OPEN(
        '#1################## \\' +
        Text001);
      WindowUpdateDateTime := CURRENTDATETIME;

      Window.UPDATE(1,Text002);

      Item.LOCKTABLE;
      Item.MODIFYALL("Low-Level Code",0);
      ProdBOMHeader.LOCKTABLE;
      ProdBOMHeader.MODIFYALL("Low-Level Code",0);

      ProdBOMLine.SETCURRENTKEY(Type,"No.");
      CountOfRecords := Item.COUNT;
      IF Item.FIND('-') THEN
        REPEAT
          IF CURRENTDATETIME - WindowUpdateDateTime > 2000 THEN BEGIN
            Window.UPDATE(2,Item."No.");
            Window.UPDATE(3,ROUND(NoofItems / CountOfRecords * 10000,1));
            WindowUpdateDateTime := CURRENTDATETIME;
          END;

          HasProductionBOM := ProdBOMHeader.GET(Item."Production BOM No.");
          IF (ProdBOMHeader."Low-Level Code" = 0) OR NOT HasProductionBOM
          THEN BEGIN
            ProdBOMLine.SETRANGE("No.",Item."No.");
            ProdBOMLine.SETRANGE(Type,ProdBOMLine.Type::Item);

            BOMComp.SETRANGE(Type,BOMComp.Type::Item);
            BOMComp.SETRANGE("No.",Item."No.");

            IF ProdBOMLine.ISEMPTY AND BOMComp.ISEMPTY THEN BEGIN
              // handle items which are not part of any BOMs
              Item.CALCFIELDS("Assembly BOM");
              IF Item."Assembly BOM" THEN
                CalcLowLevelCode.RecalcAsmLowerLevels(Item."No.",CalcLowLevelCode.CalcLevels(3,Item."No.",0,0),TRUE);
              IF HasProductionBOM THEN
                CalcLevelsForBOM(ProdBOMHeader);
            END ELSE
              IF HasProductionBOM THEN BEGIN
                IF ProdBOMLine.FIND('-') THEN
                  REPEAT
                    // handle items which are part of un-certified, active BOMs
                    IF ProdBOMHeader2.GET(ProdBOMLine."Production BOM No.") THEN
                      IF ProdBOMHeader2.Status IN [ProdBOMHeader2.Status::New,ProdBOMHeader2.Status::"Under Development"] THEN
                        CalcLevelsForBOM(ProdBOMHeader);
                  UNTIL ProdBOMLine.NEXT = 0;
              END;
          END;

          NoofItems := NoofItems + 1;
        UNTIL Item.NEXT = 0;

      NoofItems := 0;
      Window.UPDATE(1,Text003);
      ProdBOMHeader.RESET;
      ProdBOMHeader.SETCURRENTKEY(Status);
      ProdBOMHeader.SETRANGE(Status,ProdBOMHeader.Status::Certified);
      ProdBOMHeader.SETRANGE("Low-Level Code",0);
      CountOfRecords := ProdBOMHeader.COUNT;
      IF ProdBOMHeader.FIND('-') THEN
        REPEAT
          IF CURRENTDATETIME - WindowUpdateDateTime > 2000 THEN BEGIN
            Window.UPDATE(2,ProdBOMHeader."No.");
            Window.UPDATE(3,ROUND(NoofItems / CountOfRecords * 10000,1));
            WindowUpdateDateTime := CURRENTDATETIME;
          END;
          ProdBOMHeader2 := ProdBOMHeader;
          CalcLevelsForBOM(ProdBOMHeader2);
          NoofItems := NoofItems + 1;
        UNTIL ProdBOMHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcLevelsForBOM@10(VAR ProdBOM@1000 : Record 99000771);
    VAR
      ProdBOMLine@1004 : Record 99000772;
      CalcLowLevelCode@1001 : Codeunit 99000793;
    BEGIN
      IF ProdBOM.Status = ProdBOM.Status::Certified THEN BEGIN
        ProdBOM."Low-Level Code" := CalcLowLevelCode.CalcLevels(ProdBOMLine.Type::"Production BOM",ProdBOM."No.",0,0);
        CalcLowLevelCode.RecalcLowerLevels(ProdBOM."No.",ProdBOM."Low-Level Code",TRUE);
        ProdBOM.MODIFY;
      END;
    END;

    PROCEDURE SetHideDialogs@4(NewHideDialogs@1000 : Boolean);
    BEGIN
      HideDialogs := NewHideDialogs;
    END;

    BEGIN
    END.
  }
}

