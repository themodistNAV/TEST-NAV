OBJECT Table 9087013 Style Colour
{
  OBJECT-PROPERTIES
  {
    Date=04/02/18;
    Time=[ 1:03:50 PM];
    Modified=Yes;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Style No.");
               Rec.TESTFIELD("Colour Code");
               Style.GET(Rec."Style No.");
               Rec.VALIDATE("Vendor No.", Style."Vendor No.");
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               //Rec.TESTFIELD("Item No.", '');

               StyleColourSize.RESET();
               StyleColourSize.SETRANGE("Style No.", Rec."Style No.");
               StyleColourSize.SETRANGE("Colour Code", Rec."Colour Code");
               StyleColourSize.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Style Colour;
               ENG=Style Colour];
    LookupPageID=Page9087036;
    DrillDownPageID=Page9087036;
  }
  FIELDS
  {
    { 1   ;   ;Style No.           ;Code20        ;TableRelation=Style;
                                                   CaptionML=[ENU=Style No.;
                                                              ENG=Style No.] }
    { 2   ;   ;Colour Code         ;Code20        ;TableRelation=Colour;
                                                   OnValidate=BEGIN
                                                                IF (Rec."Colour Code" <> xRec."Colour Code") THEN
                                                                BEGIN
                                                                  Rec.TESTFIELD("Item No.", '');
                                                                  Style.GET(Rec."Style No.");
                                                                  StyleManagement.UpdateStyleColourSizesForStyleColour(Style, xRec, Rec);
                                                                END;
                                                                Rec.CALCFIELDS("Colour Name");
                                                              END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 10  ;   ;Enabled             ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF (Rec.Enabled <> xRec.Enabled) THEN
                                                                  Rec.TESTFIELD("Item No.", '');
                                                              END;

                                                   CaptionML=[ENU=Enabled;
                                                              ENG=Enabled] }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                UpdateSizes(Rec.FIELDNO("Item No."));
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.] }
    { 30  ;   ;Sequence            ;Integer       ;CaptionML=[ENU=Sequence;
                                                              ENG=Sequence] }
    { 40  ;   ;Style Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Style.Description WHERE (No.=FIELD(Style No.)));
                                                   CaptionML=[ENU=Style Description;
                                                              ENG=Style Description];
                                                   Editable=No }
    { 41  ;   ;Colour Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Colour.Name WHERE (Code=FIELD(Colour Code)));
                                                   TableRelation=Colour;
                                                   CaptionML=[ENU=Colour Name;
                                                              ENG=Colour Name];
                                                   Editable=No }
    { 50  ;   ;Vendor Colour Code  ;Code50        ;TableRelation="Vendor Colour".Code WHERE (Vendor No.=FIELD(Vendor No.));
                                                   CaptionML=[ENU=Vendor Colour Code;
                                                              ENG=Vendor Colour Code];
                                                   Description=,Increased from 20 }
    { 60  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                lVendor@1000000000 : Record 23;
                                                              BEGIN
                                                                IF (Rec."Vendor No." <> xRec."Vendor No.") THEN
                                                                  Rec.VALIDATE("Vendor Colour Code", '');
                                                                Rec.CALCFIELDS("Vendor Name");
                                                              END;

                                                   ValidateTableRelation=Yes;
                                                   TestTableRelation=Yes;
                                                   CaptionML=[ENU=Vendor No.;
                                                              ENG=Vendor No.] }
    { 61  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              ENG=Vendor Name];
                                                   Editable=No }
    { 62  ;   ;Vendor Item No.     ;Text20        ;CaptionML=[ENU=Vendor Item No.;
                                                              ENG=Vendor Item No.] }
    { 70  ;   ;Picture             ;BLOB          ;CaptionML=[ENU=Picture;
                                                              ENG=Picture];
                                                   SubType=Bitmap }
    { 80  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ENG=Date Filter] }
    { 90  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              ENG=Global Dimension 1 Filter];
                                                   CaptionClass='1,3,1' }
    { 91  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              ENG=Global Dimension 2 Filter];
                                                   CaptionClass='1,3,2' }
    { 100 ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              ENG=Location Filter] }
    { 110 ;   ;Variant Filter      ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Filter;
                                                              ENG=Variant Filter] }
    { 120 ;   ;Inventory           ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry".Quantity WHERE (Item No.=FIELD(Item No.),
                                                                                                       Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                       Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                       Location Code=FIELD(Location Filter),
                                                                                                       Variant Code=FIELD(Variant Filter)));
                                                   CaptionML=[ENU=Inventory;
                                                              ENG=Inventory];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 130 ;   ;Purchases (Qty.)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry"."Invoiced Quantity" WHERE (Entry Type=CONST(Purchase),
                                                                                                                  Item No.=FIELD(Item No.),
                                                                                                                  Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                  Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                  Location Code=FIELD(Location Filter),
                                                                                                                  Variant Code=FIELD(Variant Filter),
                                                                                                                  Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Purchases (Qty.);
                                                              ENG=Purchases (Qty.)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 131 ;   ;Purchases (LCY)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Purchase Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Purchase),
                                                                                                                   Item No.=FIELD(Item No.),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Location Code=FIELD(Location Filter),
                                                                                                                   Variant Code=FIELD(Variant Filter),
                                                                                                                   Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Purchases (LCY);
                                                              ENG=Purchases (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 140 ;   ;Sales (Qty.)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Value Entry"."Invoiced Quantity" WHERE (Item Ledger Entry Type=CONST(Sale),
                                                                                                             Item No.=FIELD(Item No.),
                                                                                                             Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                             Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                             Location Code=FIELD(Location Filter),
                                                                                                             Variant Code=FIELD(Variant Filter),
                                                                                                             Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (Qty.);
                                                              ENG=Sales (Qty.)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 141 ;   ;Sales (LCY)         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Value Entry"."Sales Amount (Actual)" WHERE (Item Ledger Entry Type=CONST(Sale),
                                                                                                                Item No.=FIELD(Item No.),
                                                                                                                Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                Location Code=FIELD(Location Filter),
                                                                                                                Variant Code=FIELD(Variant Filter),
                                                                                                                Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (LCY);
                                                              ENG=Sales (LCY)];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 150 ;   ;Web Reference       ;Text100       ;CaptionML=[ENU=Web Reference;
                                                              ENG=Web Reference] }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Style No.,Colour Code                   ;Clustered=Yes }
    {    ;Style No.,Sequence,Colour Code           }
    {    ;Vendor No.,Vendor Colour Code            }
    {    ;Item No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Item@1000000004 : Record 27;
      Style@1000000003 : Record 9087012;
      StyleColourSize@1000000001 : Record 9087014;
      StyleManagement@1000000002 : Codeunit 9087006;
      TnpToolkit@1000000000 : Codeunit 9062230;

    PROCEDURE AddColours@1000000000();
    BEGIN
      //DOC NS2015.1 - Adding (selected) colours

      Rec.TESTFIELD("Style No.");
      StyleManagement.AddColoursToStyleColours(Rec."Style No.");
    END;

    PROCEDURE UpdateSizes@1000000002(pFieldId@1000000000 : Integer);
    BEGIN
      //DOC NS2015.1 - Updating sizes for specified field
      //  -> pFieldId: The field ID

      StyleColourSize.RESET();
      StyleColourSize.SETRANGE("Style No.", Rec."Style No.");
      StyleColourSize.SETRANGE("Colour Code", Rec."Colour Code");
      IF (StyleColourSize.FINDSET(TRUE)) THEN
      REPEAT
        CASE (pFieldId) OF
          Rec.FIELDNO("Item No."):
            StyleColourSize.VALIDATE("Item No.", Rec."Item No.");
        END;
        StyleColourSize.MODIFY(TRUE);
      UNTIL (StyleColourSize.NEXT() = 0);
    END;

    PROCEDURE EnableAllSizes@1000000005();
    BEGIN
      //DOC NS2015.1 - Enabling all sizes

      EnableDisableAllSizes(TRUE);
    END;

    PROCEDURE DisableAllSizes@1000000006();
    BEGIN
      //DOC NS2015.1 - Disabling all sizes

      EnableDisableAllSizes(FALSE);
    END;

    PROCEDURE EnableDisableAllSizes@1000000004(pEnable@1000000000 : Boolean);
    BEGIN
      //DOC NS2015.1 - Enabling or disabling all sizes
      //  -> pEnable: Enable flag

      StyleColourSize.RESET();
      StyleColourSize.SETRANGE("Style No.", Rec."Style No.");
      StyleColourSize.SETRANGE("Colour Code", Rec."Colour Code");
      IF (StyleColourSize.FINDSET(TRUE)) THEN
      REPEAT
        StyleColourSize.VALIDATE(Enabled, pEnable);
        StyleColourSize.MODIFY(TRUE);
      UNTIL (StyleColourSize.NEXT() = 0);
    END;

    PROCEDURE PictureInformation@1000000007() : Text[20];
    BEGIN
      //DOC NS2015.1 - Returning picture information
      //  <- The information

      Rec.CALCFIELDS(Picture);
      EXIT(FORMAT(Rec.Picture.HASVALUE));
    END;

    PROCEDURE ShowSizes@1000000001();
    BEGIN
      //DOC NS2015.1 - Showing sizes

      StyleColourSize.RESET();
      StyleColourSize.FILTERGROUP(2);
      StyleColourSize.SETRANGE("Style No.", Rec."Style No.");
      StyleColourSize.SETRANGE("Colour Code", Rec."Colour Code");
      StyleColourSize.FILTERGROUP(0);
      PAGE.RUN(PAGE::"Style Colour Sizes", StyleColourSize);
    END;

    PROCEDURE ShowItem@1000000003();
    BEGIN
      //DOC NS2015.1 - Showing item

      Rec.TESTFIELD("Item No.");
      Item.GET(Rec."Item No.");
      PAGE.RUN(PAGE::"Item Card", Item);
    END;

    PROCEDURE ShowPicture@1000000009();
    VAR
      lStyleColour@1000000000 : Record 9087013;
    BEGIN
      //DOC NS2015.1 - Showing picture

      lStyleColour := Rec;
      lStyleColour.FILTERGROUP(2);
      lStyleColour.SETRECFILTER();
      lStyleColour.FILTERGROUP(0);

      PAGE.RUN(PAGE::"Style Colour Picture", lStyleColour);
    END;

    PROCEDURE ShowStyle@1000000008();
    BEGIN
      //DOC NS2015.1 - Showing style

      Rec.TESTFIELD("Style No.");

      Style.GET(Rec."Style No.");
      PAGE.RUN(PAGE::"Style Card", Style);
    END;

    BEGIN
    {
      //DOC NS2015.1 JH 08/01/2014 - Created
      //DOC NS2015.3 JH 30/07/2014 - "Vendor Item No." added
    }
    END.
  }
}

