OBJECT Codeunit 9062693 PowerBI Viewer Session Mgt
{
  OBJECT-PROPERTIES
  {
    Date=14/07/16;
    Time=12:00:00 PM;
    Version List=NA2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      HtmlControlRequestProxy@1000000001 : Codeunit 9062665;
      Initialized@1000000000 : Boolean;
      ReportUrl@1000000002 : Text;
      AccessToken@1000000003 : Text;
      QueryFilter@1000000004 : Text;
      TileUrl@1000000005 : Text;
      TileHeight@1000000006 : Text;
      TileWidth@1000000007 : Text;

    PROCEDURE Initialize@1000000000(VAR Value@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication, Version=1.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.RequestMessageEvent";Height@1000000001 : Text);
    VAR
      UIManipulationMgt@1000000002 : Codeunit 9062672;
    BEGIN
      //initialize control and libraries
      HtmlControlRequestProxy.SetRequestEvent(Value);
      //update client ui
      UIManipulationMgt.ClearScript();
      IF ((Height = '100%') OR (Height = '')) THEN
        UIManipulationMgt.UseWindowHeight()
      ELSE
        UIManipulationMgt.SetHeightProperties(Height, Height, Height);
      IF (UIManipulationMgt.HasScript) THEN
        HtmlControlRequestProxy.SendJavaScriptRequest(UIManipulationMgt.GetScript);
      Initialized := TRUE;

      //RenderReportContent();
      //RenderTileContent();
    END;

    PROCEDURE LinksLoaded@1000000001();
    BEGIN
      //LinksLoaded event handler
      //Not used here as we are not loading anything
    END;

    PROCEDURE HandleControlMessage@1000000002(ControlMessage@1000000000 : DotNet "'TheNavPeople.HtmlControlCommunication, Version=1.0.0.0, Culture=neutral, PublicKeyToken=7c0c47cc7f65ff95'.TheNavPeople.HtmlControlCommunication.ControlMessage";VAR ClosePage@1000000001 : Boolean);
    VAR
      CellIdText@1000000002 : Text;
      CellIndexText@1000000003 : Text;
      ColIdText@1000000004 : Text;
    BEGIN
      //Handling control messages
      //Not used here as we are not loading anything
    END;

    LOCAL PROCEDURE "--- Rendering and Html Interactions ---"@1000000007();
    BEGIN
    END;

    PROCEDURE RenderReportContent@1000000003();
    VAR
      HtmlBigText@1000000000 : BigText;
      HtmlText@1000000001 : Text;
    BEGIN
      IF ((NOT(Initialized)) OR (ReportUrl = '') OR (AccessToken = '')) THEN
        EXIT;

      HtmlBigText.ADDTEXT(
        STRSUBSTNO(
          '<iframe ID="PowerBIReport" src="%1" style="%2" frameborder="0" seamless onload="frameLoaded();"></iframe>',
          ReportUrl,
          'height:100%;width:100%;'));
      HtmlBigText.ADDTEXT('<script>');
      HtmlBigText.ADDTEXT(STRSUBSTNO('var accessToken = "%1";', AccessToken));
      HtmlBigText.ADDTEXT('function frameLoaded() {');
      IF (QueryFilter = '') THEN
        HtmlBigText.ADDTEXT(  'var msgObj = { action: "loadReport", accessToken: accessToken};')
      ELSE
        HtmlBigText.ADDTEXT( STRSUBSTNO( 'var msgObj = { action: "loadReport", accessToken: accessToken, oDataFilter: "%1"};',QueryFilter));
      HtmlBigText.ADDTEXT(  'msgText = JSON.stringify(msgObj);');
      HtmlBigText.ADDTEXT(  'document.getElementById("PowerBIReport").contentWindow.postMessage(msgText, "*");');
      HtmlBigText.ADDTEXT('}');
      HtmlBigText.ADDTEXT('</script>');

      HtmlBigText.GETSUBTEXT(HtmlText, 1);
      HtmlControlRequestProxy.SendHtmlRequest('', HtmlText, FALSE);
    END;

    PROCEDURE RenderTileContent@1000000005();
    VAR
      HtmlBigText@1000000000 : BigText;
      HtmlText@1000000001 : Text;
    BEGIN
      IF ((NOT(Initialized)) OR (TileUrl = '') OR (AccessToken = '')) THEN
        EXIT;

      HtmlBigText.ADDTEXT(
        STRSUBSTNO(
          '<iframe ID="PowerBIReport" src="%1" style="%2" frameborder="0" seamless onload="frameLoaded();"></iframe>',
          TileUrl,
          'height:100%;width:100%;'));
      HtmlBigText.ADDTEXT('<script>');
      HtmlBigText.ADDTEXT(STRSUBSTNO('var accessToken = "%1";', AccessToken));
      HtmlBigText.ADDTEXT('function frameLoaded() {');
      IF (QueryFilter = '') THEN
        HtmlBigText.ADDTEXT(STRSUBSTNO(  'var msgObj = { action: "loadTile", accessToken: accessToken,height: %1, width: %2};',TileHeight,TileWidth))
      ELSE
        HtmlBigText.ADDTEXT( STRSUBSTNO( 'var msgObj = { action: "loadTile", accessToken: accessToken, oDataFilter: "%1",height: %2, width: %3};',QueryFilter,TileHeight,TileWidth));
      HtmlBigText.ADDTEXT(  'msgText = JSON.stringify(msgObj);');
      HtmlBigText.ADDTEXT(  'document.getElementById("PowerBIReport").contentWindow.postMessage(msgText, "*");');
      HtmlBigText.ADDTEXT('}');
      HtmlBigText.ADDTEXT('</script>');

      HtmlBigText.GETSUBTEXT(HtmlText, 1);
      HtmlControlRequestProxy.SendHtmlRequest('', HtmlText, FALSE);
    END;

    LOCAL PROCEDURE "--- Report Methods ---"@1000000008();
    BEGIN
    END;

    PROCEDURE SetReportParameters@1000000004(NewReportUrl@1000000000 : Text;NewAccessToken@1000000001 : Text;NewQueryFilter@1000000002 : Text);
    BEGIN
      ReportUrl := NewReportUrl;
      AccessToken := NewAccessToken;
      QueryFilter := NewQueryFilter;
      RenderReportContent();
    END;

    PROCEDURE SetTileParameters@1000000006(NewTileUrl@1000000000 : Text;NewAccessToken@1000000001 : Text;NewQueryFilter@1000000002 : Text;NewTileHeight@1000000003 : Text;NewTileWidth@1000000004 : Text);
    BEGIN
      TileUrl := STRSUBSTNO('%1&width=%2&height=%3',NewTileUrl,NewTileWidth,NewTileHeight);
      AccessToken := NewAccessToken;
      QueryFilter := NewQueryFilter;
      TileHeight  := NewTileHeight;
      TileWidth := NewTileWidth;
      RenderTileContent();
    END;

    BEGIN
    {
      //DOC NA2016.16 AP 14/07/2016 - Created
    }
    END.
  }
}

