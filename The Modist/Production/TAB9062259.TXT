OBJECT Table 9062259 Enhanced Workflow Response
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Workflow@1000000000 : Record 9062258;
             BEGIN
               IF Workflow.GET("Workflow No.") THEN
                 IF Workflow."Checklist Type" <> '' THEN
                   ERROR(errChecklistWorkflow);

               Rec.TESTFIELD("Workflow No.");
               Rec.TESTFIELD(Code);

               Rec.VALIDATE("Workflow No.");
             END;

    OnModify=VAR
               Workflow@1000000000 : Record 9062258;
             BEGIN
               IF Workflow.GET("Workflow No.") THEN
                 IF Workflow."Checklist Type" <> '' THEN
                   ERROR(errChecklistWorkflow);
             END;

    OnDelete=VAR
               Workflow@1000000000 : Record 9062258;
             BEGIN
               IF Workflow.GET("Workflow No.") THEN
                 IF Workflow."Checklist Type" <> '' THEN
                   ERROR(errChecklistWorkflow);

               WorkflowResponseOption.RESET();
               WorkflowResponseOption.SETRANGE("Workflow No.", Rec."Workflow No.");
               WorkflowResponseOption.SETRANGE("Response Code", Rec.Code);
               WorkflowResponseOption.DELETEALL(TRUE);
             END;

    OnRename=VAR
               Workflow@1000000000 : Record 9062258;
             BEGIN
               IF Workflow.GET("Workflow No.") THEN
                 IF Workflow."Checklist Type" <> '' THEN
                   ERROR(errChecklistWorkflow);
             END;

    CaptionML=[ENU=Enhanced Workflow Response;
               ENG=Enhanced Workflow Response];
    LookupPageID=Page9062286;
    DrillDownPageID=Page9062286;
  }
  FIELDS
  {
    { 1   ;   ;Workflow No.        ;Code20        ;TableRelation="Enhanced Workflow";
                                                   OnValidate=VAR
                                                                WFlow@1000000000 : Record 9062258;
                                                              BEGIN
                                                                IF WFlow.GET("Workflow No.") THEN BEGIN
                                                                  VALIDATE("Table ID",WFlow."Table ID");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Workflow No.;
                                                              ENG=Workflow No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Auto Response       ;Boolean       ;OnValidate=VAR
                                                                WFlow@1000000001 : Record 9062258;
                                                                WFlowRes@1000000000 : Record 9062259;
                                                              BEGIN
                                                                WFlowRes.SETRANGE("Workflow No.","Workflow No.");
                                                                WFlowRes.SETFILTER(Code,'<>%1',Code);
                                                                WFlowRes.SETFILTER("Auto Response",'%1',TRUE);

                                                                IF WFlowRes.FINDSET THEN ERROR(Text001);

                                                                WFlow.GET("Workflow No.");
                                                                WFlow.VALIDATE("Auto Response","Auto Response");
                                                                WFlow.VALIDATE("Group Decision Method",'FIRST');
                                                                WFlow.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Auto Response;
                                                              ENG=Auto Response] }
    { 3   ;   ;Cancelled Response  ;Boolean       ;OnValidate=VAR
                                                                WFlowRes@1000000000 : Record 9062259;
                                                              BEGIN
                                                                IF "Cancelled Response" <> xRec."Cancelled Response" THEN BEGIN
                                                                  WFlowRes.SETRANGE("Workflow No.","Workflow No.");
                                                                  WFlowRes.SETFILTER(Code,'<>%1',Code);
                                                                  WFlowRes.SETFILTER("Cancelled Response",'%1',TRUE);

                                                                  IF WFlowRes.FINDSET THEN BEGIN
                                                                    IF WFlowRes.Code <> Code THEN ERROR(Text002);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cancelled Response;
                                                              ENG=Cancelled Response] }
    { 5   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 10  ;   ;Response Description;Text250       ;OnValidate=BEGIN
                                                                CALCFIELDS("Workflow Description");
                                                              END;

                                                   CaptionML=[ENU=Response Description;
                                                              ENG=Response Description] }
    { 11  ;   ;Next Workflow No.   ;Code20        ;TableRelation="Enhanced Workflow";
                                                   CaptionML=[ENU=Next Workflow No.;
                                                              ENG=Next Workflow No.] }
    { 12  ;   ;Workflow Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Enhanced Workflow".Description WHERE (No.=FIELD(Workflow No.)));
                                                   CaptionML=[ENU=Workflow Description;
                                                              ENG=Workflow Description];
                                                   Editable=No }
    { 16  ;   ;Response Workflow No.;Code20       ;TableRelation="Enhanced Workflow";
                                                   CaptionML=[ENU=Response Workflow No.;
                                                              ENG=Response Workflow No.] }
    { 17  ;   ;Field ID            ;Integer       ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateFieldIdOld(Rec."Table ID", Rec."Field Type", Rec."Field ID");
                                                                Rec."Field Name" := ObjectFieldToolkit.GetFieldNameOld(Rec."Table ID", Rec."Field Type", Rec."Field ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.FieldLookupOld(Rec."Table ID", Rec."Field Type", Rec."Field ID")) THEN
                                                                Rec.VALIDATE("Field ID");
                                                            END;

                                                   CaptionML=[ENU=Field ID;
                                                              ENG=Field ID];
                                                   BlankZero=Yes }
    { 18  ;   ;Field Value         ;Text100       ;CaptionML=[ENU=Field Value;
                                                              ENG=Field Value] }
    { 19  ;   ;Field Name          ;Text30        ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateFieldNameOld(Rec."Table ID", Rec."Field Type", Rec."Field ID", Rec."Field Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.FieldLookupOld(Rec."Table ID", Rec."Field Type", Rec."Field ID")) THEN
                                                                Rec.VALIDATE("Field ID");
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              ENG=Field Name];
                                                   Description=NA2015.1 }
    { 21  ;   ;Table ID            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Table ID" <> xRec."Table ID") THEN
                                                                  Rec.VALIDATE("Field ID");
                                                                Rec.CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.TableLookup(Rec."Table ID")) THEN
                                                                Rec.VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID];
                                                   BlankZero=Yes }
    { 22  ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name];
                                                   Editable=No }
    { 23  ;   ;Update Field with   ;Option        ;CaptionML=[ENU=Update Field with;
                                                              ENG=Update Field with];
                                                   OptionCaptionML=[ENU=" ,Contact No,User ID,Person,Response Value,Field Value";
                                                                    ENG=" ,Contact No,User ID,Person,Response Value,Field Value"];
                                                   OptionString=[ ,Contact No,User ID,Person,Response Value,Field Value] }
    { 24  ;   ;Response Type       ;Option        ;CaptionML=[ENU=Response Type;
                                                              ENG=Response Type];
                                                   OptionCaptionML=[ENU=Positive,Negative,Neutral;
                                                                    ENG=Positive,Negative,Neutral];
                                                   OptionString=Positive,Negative,Neutral }
    { 25  ;   ;Workflow GUID Filter;Text38        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Workflow GUID Filter;
                                                              ENG=Workflow GUID Filter] }
    { 26  ;   ;Workflow GUID       ;Text38        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Enhanced Workflow Activity Ln."."GUID No." WHERE (GUID No.=FIELD(Workflow GUID Filter)));
                                                   CaptionML=[ENU=Workflow GUID;
                                                              ENG=Workflow GUID] }
    { 28  ;   ;Resp. Option Table ID;Integer      ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Resp. Option Table ID" <> xRec."Resp. Option Table ID") THEN
                                                                BEGIN
                                                                  Rec.VALIDATE("Resp. Option Value Field ID", 0);
                                                                  Rec.VALIDATE("Resp. Option Desc. Field ID", 0);
                                                                END;
                                                                Rec.CALCFIELDS("Resp. Option Table Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.TableLookup(Rec."Resp. Option Table ID")) THEN
                                                                Rec.VALIDATE("Resp. Option Table ID");
                                                            END;

                                                   CaptionML=[ENU=Resp. Option Table ID;
                                                              ENG=Resp. Option Table ID] }
    { 29  ;   ;Resp. Option Value Field ID;Integer;TableRelation=Field.No. WHERE (TableNo=FIELD(Resp. Option Table ID));
                                                   OnValidate=BEGIN
                                                                UpdateRespOptionValueType();
                                                                Rec.CALCFIELDS("Resp. Option Value Field Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.FieldLookup(Rec."Resp. Option Table ID", Rec."Resp. Option Value Field ID")) THEN
                                                                Rec.VALIDATE("Resp. Option Value Field ID");
                                                            END;

                                                   CaptionML=[ENU=Resp. Option Value Field ID;
                                                              ENG=Resp. Option Value Field ID] }
    { 30  ;   ;Resp. Option Desc. Field ID;Integer;TableRelation=Field.No. WHERE (TableNo=FIELD(Resp. Option Table ID));
                                                   OnValidate=BEGIN
                                                                Rec.CALCFIELDS("Resp. Option Desc. Field Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.FieldLookup(Rec."Resp. Option Table ID", Rec."Resp. Option Desc. Field ID")) THEN
                                                                Rec.VALIDATE("Resp. Option Desc. Field ID");
                                                            END;

                                                   CaptionML=[ENU=Resp. Option Desc. Field ID;
                                                              ENG=Resp. Option Desc. Field ID] }
    { 31  ;   ;Resp. Option Value Type;Option     ;CaptionML=[ENU=Resp. Option Value Type;
                                                              ENG=Resp. Option Value Type];
                                                   OptionCaptionML=[ENU=" ,Code,Text,Integer,Decimal,Option,Boolean,Date,Time,DateTime";
                                                                    ENG=" ,Code,Text,Integer,Decimal,Option,Boolean,Date,Time,DateTime"];
                                                   OptionString=[ ,Code,Text,Integer,Decimal,Option,Boolean,Date,Time,DateTime];
                                                   Editable=No }
    { 32  ;   ;Resp. Option Table Name;Text30     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         ID=FIELD(Resp. Option Table ID)));
                                                   CaptionML=[ENU=Resp. Option Table Name;
                                                              ENG=Resp. Option Table Name];
                                                   Editable=No }
    { 33  ;   ;Resp. Option Value Field Name;Text30;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table ID),
                                                                                             No.=FIELD(Resp. Option Value Field ID)));
                                                   CaptionML=[ENU=Resp. Option Value Field Name;
                                                              ENG=Resp. Option Value Field Name];
                                                   Editable=No }
    { 34  ;   ;Resp. Option Desc. Field Name;Text30;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table ID),
                                                                                             No.=FIELD(Resp. Option Desc. Field ID)));
                                                   CaptionML=[ENU=Resp. Option Desc. Field Name;
                                                              ENG=Resp. Option Desc. Field Name];
                                                   Editable=No }
    { 35  ;   ;Resp. Options       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Enhanced Workflow Resp. Option" WHERE (Workflow No.=FIELD(Workflow No.),
                                                                                                             Response Code=FIELD(Code)));
                                                   CaptionML=[ENU=Resp. Options;
                                                              ENG=Resp. Options];
                                                   Editable=No }
    { 36  ;   ;Field Type          ;Option        ;CaptionML=[ENU=Field Type;
                                                              ENG=Field Type];
                                                   OptionCaptionML=[ENU=Field,Enhanced Field;
                                                                    ENG=Field,Enhanced Field];
                                                   OptionString=Field,Enhanced Field;
                                                   Description=NA2015.1 }
    { 38  ;   ;Use Field Type      ;Option        ;CaptionML=[ENU=Use Field Type;
                                                              ENG=Use Field Type];
                                                   OptionCaptionML=[ENU=Field,Enhanced Field;
                                                                    ENG=Field,Enhanced Field];
                                                   OptionString=Field,Enhanced Field;
                                                   Description=NA2015.1 }
    { 39  ;   ;Use Field ID        ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   OnValidate=VAR
                                                                EnhFieldDef@1000000002 : Record 9062327;
                                                                RecRef@1000000001 : RecordRef;
                                                                FieldRef@1000000000 : FieldRef;
                                                              BEGIN
                                                                IF "Use Field ID" = 0 THEN BEGIN
                                                                  VALIDATE("Use Field Name",'');
                                                                  EXIT;
                                                                END;

                                                                CASE "Use Field Type" OF
                                                                  "Field Type"::Field :
                                                                    BEGIN
                                                                      RecRef.OPEN("Table ID");
                                                                      FieldRef := RecRef.FIELD("Field ID");
                                                                      VALIDATE("Use Field Name",FieldRef.NAME);
                                                                    END;
                                                                  "Field Type"::"Enhanced Field" :
                                                                    BEGIN
                                                                      EnhFieldDef.GET("Table ID","Use Field ID");
                                                                      VALIDATE("Use Field Name",EnhFieldDef.Name);
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              EnhFieldDef@1000000001 : Record 9062327;
                                                              EnhFieldDefPage@1000000000 : Page 9062391;
                                                            BEGIN
                                                              CASE "Use Field Type" OF
                                                                "Field Type"::Field :
                                                                  BEGIN
                                                                    IF ObjectFieldToolkit.FieldLookup("Table ID","Use Field ID") THEN
                                                                      Rec.VALIDATE("Use Field ID");
                                                                  END;
                                                                "Field Type"::"Enhanced Field" :
                                                                  BEGIN
                                                                    EnhFieldDef.SETRANGE("Table No.","Table ID");
                                                                    EnhFieldDefPage.SETTABLEVIEW(EnhFieldDef);
                                                                    EnhFieldDefPage.LOOKUPMODE := TRUE;
                                                                    IF EnhFieldDefPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      EnhFieldDefPage.GETRECORD(EnhFieldDef);
                                                                      VALIDATE("Use Field ID",EnhFieldDef."Enhanced Field No.");
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field ID;
                                                              ENG=Field ID];
                                                   BlankZero=Yes;
                                                   Description=NA2015.1 }
    { 40  ;   ;Use Field Name      ;Text30        ;OnLookup=VAR
                                                              EnhFieldDef@1000000001 : Record 9062327;
                                                              EnhFieldDefPage@1000000000 : Page 9062391;
                                                            BEGIN
                                                              CASE "Use Field Type" OF
                                                                "Field Type"::Field :
                                                                  BEGIN
                                                                    IF ObjectFieldToolkit.FieldLookup("Table ID","Use Field ID") THEN
                                                                      Rec.VALIDATE("Use Field ID");
                                                                  END;
                                                                "Field Type"::"Enhanced Field" :
                                                                  BEGIN
                                                                    EnhFieldDef.SETRANGE("Table No.","Table ID");
                                                                    EnhFieldDefPage.SETTABLEVIEW(EnhFieldDef);
                                                                    EnhFieldDefPage.LOOKUPMODE := TRUE;
                                                                    IF EnhFieldDefPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      EnhFieldDefPage.GETRECORD(EnhFieldDef);
                                                                      VALIDATE("Use Field ID",EnhFieldDef."Enhanced Field No.");
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              ENG=Field Name];
                                                   Description=NA2015.1;
                                                   Editable=No }
    { 50  ;   ;Restrict Record     ;Boolean       ;CaptionML=[ENU=Restrict Record;
                                                              ENG=Restrict Record];
                                                   Description=NA2016.10 }
    { 51  ;   ;Unrestrict Record   ;Boolean       ;CaptionML=[ENU=Unrestrict Record;
                                                              ENG=Unrestrict Record];
                                                   Description=NA2016.10 }
    { 60  ;   ;Release Record      ;Boolean       ;CaptionML=[ENU=Release Record;
                                                              ENG=Release Record];
                                                   Description=NA2016.10 }
    { 61  ;   ;Reopen Record       ;Boolean       ;CaptionML=[ENU=Reopen Record;
                                                              ENG=Reopen Record];
                                                   Description=NA2016.10 }
  }
  KEYS
  {
    {    ;Workflow No.,Code                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'ENU=You may only choose one workflow response as Auto Response;ENG=You may only choose one workflow response as Auto Response';
      Text002@1000000001 : TextConst 'ENU=You may only choose one workflow response as Cancelled Response;ENG=You may only choose one workflow response as Cancelled Response';
      WorkflowResponseOption@1000000003 : Record 9062260;
      TNPUtils@1000000004 : Codeunit 9062229;
      ObjectFieldToolkit@1000000005 : Codeunit 9062343;
      tSendResponseWorkflow@9062229 : TextConst 'ENU=tSendResponseWorkflow;ENG=tSendResponseWorkflow';
      tSendNextWorkflow@9062230 : TextConst 'ENU=tSendNextWorkflow;ENG=tSendNextWorkflow';
      errChecklistWorkflow@1000000002 : TextConst 'ENU=errChecklistWorkflow;ENG=errChecklistWorkflow';

    LOCAL PROCEDURE UpdateRespOptionValueType@1000000001();
    VAR
      lField@1000000000 : Record 2000000041;
      lRespOptionValueType@1000000001 : Integer;
    BEGIN
      //DOC NA2015.1 - Setting response option type

      IF ( NOT (lField.GET(Rec."Resp. Option Table ID", Rec."Resp. Option Value Field ID"))) THEN
        CLEAR(lField);

      CASE (lField.Type) OF
        lField.Type::Code:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Code;
        lField.Type::Text:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Text;
        lField.Type::Integer:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Integer;
        lField.Type::Decimal:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Decimal;
        lField.Type::Option:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Option;
        lField.Type::Boolean:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Boolean;
        lField.Type::Date:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Date;
        lField.Type::Time:
          lRespOptionValueType := Rec."Resp. Option Value Type"::Time;
        lField.Type::DateTime:
          lRespOptionValueType := Rec."Resp. Option Value Type"::DateTime;
        ELSE
          lRespOptionValueType := Rec."Resp. Option Value Type"::" ";
      END;

      Rec.VALIDATE("Resp. Option Value Type", lRespOptionValueType);
    END;

    PROCEDURE GetDescription@9062229() Description : Text;
    BEGIN
      IF "Restrict Record" THEN
        AddToDescription(Description,FIELDCAPTION("Restrict Record"));

      IF "Unrestrict Record" THEN
        AddToDescription(Description,FIELDCAPTION("Unrestrict Record"));

      IF "Release Record" THEN
        AddToDescription(Description,FIELDCAPTION("Release Record"));

      IF "Reopen Record" THEN
        AddToDescription(Description,FIELDCAPTION("Reopen Record"));

      IF "Field Name" <> '' THEN BEGIN
        CASE "Update Field with" OF
          "Update Field with"::" " :
            AddToDescription(Description,"Field Name" + ' = ' + '"' + "Field Value" + '"');
          ELSE
            AddToDescription(Description,"Field Name" + ' = ' + FORMAT("Update Field with"));
        END;
      END;

      IF "Response Workflow No." <> '' THEN
        AddToDescription(Description,STRSUBSTNO(tSendResponseWorkflow,"Response Workflow No."));

      IF "Next Workflow No." <> '' THEN
        AddToDescription(Description,STRSUBSTNO(tSendNextWorkflow,"Next Workflow No."));
    END;

    LOCAL PROCEDURE AddToDescription@9062230(VAR Description@9062229 : Text;TextToAdd@9062230 : Text);
    BEGIN
      IF Description = '' THEN
        Description := TextToAdd
      ELSE
        Description += ', ' + TextToAdd;
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 21/08/2012 - Migrated to 2013
      //DOC NA2015.1  PC 24/07/2013 - Added code to OnValidate of "Workflow No." to set "Table ID" from Workflow
      //DOC NA2015.1  PC 24/07/2013 - Added lookup code to OnLookup of "Field Name"
      //DOC NA2016.10 JH 26/08/2015 - Upgraded to 2016
      //DOC NA2016.10 PC 24/09/2015 - Added fields "Restrict Record" and "Unrestrict Record"
      //DOC NA2016.10 PC 25/09/2015 - Added fields "Release Record" and "Reopen Record"
      //DOC NA2016.10 PC 09/10/2015 - Prevent changes if Workflow has "Checklist" set
      //DOC NA2016.11 JH 05/11/2015 - Workflow no. validation added to "OnInsert"
      //DOC NA2016.15 JH 15/04/2016 - CfMD amendments
    }
    END.
  }
}

