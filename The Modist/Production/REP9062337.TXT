OBJECT Report 9062337 POS - Copy Button Group
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=POS - Copy Button Group;
               ENG=POS - Copy Button Group];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  ProcessData();
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   SrcButtonGroupCode := SetSrcGroupCode;
                   DestButtonGroupCode := SetDestGroupCode;
                 END;

    }
    CONTROLS
    {
      { 9062230; ;Container ;
                  ContainerType=ContentArea }

      { 9062231;1;Field     ;
                  CaptionML=ENU=Source Buttons Group Code;
                  SourceExpr=SrcButtonGroupCode;
                  TableRelation="POS Button Group" }

      { 9062232;1;Field     ;
                  CaptionML=ENU=Dest. Button Group Code;
                  SourceExpr=DestButtonGroupCode;
                  TableRelation="POS Button Group" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SrcButtonGroup@9062237 : Record 9062787;
      DestButtonGroup@9062238 : Record 9062787;
      POSManagement@9062239 : Codeunit 9062650;
      SrcButtonGroupCode@9062230 : Code[20];
      DestButtonGroupCode@9062231 : Code[20];
      SetSrcGroupCode@9062232 : Code[20];
      SetDestGroupCode@9062233 : Code[20];
      errSelectSourceButtonGroup@9062234 : TextConst 'ENU=Please select source Button Group;ENG=Please select source Button Group';
      errSelectDestButtonGroup@9062240 : TextConst 'ENU=Please select dest. Button Group;ENG=Please select dest. Button Group';
      cnfCreateDestButtonGroup@9062235 : TextConst 'ENU=Dest. Button Group ''%1'' does not exist. Do you want to create it?;ENG=Dest. Button Group ''%1'' does not exist. Do you want to create it?';
      errDestButtonGroupNotFound@9062236 : TextConst 'ENU=Dest. Button Group ''%1'' not found.;ENG=Dest. Button Group ''%1'' not found.';
      cnfDeleteDestButtons@9062241 : TextConst 'ENU=Destination Buttons Group ''%1'' already has some buttons. Do you want to continue?;ENG=Destination Buttons Group ''%1'' already has some buttons. Do you want to continue?';
      errTheSameCodes@9062242 : TextConst 'ENU="Source and destination Button Group Codes must be different. ";ENG="Source and destination Button Group Codes must be different. "';

    PROCEDURE SetParameters@9062231(newSrcGroupCode@9062230 : Code[20];newDestGroupCode@9062231 : Code[20]);
    BEGIN
      SetSrcGroupCode := newSrcGroupCode;
      SetDestGroupCode := newDestGroupCode;
    END;

    LOCAL PROCEDURE ProcessData@9062230();
    BEGIN
      IF (SrcButtonGroupCode = '') THEN
        ERROR(errSelectSourceButtonGroup);
      SrcButtonGroup.GET(SrcButtonGroupCode);

      IF (DestButtonGroupCode = '') THEN
        ERROR(errSelectDestButtonGroup);

      IF (SrcButtonGroupCode = DestButtonGroupCode) THEN
        ERROR(errTheSameCodes);

      IF (NOT(DestButtonGroup.GET(DestButtonGroupCode))) THEN BEGIN
        IF (NOT(CONFIRM(STRSUBSTNO(cnfCreateDestButtonGroup)))) THEN
          ERROR(errDestButtonGroupNotFound, DestButtonGroupCode);

        DestButtonGroup.INIT();
        DestButtonGroup.Code := DestButtonGroupCode;
        DestButtonGroup.INSERT(TRUE);
      END;

      POSManagement.CopyButtonsGroup(SrcButtonGroup, DestButtonGroup);
    END;

    BEGIN
    {
      //DOC NH2016.12 BM 11/02/2016 - Created
      //DOC NH2016.15 JH 09/05/2016 - CfMD amendments
    }
    END.
  }
  RDLDATA
  {
  }
}

