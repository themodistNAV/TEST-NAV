OBJECT Codeunit 1251 Record Match Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetLongestCommonSubstring@12(FirstString@1000 : Text;SecondString@1001 : Text) : Text;
    VAR
      Result@1002 : Text;
      Buffer@1006 : Text;
      i@1004 : Integer;
      j@1003 : Integer;
    BEGIN
      FirstString := UPPERCASE(FirstString);
      SecondString := UPPERCASE(SecondString);
      Result := '';

      i := 1;
      WHILE i + STRLEN(Result) - 1 <= STRLEN(FirstString) DO BEGIN
        j := 1;
        WHILE (j + i - 1 <= STRLEN(FirstString)) AND (j <= STRLEN(SecondString)) DO BEGIN
          IF STRPOS(SecondString,COPYSTR(FirstString,i,j)) > 0 THEN
            Buffer := COPYSTR(FirstString,i,j);

          IF STRLEN(Buffer) > STRLEN(Result) THEN
            Result := Buffer;
          Buffer := '';
          j += 1;
        END;
        i += 1;
      END;

      EXIT(Result);
    END;

    PROCEDURE CalculateStringNearness@15(FirstString@1002 : Text;SecondString@1001 : Text;Threshold@1004 : Integer;NormalizingFactor@1000 : Integer) : Integer;
    VAR
      Result@1006 : Text;
      TotalMatchedChars@1003 : Integer;
      MinLength@1005 : Integer;
    BEGIN
      FirstString := UPPERCASE(FirstString);
      SecondString := UPPERCASE(SecondString);

      MinLength := (STRLEN(FirstString) + STRLEN(SecondString) - ABS(STRLEN(FirstString) - STRLEN(SecondString))) / 2;
      IF MinLength = 0 THEN
        MinLength := 1;

      TotalMatchedChars := 0;
      Result := GetLongestCommonSubstring(FirstString,SecondString);
      WHILE STRLEN(Result) >= Threshold DO BEGIN
        TotalMatchedChars += STRLEN(Result);
        FirstString := DELSTR(FirstString,STRPOS(FirstString,Result),STRLEN(Result));
        SecondString := DELSTR(SecondString,STRPOS(SecondString,Result),STRLEN(Result));
        Result := GetLongestCommonSubstring(FirstString,SecondString);
      END;

      EXIT(NormalizingFactor * TotalMatchedChars DIV MinLength);
    END;

    PROCEDURE Trim@1060(InputString@1061 : Text) : Text;
    BEGIN
      EXIT(DELCHR(DELCHR(InputString,'<'),'>'));
    END;

    BEGIN
    END.
  }
}

