OBJECT Codeunit 9062547 Help Management
{
  OBJECT-PROPERTIES
  {
    Date=26/09/16;
    Time=12:00:00 PM;
    Version List=NA2016.16.2;
  }
  PROPERTIES
  {
    OnRun=VAR
            i@1000000004 : Integer;
            HelpHeader@1000000000 : Record 9062710;
          BEGIN
          END;

  }
  CODE
  {
    VAR
      tHelp@1000000035 : TextConst 'ENU=Help;ENG=Help';
      tHelpBaseFolderSysB@1000000012 : TextConst 'ENU=HELP_BASEFOLDER;ENG=HELP_BASEFOLDER';
      tHelpBaseUrlSysB@1000000029 : TextConst 'ENU=HELP_BASEURL;ENG=HELP_BASEURL';
      tLocalFolder@1000000034 : TextConst 'ENU=local;ENG=local';
      tUrl@1000000014 : TextConst 'ENU="%1/main.aspx?lang=en&content=%2";ENG="%1/main.aspx?lang=en&content=%2"';
      tYoutubeVideoPlaceholder@1000000019 : TextConst 'ENU=Youtube Video;ENG=Youtube Video';
      tAnimatedGifPlaceholder@1000000021 : TextConst 'ENU=Animated GIF;ENG=Animated GIF';
      tSmallPicturePlaceholder@1000000022 : TextConst 'ENU=Small Picture;ENG=Small Picture';
      tScreenShot1Placeholder@1000000023 : TextConst 'ENU=Screen Shot 1;ENG=Screen Shot 1';
      tScreenShot2Placeholder@1000000024 : TextConst 'ENU=Screen Shot 2;ENG=Screen Shot 2';
      tScreenShot3Placeholder@1000000015 : TextConst 'ENU=Screen Shot 3;ENG=Screen Shot 3';
      tNoHelpBaseURL@1000000000 : TextConst 'ENU=The Help Server URL is not defined on the HELP_BASEURL system behaviour;ENG=The Help Server URL is not defined on the HELP_BASEURL system behaviour';
      HelpHeader@1000000013 : Record 9062710;
      HelpLine@1000000025 : Record 9062711;
      HelpImage@1000000040 : Record 9062713;
      SystemBehaviour@1000000003 : Record 9062230;
      Languages@1000000004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ObjectModel.Collection`1";
      tRedirectURL@1000000006 : TextConst 'ENU="<meta http-equiv=""refresh"" content=""0; URL=%1.htm"">";ENG="<meta http-equiv=""refresh"" content=""0; URL=%1.htm"">"';
      tDefaultTip@1000000007 : TextConst 'ENU="For more information about how to navigate in the user interface, see <a HREF=""./conWorkingWithNav.htm"">Work with Microsoft Dynamics NAV</a>. For more information about how to find specific pages, see <a HREF=""./S_SEARCH.htm"">Search</a>.";ENG="For more information about how to navigate in the user interface, see <a HREF=""./conWorkingWithNav.htm"">Work with Microsoft Dynamics NAV</a>. For more information about how to find specific pages, see <a HREF=""./S_SEARCH.htm"">Search</a>."';
      tKeyFields@1000000008 : TextConst 'ENU="<h2 class=""heading"">Key Fields</h2><div id=""inThisSectionSection"" class=""section""><p>";ENG="<h2 class=""heading"">Key Fields</h2><div id=""inThisSectionSection"" class=""section""><p>"';
      tKeyButtons@1000000009 : TextConst 'ENU="<h2 class=""heading"">Key Buttons</h2><div id=""inThisSectionSection"" class=""section""><p>";ENG="<h2 class=""heading"">Key Buttons</h2><div id=""inThisSectionSection"" class=""section""><p>"';
      tRelatedPages@1000000010 : TextConst 'ENU="<h2 class=""heading"">Related Pages</h2><div id=""inThisSectionSection"" class=""section""><p>";ENG="<h2 class=""heading"">Related Pages</h2><div id=""inThisSectionSection"" class=""section""><p>"';
      tRelatedLinks@1000000011 : TextConst 'ENU="<h2 class=""heading"">Related Links</h2><div id=""inThisSectionSection"" class=""section""><p>";ENG="<h2 class=""heading"">Related Links</h2><div id=""inThisSectionSection"" class=""section""><p>"';
      FileSystem@9062229 : Codeunit 9062264;
      LanguageToolkit@1000000036 : Codeunit 9062586;
      SequenceToolkit@1000000041 : Codeunit 9062519;
      String@1000000018 : Codeunit 9062236;
      TextExtensionManagement@1000000026 : Codeunit 9062250;
      TnpUtils@1000000005 : Codeunit 9062229;
      TnpToolkit@1000000017 : Codeunit 9062230;
      Preview@1000000016 : Boolean;
      HelpFolderPath@1000000002 : Text;
      HelpLocalFolderPath@1000000033 : Text;
      HelpId@1000000031 : Text;
      HelpIdWithPrefix@1000000042 : Text;
      HelpFileType@1000000039 : ' ,Default,Standard,Preview';
      HelpFileNames@1000000028 : ARRAY [3] OF Text;
      HelpBaseUrl@1000000001 : Text;
      PartType@1000000030 : ' ,PageTitle,HelpId,OpenObjectType,OpenObjectId,OpenObjectName,Main,Tip,Note,KeyFields,KeyButtons,RelatedLinks,RelatedPages';
      ElementType@1000000020 : ' ,YoutubeVideoUrl,AnimatedGif,SmallPicture,ScreenShot1,ScreenShot2,ScreenShot3';
      Buffer@1000000027 : Text;
      tOpenText@1000000032 : TextConst 'ENU=Open the %1 window.;ENG=Open the %1 window.';
      tNoHelps@1000000037 : TextConst 'ENU=No helps have been selected.;ENG=No helps have been selected.';
      DialogManagement@1000000038 : Codeunit 9062249;

    LOCAL PROCEDURE "--- General ---"@1000000024();
    BEGIN
    END;

    PROCEDURE PublishMultiple@1000000016(VAR pHelpHeader@1000000000 : Record 9062710;pPreview@1000000001 : Boolean;pInteraction@1000000002 : Boolean) : Boolean;
    VAR
      eConfirmation@1000000003 : TextConst 'ENU=Do you want to publish %1 %2?;ENG=Do you want to publish %1 %2?';
      lCount@1000000007 : Integer;
      lHelpText@1000000005 : Text;
      eFinalMessage@1000000006 : TextConst 'ENU=%1 %2 published.;ENG=%1 %2 published.';
    BEGIN
      //DOC NA2016.16 - Publishing multiple helps
      //  <> pHelpHeader: The help recordset
      //  -> pPreview: Preview flag
      //  -> pInteraction: Interaction flag
      //  <- Result

      IF ( NOT (InitialiseMultiple(pHelpHeader, pInteraction, eConfirmation))) THEN
        EXIT(FALSE);
      IF (pHelpHeader.FINDSET()) THEN
      REPEAT
        DialogManagement.SetCounterTextValue(1, pHelpHeader."No.");
        Publish(pHelpHeader, pPreview);
      UNTIL (pHelpHeader.NEXT() = 0);
      EXIT(FinaliseMultiple(pHelpHeader, pInteraction, eFinalMessage));
    END;

    PROCEDURE Publish@1000000000(VAR pHelpHeader@1000000000 : Record 9062710;pPreview@1000000018 : Boolean);
    VAR
      lContent@1000000003 : Text;
    BEGIN
      //DOC NA2016.16 - Publishing one help
      //  <> pHelpHeader: The help
      //  -> pPreview: Preview flag

      HelpHeader := pHelpHeader;
      Preview := pPreview;

      Initialise();

      lContent := CreateContent();
      SaveContent(lContent);

      UpdateHelpHeader(TRUE);
      IF (pPreview) THEN
        ShowHelp(pPreview);

      pHelpHeader := HelpHeader;
    END;

    PROCEDURE UnpublishMultiple@1000000020(VAR pHelpHeader@1000000002 : Record 9062710;pInteraction@1000000000 : Boolean) : Boolean;
    VAR
      eConfirmation@1000000003 : TextConst 'ENU=Do you want to unpublish %1 %2?;ENG=Do you want to unpublish %1 %2?';
      eFinalMessage@1000000001 : TextConst 'ENU=%1 %2 unpublished.;ENG=%1 %2 unpublished.';
    BEGIN
      //DOC NA2016.16 - Unpublishing multiple helps
      //  <> pHelpHeader: The help recordset
      //  -> pInteraction: Interaction flag
      //  <- Result

      IF ( NOT (InitialiseMultiple(pHelpHeader, pInteraction, eConfirmation))) THEN
        EXIT(FALSE);
      IF (pHelpHeader.FINDSET()) THEN
      REPEAT
        DialogManagement.SetCounterTextValue(1, pHelpHeader."No.");
        Unpublish(pHelpHeader);
      UNTIL (pHelpHeader.NEXT() = 0);
      EXIT(FinaliseMultiple(pHelpHeader, pInteraction, eFinalMessage));
    END;

    PROCEDURE Unpublish@1000000037(VAR pHelpHeader@1000000001 : Record 9062710);
    BEGIN
      //DOC NA2016.16 - Unpublishing one help
      //  <> pHelpHeader: The help

      HelpHeader := pHelpHeader;

      Initialise();
      RemoveContent();
      UpdateHelpHeader(FALSE);

      pHelpHeader := HelpHeader;
    END;

    PROCEDURE RestoreStandardFile@1000000036(pHelpHeader@1000000028 : Record 9062710;pInteraction@1000000004 : Boolean) : Boolean;
    VAR
      lSourcePath@1000000000 : Text;
      lDestinationPath@1000000001 : Text;
      eConfirmation@1000000005 : TextConst 'ENU=Do you want to restore file "%1" from standard?;ENG=Do you want to restore file "%1" from standard?';
      eStandardFileDoesntExist@1000000002 : TextConst 'ENU=Standard file "%1" doesn''t exist.;ENG=Standard file "%1" doesn''t exist.';
      eFinalMessage@1000000003 : TextConst 'ENU=File "%1" has been restored from standard.;ENG=File "%1" has been restored from standard.';
    BEGIN
      //DOC NA2016.16 - Restoring help standard file from a copy saved
      //  -> pHelpHeader: The help header
      //  -> pInteraction: Interaction flag

      HelpHeader := pHelpHeader;
      pInteraction := ((pInteraction) AND (GUIALLOWED()));

      Initialise();

      IF ( NOT (CONFIRM(eConfirmation, TRUE, HelpFileNames[HelpFileType::Default]))) THEN
        EXIT(FALSE);

      //^^^lSourcePath := FileSystem.Combine(FALSE, HelpFolderPath, HelpFileNames[HelpFileType::Standard]);
      //^^^IF ( NOT (FileSystem.FileExists(FALSE, lSourcePath))) THEN
      //^^^  ERROR(eStandardFileDoesntExist, lSourcePath);

      //^^^lDestinationPath := FileSystem.Combine(FALSE, HelpFolderPath, HelpFileNames[HelpFileType::Default]);
      //^^^FileSystem.DeleteFileIfExists(FALSE, lDestinationPath);

      //^^^FileSystem.CopyFile(FALSE, lSourcePath, lDestinationPath);

      MESSAGE(eFinalMessage, HelpFileNames[HelpFileType::Default]);

      EXIT(TRUE);
    END;

    PROCEDURE OpenHelpFolder@1000000008(pHelpHeader@1000000000 : Record 9062710);
    BEGIN
      //DOC NA2016.16 - Opening help folder
      //  -> pHelpHeader: The help header

      HelpHeader := pHelpHeader;
      Initialise();
      HYPERLINK(HelpFolderPath);
    END;

    PROCEDURE Show@1000000011(pHelpHeader@1000000000 : Record 9062710);
    BEGIN
      //DOC NA2016.16 - Showing help
      //  -> pHelpHeader: The help header

      HelpHeader := pHelpHeader;
      Initialise();
      ShowHelp(FALSE);
    END;

    PROCEDURE ExportHelpIndex@1000000012();
    BEGIN
      //DOC NA2016.16.1 - Exporting help index

      XMLPORT.RUN(XMLPORT::"Export Help Index", TRUE, FALSE);
    END;

    LOCAL PROCEDURE "--- Sequence ---"@1000000060();
    BEGIN
    END;

    PROCEDURE MoveHelpUp@1000000043(VAR pHelpHeader@1000000000 : Record 9062710) : Boolean;
    BEGIN
      //DOC NA2016.16.1 - Moving help up
      //  <> pHelpHeader: The help header
      //  <- TRUE = Sequence changed, FALSE = Not

      EXIT(MoveHelpUpDown(pHelpHeader, SequenceToolkit.Up()));
    END;

    PROCEDURE MoveHelpDown@1000000047(VAR pHelpHeader@1000000000 : Record 9062710) : Boolean;
    BEGIN
      //DOC NA2016.16.1 - Moving help down
      //  <> pHelpHeader: The help header
      //  <- TRUE = Sequence changed, FALSE = Not

      EXIT(MoveHelpUpDown(pHelpHeader, SequenceToolkit.Down()));
    END;

    LOCAL PROCEDURE MoveHelpUpDown@1000000059(VAR pHelpHeader@1000000002 : Record 9062710;pDirection@1000000003 : Option) : Boolean;
    VAR
      lHelpHeaderVariant@1000000001 : Variant;
      lResult@1000000004 : Boolean;
    BEGIN
      //DOC NA2016.16.1 - Moving help (up / down)
      //  <> pHelpHeader: The help header
      //  -> pDirection: Direction
      //  <- TRUE = Sequence changed, FALSE = Not

      lHelpHeaderVariant := pHelpHeader;
      lResult := SequenceToolkit.SequenceUpDown(lHelpHeaderVariant, DATABASE::"Help Header", HelpHeader.FIELDNO(Sequence), pDirection);
      pHelpHeader := lHelpHeaderVariant;
      EXIT(lResult);
    END;

    PROCEDURE MoveHelps@1000000063(VAR pHelpHeader@1000000000 : Record 9062710) : Boolean;
    VAR
      lHelpHeaderSelected@1000000004 : Record 9062710;
      lHelpIncludeFilter@1000000001 : Text;
      eNoHelpsToMove@1000000002 : TextConst 'ENU=There are no helps to move.;ENG=There are no helps to move.';
      lHelpExcludeFilter@1000000003 : Text;
      lHelpCount@1000000005 : Integer;
      lHelpSequenceShift@1000000006 : Integer;
      lHelpSequence@1000000007 : Integer;
    BEGIN
      //DOC NA2016.16.2 - Moving helps to specified place (changing their sequence)
      //  <> pHelpHeader: The help headers
      //  <- TRUE = Moved, FALSE = Cancelled

      lHelpCount := pHelpHeader.COUNT();
      IF (lHelpCount = 0) THEN
        ERROR(eNoHelpsToMove);

      lHelpSequenceShift := lHelpCount * 10;

      pHelpHeader.FINDSET();
      REPEAT
        IF (lHelpIncludeFilter <> '') THEN
          lHelpIncludeFilter := lHelpIncludeFilter + '|';
        lHelpIncludeFilter := lHelpIncludeFilter + pHelpHeader."No.";

        IF (lHelpExcludeFilter <> '') THEN
          lHelpExcludeFilter := lHelpExcludeFilter + '&';
        lHelpExcludeFilter := lHelpExcludeFilter + '<>' + pHelpHeader."No.";
      UNTIL (pHelpHeader.NEXT() = 0);

      HelpHeader.RESET();
      HelpHeader.FILTERGROUP(2);
      HelpHeader.SETFILTER("No.", lHelpExcludeFilter);
      HelpHeader.FILTERGROUP(0);
      IF (PAGE.RUNMODAL(0, HelpHeader) <> ACTION::LookupOK) THEN
        EXIT(FALSE);

      lHelpHeaderSelected := HelpHeader;

      HelpHeader.RESET();
      HelpHeader.SETCURRENTKEY(Sequence);
      HelpHeader.SETFILTER("No.", lHelpExcludeFilter);
      HelpHeader.SETFILTER(Sequence, '>%1', lHelpHeaderSelected.Sequence);
      IF (HelpHeader.FINDSET(TRUE)) THEN
      REPEAT
        HelpHeader.Sequence += lHelpSequenceShift;
        HelpHeader.MODIFY(TRUE);
      UNTIL (HelpHeader.NEXT() = 0);

      lHelpSequence := lHelpHeaderSelected.Sequence;
      HelpHeader.RESET();
      HelpHeader.SETFILTER("No.", lHelpIncludeFilter);
      IF (HelpHeader.FINDSET(TRUE)) THEN
      REPEAT
        lHelpSequence += 10;
        HelpHeader.Sequence := lHelpSequence;
        HelpHeader.MODIFY(TRUE);
      UNTIL (HelpHeader.NEXT() = 0);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE "--- Logic ---"@1000000015();
    BEGIN
    END;

    LOCAL PROCEDURE Initialise@1000000001();
    BEGIN
      //DOC NA2016.16 - Initialising the codeunit

      GetHelpFolderPath();
      GetHelpBaseUrl();
      CollectLanguages();
      GetHelpIdAndFileName();
    END;

    LOCAL PROCEDURE GetHelpFolderPath@1000000028();
    VAR
      lFolderBuffer@1000000000 : TEMPORARY Record 9062386;
    BEGIN
      //DOC NA2016.16 - Working out help folder path and storing it in a global variable

      IF (HelpFolderPath <> '') THEN
        EXIT;

      SystemBehaviour.TestText(tHelpBaseFolderSysB);
      HelpFolderPath := SystemBehaviour.GetText(tHelpBaseFolderSysB);
      HelpLocalFolderPath := FileSystem.Combine(FALSE, HelpFolderPath, tLocalFolder);
    END;

    LOCAL PROCEDURE GetHelpBaseUrl@1000000023();
    VAR
      lFolderBuffer@1000000000 : TEMPORARY Record 9062386;
    BEGIN
      //DOC NA2016.16 - Working out help base URL and storing it in a global variable

      IF (HelpBaseUrl <> '') THEN
        EXIT;

      SystemBehaviour.TestText(tHelpBaseUrlSysB);
      HelpBaseUrl := SystemBehaviour.GetText(tHelpBaseUrlSysB);
    END;

    LOCAL PROCEDURE CollectLanguages@1000000021();
    VAR
      lFolderBuffer@1000000000 : TEMPORARY Record 9062386;
    BEGIN
      //DOC NA2016.16 - Collecting languages and storing them in a global variable (collection)

      IF ( NOT (ISNULL(Languages))) THEN
        EXIT;

      Languages := Languages.Collection();
      FileSystem.GetFolders(FALSE, HelpFolderPath, '', lFolderBuffer);
      IF (lFolderBuffer.FINDSET()) THEN
      REPEAT
        IF (lFolderBuffer.Name <> tLocalFolder) THEN
          Languages.Add(lFolderBuffer.Name);
      UNTIL (lFolderBuffer.NEXT() = 0);
    END;

    LOCAL PROCEDURE GetHelpIdAndFileName@1000000017();
    VAR
      lFolderBuffer@1000000000 : TEMPORARY Record 9062386;
      lHelpFileType@1000000002 : Integer;
      lHelpTypePrefix@1000000001 : Text;
      lHelpTypeSuffix@1000000003 : Text;
    BEGIN
      //DOC NA2016.16 - Working out help ID and file name and storing them in global variables

      IF (HelpHeader."Object ID" <> 0) THEN
        HelpId := FORMAT(HelpHeader."Object ID", 0, 1)
      ELSE
        HelpId := CreateHelpId(HelpHeader.Title);

      lHelpTypePrefix := GetHelpTypePrefix();
      HelpIdWithPrefix := lHelpTypePrefix + HelpId;

      FOR lHelpFileType := HelpFileType::Default TO HelpFileType::Preview DO
      BEGIN
        lHelpTypeSuffix := GetHelpTypeSuffix(lHelpFileType);
        HelpFileNames[lHelpFileType] := STRSUBSTNO('%1%2%3.htm', lHelpTypePrefix, HelpId, lHelpTypeSuffix);
      END;
    END;

    LOCAL PROCEDURE GetHelpTypePrefix@1000000002() : Text;
    VAR
      lPrefix@1000000000 : Text;
    BEGIN
      //DOC NA2016.16 - Returning help type prefix
      //  <- The prefix

      lPrefix := '';

      HelpHeader.TESTFIELD("Help Type");
      CASE (HelpHeader."Help Type") OF
        HelpHeader."Help Type"::"Batch Job": lPrefix := 'B_';
        HelpHeader."Help Type"::"Conceptual Overview": lPrefix := 'con';
        HelpHeader."Help Type"::"Orientation Topic": lPrefix := 'ori';
        HelpHeader."Help Type"::Page: lPrefix := 'N_';
        HelpHeader."Help Type"::"Reference Content": lPrefix := 'ref';
        HelpHeader."Help Type"::Report: lPrefix := 'R_';
        HelpHeader."Help Type"::"System Window / Dialog": lPrefix := 'S_';
        HelpHeader."Help Type"::"Table / Field": lPrefix := 'T_';
        HelpHeader."Help Type"::Task: lPrefix := 'tsk';
        HelpHeader."Help Type"::Walkthrough: lPrefix := 'wlk';
      END;

      EXIT(lPrefix);
    END;

    LOCAL PROCEDURE GetHelpTypeSuffix@1000000045(pHelpFileType@1000000000 : Option) : Text;
    VAR
      lSuffix@1000000001 : Text;
    BEGIN
      //DOC NA2016.16 - Returning help type suffix
      //  -> pHelpFileType: Help file type
      //  <- The suffix

      lSuffix := '';

      CASE (pHelpFileType) OF
        HelpFileType::Default: lSuffix := '';
        HelpFileType::Standard: lSuffix := '_Standard';
        HelpFileType::Preview: lSuffix := '_Preview';
      END;

      EXIT(lSuffix);
    END;

    LOCAL PROCEDURE CreateContent@1000000052() : Text;
    VAR
      lContent@1000000000 : Text;
      lPartType@1000000001 : Option;
      lPartPlaceholder@1000000002 : Text;
      lPartText@1000000005 : Text;
      lOpenObjectTypeText@1000000003 : Text;
      lOpenObjectIdText@1000000004 : Text;
      lOpenObjectNameText@1000000006 : Text;
    BEGIN
      IF ((HelpHeader."Object Type" <> HelpHeader."Object Type"::" ") AND (HelpHeader."Object ID" <> 0)) THEN
      BEGIN
        HelpHeader.CALCFIELDS("Object Name");
        lOpenObjectTypeText := FORMAT(HelpHeader."Object Type");
        lOpenObjectIdText := FORMAT(HelpHeader."Object ID", 0, 1);
        lOpenObjectNameText := HelpHeader."Object Name";
      END;
      lContent := CreateContentTemplate();
      FOR lPartType := PartType::PageTitle TO PartType::RelatedPages DO
      BEGIN
        lPartPlaceholder := GetPartPlaceholder(lPartType);
        IF (STRPOS(lContent, lPartPlaceholder) > 0) THEN
        BEGIN
          CASE (lPartType) OF
            PartType::PageTitle: lPartText := HelpHeader.Title;
            PartType::HelpId: lPartText := HelpId;
            PartType::OpenObjectType: lPartText := lOpenObjectTypeText;
            PartType::OpenObjectId: lPartText := lOpenObjectIdText;
            PartType::OpenObjectName: lPartText := lOpenObjectNameText;
            PartType::Main: lPartText := CreateMainPart();
            PartType::Tip: lPartText := CreateTipPart();
            PartType::Note: lPartText := CreateNotePart();
            PartType::KeyFields: lPartText := CreateKeyFieldsPart();
            PartType::KeyButtons: lPartText := CreateKeyButtonsPart();
            PartType::RelatedLinks: lPartText := CreateRelatedLinksPart();
            PartType::RelatedPages: lPartText := CreateRelatedPagesPart();
          END;
          lContent := String.ReplaceAll(lContent, lPartPlaceholder, lPartText);
        END;
      END;

      EXIT(lContent);
    END;

    LOCAL PROCEDURE CreateContentTemplate@1000000070() TemplateText : Text;
    BEGIN
      ClearBuffer();
      AddToBuffer0('<html DIR="LTR" xmlns:MSHelp="http://msdn.microsoft.com/mshelp" xmlns:ddue="http://ddue.schemas.microsoft.com/authoring/2003/5" xmlns:xlink="http://www.w3.org/1999/xlink" xmlns:tool="http://www.microsoft.com/tooltip">');
      AddToBuffer0('  <head>');
      AddToBuffer0('    <meta http-equiv="content-type" content="text/html; charset=Windows-1252"></meta>');
      AddToBuffer0('    <meta http-equiv="msthemecompatible" content="Yes"></meta>');
      AddToBuffer0('    <meta name="save" content="history"></meta>');
      AddToBuffer1('    <meta name="ms-hkwd" content="%1"></meta>', GetPartPlaceholder(PartType::PageTitle));
      AddToBuffer1('    <meta name="ms-haid" content="%1"></meta>', GetPartPlaceholder(PartType::HelpId));
      AddToBuffer1('    <title>%1</title>', GetPartPlaceholder(PartType::PageTitle));
      AddToBuffer0('    <link rel="stylesheet" type="text/css" href="../local/Classic.css">');
      AddToBuffer0('    <script language="jscript" src="../local/script.js"></script>');
      AddToBuffer0('    <script language="jscript" src="../Feedback.js"></script>');
      AddToBuffer0('  </head>');
      AddToBuffer0('  <body>');
      AddToBuffer0('    <style>');
      AddToBuffer0('      .SmallImageContainer { width: 95%; margin: 10 auto; }');
      AddToBuffer0('      .NormalImageContainer { width: 95%; margin: 10 auto; }');
      AddToBuffer0('      .VideoContainer {width: 95%; height: 300px; margin: 10 auto; }');
      AddToBuffer0('      .Video {width: 400px; height: 280px; }');
      AddToBuffer0('    </style>');
      AddToBuffer0('    <div><input type="hidden" id="userDataCache" class="userDataStyle"></input><input type="hidden" id="hiddenScrollOffset"></input></div>');
      AddToBuffer0('    <img id="dropDownImage" style="display:none; height:0; width:0;" src="../local/drpdown.gif"></img>');
      AddToBuffer0('    <img id="dropDownHoverImage" style="display:none; height:0; width:0;" src="../local/drpdown_orange.gif"></img>');
      AddToBuffer0('    <img id="collapseImage" style="display:none; height:0; width:0;" src="../local/collapse.gif"></img>');
      AddToBuffer0('    <img id="expandImage" style="display:none; height:0; width:0;" src="../local/exp.gif"></img>');
      AddToBuffer0('    <img id="copyImage" style="display:none; height:0; width:0;" src="../local/copycode.gif"></img>');
      AddToBuffer0('    <img id="copyHoverImage" style="display:none; height:0; width:0;" src="../local/copycodeHighlight.gif"></img>');
      AddToBuffer0('    <div id="header"><table width="100%" id="topTable">');
      AddToBuffer0('      <tr id="headerTableRow1"><td align="left"><span id="runningHeaderText"></span></td></tr>');
      AddToBuffer1('      <tr id="headerTableRow2"><td align="left"><span id="nsrTitle">%1</span></td></tr>', GetPartPlaceholder(PartType::PageTitle));
      AddToBuffer0('    </table></div>');
      AddToBuffer0('    <div id="mainSection"><div id="mainBody">');
      AddToBuffer0('      <div id="allHistory" class="saveHistory" onsave="saveAll()" onload="loadAll()"></div>');
      AddToBuffer0('      <p />');
      AddToBuffer1('      %1', CreateContentTemplateOpenObject());
      AddToBuffer1('      <p>%1</p>', GetPartPlaceholder(PartType::Main));
      AddToBuffer1('      %1', GetPartPlaceholder(PartType::KeyFields));
      AddToBuffer1('      %1', GetPartPlaceholder(PartType::KeyButtons));
      AddToBuffer1('      %1', GetPartPlaceholder(PartType::RelatedPages));
      AddToBuffer1('      %1', GetPartPlaceholder(PartType::RelatedLinks));
      AddToBuffer1('      %1', GetPartPlaceholder(PartType::Note));
      AddToBuffer0('      <div class="alert">');
      AddToBuffer0('        <table width="100%" cellspacing="0" cellpadding="0">');
      AddToBuffer0('          <tr><th align="left"><img class="note" src="../local/Tip.gif"></img>Tip </th></tr>');
      AddToBuffer1('          <tr><td><p id="hyperlinks">%1</p></td></tr>', GetPartPlaceholder(PartType::Tip));
      AddToBuffer0('        </table>');
      AddToBuffer0('        <p></p>');
      AddToBuffer0('        <!--[if gte IE 5]>');
      AddToBuffer0('          <tool:tip element="seeAlsoToolTip" avoidmouse="false"/>');
      AddToBuffer0('          <tool:tip element="languageFilterToolTip" avoidmouse="false"/>');
      AddToBuffer0('        <![endif]--></div><div id="footer"><hr></hr>');
      AddToBuffer0('        <div>');
      AddToBuffer0('          <span ID="fb"></span>');
      AddToBuffer0('          <span ID="fbb">');
      AddToBuffer0('            <script type="text/javascript">FeedBackLink();</script>');
      AddToBuffer0('          </span>');
      AddToBuffer0('          <br>');
      AddToBuffer0('        </div>');
      AddToBuffer0('        <p>Ñ 2014 Microsoft. All rights reserved.</p>');
      AddToBuffer0('        <p></p>');
      AddToBuffer0('      </div>');
      AddToBuffer0('    </div>');
      AddToBuffer0('  </body>');
      AddToBuffer0('</html>');
      EXIT(Buffer);
    END;

    LOCAL PROCEDURE CreateContentTemplateOpenObject@1000000061() : Text;
    VAR
      lHtml@1000000000 : Text;
    BEGIN
      IF ((HelpHeader."Object Type" = HelpHeader."Object Type"::" ") OR (HelpHeader."Object ID" = 0)) THEN
        EXIT('');
      lHtml := STRSUBSTNO('<img src="..\shortcutCold.gif" alt=""></img><b><a href="DynamicsNAV:////runpage?%1=%2" alt="">%3</a></b>',
        GetPartPlaceholder(PartType::OpenObjectType), GetPartPlaceholder(PartType::OpenObjectId), GetPartPlaceholder(PartType::OpenObjectName));
      lHtml := STRSUBSTNO(tOpenText, lHtml);
      lHtml := STRSUBSTNO('<p>%1</p>', lHtml);
      EXIT(lHtml);
    END;

    LOCAL PROCEDURE ConstructFilePath@1000000057(pLanguage@1000000001 : Text;pHelpFileType@1000000000 : Option) : Text;
    VAR
      lFilePath@1000000002 : Text;
    BEGIN
      //DOC NA2016.16 - Constructing file path
      //  -> pLanguage: Language
      //  -> pHelpFileType: Help file type
      //  <- The file path

      lFilePath := HelpFolderPath;
      lFilePath := FileSystem.Combine(FALSE, lFilePath, pLanguage);
      lFilePath := FileSystem.Combine(FALSE, lFilePath, HelpFileNames[pHelpFileType]);
      EXIT(lFilePath);
    END;

    LOCAL PROCEDURE SaveContent@1000000050(pContent@1000000000 : Text);
    VAR
      lLanguage@1000000001 : Text;
    BEGIN
      //DOC NA2016.16 - Saving content
      //  -> pContent: The content

      FOREACH lLanguage IN Languages DO
        IF ( NOT (Preview)) THEN
        BEGIN
          SaveContentToFile(pContent, lLanguage, HelpFileType::Default);
          SaveRelatedObjectsToFiles(pContent, lLanguage);
        END
        ELSE
          SaveContentToFile(pContent, lLanguage, HelpFileType::Preview);
    END;

    LOCAL PROCEDURE SaveContentToFile@1000000053(pContent@1000000001 : Text;pLanguage@1000000005 : Text;pHelpFileType@1000000002 : Option);
    VAR
      lFilePath@1000000003 : Text;
    BEGIN
      //DOC NA2016.16 - Saving content to a file
      //  -> pContent: The content
      //  -> pLanguage: Language
      //  -> pHelpFileType: Help file type

      lFilePath := ConstructFilePath(pLanguage, pHelpFileType);
      SaveContentToFilePath(pContent, lFilePath);
    END;

    LOCAL PROCEDURE SaveRelatedObjectsToFiles@1000000013(pContent@1000000007 : Text;pLanguage@1000000006 : Text);
    VAR
      lHelpRelatedObject@1000000005 : Record 9062712;
      lStreamWriter@1000000001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
      lFilePath@1000000000 : Text;
      lHelpTypePrefix@1000000002 : Text;
      lHelpTypeSuffix@1000000003 : Text;
      lFileName@1000000004 : Text;
    BEGIN
      //DOC NA2016.16.1 - Saving related objects to files
      //  -> pContent: The content
      //  -> pLanguage: Language

      lHelpTypePrefix := GetHelpTypePrefix();
      lHelpTypeSuffix := GetHelpTypeSuffix(HelpFileType::Default);

      lHelpRelatedObject.RESET();
      lHelpRelatedObject.SETRANGE("Help No.", HelpHeader."No.");
      IF (lHelpRelatedObject.FINDSET()) THEN
      REPEAT
        lFileName := STRSUBSTNO('%1%2%3.htm', lHelpTypePrefix, lHelpRelatedObject."Object ID", lHelpTypeSuffix);
        lFilePath := HelpFolderPath;
        lFilePath := FileSystem.Combine(FALSE, lFilePath, pLanguage);
        lFilePath := FileSystem.Combine(FALSE, lFilePath, lFileName);
        SaveContentToFilePath(pContent, lFilePath);
      UNTIL (lHelpRelatedObject.NEXT() = 0);
    END;

    LOCAL PROCEDURE SaveContentToFilePath@1000000058(pContent@1000000001 : Text;pFilePath@1000000002 : Text);
    VAR
      lStreamWriter@1000000004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
    BEGIN
      //DOC NA2016.16 - Saving content to a file path
      //  -> pContent: The content
      //  -> pFilePath: File path

      FileSystem.DeleteFileIfExists(FALSE, pFilePath);
      lStreamWriter := lStreamWriter.StreamWriter(pFilePath);
      lStreamWriter.Write(pContent);
      lStreamWriter.Close();
    END;

    LOCAL PROCEDURE RemoveContent@1000000048();
    VAR
      lLanguage@1000000000 : Text;
    BEGIN
      //DOC NA2016.16 - Removing help content

      FOREACH lLanguage IN Languages DO
      BEGIN
        RemoveContentFile(lLanguage, HelpFileType::Default);
        RemoveContentFile(lLanguage, HelpFileType::Standard);
        RemoveContentFile(lLanguage, HelpFileType::Preview);
      END;
    END;

    LOCAL PROCEDURE RemoveContentFile@1000000049(pLanguage@1000000001 : Text;pHelpFileType@1000000003 : Option);
    VAR
      lFilePath@1000000002 : Text;
    BEGIN
      //DOC NA2016.16 - Removing help content file
      //  -> pLanguage: Language
      //  -> pHelpFileType: Help file type

      lFilePath := ConstructFilePath(pLanguage, pHelpFileType);
      FileSystem.DeleteFileIfExists(FALSE, lFilePath);
    END;

    LOCAL PROCEDURE UpdateHelpHeader@1000000051(pPublish@1000000000 : Boolean);
    BEGIN
      //DOC NA2016.16 - Updating help header
      //  -> pPublish: Publish flag (TRUE = publish, FALSE = unpublish)

      IF (pPublish) THEN
      BEGIN
        HelpHeader.VALIDATE("File Name", HelpFileNames[HelpFileType::Default]);
        HelpHeader.VALIDATE(URL, STRSUBSTNO(tUrl, HelpBaseUrl, HelpFileNames[HelpFileType::Default]));
        HelpHeader.VALIDATE("Preview URL", STRSUBSTNO(tUrl, HelpBaseUrl, HelpFileNames[HelpFileType::Preview]));
      END
      ELSE
      BEGIN
        HelpHeader.VALIDATE("File Name", '');
        HelpHeader.VALIDATE(URL, '');
        HelpHeader.VALIDATE("Preview URL", '');
      END;
      HelpHeader.VALIDATE(Published, pPublish);
      HelpHeader.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE ShowHelp@1000000033(pPreview@1000000000 : Boolean);
    BEGIN
      //DOC NA2016.16 - Showing (or previewing) help
      //  -> pPreview: Preview flag

      CLEARLASTERROR();
      IF ( NOT (TryToShowHelp(pPreview))) THEN
        MESSAGE(GETLASTERRORTEXT());
    END;

    [TryFunction]
    LOCAL PROCEDURE TryToShowHelp@1000000054(pPreview@1000000000 : Boolean);
    BEGIN
      //DOC NA2016.16 - Trying to show (preview) help
      //  -> pPreview: Preview flag

      IF (Preview) THEN
      BEGIN
        HelpHeader.TESTFIELD("Preview URL");
        HYPERLINK(HelpHeader."Preview URL");
      END
      ELSE
      BEGIN
        HelpHeader.TESTFIELD(URL);
        HYPERLINK(HelpHeader.URL)
      END;
    END;

    LOCAL PROCEDURE "--- Parts ---"@1000000022();
    BEGIN
    END;

    LOCAL PROCEDURE GetPartPlaceholder@1000000025(pPartType@1000000000 : Option) : Text;
    VAR
      lPlaceholder@1000000001 : Text;
    BEGIN
      //DOC NA2016.16 - Returning a placeholder for given part
      //  -> pPartType: The part type
      //  <- The placeholder

      CASE (pPartType) OF
        PartType::PageTitle: lPlaceholder := 'PAGETITLE';
        PartType::HelpId: lPlaceholder := 'HELPID';
        PartType::OpenObjectType: lPlaceholder := 'OPENOBJECTTYPE';
        PartType::OpenObjectId: lPlaceholder := 'OPENOBJECTID';
        PartType::OpenObjectName: lPlaceholder := 'OPENOBJECTNAME';
        PartType::Main: lPlaceholder := 'MAIN';
        PartType::Tip: lPlaceholder := 'TIP';
        PartType::Note: lPlaceholder := 'NOTE';
        PartType::KeyFields: lPlaceholder := 'KEYFIELDS';
        PartType::KeyButtons: lPlaceholder := 'KEYBUTTONS';
        PartType::RelatedLinks: lPlaceholder := 'RELATEDLINKS';
        PartType::RelatedPages: lPlaceholder := 'RELATEDPAGES';
      END;
      EXIT(lPlaceholder);
    END;

    LOCAL PROCEDURE CreateMainPart@1000000026() : Text;
    VAR
      lPart@1000000000 : Text;
    BEGIN
      lPart := TextExtensionManagement.GetFullText(DATABASE::"Help Header", HelpHeader.FIELDNO("Help Text"), HelpHeader.GETPOSITION(TRUE));
      ApplyElements(lPart);
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateTipPart@1000000027() : Text;
    VAR
      lPart@1000000000 : Text;
    BEGIN
      lPart := TnpToolkit.IIFText(HelpHeader.Tip <> '', HelpHeader.Tip, tDefaultTip);
      ApplyElements(lPart);
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateNotePart@1000000039() : Text;
    VAR
      lPart@1000000001 : Text;
      lNote@1000000000 : Text;
    BEGIN
      lNote := TextExtensionManagement.GetFullText(DATABASE::"Help Header", HelpHeader.FIELDNO("Note Text"), HelpHeader.GETPOSITION(TRUE));
      IF (lNote <> '') THEN
      BEGIN
        ClearBuffer();
        AddToBuffer0('<table width="100%" cellspacing="0" cellpadding="0">');
        AddToBuffer0('  <tr><th align="left"><img class="note" src="../local/note.gif"></img>Note </th></tr>');
        AddToBuffer0('  <tr><td><p>%1</p></td></tr>');
        AddToBuffer0('</table>');
        lPart := STRSUBSTNO(Buffer, lNote);
        ApplyElements(lPart);
      END;
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateKeyFieldsPart@1000000029() : Text;
    VAR
      lPart@1000000001 : Text;
      lFieldPart@1000000000 : Text;
      eTitle@1000000002 : TextConst 'ENU=Key Fields;ENG=Key Fields';
    BEGIN
      HelpLine.RESET();
      HelpLine.SETRANGE("Help No.", HelpHeader."No.");
      HelpLine.SETRANGE(Type, HelpLine.Type::Field);
      IF (HelpLine.FINDSET()) THEN
      BEGIN
        lPart := lPart + STRSUBSTNO('<h2 class="heading">%1</h2>', eTitle);
        REPEAT
          lFieldPart := TextExtensionManagement.GetFullText(DATABASE::"Help Line", HelpLine.FIELDNO("Help Text"), HelpLine.GETPOSITION(TRUE));
          lFieldPart := STRSUBSTNO('<p><b>%1:</b> %2</p>', HelpLine."Field Name", lFieldPart);
          ApplyElements(lFieldPart);
          lPart := lPart + lFieldPart;
        UNTIL (HelpLine.NEXT() = 0);
      END;
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateKeyButtonsPart@1000000031() : Text;
    VAR
      lPart@1000000000 : Text;
      lButtonPart@1000000001 : Text;
      eTitle@1000000002 : TextConst 'ENU=Key Buttons;ENG=Key Buttons';
    BEGIN
      HelpLine.RESET();
      HelpLine.SETRANGE("Help No.", HelpHeader."No.");
      HelpLine.SETRANGE(Type, HelpLine.Type::Button);
      IF (HelpLine.FINDSET()) THEN
      BEGIN
        lPart := lPart + STRSUBSTNO('<h2 class="heading">%1</h2>', eTitle);
        REPEAT
          lButtonPart := TextExtensionManagement.GetFullText(DATABASE::"Help Line", HelpLine.FIELDNO("Help Text"), HelpLine.GETPOSITION(TRUE));
          lButtonPart := STRSUBSTNO('<p><b>%1:</b> %2</p>', HelpLine."Field Name", lButtonPart);
          ApplyElements(lButtonPart);
          lPart := lPart + lButtonPart;
        UNTIL (HelpLine.NEXT() = 0);
      END;
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateRelatedLinksPart@1000000032() : Text;
    VAR
      lPart@1000000000 : Text;
      lRelatedLinkPart@1000000001 : Text;
      eTitle@1000000002 : TextConst 'ENU=Related Links;ENG=Related Links';
    BEGIN
      HelpLine.RESET();
      HelpLine.SETRANGE("Help No.", HelpHeader."No.");
      HelpLine.SETRANGE(Type, HelpLine.Type::"Related Link");
      IF (HelpLine.FINDSET()) THEN
      BEGIN
        lPart := lPart + STRSUBSTNO('<h2 class="heading">%1</h2>', eTitle);
        REPEAT
          lRelatedLinkPart := STRSUBSTNO('<a href="%2">%1</a><p></p>', HelpLine."Field Name", lRelatedLinkPart);
          lPart := lPart + lRelatedLinkPart;
        UNTIL (HelpLine.NEXT() = 0);
      END;
      EXIT(lPart);
    END;

    LOCAL PROCEDURE CreateRelatedPagesPart@1000000034() : Text;
    VAR
      lRelatedHelpHeader@1000000002 : Record 9062710;
      lPart@1000000000 : Text;
      lRelatedPagePart@1000000001 : Text;
      eTitle@1000000003 : TextConst 'ENU=Related Pages;ENG=Related Pages';
    BEGIN
      HelpLine.RESET();
      HelpLine.SETRANGE("Help No.", HelpHeader."No.");
      HelpLine.SETRANGE(Type, HelpLine.Type::"Related Help");
      IF (HelpLine.FINDSET()) THEN
      BEGIN
        lPart := lPart + STRSUBSTNO('<h2 class="heading">%1</h2>', eTitle);
        REPEAT
          IF (HelpLine."Related Help No." <> '') THEN
            IF (lRelatedHelpHeader.GET(HelpLine."Related Help No.")) THEN
              IF (lRelatedHelpHeader.URL <> '') THEN
              BEGIN
                lRelatedPagePart := STRSUBSTNO('<a href="%2">%1</a><p></p>', lRelatedHelpHeader.Title, lRelatedHelpHeader.URL);
                lPart := lPart + lRelatedPagePart;
              END;
        UNTIL (HelpLine.NEXT() = 0);
      END;
      EXIT(lPart);
    END;

    LOCAL PROCEDURE "--- Elements ---"@1000000018();
    BEGIN
    END;

    LOCAL PROCEDURE GetElementPlaceholder@1000000044(pElementType@1000000000 : Option) : Text;
    VAR
      lPlaceholder@1000000001 : Text;
    BEGIN
      CASE (pElementType) OF
        ElementType::YoutubeVideoUrl: lPlaceholder := tYoutubeVideoPlaceholder;
        ElementType::AnimatedGif: lPlaceholder := tAnimatedGifPlaceholder;
        ElementType::SmallPicture: lPlaceholder := tSmallPicturePlaceholder;
        ElementType::ScreenShot1: lPlaceholder := tScreenShot1Placeholder;
        ElementType::ScreenShot2: lPlaceholder := tScreenShot2Placeholder;
        ElementType::ScreenShot3: lPlaceholder := tScreenShot3Placeholder;
      END;
      EXIT(CreatePlaceholder(lPlaceholder));
    END;

    LOCAL PROCEDURE ApplyElements@1000000041(VAR pContent@1000000000 : Text);
    VAR
      lElementType@1000000001 : Option;
      lHelpImagePlaceholder@1000000002 : Text;
      lHelpImageElement@1000000003 : Text;
      lHelpImageBlob@1000000004 : TEMPORARY Record 99008535;
    BEGIN
      FOR lElementType := ElementType::YoutubeVideoUrl TO ElementType::ScreenShot3 DO
        ApplyElement(pContent, lElementType);

      lHelpImageBlob.INSERT();
      HelpImage.RESET();
      HelpImage.SETRANGE("Help No.", HelpHeader."No.");
      HelpImage.SETFILTER(Name, '<>%1');
      IF (HelpImage.FINDSET()) THEN
      REPEAT
        lHelpImagePlaceholder := CreatePlaceholder(HelpImage.Name);
        IF (STRPOS(pContent, lHelpImagePlaceholder) > 0) THEN
        BEGIN
          HelpImage.CALCFIELDS(Content);
          IF (HelpImage.Content.HASVALUE()) THEN
          BEGIN
            lHelpImageBlob.Blob := HelpImage.Content;
            lHelpImageBlob.MODIFY();
            lHelpImageElement := CreatePictureElementForBlob(lHelpImageBlob, HelpImage.Name, 'NormalImageContainer');
            pContent := String.Replace(pContent, lHelpImagePlaceholder, lHelpImageElement);
          END;
        END;
      UNTIL (HelpImage.NEXT() = 0);
    END;

    LOCAL PROCEDURE ApplyElement@1000000005(VAR pContent@1000000000 : Text;pElementType@1000000002 : Option);
    VAR
      lElementPlaceholder@1000000001 : Text;
      lElement@1000000003 : Text;
    BEGIN
      lElementPlaceholder := GetElementPlaceholder(pElementType);
      IF (STRPOS(pContent, lElementPlaceholder) > 0) THEN
      BEGIN
        CASE (pElementType) OF
          ElementType::YoutubeVideoUrl: lElement := CreateYoutubeElement();
          ElementType::AnimatedGif: lElement := CreateAnimatedGifElement();
          ElementType::SmallPicture: lElement := CreateSmallPictureElement();
          ElementType::ScreenShot1: lElement := CreateNormalPictureElement(1);
          ElementType::ScreenShot2: lElement := CreateNormalPictureElement(2);
          ElementType::ScreenShot3: lElement := CreateNormalPictureElement(3);
        END;
        pContent := String.Replace(pContent, lElementPlaceholder, lElement);
      END;
    END;

    LOCAL PROCEDURE CreateYoutubeElement@1000000004() : Text;
    VAR
      lElement@1000000000 : Text;
    BEGIN
      lElement := '';
      IF (HelpHeader."Youtube Video URL" <> '') THEN
        lElement := STRSUBSTNO('<div class="VideoContainer"><iframe class="Video" src="%1" allowfullscreen="1"></iframe></div>',
          HelpHeader."Youtube Video URL");
      EXIT(lElement);
    END;

    LOCAL PROCEDURE CreateAnimatedGifElement@1000000014() : Text;
    BEGIN
      EXIT(CreatePictureElementForField(HelpHeader.FIELDNO("Animated GIF"), 'AnimatedGif', 'SmallImageContainer'));
    END;

    LOCAL PROCEDURE CreateSmallPictureElement@1000000056() : Text;
    BEGIN
      EXIT(CreatePictureElementForField(HelpHeader.FIELDNO("Small Picture"), 'SmallPicture', 'SmallImageContainer'));
    END;

    LOCAL PROCEDURE CreateNormalPictureElement@1000000006(pIndex@1000000000 : Integer) : Text;
    VAR
      ePictureId@1000000011 : TextConst 'ENU=ScreenShot%1;ENG=ScreenShot%1';
      lFieldId@1000000001 : Integer;
      lPictureId@1000000002 : Text;
    BEGIN
      IF ((pIndex < 1) OR (pIndex > 3)) THEN
        EXIT('');
      CASE (pIndex) OF
        1: lFieldId := HelpHeader.FIELDNO("Screen Shot 1");
        2: lFieldId := HelpHeader.FIELDNO("Screen Shot 2");
        3: lFieldId := HelpHeader.FIELDNO("Screen Shot 3");
      END;
      lPictureId := STRSUBSTNO(ePictureId, pIndex);
      EXIT(CreatePictureElementForField(lFieldId, lPictureId, 'NormalImageContainer'));
    END;

    LOCAL PROCEDURE CreatePictureElementForField@1000000030(pFieldId@1000000002 : Integer;pPictureId@1000000006 : Text;pDivClass@1000000003 : Text) : Text;
    VAR
      lElement@1000000001 : Text;
      lBlob@1000000004 : TEMPORARY Record 99008535;
    BEGIN
      //DOC NA2016.16.1 - Creating picture element for a field
      //  -> pFieldId: Field ID
      //  -> pPictureId: Picture ID
      //  -> pDivClass: Div class
      //  <- The element

      CLEAR(lElement);

      IF (GetPicture(pFieldId, lBlob)) THEN
        lElement := CreatePictureElementForBlob(lBlob, pPictureId, pDivClass);

      EXIT(lElement);
    END;

    LOCAL PROCEDURE CreatePictureElementForBlob@1000000055(VAR pPictureBlob@1000000003 : Record 99008535;pPictureId@1000000001 : Text;pDivClass@1000000000 : Text) : Text;
    VAR
      lElement@1000000007 : Text;
      lFilePath@1000000005 : Text;
      lFileName@1000000004 : Text;
      lFileExtension@1000000002 : Text;
    BEGIN
      //DOC NA2016.16.1 - Creating picture element for a BLOB
      //  <> pPictureBlob: The blob
      //  -> pPictureId: Picture ID
      //  -> pDivClass: Div class
      //  <- The element

      CLEAR(lElement);

      IF (TnpUtils.GetImageExtension(pPictureBlob, lFileExtension)) THEN
      BEGIN
        lFileName := STRSUBSTNO('%1_%2.%3', HelpIdWithPrefix, pPictureId, lFileExtension);
        lFilePath := FileSystem.Combine(FALSE, HelpLocalFolderPath, lFileName);
        FileSystem.ExportBlobToFileSilent(FALSE, pPictureBlob, lFilePath);
        lElement := STRSUBSTNO('<div class="%1"><img id="%2" src="../%3/%4" ></div>', pDivClass, pPictureId, tLocalFolder, lFileName);
      END;

      EXIT(lElement);
    END;

    LOCAL PROCEDURE GetPicture@1000000035(pFieldId@1000000000 : Integer;VAR pBlob@1000000001 : Record 99008535) : Boolean;
    BEGIN
      CLEAR(pBlob);
      CASE (pFieldId) OF
        HelpHeader.FIELDNO("Animated GIF"):
          BEGIN
            HelpHeader.CALCFIELDS("Animated GIF");
            pBlob.Blob := HelpHeader."Animated GIF";
          END;
        HelpHeader.FIELDNO("Small Picture"):
          BEGIN
            HelpHeader.CALCFIELDS("Small Picture");
            pBlob.Blob := HelpHeader."Small Picture";
          END;
        HelpHeader.FIELDNO("Screen Shot 1"):
          BEGIN
            HelpHeader.CALCFIELDS("Screen Shot 1");
            pBlob.Blob := HelpHeader."Screen Shot 1";
          END;
        HelpHeader.FIELDNO("Screen Shot 2"):
          BEGIN
            HelpHeader.CALCFIELDS("Screen Shot 2");
            pBlob.Blob := HelpHeader."Screen Shot 2";
          END;
        HelpHeader.FIELDNO("Screen Shot 3"):
          BEGIN
            HelpHeader.CALCFIELDS("Screen Shot 3");
            pBlob.Blob := HelpHeader."Screen Shot 3";
          END;
      END;
      pBlob.INSERT();
      EXIT(pBlob.Blob.HASVALUE());
    END;

    LOCAL PROCEDURE "--- Multiple ---"@1000000038();
    BEGIN
    END;

    LOCAL PROCEDURE InitialiseMultiple@1000000040(VAR pHelpHeader@1000000002 : Record 9062710;VAR pInteraction@1000000000 : Boolean;pMessage@1000000006 : Text) : Boolean;
    VAR
      lCount@1000000001 : Integer;
      lHelpText@1000000005 : Text;
      eDialogTitle@1000000003 : TextConst 'ENU=Processing...;ENG=Processing...';
    BEGIN
      pInteraction := ((pInteraction) AND (GUIALLOWED()));
      IF (pInteraction) THEN
      BEGIN
        lCount := WorkOutMultipleCount(pHelpHeader, lHelpText);
        IF ( NOT (CONFIRM(pMessage, TRUE, lCount, LOWERCASE(lHelpText)))) THEN
          EXIT(FALSE);
        CLEAR(DialogManagement);
        DialogManagement.Add(tHelp, 10, 1, 30, FALSE, FALSE);
        DialogManagement.Open(eDialogTitle);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FinaliseMultiple@1000000042(VAR pHelpHeader@1000000003 : Record 9062710;pInteraction@1000000000 : Boolean;pMessage@1000000001 : Text) : Boolean;
    VAR
      lCount@1000000002 : Integer;
      lHelpText@1000000004 : Text;
    BEGIN
      IF (pInteraction) THEN
      BEGIN
        DialogManagement.Close();
        lCount := WorkOutMultipleCount(pHelpHeader, lHelpText);
        MESSAGE(pMessage, lCount, LOWERCASE(lHelpText));
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE WorkOutMultipleCount@1000000007(VAR pHelpHeader@1000000001 : Record 9062710;VAR pHelpText@1000000002 : Text) : Integer;
    VAR
      lCount@1000000000 : Integer;
    BEGIN
      lCount := pHelpHeader.COUNT();
      CASE (lCount) OF
        0: ERROR(tNoHelps);
        1: pHelpText := tHelp;
        ELSE pHelpText := LanguageToolkit.ToPlural(tHelp);
      END;
      EXIT(lCount);
    END;

    LOCAL PROCEDURE "--- Tools ---"@1000000009();
    BEGIN
    END;

    LOCAL PROCEDURE CreateHelpId@1000000003(pText@1000000000 : Text) : Text;
    VAR
      lHelpId@1000000001 : Text;
    BEGIN
      lHelpId := DELCHR(pText, '<>', ' ');
      lHelpId := String.ToTitleCase(lHelpId);
      lHelpId := DELCHR(pText, '=', ' ');
      lHelpId := FileSystem.ToFileName(lHelpId);
      EXIT(lHelpId);
    END;

    LOCAL PROCEDURE CreatePlaceholder@1000000046(pName@1000000000 : Text) : Text;
    VAR
      lPlaceholder@1000000001 : Text;
    BEGIN
      IF (pName <> '') THEN
        EXIT('[' + pName + ']')
      ELSE
        EXIT('');
    END;

    LOCAL PROCEDURE ClearBuffer@1000000010();
    BEGIN
      CLEAR(Buffer);
    END;

    LOCAL PROCEDURE AddToBuffer0@1000000019(pText@1000000000 : Text);
    BEGIN
      AddToBuffer1(pText, '');
    END;

    LOCAL PROCEDURE AddToBuffer1@1000000064(pText@1000000000 : Text;pParameter1@1000000001 : Text);
    BEGIN
      Buffer := Buffer + STRSUBSTNO(pText, pParameter1);
    END;

    BEGIN
    {
      //DOC NA2015.7    AP 04/06/2015 - Created
      //DOC NA2016.10   JH 28/09/2015 - Upgraded to 2016
      //DOC NA2016.15   JH 09/05/2016 - CfMD modifications
      //DOC NA2016.16   JH 13/06/2016 - Bugfixes
      //DOC NA2016.16   JH 13/07/2016 - Fixes
      //DOC NA2016.16.1 JH 15/07/2016 - Related objects, images
      //DOC NA2016.16.2 JH 01/09/2016 - Moving help
    }
    END.
  }
}

