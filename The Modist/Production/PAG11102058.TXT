OBJECT Page 11102058 OM - Document Sheet
{
  OBJECT-PROPERTIES
  {
    Date=12/10/14;
    Time=12:00:00 PM;
    Version List=OMA9.04;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Document Sheet;
               ENU=Document Sheet;
               ESP=Document Sheet;
               FRA=Document Sheet;
               NLD=Documenten;
               DEA=Document Sheet];
    InsertAllowed=No;
    SourceTable=Table11102046;
    DelayedInsert=Yes;
    PopulateAllFields=Yes;
    DataCaptionFields=No.;
    PageType=Worksheet;
    OnNewRecord=BEGIN
                  SetUpNewLine(BelowxRec);
                END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add;
                                 ENU=Add;
                                 ESP=Add;
                                 FRA=Add;
                                 NLD=Toevoegen;
                                 DEA=Add];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Add;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Save as;
                                 ENU=Save as;
                                 ESP=Save as;
                                 FRA=Save as;
                                 NLD=Opslaan &als;
                                 DEA=Save as];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Save;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SaveAs2;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      ShortCutKey=Return;
                      CaptionML=[DEU=Open;
                                 ENU=Open;
                                 ESP=Open;
                                 FRA=Open;
                                 NLD=Openen;
                                 DEA=Open];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Open;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                GroupType=Repeater }

    { 7   ;2   ;Field     ;
                SourceExpr="Insert Date" }

    { 8   ;2   ;Field     ;
                SourceExpr="Insert Time" }

    { 9   ;2   ;Field     ;
                SourceExpr=Path }

    { 10  ;2   ;Field     ;
                SourceExpr="File Name" }

    { 11  ;2   ;Field     ;
                SourceExpr="Attach to Transport" }

    { 12  ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 13  ;1   ;Part      ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 14  ;1   ;Part      ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {

    LOCAL PROCEDURE Add@1();
    VAR
      DocumentLine@1000 : Record 11102046;
      CommonDialog@1001 : Codeunit 11102059;
      FileHandler@1002 : Codeunit 11102097;
      oFile@1003 : File;
      InStr@1004 : InStream;
      OutStr@1005 : OutStream;
      ImportFileName@1006 : Text;
      ServerFileName@1007 : Text;
      LastLineNo@1008 : Integer;
    BEGIN
      IF NOT CommonDialog.OpenFile(ImportFileName, ServerFileName) THEN
        EXIT;

      IF ServerFileName = '' THEN
        EXIT;

      DocumentLine.SETRANGE("Table Name", "Table Name");
      DocumentLine.SETRANGE("No.", "No.");
      IF DocumentLine.FINDLAST THEN
        LastLineNo := DocumentLine."Line No.";

      INIT;
      "Line No." := LastLineNo + 10000;

      oFile.OPEN(ServerFileName);
      oFile.CREATEINSTREAM(InStr);
      Document.CREATEOUTSTREAM(OutStr);
      COPYSTREAM(OutStr, InStr);
      oFile.CLOSE;

      "Insert Date" := TODAY;
      "Insert Time" := TIME;

      Path := FileHandler.GetDirectoryName(ImportFileName);
      "File Name" := FileHandler.GetFileName(ImportFileName);

      INSERT(TRUE);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SaveAs2@2();
    VAR
      CommonDialog@1000 : Codeunit 11102059;
      FileHandler@1001 : Codeunit 11102097;
      oFile@1002 : File;
      InStr@1003 : InStream;
      OutStr@1004 : OutStream;
      ClientFileName@1005 : Text;
      ServerFileName@1006 : Text;
      cNoDocument@1007 : TextConst 'DEU=No document found.;ENU=No document found.;ESP=No document found.;FRA=No document found.;NLD=Geen document gevonden.;DEA=No document found.';
    BEGIN
      CALCFIELDS(Document);
      IF NOT Document.HASVALUE THEN
        ERROR(cNoDocument);

      ClientFileName := Path + "File Name";
      IF NOT CommonDialog.SaveFile(ClientFileName, ServerFileName, '') THEN
        EXIT;

      oFile.CREATE(ServerFileName);
      oFile.CREATEOUTSTREAM(OutStr);

      Document.CREATEINSTREAM(InStr);
      COPYSTREAM(OutStr, InStr);

      oFile.CLOSE;

      FileHandler.DownloadToClient(ServerFileName, ClientFileName, '');
    END;

    LOCAL PROCEDURE Open@3();
    VAR
      FileHandler@1000 : Codeunit 11102097;
      oFile@1001 : File;
      InStr@1002 : InStream;
      OutStr@1003 : OutStream;
      Directory@1004 : Text;
      ExportFileName@1005 : Text;
      cNoDocument@1006 : TextConst 'DEU=No document found.;ENU=No document found.;ESP=No document found.;FRA=No document found.;NLD=Geen document gevonden.;DEA=No document found.';
    BEGIN
      CALCFIELDS(Document);
      IF NOT Document.HASVALUE THEN
        ERROR(cNoDocument);

      IF FileHandler.IsWebClient THEN BEGIN
        Document.CREATEINSTREAM(InStr);
        ExportFileName := "File Name";

        DOWNLOADFROMSTREAM(InStr, '', '', '', ExportFileName);
      END ELSE BEGIN
        Directory := FileHandler.TempDirectory;

        oFile.CREATE(Directory + "File Name");
        oFile.CREATEOUTSTREAM(OutStr);

        Document.CREATEINSTREAM(InStr);
        COPYSTREAM(OutStr, InStr);

        oFile.CLOSE;

        HYPERLINK(Directory + "File Name");
      END;
    END;

    BEGIN
    END.
  }
}

