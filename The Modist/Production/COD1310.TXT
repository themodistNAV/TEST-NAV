OBJECT Codeunit 1310 Mini Purch-Calc Disc. By Type
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InvDiscBaseAmountIsZeroErr@1000 : TextConst 'ENU=There is no amount that you can apply an invoice discount to.;ENG=There is no amount that you can apply an invoice discount to.';

    PROCEDURE ApplyDefaultInvoiceDiscount@5(InvoiceDiscountAmount@1000 : Decimal;VAR PurchaseHeader@1001 : Record 38);
    BEGIN
      IF PurchaseHeader."Invoice Discount Calculation" = PurchaseHeader."Invoice Discount Calculation"::Amount THEN
        ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,PurchaseHeader)
      ELSE
        ApplyInvDiscBasedOnPct(PurchaseHeader);
    END;

    PROCEDURE ApplyInvDiscBasedOnAmt@1(InvoiceDiscountAmount@1000 : Decimal;VAR PurchaseHeader@1004 : Record 38);
    VAR
      TempVATAmountLine@1001 : TEMPORARY Record 290;
      PurchaseLine@1002 : Record 39;
      InvDiscBaseAmount@1003 : Decimal;
    BEGIN
      WITH PurchaseHeader DO BEGIN
        PurchaseLine.SETRANGE("Document No.","No.");
        PurchaseLine.SETRANGE("Document Type","Document Type");

        PurchaseLine.CalcVATAmountLines(0,PurchaseHeader,PurchaseLine,TempVATAmountLine);

        InvDiscBaseAmount := TempVATAmountLine.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");

        IF (InvDiscBaseAmount = 0) AND (InvoiceDiscountAmount > 0) THEN
          ERROR(InvDiscBaseAmountIsZeroErr);

        TempVATAmountLine.SetInvoiceDiscountAmount(InvoiceDiscountAmount,"Currency Code",
          "Prices Including VAT","VAT Base Discount %");

        PurchaseLine.UpdateVATOnLines(0,PurchaseHeader,PurchaseLine,TempVATAmountLine);

        "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
        "Invoice Discount Value" := InvoiceDiscountAmount;

        MODIFY;
      END;
    END;

    LOCAL PROCEDURE ApplyInvDiscBasedOnPct@6(VAR PurchaseHeader@1002 : Record 38);
    VAR
      PurchaseLine@1000 : Record 39;
    BEGIN
      WITH PurchaseHeader DO BEGIN
        PurchaseLine.SETRANGE("Document No.","No.");
        PurchaseLine.SETRANGE("Document Type","Document Type");
        IF PurchaseLine.FINDFIRST THEN BEGIN
          CODEUNIT.RUN(CODEUNIT::"Purch.-Calc.Discount",PurchaseLine);
          GET("Document Type","No.");
        END;
      END;
    END;

    PROCEDURE GetVendInvoiceDiscountPct@7(PurchaseLine@1001 : Record 39) : Decimal;
    VAR
      PurchaseHeader@1000 : Record 38;
      VendInvDisc@1002 : Record 24;
    BEGIN
      WITH PurchaseHeader DO
        IF GET(PurchaseLine."Document Type",PurchaseLine."Document No.") THEN
          IF "Invoice Discount Calculation" = "Invoice Discount Calculation"::"%" THEN BEGIN
            // Only if CustInvDisc table is empty header is not updated
            VendInvDisc.SETRANGE(Code,"Invoice Disc. Code");
            IF NOT VendInvDisc.FINDFIRST THEN
              EXIT(0);

            EXIT("Invoice Discount Value");
          END;

      // We are returning zero because Discount percentage from customer is not used
      EXIT(0);
    END;

    BEGIN
    END.
  }
}

