OBJECT Codeunit 9087002 Size Management
{
  OBJECT-PROPERTIES
  {
    Date=21/11/17;
    Time=[ 4:55:49 PM];
    Modified=Yes;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      nSembleSetup@1000000001 : Record 9087000;
      StyleManagement@1000000000 : Codeunit 9087006;

    PROCEDURE CopySelectedSizes@1000000001(pToSizeGroupCode@1000000000 : Code[20]);
    VAR
      lFromSizeGroup@1000000001 : Record 9087001;
    BEGIN
      lFromSizeGroup.RESET();
      lFromSizeGroup.FILTERGROUP(2);
      lFromSizeGroup.SETFILTER(Code, '<>%1', pToSizeGroupCode);
      lFromSizeGroup.FILTERGROUP(0);
      IF (PAGE.RUNMODAL(0, lFromSizeGroup) = ACTION::LookupOK) THEN
        CopySizes(lFromSizeGroup.Code, pToSizeGroupCode);
    END;

    PROCEDURE CopySizes@1000000002(pFromSizeGroupCode@1000000001 : Code[20];pToSizeGroupCode@1000000000 : Code[20]) : Boolean;
    VAR
      lFromSizeGroup@1000000003 : Record 9087001;
      lFromSize@1000000005 : Record 9087002;
      lToSizeGroup@1000000004 : Record 9087001;
      lToSize@1000000002 : Record 9087002;
    BEGIN
      IF ((pFromSizeGroupCode = '') OR (pToSizeGroupCode = '') OR (pFromSizeGroupCode = pToSizeGroupCode)) THEN
        EXIT(FALSE);
      lFromSize.RESET();
      lFromSize.SETRANGE("Size Group Code", pFromSizeGroupCode);
      IF (lFromSize.FINDSET()) THEN
      REPEAT
        IF ( NOT (lToSize.GET(pToSizeGroupCode, lFromSize.Code))) THEN
        BEGIN
          lFromSizeGroup.GET(lFromSize."Size Group Code");
          lToSize := lFromSize;
          lToSize.VALIDATE("Size Group Code", pToSizeGroupCode);
          IF (lFromSizeGroup."Standard Size Group Code" <> lToSizeGroup."Standard Size Group Code") THEN
            lToSize.VALIDATE("Standard Size Group Code", lToSizeGroup."Standard Size Group Code");
          lToSize.INSERT(TRUE);
        END;
      UNTIL (lFromSize.NEXT() = 0);
      EXIT(TRUE);
    END;

    PROCEDURE BuildItemVariants@1000000000(pItem@1000000000 : Record 27;pOldSizeGroupCode@1000000001 : Code[20]);
    VAR
      lColour@1000000007 : Record 9087003;
      lItemVariant@1000000002 : Record 5401;
      lSizeGroup@1000000006 : Record 9087001;
      lSize@1000000003 : Record 9087002;
      lDescriptionTemplate@1000000005 : Text[50];
      lItemVariantDescription@1000000004 : ARRAY [2] OF Text[50];
    BEGIN
      //DOC NS2015.1 - Building item variants for the item
      //  -> pItem: The item

      nSembleSetup.VerifyAndGet();

      IF (pOldSizeGroupCode <> '') THEN
      BEGIN
        lItemVariant.RESET();
        lItemVariant.SETRANGE("Item No.", pItem."No.");
        //lItemVariant.DELETEALL(TRUE);
      END;

      IF (pItem."Size Group Code" <> '') THEN
      BEGIN
        IF ( NOT (lColour.GET(pItem."Colour Code"))) THEN
          CLEAR(lColour);
        lSizeGroup.GET(pItem."Size Group Code");

        lSize.RESET();
        lSize.SETRANGE("Size Group Code", pItem."Size Group Code");
        IF (lSize.FINDSET()) THEN
        REPEAT
          lDescriptionTemplate := StyleManagement.CreateDescriptionTemplate(nSembleSetup."Style Item Variant Description", '%5 %2 %3 %8');
          StyleManagement.DescriptionToDescriptionFields(STRSUBSTNO(lDescriptionTemplate, '', pItem.Description, pItem."Description 2",
            pItem."Colour Code", lColour.Name, pItem."Size Group Code", lSizeGroup.Name, lSize.Code, lSize.Name), lItemVariantDescription);

          CLEAR(lItemVariant);
          lItemVariant.INIT();
          lItemVariant.VALIDATE("Item No.", pItem."No.");
          lItemVariant.VALIDATE(Code, lSize.Code);
          lItemVariant.VALIDATE("Colour Code", pItem."Colour Code");
          lItemVariant.VALIDATE("Size Group Code", pItem."Size Group Code");
          lItemVariant.VALIDATE("Size Code", lSize.Code);
          lItemVariant.VALIDATE(Description, lItemVariantDescription[1]);
          lItemVariant.VALIDATE("Description 2", lItemVariantDescription[2]);
          lItemVariant.VALIDATE("Brand Code", pItem."Brand Code");
          lItemVariant.VALIDATE("Season Code", pItem."Season Code");
          lItemVariant.VALIDATE("Division Code", pItem."Division Code");
          lItemVariant.VALIDATE("Department Code", pItem."Department Code");
          lItemVariant.VALIDATE("Group Code", pItem."Group Code");
          lItemVariant.VALIDATE("Category Code", pItem."Category Code");
          lItemVariant.VALIDATE("Sub-Category Code", pItem."Sub-Category Code");
          lItemVariant.VALIDATE(Sequence, lSize.Sequence);
          IF lItemVariant.INSERT(TRUE) THEN;
        UNTIL (lSize.NEXT() = 0);
      END;
    END;

    BEGIN
    {
      //DOC NS2015.1  JH 04/11/2013 - Created
      //DOC NS2016.16 JH 07/09/2016 - nSemble 2016
    }
    END.
  }
}

