OBJECT Codeunit 412 SMTP Test Mail
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            TempNameValueBuffer@1000 : TEMPORARY Record 823;
            Mail@1001 : Codeunit 397;
            AddressChoice@1003 : Text;
            ChosenAddress@1004 : ',ADMail,BasicAuthMail,UserTableMail,OtherMail';
          BEGIN
            Mail.CollectCurrentUserEmailAddresses(TempNameValueBuffer);
            TempNameValueBuffer.RESET;
            IF TempNameValueBuffer.FINDSET THEN
              REPEAT
                IF AddressChoice <> '' THEN
                  AddressChoice := AddressChoice + ',';
                AddressChoice := AddressChoice + TempNameValueBuffer.Value;
              UNTIL TempNameValueBuffer.NEXT = 0;

            AddressChoice := STRSUBSTNO('%1,%2',AddressChoice,TestMailOtherTxt);

            IF AddressChoice = ',' + TestMailOtherTxt THEN
              PromptAndSendEmail
            ELSE BEGIN
              ChosenAddress := STRMENU(AddressChoice,TempNameValueBuffer.COUNT + 1,TestMailChoiceTxt);
              IF ChosenAddress = 0 THEN
                EXIT;
              IF ChosenAddress < TempNameValueBuffer.COUNT + 1 THEN
                SendTestMail(SELECTSTR(ChosenAddress,AddressChoice))
              ELSE
                PromptAndSendEmail;
            END;
          END;

  }
  CODE
  {
    VAR
      TestMailChoiceTxt@1003 : TextConst 'ENU=Choose the email address that you want to send the test email message to and from:;ENG=Choose the email address that you want to send the test email message to and from:';
      TestMailTitleTxt@1002 : TextConst '@@@="{Locked=""Dynamics NAV"",""SMTP""}";ENU=Microsoft Dynamics NAV - SMTP Test Email Message;ENG=Microsoft Dynamics NAV - SMTP Test Email Message';
      TestMailBodyTxt@1001 : TextConst
        '@@@="{Locked=""p style="",""font-family:"",""font-size"",""pt"",""<b>"",""</b>"",""</p>"",""<BR>"",""SMTP""} %1 is an email address, such as user@domain.com; %2 is the name of a mail server, such as mail.live.com; %3 is the TCP port number, such as 25; %4 is the authentication method, such as Basic Authentication; %5 is a boolean value, such as True; %6 is a numeric ID such as 12; %7 is the name identifier of a tenant of a Microsoft Dynamics NAV Server instance, such as ''MyTenant1'';"',
        'ENU="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>This mail message has been generated by the user %1 from Microsoft Dynamics NAV for test purposes.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Sent through SMTP Server:</b> %2<BR><b>SMTP Port:</b> %3<BR><b>Authentication:</b> %4<BR><b>Using Secure Connection:</b> %5<BR><b>Server Instance ID:</b> %6<BR><b>Tenant ID:</b> %7</p>"',
        'ENG="<p style=""font-family:Verdana,Arial;font-size:10pt""><b>This mail message has been generated by the user %1 from Microsoft Dynamics NAV for test purposes.</b></p><p style=""font-family:Verdana,Arial;font-size:9pt""><b>Sent through SMTP Server:</b> %2<BR><b>SMTP Port:</b> %3<BR><b>Authentication:</b> %4<BR><b>Using Secure Connection:</b> %5<BR><b>Server Instance ID:</b> %6<BR><b>Tenant ID:</b> %7</p>"';
      TestMailSuccessMsg@1000 : TextConst '@@@="{Locked=""SMTP""} %1 is an email address.";ENU=Test email has been sent to ''%1'' based on your current SMTP settings.\Check your email for messages to make sure that the email was delivered successfully.;ENG=Test email has been sent to ''%1'' based on your current SMTP settings.\Check your email for messages to make sure that the email was delivered successfully.';
      TestMailOtherTxt@1004 : TextConst 'ENU=Other...;ENG=Other...';

    LOCAL PROCEDURE SendTestMail@5(EmailAddress@1002 : Text);
    VAR
      SMTPMailSetup@1003 : Record 409;
      SMTPMail@1000 : Codeunit 400;
    BEGIN
      SMTPMailSetup.GET;

      SMTPMail.CreateMessage(
        '',
        EmailAddress,
        EmailAddress,
        TestMailTitleTxt,
        STRSUBSTNO(
          TestMailBodyTxt,
          USERID,
          SMTPMailSetup."SMTP Server",
          FORMAT(SMTPMailSetup."SMTP Server Port"),
          SMTPMailSetup.Authentication,
          SMTPMailSetup."Secure Connection",
          SERVICEINSTANCEID,
          TENANTID),
        TRUE);

      SMTPMail.Send;
      MESSAGE(TestMailSuccessMsg,EmailAddress);
    END;

    LOCAL PROCEDURE PromptAndSendEmail@13();
    VAR
      SMTPUserSpecifiedAddress@1001 : Page 410;
      Address@1000 : Text;
    BEGIN
      IF SMTPUserSpecifiedAddress.RUNMODAL = ACTION::OK THEN BEGIN
        Address := SMTPUserSpecifiedAddress.GetEmailAddress;
        IF Address = '' THEN
          EXIT;
        SendTestMail(Address);
      END;
    END;

    BEGIN
    END.
  }
}

