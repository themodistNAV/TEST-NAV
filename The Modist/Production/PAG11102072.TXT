OBJECT Page 11102072 OM - C/AL History Objects
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=C/AL History Objects;
               ENU=C/AL History Objects;
               ESP=C/AL History Objects;
               FRA=C/AL History Objects;
               NLD=C/AL Historie objecten;
               DEA=C/AL History Objects];
    SaveValues=Yes;
    DeleteAllowed=Yes;
    SourceTable=Table11102052;
    DataCaptionExpr=DataCaptionExpression;
    PageType=List;
    OnOpenPage=BEGIN
                 IF GETFILTER("Branch No.") <> '' THEN
                   SETCURRENTKEY("Branch No.", "Object Type", "Object No.", "Entry No.");
               END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unctions;
                                 ENU=F&unctions;
                                 ESP=F&unctions;
                                 FRA=F&unctions;
                                 NLD=F&uncties;
                                 DEA=F&unctions] }
      { 3       ;2   ;Action    ;
                      ShortCutKey=Ctrl+D;
                      CaptionML=[DEU=Code;
                                 ENU=Code;
                                 ESP=Code;
                                 FRA=Code;
                                 NLD=Code;
                                 DEA=Code];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=DesignCodeBehind;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowCode;
                               END;
                                }
      { 4       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+C;
                      CaptionML=[DEU=Com&pare;
                                 ENU=Com&pare;
                                 ESP=Com&pare;
                                 FRA=Com&pare;
                                 NLD=Vergelijken;
                                 DEA=Com&pare];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewDetails;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 LeftCALHistoryObject@1001 : Record 11102052;
                                 SourceControl@1002 : Codeunit 11102050;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);

                                 CALHistoryObject.FINDFIRST;
                                 LeftCALHistoryObject := CALHistoryObject;
                                 CALHistoryObject.NEXT;

                                 SourceControl.Compare(LeftCALHistoryObject, CALHistoryObject);
                               END;
                                }
      { 5       ;2   ;Separator  }
      { 6       ;2   ;Action    ;
                      ShortCutKey=Ctrl+B;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add to Branch;
                                 ENU=Add to Branch;
                                 ESP=Add to Branch;
                                 FRA=Add to Branch;
                                 NLD=Aan branch toevoegen;
                                 DEA=Add to Branch];
                      Image=Hierarchy;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Branch@1001 : Record 11102093;
                               BEGIN
                                 IF PAGE.RUNMODAL(PAGE::"OM - Branches", Branch) = ACTION::LookupOK THEN BEGIN
                                   CurrPage.SETSELECTIONFILTER(CALHistoryObject);
                                   IF CALHistoryObject.FINDSET THEN
                                     REPEAT
                                       CALHistoryObject.TESTFIELD("Branch No.", '');
                                       CALHistoryObject.VALIDATE("Branch No.", Branch."No.");
                                       CALHistoryObject.MODIFY(TRUE);
                                     UNTIL CALHistoryObject.NEXT = 0;
                                 END;
                               END;
                                }
      { 7       ;2   ;Separator  }
      { 8       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=[DEU=Update C/AL History;
                                 ENU=Update C/AL History;
                                 ESP=Update C/AL History;
                                 FRA=Update C/AL History;
                                 NLD=C/AL Historie &bijwerken;
                                 DEA=Update C/AL History];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Object@1000 : Record 2000000001;
                                 SourceControl@1001 : Codeunit 11102050;
                               BEGIN
                                 Object.SETFILTER(Type, '<>%1', Object.Type::TableData);
                                 Object.SETFILTER("BLOB Size", '>0');
                                 SourceControl.UpdateCALHistory(Object, FALSE, TRUE);
                               END;
                                }
      { 9       ;2   ;ActionGroup;
                      CaptionML=[DEU=Mar&k Objects;
                                 ENU=Mar&k Objects;
                                 ESP=Mar&k Objects;
                                 FRA=Mar&k Objects;
                                 NLD=Ob&jecten markeren;
                                 DEA=Mar&k Objects];
                      Image=EditFilter }
      { 10      ;3   ;Action    ;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.CheckMarkLock;

                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);
                                 IF CALHistoryObject.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(CALHistoryObject."Object Type", '', CALHistoryObject."Object No.") THEN
                                       VersionControl.MarkObject(Object, TRUE);
                                   UNTIL CALHistoryObject.NEXT = 0;
                               END;
                                }
      { 11      ;3   ;Action    ;
                      CaptionML=[DEU=Remove Marks;
                                 ENU=Remove Marks;
                                 ESP=Remove Marks;
                                 FRA=Remove Marks;
                                 NLD=Verwijder markeringen;
                                 DEA=Remove Marks];
                      Image=RemoveFilterLines;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);

                                 IF CALHistoryObject.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(CALHistoryObject."Object Type", '', CALHistoryObject."Object No.") THEN
                                       VersionControl.RemoveObjectMark(Object);
                                   UNTIL CALHistoryObject.NEXT = 0;

                                 VersionControl.RemoveMarkLock(TRUE);
                               END;
                                }
      { 12      ;3   ;Action    ;
                      ShortCutKey=Shift+Ctrl+M;
                      CaptionML=[DEU=Remove &all Marks;
                                 ENU=Remove &all Marks;
                                 ESP=Remove &all Marks;
                                 FRA=Remove &all Marks;
                                 NLD=Verwijder &alle markeringen;
                                 DEA=Remove &all Marks];
                      Image=ClearFilter;
                      OnAction=VAR
                                 VersionControl@1000 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.RemoveObjectMarks(TRUE);
                               END;
                                }
      { 13      ;1   ;ActionGroup;
                      CaptionML=[DEU=File;
                                 ENU=File;
                                 ESP=File;
                                 FRA=File;
                                 NLD=Bestand;
                                 DEA=File];
                      Image=ImportExport }
      { 14      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Import;
                                 ENU=Import;
                                 ESP=Import;
                                 FRA=Import;
                                 NLD=Importeren;
                                 DEA=Import];
                      Image=Import;
                      OnAction=VAR
                                 SourceControl@1000 : Codeunit 11102050;
                                 CommonDialog@1001 : Codeunit 11102059;
                                 ServerFileName@1002 : Text;
                               BEGIN
                                 CommonDialog.AddKnownExtension('txt');
                                 IF NOT CommonDialog.OpenFile(ImportFileName, ServerFileName) THEN
                                   EXIT;

                                 SourceControl.ImportTextFile(ServerFileName, TRUE);
                               END;
                                }
      { 15      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Export;
                                 ENU=Export;
                                 ESP=Export;
                                 FRA=Export;
                                 NLD=Exporteren;
                                 DEA=Export];
                      Image=Export;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 ImExportMgt@1001 : Codeunit 11102043;
                                 CommonDialog@1002 : Codeunit 11102059;
                                 FileHandler@1003 : Codeunit 11102097;
                                 ServerFileName@1004 : Text;
                                 ctDefaultFileName@1005 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                               BEGIN
                                 CommonDialog.AddKnownExtension('txt');
                                 IF NOT CommonDialog.SaveFile(ExportFileName, ServerFileName, ctDefaultFileName) THEN
                                   EXIT;

                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);

                                 ImExportMgt.ExportHistoryTextFile(CALHistoryObject, ServerFileName);

                                 FileHandler.DownloadToClient(ServerFileName, ExportFileName, ctDefaultFileName);
                               END;
                                }
      { 16      ;1   ;ActionGroup;
                      CaptionML=[DEU=Object;
                                 ENU=Object;
                                 ESP=Object;
                                 FRA=Object;
                                 NLD=Object;
                                 DEA=Object] }
      { 17      ;2   ;Action    ;
                      CaptionML=[DEU=Restore;
                                 ENU=Restore;
                                 ESP=Restore;
                                 FRA=Restore;
                                 NLD=Object terugzetten;
                                 DEA=Restore];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Restore;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RestoreSelection;
                               END;
                                }
      { 18      ;2   ;Action    ;
                      ShortCutKey=Ctrl+Alt+C;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Last Changes;
                                 ENU=Last Changes;
                                 ESP=Last Changes;
                                 FRA=Last Changes;
                                 NLD=Laatste wijzigingen;
                                 DEA=Last Changes];
                      Image=ViewDetails;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 SourceControl@1001 : Codeunit 11102050;
                                 ctError@1002 : TextConst 'DEU=There is only one version of the C/AL code present.;ENU=There is only one version of the C/AL code present.;ESP=There is only one version of the C/AL code present.;FRA=There is only one version of the C/AL code present.;NLD=Er is maar ‚‚n versie van de C/AL code aanwezig.;DEA=There is only one version of the C/AL code present.';
                               BEGIN
                                 CALHistoryObject.SETCURRENTKEY("Object Type", "Object No.");
                                 CALHistoryObject.SETRANGE("Object Type", "Object Type");
                                 CALHistoryObject.SETRANGE("Object No.", "Object No.");
                                 CALHistoryObject.SETFILTER("Entry No.", '<%1', "Entry No.");
                                 CALHistoryObject.SETRANGE("C/AL Code Present", TRUE);
                                 IF NOT CALHistoryObject.FINDLAST THEN
                                   ERROR(ctError);

                                 SourceControl.Compare(CALHistoryObject, Rec);
                               END;
                                }
      { 19      ;1   ;ActionGroup;
                      CaptionML=[DEU=Merge;
                                 ENU=Merge;
                                 ESP=Merge;
                                 FRA=Merge;
                                 NLD=Mergen;
                                 DEA=Merge];
                      Image=StepInto }
      { 20      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Merge into Old;
                                 ENU=Merge into Old;
                                 ESP=Merge into Old;
                                 FRA=Merge into Old;
                                 NLD=Mergen in oudste;
                                 DEA=Merge into Old];
                      Image=StepInto;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 SourceControl@1001 : Codeunit 11102050;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);
                                 CALHistoryObject.SETRANGE("C/AL Code Present", TRUE);
                                 SourceControl.MergeInto(CALHistoryObject, FALSE);
                               END;
                                }
      { 21      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Merge into &New;
                                 ENU=Merge into &New;
                                 ESP=Merge into &New;
                                 FRA=Merge into &New;
                                 NLD=Mergen in &nieuwste;
                                 DEA=Merge into &New];
                      PromotedIsBig=Yes;
                      Image=StepInto;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 SourceControl@1001 : Codeunit 11102050;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(CALHistoryObject);
                                 CALHistoryObject.SETRANGE("C/AL Code Present", TRUE);
                                 SourceControl.MergeInto(CALHistoryObject, TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 22  ;0   ;Container ;
                ContainerType=ContentArea }

    { 23  ;1   ;Group     ;
                GroupType=Repeater }

    { 24  ;2   ;Field     ;
                SourceExpr="Entry No." }

    { 25  ;2   ;Field     ;
                SourceExpr="Import Entry No.";
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="Object Type" }

    { 27  ;2   ;Field     ;
                SourceExpr="Object No.";
                LookupPageID=OM - Object List }

    { 28  ;2   ;Field     ;
                SourceExpr="Object Name" }

    { 29  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Object Caption";
                Visible=FALSE }

    { 30  ;2   ;Field     ;
                SourceExpr=Modified }

    { 31  ;2   ;Field     ;
                SourceExpr=Compiled }

    { 32  ;2   ;Field     ;
                SourceExpr="Version List" }

    { 33  ;2   ;Field     ;
                SourceExpr="Object Date" }

    { 34  ;2   ;Field     ;
                SourceExpr="Object Time" }

    { 35  ;2   ;Field     ;
                SourceExpr="BLOB Reference Present" }

    { 36  ;2   ;Field     ;
                SourceExpr="Locked By";
                Visible=FALSE }

    { 37  ;2   ;Field     ;
                CaptionML=[DEU=Assigned Projects;
                           ENU=Assigned Projects;
                           ESP=Assigned Projects;
                           FRA=Assigned Projects;
                           NLD=Toegewezen projecten;
                           DEA=Assigned Projects];
                SourceExpr=AssignedProjectsText;
                Visible=FALSE;
                OnAssistEdit=BEGIN
                               DrillDownNoOfAssignedProject;
                             END;
                              }

    { 38  ;2   ;Field     ;
                SourceExpr="Action Type" }

    { 39  ;2   ;Field     ;
                SourceExpr="Added By";
                Visible=FALSE }

    { 40  ;2   ;Field     ;
                SourceExpr="Added Date";
                Visible=FALSE }

    { 41  ;2   ;Field     ;
                SourceExpr="Added Time";
                Visible=FALSE }

    { 42  ;2   ;Field     ;
                SourceExpr="Added Date Time";
                Visible=FALSE }

    { 43  ;2   ;Field     ;
                SourceExpr="Restored from Entry No.";
                Visible=FALSE }

    { 44  ;2   ;Field     ;
                SourceExpr="Branch No.";
                Visible=FALSE }

    { 45  ;2   ;Field     ;
                SourceExpr="Branch Description";
                Visible=FALSE }

    { 46  ;1   ;Field     ;
                SourceExpr=ExportFileName;
                Visible=FALSE }

    { 47  ;1   ;Field     ;
                SourceExpr=ImportFileName;
                Visible=FALSE }

    { 48  ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 49  ;1   ;Part      ;
                SubPageLink=Object Type=FIELD(Object Type),
                            Object No.=FIELD(Object No.);
                PagePartID=Page11175794;
                PartType=Page }

    { 50  ;1   ;Part      ;
                PagePartID=Page11175795;
                PartType=Page }

    { 51  ;1   ;Part      ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 52  ;1   ;Part      ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ExportFileName@1000 : Text;
      ImportFileName@1001 : Text;

    PROCEDURE GetSelectionFilter@1(VAR CALHistoryObject@1000 : Record 11102052);
    BEGIN
      CurrPage.SETSELECTIONFILTER(CALHistoryObject);
    END;

    LOCAL PROCEDURE RestoreSelection@2();
    VAR
      CALHistoryObject@1000 : Record 11102052;
      SourceControl@1001 : Codeunit 11102050;
    BEGIN
      CurrPage.SETSELECTIONFILTER(CALHistoryObject);
      PreventAllSelectedTempTable(CALHistoryObject);
      SourceControl.RestoreObjects(CALHistoryObject);
    END;

    LOCAL PROCEDURE PreventAllSelectedTempTable@3(VAR CALHistoryObject@1000 : Record 11102052);
    VAR
      Progressbar@1001 : Codeunit 11102044;
      ctSelectingRecords@1002 : TextConst 'DEU=Selecting Records...;ENU=Selecting Records...;ESP=Selecting Records...;FRA=Selecting Records...;NLD=Records selecteren...;DEA=Selecting Records...';
    BEGIN
      IF CALHistoryObject.COUNT <= 1 THEN
        EXIT;

      CALHistoryObject.FINDFIRST;
      IF CALHistoryObject.MARK THEN
        EXIT;

      Progressbar.Open(ctSelectingRecords, 0);
      Progressbar.BarSize := CALHistoryObject.COUNT;
      IF CALHistoryObject.FINDSET THEN
        REPEAT
          Progressbar.AddToBar(1);

          FILTERGROUP(10);
          SETRANGE("Entry No.", CALHistoryObject."Entry No.");
          CALHistoryObject.MARK := NOT ISEMPTY;
          SETRANGE("Entry No.");
          FILTERGROUP(0);

        UNTIL CALHistoryObject.NEXT = 0;

      CALHistoryObject.MARKEDONLY(TRUE);
    END;

    BEGIN
    END.
  }
}

