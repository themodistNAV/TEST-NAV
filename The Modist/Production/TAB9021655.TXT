OBJECT Table 9021655 Valid Promotions
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:05 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Created By" := USERID;
               "Created DateTime" := CURRENTDATETIME;

               IF "Benefit Line Applies-to Type" = "Benefit Line Applies-to Type"::" " THEN
                 ERROR(Text000);
             END;

    OnModify=BEGIN
               "Modified By" := USERID;
               "Modified DateTime" := CURRENTDATETIME;

               VALIDATE("Qualifying Line Allowed");
             END;

    CaptionML=[ENU=Valid Promotions;
               ENG=Valid Promotions];
  }
  FIELDS
  {
    { 1   ;   ;Benefit Line Type   ;Option        ;OnValidate=BEGIN
                                                                IF "Benefit Line Type" <> xRec."Benefit Line Type" THEN
                                                                  ClearValues;
                                                              END;

                                                   CaptionML=[ENU=Benefit Line Type;
                                                              ENG=Benefit Line Type];
                                                   OptionCaptionML=[ENU=Deal Price,Discount Amount,Discount %,Promo Item,Cheapest Free,Dearest Free,Shipping Discount %,Shipping Discount Amount;
                                                                    ENG=Deal Price,Discount Amount,Discount %,Promo Item,Cheapest Free,Dearest Free,Shipping Discount %,Shipping Discount Amount];
                                                   OptionString=Deal Price,Discount Amount,Discount %,Promo Item,Cheapest Free,Dearest Free,Shipping Discount %,Shipping Discount Amount;
                                                   Description=EC2015.5.01 MQ 22/12/2014 - GW141222 Add Shipping Discounts }
    { 10  ;   ;Benefit Line Applies-to Type;Option;OnValidate=BEGIN
                                                                IF "Benefit Line Applies-to Type" <> xRec."Benefit Line Applies-to Type" THEN
                                                                  ClearValues;
                                                              END;

                                                   CaptionML=[ENU=Benefit Line Applies-to Type;
                                                              ENG=Benefit Line Applies-to Type];
                                                   OptionCaptionML=[ENU=" ,Order Header,Marked Lines";
                                                                    ENG=" ,Order Header,Marked Lines"];
                                                   OptionString=[ ,Order Header,Marked Lines] }
    { 20  ;   ;Qual Line ATT Item  ;Boolean       ;CaptionML=[ENU=Qual Line ATT Item;
                                                              ENG=Qual Line ATT Item] }
    { 22  ;   ;Qual Line ATT Order Total;Boolean  ;OnValidate=BEGIN
                                                                IF ("Qual Line ATT Order Total" = TRUE) AND
                                                                   ("Benefit Line Applies-to Type" = "Benefit Line Applies-to Type"::"Marked Lines") THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Qual Line ATT Order Total;
                                                              ENG=Qual Line ATT Order Total] }
    { 24  ;   ;Qual Line ATT Voucher;Boolean      ;OnValidate=BEGIN
                                                                IF ("Qual Line ATT Voucher" = TRUE) AND
                                                                   ("Benefit Line Applies-to Type" = "Benefit Line Applies-to Type"::"Marked Lines") THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Qual Line ATT Voucher;
                                                              ENG=Qual Line ATT Voucher] }
    { 26  ;   ;Qual Line ATT Promotion Group;Boolean;
                                                   CaptionML=[ENU=Qual Line ATT Promotion Group;
                                                              ENG=Qual Line ATT Promotion Group] }
    { 28  ;   ;QL ATT Dist. Promo Group Items;Boolean;
                                                   CaptionML=[ENU=QL ATT Dist. Promo Group Items;
                                                              ENG=QL ATT Dist. Promo Group Items];
                                                   Description=EC2015.5.01 MQ 22/12/2014 - GW141222 add field }
    { 30  ;   ;Qualifying Line Allowed;Boolean    ;OnValidate=BEGIN
                                                                IF ("Qual Line ATT Item" = TRUE) OR
                                                                   ("Qual Line ATT Order Total" = TRUE) OR
                                                                   ("Qual Line ATT Voucher" = TRUE) OR
                                                                   ("Qual Line ATT Promotion Group" = TRUE) THEN
                                                                  "Qualifying Line Allowed" := TRUE
                                                                ELSE
                                                                  "Qualifying Line Allowed" := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Qualifying Line Allowed;
                                                              ENG=Qualifying Line Allowed];
                                                   Editable=No }
    { 100 ;   ;Enabled             ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Enabled;
                                                              ENG=Enabled] }
    { 101 ;   ;Created By          ;Code40        ;CaptionML=[ENU=Created By;
                                                              ENG=Created By];
                                                   Description=MQ 10/01/2014 - GW140110 from C20 }
    { 102 ;   ;Created DateTime    ;DateTime      ;CaptionML=[ENU=Created DateTime;
                                                              ENG=Created DateTime] }
    { 103 ;   ;Modified By         ;Code40        ;CaptionML=[ENU=Modified By;
                                                              ENG=Modified By];
                                                   Description=MQ 10/01/2014 - GW140110 from C20 }
    { 104 ;   ;Modified DateTime   ;DateTime      ;CaptionML=[ENU=Modified DateTime;
                                                              ENG=Modified DateTime] }
    { 220 ;   ;No. Qual Line ATT Item;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Promotion Summary" WHERE (Benefit Line Type=FIELD(Benefit Line Type),
                                                                                                Benefit Line Applies-to Type=FIELD(Benefit Line Applies-to Type),
                                                                                                Qual Line ATT Item=CONST(Yes)));
                                                   CaptionML=[ENU=No. Qual Line ATT Item;
                                                              ENG=No. Qual Line ATT Item];
                                                   Description=Flowfield }
    { 222 ;   ;No. Qual Line ATT Order Total;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Promotion Summary" WHERE (Benefit Line Type=FIELD(Benefit Line Type),
                                                                                                Benefit Line Applies-to Type=FIELD(Benefit Line Applies-to Type),
                                                                                                Qual Line ATT Order Total=CONST(Yes)));
                                                   OnValidate=BEGIN
                                                                IF ("Qual Line ATT Order Total" = TRUE) AND
                                                                   ("Benefit Line Applies-to Type" = "Benefit Line Applies-to Type"::"Marked Lines") THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=No. Qual Line ATT Order Total;
                                                              ENG=No. Qual Line ATT Order Total];
                                                   Description=Flowfield }
    { 224 ;   ;No. Qual Line ATT Voucher;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Promotion Summary" WHERE (Benefit Line Type=FIELD(Benefit Line Type),
                                                                                                Benefit Line Applies-to Type=FIELD(Benefit Line Applies-to Type),
                                                                                                Qual Line ATT Voucher=CONST(Yes)));
                                                   OnValidate=BEGIN
                                                                IF ("Qual Line ATT Voucher" = TRUE) AND
                                                                   ("Benefit Line Applies-to Type" = "Benefit Line Applies-to Type"::"Marked Lines") THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=No. Qual Line ATT Voucher;
                                                              ENG=No. Qual Line ATT Voucher];
                                                   Description=Flowfield }
    { 226 ;   ;No. Qual Line ATT Promo Group;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Promotion Summary" WHERE (Benefit Line Type=FIELD(Benefit Line Type),
                                                                                                Benefit Line Applies-to Type=FIELD(Benefit Line Applies-to Type),
                                                                                                Qual Line ATT Promotion Group=CONST(Yes)));
                                                   CaptionML=[ENU=No. Qual Line ATT Promo Group;
                                                              ENG=No. Qual Line ATT Promo Group];
                                                   Description=Flowfield }
  }
  KEYS
  {
    {    ;Benefit Line Applies-to Type,Benefit Line Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000000000 : TextConst 'ENU=Benefit Line Applies-to Type must not be blank.;ENG=Benefit Line Applies-to Type must not be blank.';
      Text001@1000000001 : TextConst 'ENU=You cannot have Order Total allowed when the Applies-to Type is Marked Lines.;ENG=You cannot have Order Total allowed when the Applies-to Type is Marked Lines.';
      Text002@1000000002 : TextConst 'ENU=You cannot have Voucher allowed when the Applies-to Type is Marked Lines.;ENG=You cannot have Voucher allowed when the Applies-to Type is Marked Lines.';
      ValidPromos@1000000003 : Record 9021655;

    PROCEDURE ClearValues@1000000000();
    BEGIN
      "Qual Line ATT Item" := FALSE;
      "Qual Line ATT Order Total" := FALSE;
      "Qual Line ATT Voucher" := FALSE;
      "Qual Line ATT Promotion Group" := FALSE;
      "Qualifying Line Allowed" := FALSE;
      Enabled := FALSE;
    END;

    PROCEDURE BuildBaseValues@1000000001();
    VAR
      TxtConfirmation@1000000000 : TextConst 'ENU=This will delete all the current Valid Promotions and replace them with the latest deafaults\\Are you sure you want to continue?;ENG=This will delete all the current Valid Promotions and replace them with the latest deafaults\\Are you sure you want to continue?';
      TxtAbandoned@1000000001 : TextConst 'ENU=Abandoned;ENG=Abandoned';
    BEGIN
      IF NOT CONFIRM(TxtConfirmation) THEN
        ERROR(TxtAbandoned);
      ValidPromos.RESET;
      ValidPromos.DELETEALL;
      //BenLineType
      //0=Deal Price,1=Discount Amount,2=Discount %,3=Promo Item,4=Cheapest Free,5=Dearest Free,
      //6=Shipping Discount %,7=Shipping Discount Amount

      //BenLineAppToType
      //0= ,1=Order Header,2=Marked Lines
      //ValidPromos.InsertBaseValue(BenLineType,BenLineAppToType,QualLineATTItem,QualLineATTOrdTotal,QualLineATTVouch,QualLineATTPromoG,
      //                            Enabled)

      //DOC EC2015.5.01 Old Order Header                Item,OrdTotal,Vouch,PromoG,Enabled
      //DOC EC2015.5.01 New Order Header                Item,OrdTotal,Vouch,PromoG,Dist Promo Group,Enabled

      ValidPromos.InsertBaseValue(0,1,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Deal Price
      ValidPromos.InsertBaseValue(1,1,TRUE,TRUE,TRUE,TRUE,FALSE,TRUE);  //Discount Amount
      ValidPromos.InsertBaseValue(2,1,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE);  //Discount %
      ValidPromos.InsertBaseValue(3,1,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE);  //Promo Item
      ValidPromos.InsertBaseValue(4,1,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Cheapest
      ValidPromos.InsertBaseValue(5,1,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Dearest
      ValidPromos.InsertBaseValue(6,1,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE);  //Shipping Discount Amount  //DOC EC2015.5.01
      ValidPromos.InsertBaseValue(7,1,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE);  //Shipping Discount %  //DOC EC2015.5.01


      //DOC EC2015.5.01 Old Marked Lines                Item,OrdTotal,Vouch,PromoG,Enabled
      //DOC EC2015.5.01 New Marked Lines                Item,OrdTotal,Vouch,PromoG,Dist. Promo Group,Enabled

      ValidPromos.InsertBaseValue(0,2,TRUE,FALSE,FALSE,FALSE,FALSE,TRUE);  //Deal Price
      ValidPromos.InsertBaseValue(1,2,TRUE,FALSE,FALSE,TRUE,FALSE,TRUE);  //Discount Amount
      ValidPromos.InsertBaseValue(2,2,TRUE,FALSE,FALSE,TRUE,TRUE,TRUE);  //Discount %
      ValidPromos.InsertBaseValue(3,2,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Promo Item
      ValidPromos.InsertBaseValue(4,2,TRUE,FALSE,FALSE,TRUE,TRUE,TRUE);  //Cheapest
      ValidPromos.InsertBaseValue(5,2,TRUE,FALSE,FALSE,TRUE,TRUE,TRUE);  //Dearest
      ValidPromos.InsertBaseValue(6,2,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Shipping Discount Amount //DOC EC2015.5.01
      ValidPromos.InsertBaseValue(7,2,FALSE,FALSE,FALSE,FALSE,FALSE,FALSE);  //Shipping Discount %  //DOC EC2015.5.01

      MESSAGE('Done');
    END;

    PROCEDURE InsertBaseValue@1000000003(BenLineType@1000000000 : 'Deal Price,Discount Amount,Discount %,Promo Item,Cheapest Free,Dearest Free';BenLineAppToType@1000000001 : ' ,Order Header,Marked Lines';QualLineATTItem@1000000002 : Boolean;QualLineATTOrdTotal@1000000003 : Boolean;QualLineATTVouch@1000000004 : Boolean;QualLineATTPromoG@1000000005 : Boolean;QLATTDistPromoGItems@1000000007 : Boolean;Enabled@1000000006 : Boolean);
    VAR
      TxtSystem@1000000008 : TextConst 'ENU=SYSTEM;ENG=SYSTEM';
    BEGIN
      ValidPromos.INIT;
      ValidPromos."Benefit Line Applies-to Type" := BenLineAppToType;
      ValidPromos."Benefit Line Type" := BenLineType;
      ValidPromos."Qual Line ATT Item" := QualLineATTItem;
      ValidPromos."Qual Line ATT Order Total" := QualLineATTOrdTotal;
      ValidPromos."Qual Line ATT Voucher" := QualLineATTVouch;
      ValidPromos."Qual Line ATT Promotion Group" := QualLineATTPromoG;
      ValidPromos."QL ATT Dist. Promo Group Items" := QLATTDistPromoGItems; //DOC EC2015.5.01
      ValidPromos."Qualifying Line Allowed" := Enabled;
      ValidPromos.Enabled := Enabled;
      ValidPromos."Created By" := TxtSystem;
      ValidPromos."Created DateTime" := CURRENTDATETIME;
      ValidPromos.INSERT;
    END;

    BEGIN
    {
      //DOC EC2013.4.00 MQ 10/01/2014 - GW140110 Made USERID fields C40
      //DOC EC2013.4.60 MQ 11/04/2014 - GW140411 Add options to Benefit Line Type
      //DOC EC2013.4.62 MQ 23/04/2014 - GW140423 Updated
      //DOC EC2013.4.62 MA 29/04/2014 - MA140429 Updated to (re)allow % Discount off Order Total for qualifying Order Header
      //DOC EC2015.5.02 MQ 20/01/2015 - GW150120 Add field 28 QL ATT Dist. Promo Group Items; Add to Benefit Line Type options for Shipping Discount % and Amount
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

