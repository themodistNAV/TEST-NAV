OBJECT Page 189 Incoming Document
{
  OBJECT-PROPERTIES
  {
    Date=26/11/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.43897;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Incoming Document;
               ENG=Incoming Document];
    SourceTable=Table130;
    PageType=Document;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Release,Incoming Document,OCR,Approve,Request Approval;
                                ENG=New,Process,Report,Release,Incoming Document,OCR,Approve,Request Approval];
    OnInit=BEGIN
             IsDataExchTypeEditable := TRUE;
             EnableReceiveFromOCR := WaitingToReceiveFromOCR;
           END;

    OnOpenPage=BEGIN
                 HasCamera := CameraProvider.IsAvailable;
                 IF HasCamera THEN
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetRecord=BEGIN
                       URL := GetURL;
                       ShowErrors;
                       EnableReceiveFromOCR := WaitingToReceiveFromOCR;
                     END;

    OnNewRecord=BEGIN
                  URL := '';
                END;

    OnModifyRecord=VAR
                     IncomingDocumentAttachment@1000 : Record 133;
                   BEGIN
                     OCRDataCorrectionEnabled := GetGeneratedFromOCRAttachment(IncomingDocumentAttachment);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IsDataExchTypeEditable := NOT (Status IN [Status::Created,Status::Posted]);
                           ShowErrors;
                           SetCalculatedFields;
                           SetControlVisibility;
                         END;

    ActionList=ACTIONS
    {
      { 52      ;    ;ActionContainer;
                      CaptionML=[ENU=New;
                                 ENG=New];
                      ActionContainerType=NewDocumentItems }
      { 46      ;    ;ActionContainer;
                      CaptionML=[ENU=Related Information;
                                 ENG=Related Information];
                      ActionContainerType=RelatedInformation }
      { 57      ;1   ;ActionGroup;
                      CaptionML=[ENU=Setup;
                                 ENG=Setup] }
      { 44      ;2   ;Action    ;
                      Name=Setup;
                      CaptionML=[ENU=Setup;
                                 ENG=Setup];
                      RunObject=Page 191;
                      Image=Setup }
      { 22      ;2   ;Action    ;
                      Name=DataExchangeTypes;
                      CaptionML=[ENU=Data Exchange Types;
                                 ENG=Data Exchange Types];
                      RunObject=Page 1213;
                      Promoted=Yes;
                      Image=Entries;
                      PromotedCategory=Category5 }
      { 73      ;2   ;Action    ;
                      Name=ActivityLog;
                      CaptionML=[ENU=Activity Log;
                                 ENG=Activity Log];
                      Image=Log;
                      OnAction=VAR
                                 ActivityLog@1002 : Record 710;
                               BEGIN
                                 ActivityLog.ShowEntries(RECORDID);
                               END;
                                }
      { 69      ;2   ;Action    ;
                      Name=OCRSetup;
                      CaptionML=[ENU=OCR Setup;
                                 ENG=OCR Setup];
                      RunObject=Page 1270;
                      Image=ServiceSetup }
      { 43      ;    ;ActionContainer;
                      CaptionML=[ENU=Process;
                                 ENG=Process];
                      ActionContainerType=ActionItems }
      { 121     ;1   ;Action    ;
                      Name=CreateDocument;
                      CaptionML=[ENU=Create Document;
                                 ENG=Create Document];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CreateDocument;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 "Created Doc. Error Msg. Type" := "Created Doc. Error Msg. Type"::Warning;
                                 MODIFY;
                                 CreateDocumentWithDataExchange;
                               END;
                                }
      { 31      ;1   ;Action    ;
                      Name=CreateGenJnlLine;
                      CaptionML=[ENU=Create Journal Line;
                                 ENG=Create Journal Line];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=TransferToGeneralJournal;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateGeneralJournalLineWithDataExchange;
                               END;
                                }
      { 285     ;1   ;Action    ;
                      Name=CreateManually;
                      CaptionML=[ENU=Create Manually;
                                 ENG=Create Manually];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CreateCreditMemo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreateManually;
                               END;
                                }
      { 21      ;1   ;Action    ;
                      Name=AttachFile;
                      CaptionML=[ENU=Attach File;
                                 ENG=Attach File];
                      Promoted=Yes;
                      Image=Attach;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.UPDATE(TRUE);
                                 ImportAttachment;
                               END;
                                }
      { 85      ;1   ;Action    ;
                      Name=ReplaceMainAttachment;
                      CaptionML=[ENU=Replace Main Attachment;
                                 ENG=Replace Main Attachment];
                      Image=Interaction;
                      OnAction=BEGIN
                                 ReplaceOrInsertMainAttachment;
                               END;
                                }
      { 30      ;1   ;Action    ;
                      Name=AttachFromCamera;
                      CaptionML=[ENU=Attach Image from Camera;
                                 ENG=Attach Image from Camera];
                      Promoted=Yes;
                      Visible=HasCamera;
                      PromotedIsBig=Yes;
                      Image=Camera;
                      OnAction=VAR
                                 CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                               BEGIN
                                 IF NOT HasCamera THEN
                                   EXIT;
                                 CameraOptions := CameraOptions.CameraOptions;
                                 CameraOptions.Quality := 100; // 100%
                                 CameraProvider.RequestPictureAsync(CameraOptions);
                               END;
                                }
      { 64      ;1   ;Action    ;
                      Name=TextToAccountMapping;
                      CaptionML=[ENU=Map Text to Account;
                                 ENG=Map Text to Account];
                      RunObject=Page 1254;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=MapAccounts;
                      PromotedCategory=Process }
      { 45      ;1   ;ActionGroup;
                      CaptionML=[ENU=Release;
                                 ENG=Release] }
      { 12      ;2   ;Action    ;
                      Name=Release;
                      CaptionML=[ENU=Release;
                                 ENG=Release];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ReleaseIncomingDocument@1000 : Codeunit 132;
                               BEGIN
                                 ReleaseIncomingDocument.PerformManualRelease(Rec);
                               END;
                                }
      { 82      ;2   ;Action    ;
                      Name=Reopen;
                      CaptionML=[ENU=Reopen;
                                 ENG=Reopen];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ReleaseIncomingDocument@1000 : Codeunit 132;
                               BEGIN
                                 ReleaseIncomingDocument.PerformManualReopen(Rec);
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Reject;
                      CaptionML=[ENU=Reject;
                                 ENG=Reject];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ReleaseIncomingDocument@1000 : Codeunit 132;
                               BEGIN
                                 ReleaseIncomingDocument.PerformManualReject(Rec);
                               END;
                                }
      { 74      ;1   ;ActionGroup;
                      CaptionML=[ENU=Approval;
                                 ENG=Approval] }
      { 75      ;2   ;Action    ;
                      Name=Approve;
                      CaptionML=[ENU=Approve;
                                 ENG=Approve];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.ApproveRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 76      ;2   ;Action    ;
                      Name=RejectApproval;
                      CaptionML=[ENU=Reject;
                                 ENG=Reject];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.RejectRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 77      ;2   ;Action    ;
                      Name=Delegate;
                      CaptionML=[ENU=Delegate;
                                 ENG=Delegate];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      Image=Delegate;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.DelegateRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 78      ;2   ;Action    ;
                      Name=Comment;
                      CaptionML=[ENU=Comments;
                                 ENG=Comments];
                      RunObject=Page 660;
                      RunPageLink=Table ID=CONST(130),
                                  Document No.=FIELD(Entry No.);
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      Image=ViewComments;
                      PromotedCategory=Category7 }
      { 81      ;1   ;ActionGroup;
                      CaptionML=[ENU=Request Approval;
                                 ENG=Request Approval];
                      Image=SendApprovalRequest }
      { 80      ;2   ;Action    ;
                      Name=SendApprovalRequest;
                      CaptionML=[ENU=Send A&pproval Request;
                                 ENG=Send A&pproval Request];
                      Promoted=Yes;
                      Enabled=NOT OpenApprovalEntriesExist;
                      Image=SendApprovalRequest;
                      PromotedCategory=Category8;
                      OnAction=VAR
                                 ApprovalsMgmt@1001 : Codeunit 1535;
                               BEGIN
                                 IF ApprovalsMgmt.CheckIncomingDocApprovalsWorkflowEnabled(Rec) THEN
                                   ApprovalsMgmt.OnSendIncomingDocForApproval(Rec);
                               END;
                                }
      { 79      ;2   ;Action    ;
                      Name=CancelApprovalRequest;
                      CaptionML=[ENU=Cancel Approval Re&quest;
                                 ENG=Cancel Approval Re&quest];
                      Promoted=Yes;
                      Enabled=OpenApprovalEntriesExist;
                      Image=Cancel;
                      PromotedCategory=Category8;
                      OnAction=VAR
                                 ApprovalsMgmt@1001 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.OnCancelIncomingDocApprovalRequest(Rec);
                               END;
                                }
      { 66      ;1   ;ActionGroup;
                      CaptionML=[ENU=Incoming Document;
                                 ENG=Incoming Document] }
      { 67      ;2   ;Action    ;
                      Name=OpenDocument;
                      CaptionML=[ENU=Open Record;
                                 ENG=Open Record];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewDetails;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 ShowNAVRecord;
                               END;
                                }
      { 5       ;2   ;Action    ;
                      Name=RemoveReferencedRecord;
                      CaptionML=[ENU=Remove Reference to Record;
                                 ENG=Remove Reference to Record];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ClearLog;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 RemoveReferencedRecords;
                               END;
                                }
      { 68      ;2   ;Action    ;
                      Name=Navigate;
                      CaptionML=[ENU=Navigate;
                                 ENG=Navigate];
                      Image=Navigate;
                      OnAction=VAR
                                 NavigatePage@1000 : Page 344;
                               BEGIN
                                 TESTFIELD(Posted);
                                 NavigatePage.SetDoc("Posting Date","Document No.");
                                 NavigatePage.RUN;
                               END;
                                }
      { 51      ;2   ;ActionGroup;
                      CaptionML=[ENU=Record;
                                 ENG=Record];
                      Visible=FALSE;
                      Enabled=FALSE;
                      Image=Document }
      { 65      ;3   ;Action    ;
                      Name=Journal;
                      CaptionML=[ENU=Journal Line;
                                 ENG=Journal Line];
                      Image=Journal;
                      OnAction=VAR
                                 GenJournalBatch@1001 : Record 232;
                                 GenJnlManagement@1002 : Codeunit 230;
                               BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreateGenJnlLine;
                                 IncomingDocumentsSetup.Fetch;
                                 GenJournalBatch.GET(IncomingDocumentsSetup."General Journal Template Name",IncomingDocumentsSetup."General Journal Batch Name");
                                 GenJnlManagement.TemplateSelectionFromBatch(GenJournalBatch);
                               END;
                                }
      { 49      ;3   ;Action    ;
                      Name=PurchaseInvoice;
                      CaptionML=[ENU=Purchase Invoice;
                                 ENG=Purchase Invoice];
                      Image=Purchase;
                      OnAction=BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreatePurchInvoice;
                               END;
                                }
      { 50      ;3   ;Action    ;
                      Name=PurchaseCreditMemo;
                      CaptionML=[ENU=Purchase Credit Memo;
                                 ENG=Purchase Credit Memo];
                      Image=CreditMemo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreatePurchCreditMemo;
                               END;
                                }
      { 47      ;3   ;Action    ;
                      Name=SalesInvoice;
                      CaptionML=[ENU=Sales Invoice;
                                 ENG=Sales Invoice];
                      Image=Sales;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreateSalesInvoice;
                               END;
                                }
      { 48      ;3   ;Action    ;
                      Name=SalesCreditMemo;
                      CaptionML=[ENU=Sales Credit Memo;
                                 ENG=Sales Credit Memo];
                      Image=CreditMemo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT AskUserPermission THEN
                                   EXIT;

                                 CreateSalesCreditMemo;
                               END;
                                }
      { 59      ;1   ;ActionGroup;
                      CaptionML=[ENU=OCR;
                                 ENG=OCR] }
      { 42      ;2   ;Action    ;
                      Name=SendToJobQueue;
                      CaptionML=[ENU=Send to Job Queue;
                                 ENG=Send to Job Queue];
                      Image=Translation;
                      OnAction=BEGIN
                                 SendToJobQueue(TRUE);
                               END;
                                }
      { 56      ;2   ;Action    ;
                      Name=RemoveFromJobQueue;
                      CaptionML=[ENU=Remove from Job Queue;
                                 ENG=Remove from Job Queue];
                      Image=Translation;
                      OnAction=BEGIN
                                 RemoveFromJobQueue(TRUE);
                               END;
                                }
      { 54      ;2   ;Action    ;
                      Name=SendToOcr;
                      CaptionML=[ENU=Send to OCR Service;
                                 ENG=Send to OCR Service];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Translations;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 SendToOCR(TRUE);
                               END;
                                }
      { 55      ;2   ;Action    ;
                      Name=ReceiveFromOCR;
                      CaptionML=[ENU=Receive from OCR Service;
                                 ENG=Receive from OCR Service];
                      Promoted=Yes;
                      Enabled=EnableReceiveFromOCR;
                      PromotedIsBig=Yes;
                      Image=Translations;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 RetrieveFromOCR(TRUE);
                               END;
                                }
      { 35      ;2   ;Action    ;
                      Name=CorrectOCRData;
                      CaptionML=[ENU=Correct OCR Data;
                                 ENG=Correct OCR Data];
                      RunObject=Page 1272;
                      RunPageOnRec=Yes;
                      Promoted=Yes;
                      Enabled=OCRDataCorrectionEnabled;
                      PromotedIsBig=Yes;
                      Image=EditAttachment;
                      PromotedCategory=Category6 }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=[ENU=General;
                           ENG=General];
                GroupType=Group }

    { 3   ;2   ;Field     ;
                SourceExpr=Description;
                Importance=Promoted }

    { 53  ;2   ;Field     ;
                Name=URL;
                ExtendedDatatype=URL;
                CaptionML=[ENU=Link to Document;
                           ENG=Link to Document];
                SourceExpr=URL;
                Importance=Additional;
                OnValidate=BEGIN
                             SetURL(URL);
                             CurrPage.UPDATE;
                           END;
                            }

    { 90  ;2   ;Field     ;
                Name=MainAttachment;
                CaptionML=[ENU=Main Attachment;
                           ENG=Main Attachment];
                SourceExpr=AttachmentFileName;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              MainAttachmentDrillDown;
                              CurrPage.UPDATE;
                            END;
                             }

    { 115 ;2   ;Field     ;
                SourceExpr="Data Exchange Type";
                Importance=Additional;
                Editable=IsDataExchTypeEditable;
                OnValidate=BEGIN
                             IF NOT DefaultAttachmentIsXML THEN
                               ERROR(InvalidTypeErr);
                           END;
                            }

    { 58  ;2   ;Field     ;
                Name=Record;
                CaptionML=[ENU=Record;
                           ENG=Record];
                SourceExpr=RecordLinkTxt;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowNAVRecord;
                              CurrPage.UPDATE;
                            END;
                             }

    { 17  ;2   ;Field     ;
                SourceExpr="Document Type";
                Importance=Promoted;
                Visible=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr="Document No.";
                Importance=Promoted;
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr=Status;
                Importance=Promoted }

    { 87  ;2   ;Field     ;
                SourceExpr="OCR Status";
                OnDrillDown=VAR
                              OCRServiceSetup@1000 : Record 1270;
                            BEGIN
                              IF NOT OCRServiceSetup.FINDFIRST THEN
                                EXIT;

                              IF OCRServiceSetup.Enabled AND (OCRServiceSetup."Login URL" <> '') THEN
                                HYPERLINK(OCRServiceSetup."Login URL");
                            END;
                             }

    { 84  ;2   ;Field     ;
                SourceExpr="Job Queue Status" }

    { 71  ;2   ;Group     ;
                GroupType=Group }

    { 29  ;3   ;Field     ;
                SourceExpr="OCR Service Doc. Template Code";
                Importance=Additional }

    { 41  ;3   ;Field     ;
                SourceExpr="OCR Service Doc. Template Name";
                Importance=Additional }

    { 40  ;3   ;Field     ;
                CaptionML=[ENU=OCR Result;
                           ENG=OCR Result];
                SourceExpr=OCRResultFileName;
                Importance=Additional;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              OCRResultDrillDown;
                              CurrPage.UPDATE;
                            END;
                             }

    { 72  ;2   ;Group     ;
                GroupType=Group }

    { 4   ;3   ;Field     ;
                SourceExpr="Created Date-Time";
                Importance=Additional }

    { 6   ;3   ;Field     ;
                SourceExpr="Created By User Name";
                Importance=Additional }

    { 7   ;3   ;Field     ;
                SourceExpr=Released;
                Visible=FALSE }

    { 8   ;3   ;Field     ;
                SourceExpr="Released Date-Time";
                Importance=Additional }

    { 10  ;3   ;Field     ;
                SourceExpr="Released By User Name";
                Importance=Additional }

    { 11  ;3   ;Field     ;
                SourceExpr="Last Date-Time Modified";
                Importance=Additional }

    { 13  ;3   ;Field     ;
                SourceExpr="Last Modified By User Name";
                Importance=Additional }

    { 14  ;3   ;Field     ;
                SourceExpr=Posted }

    { 15  ;3   ;Field     ;
                SourceExpr="Posted Date-Time";
                Importance=Additional }

    { 18  ;3   ;Field     ;
                SourceExpr="Posting Date";
                Visible=FALSE }

    { 37  ;1   ;Part      ;
                Name=SupportingAttachments;
                CaptionML=[ENU=Supporting Attachments;
                           ENG=Supporting Attachments];
                PagePartID=Page194;
                Visible=AdditionalAttachmentsPresent;
                PartType=Page;
                ShowFilter=No }

    { 70  ;1   ;Group     ;
                Name=FinancialInformation;
                CaptionML=[ENU=Financial Information;
                           ENG=Financial Information];
                GroupType=Group }

    { 23  ;2   ;Field     ;
                SourceExpr="Vendor Name";
                Importance=Promoted;
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr="Vendor VAT Registration No.";
                Editable=FALSE }

    { 25  ;2   ;Field     ;
                SourceExpr="Vendor IBAN";
                Editable=FALSE }

    { 27  ;2   ;Field     ;
                SourceExpr="Vendor Bank Branch No.";
                Editable=FALSE }

    { 28  ;2   ;Field     ;
                SourceExpr="Vendor Bank Account No.";
                Editable=FALSE }

    { 86  ;2   ;Field     ;
                SourceExpr="Vendor Phone No.";
                Editable=FALSE }

    { 60  ;2   ;Field     ;
                SourceExpr="Vendor Invoice No.";
                Editable=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Vendor Order No.;
                           ENG=Vendor Order No.];
                SourceExpr="Order No.";
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="Document Date";
                Importance=Promoted;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                SourceExpr="Due Date";
                Editable=FALSE }

    { 61  ;2   ;Field     ;
                SourceExpr="Currency Code";
                Editable=FALSE }

    { 62  ;2   ;Field     ;
                SourceExpr="Amount Excl. VAT";
                Editable=FALSE }

    { 63  ;2   ;Field     ;
                SourceExpr="Amount Incl. VAT";
                Editable=FALSE }

    { 34  ;2   ;Field     ;
                SourceExpr="VAT Amount";
                Editable=FALSE }

    { 20  ;1   ;Part      ;
                Name=ErrorMessagesPart;
                CaptionML=[ENU=Errors and Warnings;
                           ENG=Errors and Warnings];
                PagePartID=Page701;
                PartType=Page;
                ShowFilter=No }

    { 36  ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 83  ;1   ;Part      ;
                Name=WorkflowStatus;
                PagePartID=Page1528;
                Visible=ShowWorkflowStatus;
                Enabled=FALSE;
                Editable=FALSE;
                PartType=Page;
                ShowFilter=No }

    { 38  ;1   ;Part      ;
                PartType=System;
                SystemPartID=RecordLinks }

    { 39  ;1   ;Part      ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      IncomingDocumentsSetup@1000 : Record 131;
      AutomaticProcessingQst@1004 : TextConst '@@@=%1 is Data Exchange Type;ENU=The Data Exchange Type field is filled on at least one of the selected Incoming Documents.\\Are you sure you want to create documents manually?;ENG=The Data Exchange Type field is filled on at least one of the selected Incoming Documents.\\Are you sure you want to create documents manually?';
      CameraProvider@1005 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      HasCamera@1001 : Boolean;
      URL@1003 : Text;
      AttachmentFileName@1008 : Text;
      RecordLinkTxt@1007 : Text;
      OCRResultFileName@1009 : Text;
      IsDataExchTypeEditable@1002 : Boolean;
      OCRDataCorrectionEnabled@1006 : Boolean;
      AdditionalAttachmentsPresent@1010 : Boolean;
      InvalidTypeErr@1013 : TextConst 'ENU=The default attachment is not an XML document.;ENG=The default attachment is not an XML document.';
      OpenApprovalEntriesExistForCurrUser@1012 : Boolean;
      OpenApprovalEntriesExist@1015 : Boolean;
      ShowWorkflowStatus@1014 : Boolean;
      EnableReceiveFromOCR@1011 : Boolean;

    LOCAL PROCEDURE AskUserPermission@1() : Boolean;
    BEGIN
      IF "Data Exchange Type" = '' THEN
        EXIT(TRUE);

      IF Status <> Status::New THEN
        EXIT(TRUE);

      EXIT(CONFIRM(AutomaticProcessingQst));
    END;

    LOCAL PROCEDURE ShowErrors@2();
    VAR
      ErrorMessage@1001 : Record 700;
      TempErrorMessage@1000 : TEMPORARY Record 700;
    BEGIN
      ErrorMessage.SETRANGE("Context Record ID",RECORDID);
      ErrorMessage.CopyToTemp(TempErrorMessage);
      CurrPage.ErrorMessagesPart.PAGE.SetRecords(TempErrorMessage);
      CurrPage.ErrorMessagesPart.PAGE.UPDATE;
    END;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      IncomingDocumentAttachment@1000 : Record 133;
    BEGIN
      OCRDataCorrectionEnabled := GetGeneratedFromOCRAttachment(IncomingDocumentAttachment);
      AttachmentFileName := GetMainAttachmentFileName;
      RecordLinkTxt := GetRecordLinkText;
      OCRResultFileName := GetOCRResutlFileName;
      AdditionalAttachmentsPresent := GetAdditionalAttachments(IncomingDocumentAttachment);
      IF AdditionalAttachmentsPresent THEN
        CurrPage.SupportingAttachments.PAGE.LoadDataIntoPart(Rec);
    END;

    LOCAL PROCEDURE SetControlVisibility@9();
    VAR
      ApprovalsMgmt@1000 : Codeunit 1535;
    BEGIN
      OpenApprovalEntriesExistForCurrUser := ApprovalsMgmt.HasOpenApprovalEntriesForCurrentUser(RECORDID);
      OpenApprovalEntriesExist := ApprovalsMgmt.HasOpenApprovalEntries(RECORDID);
      ShowWorkflowStatus := CurrPage.WorkflowStatus.PAGE.SetFilterOnWorkflowRecord(RECORDID);
    END;

    EVENT CameraProvider@1005::PictureAvailable@11(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    BEGIN
      AddAttachmentFromServerFile(PictureName,PictureFilePath);
    END;

    BEGIN
    END.
  }
}

