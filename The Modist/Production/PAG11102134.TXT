OBJECT Page 11102134 OM - Change Field Options
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Change Field Options;
               ENU=Change Field Options;
               ESP=Change Field Options;
               FRA=Change Field Options;
               NLD=Veldopties wijzigen;
               DEA=Change Field Options];
    SaveValues=Yes;
    LinksAllowed=No;
    PageType=Worksheet;
    ShowFilter=No;
    OnOpenPage=VAR
                 WhereUsedStatus@1000 : Codeunit 11102047;
                 FileHandler@1001 : Codeunit 11102097;
               BEGIN
                 IF NOT Initialized THEN BEGIN
                   Initialized := TRUE;
                   UpdateData := TRUE;
                 END;

                 WebClient := FileHandler.IsWebClient;
                 IF WebClient THEN
                   SourceFileName := '';

                 WhereUsedStatus.WhereUsedUpToDate;

                 ValidateFieldNo;
                 SetEnabled;

                 FormatTableNo;
                 FormatFieldNo;
               END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Copy Update Data to Project as Actions After;
                                 ENU=Copy Update Data to Project as Actions After;
                                 ESP=Copy Update Data to Project as Actions After;
                                 FRA=Copy Update Data to Project as Actions After;
                                 NLD=Data bijwerken kopi‰ren naar project als acties achteraf;
                                 DEA=Copy Update Data to Project as Actions After];
                      Image=CopyFromTask;
                      OnAction=BEGIN
                                 CopyToProject;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=[DEU=Update Where Used;
                                 ENU=Update Where Used;
                                 ESP=Update Where Used;
                                 FRA=Update Where Used;
                                 NLD=Waar-gebruikt bijwerken;
                                 DEA=Update Where Used];
                      Image=Refresh;
                      OnAction=VAR
                                 WhereUsedMgt@1000 : Codeunit 11102045;
                               BEGIN
                                 WhereUsedMgt.UpdateObjects;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      CaptionML=[DEU=&Start;
                                 ENU=&Start;
                                 ESP=&Start;
                                 FRA=&Start;
                                 NLD=&Starten;
                                 DEA=&Start];
                      Promoted=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ChangeFieldOptions@1000 : Codeunit 11102082;
                                 FileHandler@1001 : Codeunit 11102097;
                                 FileToCreate@1002 : Text;
                                 ServerExportFileName@1003 : Text;
                                 ServerSourceFileName@1004 : Text;
                                 ctDefaultFileName@1005 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctEnterFileName@1006 : TextConst 'DEU=No file name entered.;ENU=No file name entered.;ESP=No file name entered.;FRA=No file name entered.;NLD=Geen bestandsnaam opgegeven.;DEA=No file name entered.';
                                 ctNothingChanged@1007 : TextConst 'DEU=Nothing to change.;ENU=Nothing to change.;ESP=Nothing to change.;FRA=Nothing to change.;NLD=Niets aan te passen.;DEA=Nothing to change.';
                               BEGIN
                                 IF OldOptions = NewOptions THEN
                                   ERROR(ctNothingChanged);

                                 CASE RunType OF

                                   RunType::Export :
                                     BEGIN
                                       IF NOT WebClient THEN BEGIN
                                         IF ExportFileName = '' THEN
                                           ERROR(ctEnterFileName);
                                         FileHandler.QueryReplace(ExportFileName);
                                       END;
                                       FileToCreate := ExportFileName;
                                       ServerExportFileName := FileHandler.CreateServerFileName(ExportFileName, ctDefaultFileName);
                                     END;

                                   RunType::"Update Text File" :
                                     BEGIN
                                       IF SourceFileName = '' THEN
                                         ERROR(ctEnterFileName);

                                       FileToCreate := FileHandler.CheckUpdateExisting(SourceFileName, ExportFileName);
                                       ServerExportFileName := FileHandler.CreateServerFileName(FileToCreate, ctDefaultFileName);
                                       ServerSourceFileName := FileHandler.UploadToServer(SourceFileName, '');
                                     END;

                                 END;

                                 ChangeFieldOptions.SetRunType(RunType);
                                 ChangeFieldOptions.SetSourceFileName(ServerSourceFileName);
                                 ChangeFieldOptions.SetExportFileName(ServerExportFileName);
                                 ChangeFieldOptions.SetUpdateData(UpdateData);

                                 ChangeFieldOptions.ChangeFieldOptions(TableNo, FieldNo2, OldOptions, NewOptions);

                                 ValidateFieldNo;

                                 IF RunType = RunType::"Update Text File" THEN
                                   FileHandler.FinalizeUpdateExisting(SourceFileName);

                                 IF RunType IN [RunType::Export, RunType::"Update Text File"] THEN
                                   FileHandler.DownloadToClient(ServerExportFileName, FileToCreate, ctDefaultFileName);

                                 ChangeFieldOptions.ShowSucceedMessage;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                GroupType=Group }

    { 7   ;2   ;Group     ;
                GroupType=Group }

    { 8   ;3   ;Field     ;
                CaptionML=[DEU=Table;
                           ENU=Table;
                           ESP=Table;
                           FRA=Table;
                           NLD=Tabel;
                           DEA=Table];
                BlankZero=Yes;
                SourceExpr=DisplayTableNo;
                OnValidate=VAR
                             Object@1000 : Record 2000000001;
                             Mgt@1001 : Codeunit 11102035;
                           BEGIN
                             Mgt.AfterInputObjectNo(Object.Type::Table, DisplayTableNo);
                             EVALUATE(TableNo, DisplayTableNo);
                             ValidateTableNo;
                             FormatTableNo;
                           END;

                OnLookup=VAR
                           Object@1000 : Record 2000000001;
                           Mgt@1001 : Codeunit 11102035;
                         BEGIN
                           EXIT(Mgt.LookupObjectNo(Object.Type::Table, Text));
                         END;
                          }

    { 9   ;3   ;Field     ;
                CaptionML=[DEU=Field;
                           ENU=Field;
                           ESP=Field;
                           FRA=Field;
                           NLD=Veld;
                           DEA=Field];
                BlankZero=Yes;
                SourceExpr=DisplayFieldNo;
                OnValidate=VAR
                             Mgt@1000 : Codeunit 11102035;
                           BEGIN
                             Mgt.AfterInputFieldNo(TableNo, DisplayFieldNo);
                             EVALUATE(FieldNo2, DisplayFieldNo);
                             FormatFieldNo;

                             ValidateFieldNo;
                           END;

                OnLookup=VAR
                           Field@1000 : Record 2000000041;
                           Fields@1001 : Page 11102068;
                           FieldNo3@1002 : Integer;
                         BEGIN
                           Field.FILTERGROUP(2);
                           Field.SETRANGE(TableNo, TableNo);
                           Field.SETRANGE(Type, Field.Type::Option);
                           Field.FILTERGROUP(0);

                           Fields.SETTABLEVIEW(Field);
                           Fields.LOOKUPMODE(TRUE);

                           IF Field.FINDFIRST THEN
                             Fields.SETRECORD(Field);

                           IF EVALUATE(FieldNo3, Text) THEN
                             IF Field.GET(TableNo, FieldNo3) THEN
                               Fields.SETRECORD(Field);

                           Field.SETFILTER(FieldName, '@' + Text);
                           IF Field.FINDFIRST THEN
                             Fields.SETRECORD(Field);
                           Field.SETRANGE(FieldName);

                           IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Fields.GETRECORD(Field);
                             Text := FORMAT(Field.FieldName);
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 10  ;3   ;Field     ;
                CaptionML=[DEU=Old Options;
                           ENU=Old Options;
                           ESP=Old Options;
                           FRA=Old Options;
                           NLD=Oude opties;
                           DEA=Old Options];
                SourceExpr=OldOptions;
                Editable=FALSE }

    { 11  ;3   ;Field     ;
                CaptionML=[DEU=New Options;
                           ENU=New Options;
                           ESP=New Options;
                           FRA=New Options;
                           NLD=Nieuwe opties;
                           DEA=New Options];
                SourceExpr=NewOptions;
                OnValidate=VAR
                             ChangeFieldOptions@1000 : Codeunit 11102082;
                             i@1001 : Integer;
                             OldOptionDest@1002 : ARRAY [1000] OF Integer;
                             NewOptionAction@1003 : ARRAY [1000] OF ' ,Same,Moved,Deleted,Inserted,Renamed';
                             OldOptionAction@1004 : ARRAY [1000] OF ' ,Same,Moved,Deleted,Inserted,Renamed';
                             NewOptionActionExist@1005 : ARRAY [10] OF Boolean;
                             OldOptionActionExist@1006 : ARRAY [10] OF Boolean;
                             ctToAddAndDelete@1007 : TextConst 'DEU=You cannot delete and add options at the same time.\Please do your changes in multiple steps.;ENU=You cannot delete and add options at the same time.\Please do your changes in multiple steps.;ESP=You cannot delete and add options at the same time.\Please do your changes in multiple steps.;FRA=You cannot delete and add options at the same time.\Please do your changes in multiple steps.;NLD=U kunt niet tegelijk opties verwijderen en toevoegen.\Doe u wijzigingen in meerdere stappen.;DEA=You cannot delete and add options at the same time.\Please do your changes in multiple steps.';
                           BEGIN
                             IF UpdateData THEN BEGIN
                               ChangeFieldOptions.CalculateActionToApply(OldOptions, NewOptions, OldOptionAction, NewOptionAction, OldOptionDest);

                               FOR i := 1 TO ARRAYLEN(OldOptionAction) DO
                                 IF OldOptionAction[i] <> OldOptionAction[i]::" " THEN
                                   OldOptionActionExist[OldOptionAction[i]] := TRUE;

                               FOR i := 1 TO ARRAYLEN(NewOptionAction) DO
                                 IF NewOptionAction[i] <> NewOptionAction[i]::" " THEN
                                   NewOptionActionExist[NewOptionAction[i]] := TRUE;

                               IF OldOptionActionExist[OldOptionAction::Deleted] AND
                                  NewOptionActionExist[NewOptionAction::Inserted]
                               THEN
                                 ERROR(ctToAddAndDelete);

                             END;
                           END;
                            }

    { 12  ;3   ;Field     ;
                CaptionML=[DEU=Export / Update in DB;
                           ENU=Export / Update in DB;
                           ESP=Export / Update in DB;
                           FRA=Export / Update in DB;
                           NLD=Exporteren / Bijwerken in DB;
                           DEA=Export / Update in DB];
                OptionCaptionML=[DEU=Update in DB,Export,Update Text File;
                                 ENU=Update in DB,Export,Update Text File;
                                 ESP=Update in DB,Export,Update Text File;
                                 FRA=Update in DB,Export,Update Text File;
                                 NLD=Bijwerken in DB,Exporteren,Tekstbestand bijwerken;
                                 DEA=Update in DB,Export,Update Text File];
                SourceExpr=RunType;
                OnValidate=BEGIN
                             SetEnabled;
                           END;
                            }

    { 13  ;3   ;Field     ;
                CaptionML=[DEU=Source File Name;
                           ENU=Source File Name;
                           ESP=Source File Name;
                           FRA=Source File Name;
                           NLD=Bronbestandsnaam;
                           DEA=Source File Name];
                SourceExpr=SourceFileName;
                Enabled=SourceFileNameEnabled;
                Editable=NOT WebClient;
                OnLookup=VAR
                           CommonDialog@1000 : Codeunit 11102059;
                         BEGIN
                           CommonDialog.AddKnownExtension('txt-o');
                           CommonDialog.GetOpenFileName(SourceFileName, WebClient);
                         END;
                          }

    { 14  ;3   ;Field     ;
                CaptionML=[DEU=File Name;
                           ENU=File Name;
                           ESP=File Name;
                           FRA=File Name;
                           NLD=Bestandsnaam;
                           DEA=File Name];
                SourceExpr=ExportFileName;
                Visible=NOT WebClient;
                Enabled=TargetFileNameEnabled;
                OnLookup=VAR
                           CommonDialog@1000 : Codeunit 11102059;
                         BEGIN
                           CommonDialog.AddKnownExtension('txt-o');
                           CommonDialog.GetSaveFileName(ExportFileName);
                         END;
                          }

    { 15  ;3   ;Field     ;
                CaptionML=[DEU=Update Data;
                           ENU=Update Data;
                           ESP=Update Data;
                           FRA=Update Data;
                           NLD=Data bijwerken;
                           DEA=Update Data];
                SourceExpr=UpdateData;
                Enabled=UpdateDataEnabled }

    { 16  ;2   ;Group     ;
                GroupType=Group }

    { 17  ;3   ;Field     ;
                CaptionML=[DEU=No. of Times Used;
                           ENU=No. of Times Used;
                           ESP=No. of Times Used;
                           FRA=No. of Times Used;
                           NLD=Aantal keer gebruikt;
                           DEA=No. of Times Used];
                SourceExpr=NoOfTimesUsed;
                Editable=FALSE;
                OnDrillDown=VAR
                              ChangeFieldOptions@1000 : Codeunit 11102082;
                            BEGIN
                              ChangeFieldOptions.DrillDownNoOfTimesUsed(TableNo, FieldNo2);
                            END;
                             }

    { 18  ;3   ;Field     ;
                CaptionML=[DEU=No. of Involved Objects;
                           ENU=No. of Involved Objects;
                           ESP=No. of Involved Objects;
                           FRA=No. of Involved Objects;
                           NLD=Aantal betrokken objecten;
                           DEA=No. of Involved Objects];
                SourceExpr=NoOfInvolvedObjects;
                Editable=FALSE;
                OnDrillDown=VAR
                              ChangeFieldOptions@1000 : Codeunit 11102082;
                            BEGIN
                              ChangeFieldOptions.DrillDownNoOfInvolvedObjects(TableNo, FieldNo2);
                            END;
                             }

    { 19  ;1   ;Field     ;
                SourceExpr=Initialized;
                Visible=FALSE }

    { 20  ;1   ;Field     ;
                SourceExpr=TableNo;
                Visible=FALSE }

    { 21  ;1   ;Field     ;
                SourceExpr=FieldNo2;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      DisplayFieldNo@1000 : Text;
      DisplayTableNo@1001 : Text;
      ExportFileName@1002 : Text;
      NewOptions@1003 : Text;
      OldOptions@1004 : Text;
      SourceFileName@1005 : Text;
      FieldNo2@1006 : Integer;
      NoOfInvolvedObjects@1007 : Integer;
      NoOfTimesUsed@1008 : Integer;
      TableNo@1009 : Integer;
      RunType@1010 : 'Update in DB,Export,Update Text File';
      Initialized@1011 : Boolean;
      SourceFileNameEnabled@1012 : Boolean INDATASET;
      TargetFileNameEnabled@1013 : Boolean INDATASET;
      UpdateData@1014 : Boolean;
      UpdateDataEnabled@1015 : Boolean INDATASET;
      WebClient@1016 : Boolean;

    LOCAL PROCEDURE ValidateTableNo@1();
    BEGIN
      FieldNo2 := 0;
      OldOptions := '';
      NewOptions := '';
      NoOfTimesUsed := 0;
    END;

    LOCAL PROCEDURE ValidateFieldNo@2();
    VAR
      Field@1000 : Record 2000000041;
      ChangeFieldOptions@1001 : Codeunit 11102082;
      RecRef@1002 : RecordRef;
      FldRef@1003 : FieldRef;
    BEGIN
      OldOptions := '';
      NewOptions := '';

      IF Field.GET(TableNo, FieldNo2) THEN
        IF Field.Type = Field.Type::Option THEN BEGIN
          RecRef.OPEN(Field.TableNo);
          FldRef := RecRef.FIELD(Field."No.");
          OldOptions := FldRef.OPTIONSTRING;
          NewOptions := FldRef.OPTIONSTRING;
        END;

      NoOfTimesUsed := ChangeFieldOptions.NoOfTimesUsed(TableNo, FieldNo2);
      NoOfInvolvedObjects := ChangeFieldOptions.NoOfInvolvedObjects(TableNo, FieldNo2);
    END;

    LOCAL PROCEDURE SetEnabled@3();
    BEGIN
      SourceFileNameEnabled := RunType = RunType::"Update Text File";
      TargetFileNameEnabled := RunType IN [RunType::Export, RunType::"Update Text File"];
      UpdateDataEnabled := RunType = RunType::"Update in DB";
    END;

    LOCAL PROCEDURE CopyToProject@4();
    VAR
      Project@1000 : Record 11102036;
      Actions@1001 : Record 11102048;
      TmpAction@1002 : TEMPORARY Record 11102048;
      ActionField@1003 : Record 11102049;
      TmpActionField@1004 : TEMPORARY Record 11102049;
      ChangeFieldOptions@1005 : Codeunit 11102082;
      LastLineNo@1006 : Integer;
      NoOfActionsAdded@1007 : Integer;
      ctActionsInsert@1008 : TextConst 'DEU=%1 actions added to project.;ENU=%1 actions added to project.;ESP=%1 actions added to project.;FRA=%1 actions added to project.;NLD=%1 acties aan project toegevoegd.;DEA=%1 actions added to project.';
    BEGIN
      IF PAGE.RUNMODAL(PAGE::"OM - Projects", Project) = ACTION::LookupOK THEN BEGIN

        ChangeFieldOptions.FillUpdateActions(TmpAction, TmpActionField, TableNo, FieldNo2, OldOptions, NewOptions);

        Actions.SETRANGE(Type, Actions.Type::Project);
        Actions.SETRANGE("No.", Project."No.");
        Actions.SETRANGE("Sub Type", Actions."Sub Type"::After);
        IF Actions.FINDLAST THEN
          LastLineNo := Actions."Line No.";

        IF TmpAction.FINDSET THEN
          REPEAT

            Actions := TmpAction;
            Actions.Type := Actions.Type::Project;
            Actions."No." := Project."No.";
            Actions."Sub Type" := Actions."Sub Type"::After;
            Actions."Line No." := LastLineNo + 10000;
            Actions.INSERT;

            LastLineNo += 10000;

            TmpActionField.SETRANGE(Type, TmpAction.Type);
            TmpActionField.SETRANGE("No.", TmpAction."No.");
            TmpActionField.SETRANGE("Sub Type", TmpAction."Sub Type");
            TmpActionField.SETRANGE("Line No.", TmpAction."Line No.");

            IF TmpActionField.FINDSET THEN
              REPEAT
                ActionField := TmpActionField;

                ActionField.Type := Actions.Type;
                ActionField."No." := Actions."No.";
                ActionField."Sub Type" := Actions."Sub Type";
                ActionField."Line No." := Actions."Line No.";
                ActionField.INSERT;

              UNTIL TmpActionField.NEXT = 0;

            NoOfActionsAdded += 1;

          UNTIL TmpAction.NEXT = 0;

      END;

      MESSAGE(ctActionsInsert, NoOfActionsAdded);
    END;

    LOCAL PROCEDURE FormatFieldNo@5();
    VAR
      Field@1000 : Record 2000000041;
    BEGIN
      DisplayFieldNo := '';
      IF Field.GET(TableNo, FieldNo2) THEN
        DisplayFieldNo := Field.FieldName;
    END;

    LOCAL PROCEDURE FormatTableNo@6();
    VAR
      Object@1000 : Record 2000000001;
    BEGIN
      DisplayTableNo := '';
      IF Object.GET(Object.Type::Table, '', TableNo) THEN
        DisplayTableNo := Object.Name;
    END;

    BEGIN
    END.
  }
}

