OBJECT Table 9062312 Field Lookup
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Field Lookup;
               ENG=Field Lookup];
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 2   ;   ;Field Type          ;Option        ;CaptionML=[ENU=Field Type;
                                                              ENG=Field Type];
                                                   OptionCaptionML=[ENU=Field,Enhanced Field;
                                                                    ENG=Field,Enhanced Field];
                                                   OptionString=Field,Enhanced Field }
    { 3   ;   ;Field No.           ;Integer       ;CaptionML=[ENU=Field No.;
                                                              ENG=Field No.] }
    { 4   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                Rec.Description := String.ToTitleCase(Rec.Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 5   ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
  }
  KEYS
  {
    {    ;Table ID,Field Type,Field No.,Code      ;Clustered=Yes }
    {    ;Code                                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EnhFieldMgt@1000000000 : Codeunit 9062298;
      String@1000000002 : Codeunit 9062236;
      TNPUtils@1000000001 : Codeunit 9062229;

    PROCEDURE PopulateFromExistingValues@1000000000();
    VAR
      FieldLookup@1000000000 : Record 9062312;
      CurrValue@1000000002 : Code[20];
      RecRef@1000000001 : RecordRef;
      TableNo@1000000003 : Integer;
      FieldNo@1000000004 : Integer;
      FieldType@1000000005 : 'Field,Enhanced Field';
      i@1000000006 : Integer;
      TableFilter@1000000007 : Text;
      FieldNoFilter@1000000008 : Text;
      FieldTypeFilter@1000000009 : Text;
    BEGIN
      IF "Table ID" <> 0 THEN BEGIN
        TableNo := "Table ID";
        FieldType := "Field Type";
        FieldNo := "Field No.";
      END ELSE BEGIN
        i := 0;
        REPEAT
          FILTERGROUP(i);
          TableFilter := GETFILTER("Table ID");
          IF TableFilter <> '' THEN BEGIN
            EVALUATE(TableNo,TableFilter);
            FieldTypeFilter := GETFILTER("Field Type");
            IF FieldTypeFilter <> '' THEN EVALUATE(FieldType,FieldTypeFilter);
            FieldNoFilter := GETFILTER("Field No.");
            IF FieldNoFilter <> '' THEN EVALUATE(FieldNo,FieldNoFilter);
          END;
          i += 1;
        UNTIL (i >= 10) OR (TableNo <> 0);
      END;

      IF TableNo <> 0 THEN BEGIN
        RecRef.OPEN(TableNo);
        IF RecRef.FINDSET THEN BEGIN
          REPEAT
            IF FieldType = FieldType::"Enhanced Field" THEN BEGIN
              CurrValue := COPYSTR(EnhFieldMgt.CalculateFieldValue(TableNo,FieldNo,RecRef.GETPOSITION),1,20);
              IF CurrValue <> '' THEN BEGIN
                IF NOT FieldLookup.GET(TableNo,FieldType,FieldNo,CurrValue) THEN BEGIN
                  FieldLookup.INIT;
                  FieldLookup.VALIDATE("Table ID",TableNo);
                  FieldLookup.VALIDATE("Field Type",FieldType);
                  FieldLookup.VALIDATE("Field No.",FieldNo);
                  FieldLookup.VALIDATE(Code,CurrValue);
                  FieldLookup.VALIDATE(Description,LOWERCASE(CurrValue));
                  FieldLookup.INSERT(TRUE);
                END;
              END;
            END;
          UNTIL RecRef.NEXT = 0;
        END;
      END;
    END;

    BEGIN
    {
      //DOC NA2015.1  PC 01/03/2013 - Created
      //DOC NA2016.1  JH 23/09/2015 - Upgraded to 2016
      //DOC NA2016.12 JH 28/01/2016 - New string codeunit
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

