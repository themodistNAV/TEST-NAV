OBJECT Codeunit 5613 Calculate Acq. Cost Depr.
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 field must not have a check mark because %2 is zero or negative for %3.;ENG=%1 field must not have a check mark because %2 is zero or negative for %3.';

    PROCEDURE DeprCalc@1(VAR DeprAmount@1000 : Decimal;VAR Custom1Amount@1001 : Decimal;FANo@1002 : Code[20];DeprBookCode@1003 : Code[10];LocalDeprBasis@1004 : Decimal;Custom1LocalDeprBasis@1005 : Decimal);
    VAR
      DeprBook@1006 : Record 5611;
      FADeprBook@1007 : Record 5612;
      DepreciationCalc@1008 : Codeunit 5616;
      DeprBasis@1009 : Decimal;
    BEGIN
      DeprAmount := 0;
      Custom1Amount := 0;
      DeprBook.GET(DeprBookCode);
      WITH FADeprBook DO BEGIN
        IF NOT GET(FANo,DeprBookCode) THEN
          EXIT;
        CALCFIELDS(Depreciation,"Acquisition Cost","Depreciable Basis");
        DeprBasis := "Depreciable Basis" - LocalDeprBasis;
        IF DeprBasis <= 0 THEN
          CreateError(FANo,DeprBookCode);
        IF DeprBasis > 0 THEN
          DeprAmount :=
            DepreciationCalc.CalcRounding(
              DeprBookCode,(Depreciation * LocalDeprBasis) / DeprBasis);
        IF DeprBook."Use Custom 1 Depreciation" AND
           ("Depr. Ending Date (Custom 1)" > 0D)
        THEN BEGIN
          DeprBasis := "Acquisition Cost" - Custom1LocalDeprBasis;
          CALCFIELDS("Custom 1");
          IF DeprBasis <= 0 THEN
            CreateError(FANo,DeprBookCode);
          IF DeprBasis > 0 THEN
            Custom1Amount :=
              DepreciationCalc.CalcRounding(
                DeprBookCode,("Custom 1" * Custom1LocalDeprBasis) / DeprBasis);
        END;
      END;
    END;

    LOCAL PROCEDURE CreateError@2(FANo@1000 : Code[20];DeprBookCode@1001 : Code[20]);
    VAR
      GenJnlLine@1003 : Record 81;
      FA@1002 : Record 5600;
      FADeprBook@1004 : Record 5612;
      DepreciationCalc@1005 : Codeunit 5616;
    BEGIN
      FA."No." := FANo;
      ERROR(
        Text000,
        GenJnlLine.FIELDCAPTION("Depr. Acquisition Cost"),
        FADeprBook.FIELDCAPTION("Depreciable Basis"),DepreciationCalc.FAName(FA,DeprBookCode));
    END;

    BEGIN
    END.
  }
}

