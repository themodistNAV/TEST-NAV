OBJECT Table 9021620 Label Web Configuration
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:01 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               LabelRec.RESET;
               LabelRec.SETFILTER(Code,'<>%1',"Label Code");
               IF LabelRec.FIND('-') THEN
               REPEAT
                 LabWebConfigRec.INIT;
                 LabWebConfigRec := Rec;
                 LabWebConfigRec."Label Code" := LabelRec.Code;
                 IF LabWebConfigRec.INSERT THEN;
               UNTIL LabelRec.NEXT = 0;
             END;

    OnDelete=BEGIN
               LabWebConfigRec.RESET;
               LabWebConfigRec.SETRANGE("Configuration Name","Configuration Name");
               LabWebConfigRec.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text002);
             END;

    CaptionML=[ENU=Label Web Configuration;
               ENG=Label Web Configuration];
    LookupPageID=Page9021626;
    DrillDownPageID=Page9021626;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 3   ;   ;Configuration Name  ;Text239       ;CaptionML=[ENU=Configuration Name;
                                                              ENG=Configuration Name];
                                                   NotBlank=Yes }
    { 20  ;   ;Value               ;Text250       ;OnValidate=BEGIN
                                                                IF "Same Value Across Labels" THEN BEGIN
                                                                  LabelRec.RESET;
                                                                  LabelRec.SETFILTER(Code,'<>%1',"Label Code");
                                                                  IF LabelRec.FIND('-') THEN
                                                                  REPEAT
                                                                    IF LabWebConfigRec.GET(LabelRec.Code,"Configuration Name") THEN BEGIN
                                                                      LabWebConfigRec.Value := Value;
                                                                      LabWebConfigRec.MODIFY;
                                                                    END;
                                                                  UNTIL LabelRec.NEXT = 0;

                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Value;
                                                              ENG=Value] }
    { 21  ;   ;Same Value Across Labels;Boolean   ;OnValidate=BEGIN
                                                                IF NOT CONFIRM(Text000) THEN
                                                                  ERROR(Text001);
                                                                LabelRec.RESET;
                                                                LabelRec.SETFILTER(Code,'<>%1',"Label Code");
                                                                IF LabelRec.FIND('-') THEN
                                                                REPEAT
                                                                  IF LabWebConfigRec.GET(LabelRec.Code,"Configuration Name") THEN BEGIN
                                                                    LabWebConfigRec."Same Value Across Labels" := "Same Value Across Labels";
                                                                    IF "Same Value Across Labels" THEN
                                                                      LabWebConfigRec.Value := Value;
                                                                    LabWebConfigRec.MODIFY;
                                                                  END;
                                                                UNTIL LabelRec.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Same Value Across Labels;
                                                              ENG=Same Value Across Labels] }
  }
  KEYS
  {
    {    ;Label Code,Configuration Name           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LabelRec@1000000000 : Record 9021618;
      LabWebConfigRec@1000000001 : Record 9021620;
      Text000@1000000002 : TextConst 'ENU=If you change this, it will change this and the values across all labels.  \\Are you sure you want to continue? (0);ENG=If you change this, it will change this and the values across all labels.  \\Are you sure you want to continue? (0)';
      Text001@1000000003 : TextConst 'ENU=Change Abandoned. (1);ENG=Change Abandoned. (1)';
      Text002@1000000004 : TextConst 'ENU=You cannot rename this.  Delete it and create a new one. (2);ENG=You cannot rename this.  Delete it and create a new one. (2)';

    BEGIN
    {
      //DOC EC2016.6 JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

