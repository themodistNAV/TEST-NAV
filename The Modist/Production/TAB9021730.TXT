OBJECT Table 9021730 Web Security Role
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:10 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               DateTimeInsertedModified:=CURRENTDATETIME;
               UserInsertedModified:=USERID;

               //DOC EC2009.2.71 -
               IF "Label Code" = '' THEN BEGIN
                 "Label Code" := MQGlobal.GetLabelCode;
                 IF "Label Code" = '' THEN
                   ERROR(Text002);
               END
               //DOC EC2009.2.71 +
             END;

    OnModify=BEGIN
               DateTimeInsertedModified:=CURRENTDATETIME;
               UserInsertedModified:=USERID;
             END;

    OnDelete=BEGIN
               //DOC EC2009.2.71 -
               CALCFIELDS("Usage Count");
               IF "Usage Count" > 0 THEN
                 ERROR(Text001,TABLECAPTION);
               //DOC EC2009.2.71 +

               //DOC EC2013.3.40 -
               //check not used in Page Item Content and Shipping Agent Service Price
               PageItemCont.RESET;
               PageItemCont.SETRANGE("Security Type",PageItemCont."Security Type"::"Web Security Role");
               PageItemCont.SETRANGE("Label Code","Label Code");
               PageItemCont.SETRANGE("Security Filter",Code);
               IF PageItemCont.FINDFIRST THEN
                 ERROR(Text004,TABLECAPTION,PageItemCont.TABLECAPTION);
               ShipAgentServPrice.RESET;
               ShipAgentServPrice.SETRANGE("User Context Type",ShipAgentServPrice."User Context Type"::"Web Security Role");
               ShipAgentServPrice.SETRANGE("Label Code","Label Code");
               ShipAgentServPrice.SETRANGE("User Context Code",Code);
               IF ShipAgentServPrice.FINDFIRST THEN
                 ERROR(Text004,TABLECAPTION,ShipAgentServPrice.TABLECAPTION);
               //DOC EC2013.3.40 +
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION); //DOC EC2009.2.71
             END;

    CaptionML=[ENU=Web Security Role;
               ENG=Web Security Role];
    LookupPageID=Page9021735;
    DrillDownPageID=Page9021735;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code] }
    { 3   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                TxtB2B@1000000000 : TextConst 'ENU=B2B;ENG=B2B';
                                                                TxtB2C@1000000001 : TextConst 'ENU=B2C;ENG=B2C';
                                                                TxtAnonymous@1000000002 : TextConst 'ENU=ANONYMOUS;ENG=ANONYMOUS';
                                                              BEGIN
                                                                //DOC EC2013.3.40
                                                                IF Code IN [TxtB2B,TxtB2C,TxtAnonymous] THEN
                                                                  ERROR(Text003,Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 10  ;   ;Name                ;Text30        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 20  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 30  ;   ;Usage Count         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Security User Role" WHERE (Web Security Role=FIELD(Code),
                                                                                                     Label Code=FIELD(Label Code)));
                                                   CaptionML=[ENU=Usage Count;
                                                              ENG=Usage Count];
                                                   Editable=No }
    { 32  ;   ;Excluded Role       ;Code20        ;TableRelation="Web Security Role".Code WHERE (Label Code=FIELD(Label Code),
                                                                                                 Is An Excluded Role=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                //DOC EC2013.4.60
                                                                IF "Excluded Role" = Code THEN
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Excluded Role;
                                                              ENG=Excluded Role];
                                                   Description=GW add 140312, MQ 23/12/2014 - GW141223 from C10 }
    { 33  ;   ;Is An Excluded Role ;Boolean       ;OnValidate=BEGIN
                                                                IF "Is An Excluded Role" = FALSE THEN BEGIN
                                                                  WebSecRole.RESET;
                                                                  WebSecRole.SETRANGE("Label Code","Label Code");
                                                                  WebSecRole.SETRANGE("Excluded Role",Code);
                                                                  IF WebSecRole.FINDFIRST THEN
                                                                    ERROR(Text006);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Is An Excluded Role;
                                                              ENG=Is An Excluded Role];
                                                   Description=GW add 140312 }
    { 40  ;   ;DateTimeInsertedModified;DateTime  ;CaptionML=[ENU=DateTimeInsertedModified;
                                                              ENG=DateTimeInsertedModified];
                                                   Editable=No }
    { 41  ;   ;UserInsertedModified;Code40        ;CaptionML=[ENU=UserInsertedModified;
                                                              ENG=UserInsertedModified];
                                                   Description=MQ 10/01/2014 - GW140110 from C20;
                                                   Editable=No }
    { 50  ;   ;Sort Order          ;Integer       ;CaptionML=[ENU=Sort Order;
                                                              ENG=Sort Order];
                                                   Description=MA111118: sort roles to prioritse single Home PageGuid for users with multiple roles }
    { 60  ;   ;Login Target Page GUID;Integer     ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code));
                                                   CaptionML=[ENU=Login Target Page GUID;
                                                              ENG=Login Target Page GUID];
                                                   Description=MA111118 }
  }
  KEYS
  {
    {    ;Label Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1190000005 : TextConst 'ENU=You can not rename a %1.;ENG=You can not rename a %1.';
      Text001@1190000006 : TextConst 'ENU=You cannot delete a %1 while Users are assigned to this role. (1);ENG=You cannot delete a %1 while Users are assigned to this role. (1)';
      MQGlobal@1190000007 : Codeunit 9021624;
      Text002@1190000008 : TextConst 'ENU=The Label Code must not be blank. (2);ENG=The Label Code must not be blank. (2)';
      Text003@1000000000 : TextConst 'ENU=The Code %1 is protected.  You cannot use this as a Code. (3);ENG=The Code %1 is protected.  You cannot use this as a Code. (3)';
      PageItemCont@1000000002 : Record 9021636;
      ShipAgentServPrice@1000000003 : Record 9021711;
      Text004@1000000001 : TextConst 'ENU=You cannot delete a %1 while a %2 is assigned to this role. (4);ENG=You cannot delete a %1 while a %2 is assigned to this role. (4)';
      Text005@1000000004 : TextConst 'ENU=The Excluded Role cannot be the same as the Code. (5);ENG=The Excluded Role cannot be the same as the Code. (5)';
      WebSecRole@1000000005 : Record 9021730;
      Text006@1000000006 : TextConst 'ENU=You cannot untick this while this Web Security Role exists as an Excluded Role. (6);ENG=You cannot untick this while this Web Security Role exists as an Excluded Role. (6)';

    BEGIN
    {
      //DOC EC2009.2.69 MQ 30/09/2011 - GW110930
      //DOC EC2009.2.70 MA 18/11/2011 - MA111118 Added fields: Login Target Page GUID (60) & Sort Order (50)
      //DOC EC2009.2.71 MQ 12/12/2011 - GW111212 Make more resiliant.
      //DOC EC2013.3.40 MQ 04/01/2013 - GW130104 Stop user creating records with code of B2B, B2C and ANONYMOUS; Code in OnDelete
      //DOC EC2013.4.00 MQ 10/01/2014 - GW140110 Made USERID fields C40
      //DOC EC2013.4.60 MQ 12/03/2014 - GW140312 Add fields Excluded Role and Is An Excluded Role
      //DOC EC2015.5.01 MQ 23/12/2014 - GW141223 Made Excluded Role from C10 to C20
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

