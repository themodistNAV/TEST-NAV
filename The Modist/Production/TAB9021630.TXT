OBJECT Table 9021630 Page Content
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:02 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LabelRec.GET("Label Code");
               IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text006,"Label Code",COMPANYNAME);

               IF "Line No." = 0 THEN
                 CalcLineNo;

               IF ("Content Type Code" = '') AND (Description = '') THEN
                 ERROR(Text005);

               "DateTime Created":=CURRENTDATETIME;
               SetModifiedDateTime();
             END;

    OnModify=BEGIN
               LabelRec.GET("Label Code");
               IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text006,"Label Code",COMPANYNAME);

               SetModifiedDateTime();
             END;

    OnDelete=BEGIN
               LabelRec.GET("Label Code");
               IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text006,"Label Code",COMPANYNAME);

               PageContValue.RESET;
               PageContValue.SETRANGE("Label Code","Label Code");
               PageContValue.SETRANGE("Page GroupGuID","Page GroupGuid");
               PageContValue.SETRANGE(PlaceholderID,PlaceholderID);
               PageContValue.SETRANGE("Content Line No.","Line No.");
               IF PageContValue.FINDFIRST THEN BEGIN
                 IF NOT CONFIRM (Text001) THEN
                   ERROR(Text002);
                 PageContValue.DELETEALL;
               END;
             END;

    OnRename=BEGIN
               LabelRec.GET("Label Code");
               IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                 ERROR(Text006,"Label Code",COMPANYNAME);

               PageContent.RESET;
               PageContent.SETRANGE("Label Code","Label Code");
               PageContent.SETRANGE("Page GroupGuid","Page GroupGuid");
               PageContent.SETRANGE(PlaceholderID,PlaceholderID);
               IF PageContent.FINDLAST THEN
                 LineNo := PageContent."Line No." + 1
               ELSE
                 LineNo := 1;
               PageContent.SETCURRENTKEY("Label Code","Page GroupGuid",PlaceholderID,Order);
               IF PageContent.FINDLAST THEN
                 OrderNo := PageContent.Order + 1
               ELSE
                 OrderNo := 1;

               PageContValue.RESET;
               PageContValue.SETRANGE("Label Code","Label Code");
               PageContValue.SETRANGE("Page GroupGuID",xRec."Page GroupGuid");
               PageContValue.SETRANGE(PlaceholderID,xRec.PlaceholderID);
               PageContValue.SETRANGE("Content Line No.","Line No.");
               IF PageContValue.FIND('-') THEN
               REPEAT
                 PageContValue1.INIT;
                 PageContValue1.TRANSFERFIELDS(PageContValue);
                 PageContValue1."Label Code" := "Label Code";
                 PageContValue1."Page GroupGuID" := "Page GroupGuid";
                 PageContValue1.PlaceholderID := PlaceholderID;
                 PageContValue1."Content Line No." := LineNo;
                 PageContValue1.INSERT;
                 PageContValue.DELETE;
               UNTIL PageContValue.NEXT = 0;
               "Line No." := LineNo;
               Order := OrderNo;
             END;

    CaptionML=[ENU=Page Content;
               ENG=Page Content];
    LookupPageID=Page9021660;
    DrillDownPageID=Page9021660;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                IF "Content Type Code" <> '' THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Page GroupGuid      ;Integer       ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code));
                                                   CaptionML=[ENU=Page GroupGuid;
                                                              ENG=Page GroupGuid];
                                                   NotBlank=Yes }
    { 5   ;   ;PlaceholderID       ;Text20        ;TableRelation="Layout Component".PlaceHolderID WHERE (Label Code=FIELD(Label Code),
                                                                                                         Template Page GroupGuID=FIELD(Template Page GroupGuID));
                                                   OnValidate=BEGIN
                                                                VALIDATE("Template Page GroupGuID");
                                                              END;

                                                   CaptionML=[ENU=PlaceholderID;
                                                              ENG=PlaceholderID];
                                                   NotBlank=Yes;
                                                   Description=must be defined in the .NET page referenced }
    { 8   ;   ;Line No.            ;Integer       ;OnValidate=BEGIN
                                                                ERROR(Text004);
                                                              END;

                                                   CaptionML=[ENU=Line No.;
                                                              ENG=Line No.];
                                                   NotBlank=Yes }
    { 11  ;   ;Template Page File  ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(ePage."Page File" WHERE (Label Code=FIELD(Label Code),
                                                                                               Page GUID=FIELD(Page GroupGuid)));
                                                   CaptionML=[ENU=Template Page File;
                                                              ENG=Template Page File];
                                                   Editable=No }
    { 14  ;   ;Content Type Code   ;Code20        ;TableRelation="Content Type";
                                                   OnValidate=BEGIN
                                                                IF "Line No." = 0 THEN
                                                                  CalcLineNo;

                                                                PageRec.RESET;
                                                                PageRec.GET("Label Code","Page GroupGuid");
                                                                IF PageRec."Template Page Entry" THEN
                                                                  "Is Master Data" := TRUE
                                                                ELSE
                                                                  "Is Master Data" := FALSE;

                                                                //DOC EC2013.3.70 - was not inserting Page Content Values if new line had same Content T Code
                                                                //Orig IF "Content Type Code" <> xRec."Content Type Code" THEN BEGIN
                                                                IF ("Content Type Code" <> xRec."Content Type Code") OR
                                                                  ("Page GroupGuid" <> xRec."Page GroupGuid") OR
                                                                  (PlaceholderID <> xRec.PlaceholderID) OR
                                                                  ("Line No." <> xRec."Line No.") THEN BEGIN
                                                                //DOC EC2013.3.70 +
                                                                  ContentType.GET("Content Type Code");
                                                                  LabelRec.GET("Label Code");  //DOC EC2013.3.00
                                                                  Description := ContentType.Description;
                                                                  PageContValue.RESET;
                                                                  PageContValue.SETRANGE("Label Code","Label Code");
                                                                  PageContValue.SETRANGE("Page GroupGuID","Page GroupGuid");
                                                                  PageContValue.SETRANGE(PlaceholderID,PlaceholderID);
                                                                  PageContValue.SETRANGE("Content Line No.","Line No.");
                                                                  IF PageContValue.FINDFIRST THEN
                                                                    IF NOT CONFIRM (Text001) THEN
                                                                      ERROR(Text002);
                                                                  PageContValue.DELETEALL;
                                                                  ContTypeAttrib.RESET;
                                                                  PageContValue.RESET;
                                                                  ContTypeAttrib.SETRANGE("Content Type Code","Content Type Code");
                                                                  IF ContTypeAttrib.FIND('-') THEN
                                                                  REPEAT
                                                                    PageContValue.INIT;
                                                                    PageContValue."Label Code" := "Label Code";
                                                                    PageContValue."Page GroupGuID" := "Page GroupGuid";
                                                                    PageContValue.PlaceholderID := PlaceholderID;
                                                                    PageContValue."Content Line No." := "Line No.";
                                                                    PageContValue.Attribute := ContTypeAttrib.Attribute;
                                                                    PageContValue."Content Type Code" := "Content Type Code";
                                                                    PageContValue."Html Allowed" := ContTypeAttrib."Html Allowed";
                                                                    PageContValue.Required := ContTypeAttrib.Required;
                                                                    PageContValue."Template Page GroupGuID" := "Template Page GroupGuID";
                                                                    PageContValue."Shared Html" := ContTypeAttrib."Shared Html";
                                                                    PageContValue."Value Text" := ContTypeAttrib."Default Value";
                                                                    PageContValue."Is Master Data" := "Is Master Data";
                                                                    //DOC EC2013.3.00 -
                                                                    IF LabelRec."Multi-Language Mode" THEN BEGIN
                                                                      PageContValue."In Multi Language Mode" := TRUE;
                                                                      PageContValue."Multilanguage Allowed" := ContTypeAttrib."Multilanguage Allowed";
                                                                      PageContValue."Multilanguage Key" := ContTypeAttrib."Multilanguage Key";
                                                                      IF  PageContValue."Multilanguage Key" <> '' THEN BEGIN
                                                                        MultiLang.RESET;
                                                                        MultiLang.SETCURRENTKEY("Label Code","Base File Name","Language ID","Resx Name");
                                                                        MultiLang.SETRANGE("Label Code","Label Code");
                                                                        MultiLang.SETRANGE("Base File Name",'LANGUAGE');
                                                                        MultiLang.SETRANGE("Resx Name",PageContValue."Multilanguage Key");
                                                                        IF NOT MultiLang.FINDFIRST THEN BEGIN
                                                                          MultiLang.SETRANGE("Label Code");
                                                                          IF MultiLang.FINDFIRST THEN BEGIN
                                                                            MultiLang1.INIT;
                                                                            MultiLang1 := MultiLang;
                                                                            MultiLang1."Entry No." := 0;
                                                                            MultiLang1."Label Code" := "Label Code";
                                                                            MultiLang1.INSERT(TRUE);
                                                                          END ELSE BEGIN
                                                                            ERROR(Text009);
                                                                          END;
                                                                        END;
                                                                      END;
                                                                    END ELSE BEGIN
                                                                      PageContValue."In Multi Language Mode" := FALSE;
                                                                      PageContValue."Multilanguage Allowed" := FALSE;
                                                                      PageContValue."Multilanguage Key" := '';
                                                                    END;
                                                                    //DOC EC2013.3.00 +
                                                                    PageContValue.INSERT;
                                                                  UNTIL ContTypeAttrib.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Content Type Code;
                                                              ENG=Content Type Code];
                                                   NotBlank=Yes }
    { 18  ;   ;Is Master Data      ;Boolean       ;CaptionML=[ENU=Is Master Data;
                                                              ENG=Is Master Data] }
    { 19  ;   ;Template Page GroupGuID;Integer    ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code),
                                                                                          System Entry=CONST(Yes),
                                                                                          Template Page Entry=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                IF LayoutCpt.GET("Label Code","Template Page GroupGuID",PlaceholderID) THEN BEGIN
                                                                  "Component Row Index" := LayoutCpt."Row Index";
                                                                  "Component Column Index" := LayoutCpt."Column Index";
                                                                END ELSE BEGIN
                                                                  "Component Row Index" := 0;
                                                                  "Component Column Index" := 0;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              VersionCalls@1000000000 : Codeunit 9021662;
                                                            BEGIN
                                                              //DOC EC2013.4.00 CLEAR(TemplatePageList);
                                                              PageRec.RESET;
                                                              PageRec.SETRANGE("Label Code","Label Code");
                                                              IF "Template Page GroupGuID" <> 0 THEN
                                                                PageRec.SETRANGE("Page GUID","Template Page GroupGuID");
                                                              PageRec.SETRANGE("System Entry",TRUE);
                                                              PageRec.SETRANGE("Template Page Entry");
                                                              //DOC EC2013.4.00 -
                                                              //TemplatePageList.SETTABLEVIEW(PageRec);
                                                              //TemplatePageList.RUN;
                                                              IF PageRec.FINDSET THEN;
                                                              VersionCalls.ShowTemplatePage(PageRec);
                                                              //DOC EC2013.4.00 +
                                                            END;

                                                   CaptionML=[ENU=Template Page GroupGuID;
                                                              ENG=Template Page GroupGuID];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 22  ;   ;Comments            ;Text250       ;CaptionML=[ENU=Comments;
                                                              ENG=Comments] }
    { 30  ;   ;Blocked             ;Boolean       ;OnValidate=VAR
                                                                ChildContent@1000000000 : Record 9021630;
                                                              BEGIN
                                                                IF "Is Master Data" THEN
                                                                  IF GUIALLOWED THEN
                                                                    IF NOT CONFIRM(Text007,FALSE) THEN
                                                                      ERROR(Text008);

                                                                  ChildContent.SETRANGE("Label Code","Label Code");
                                                                  ChildContent.SETRANGE(PlaceholderID,PlaceholderID);
                                                                  ChildContent.SETRANGE("Line No.","Line No.");
                                                                  ChildContent.SETRANGE("Content Type Code","Content Type Code");
                                                                  ChildContent.SETFILTER("Is Master Data",'no');
                                                                  ChildContent.SETRANGE("Template Page GroupGuID","Page GroupGuid");
                                                                  ChildContent.MODIFYALL(Blocked,Blocked,TRUE);
                                                              END;

                                                   CaptionML=[ENU=Blocked;
                                                              ENG=Blocked];
                                                   Description=Not displayed on web site if checked }
    { 32  ;   ;Not Rendered on Big Screen;Boolean ;CaptionML=[ENU=Not Rendered on Big Screen;
                                                              ENG=Not Rendered on Big Screen];
                                                   Description=MQ 21/11/2013 - GW131121 added }
    { 34  ;   ;Not Rendered on Mobile;Boolean     ;CaptionML=[ENU=Not Rendered on Mobile;
                                                              ENG=Not Rendered on Mobile];
                                                   Description=MQ 21/11/2013 - GW131121 added }
    { 40  ;   ;Order               ;Integer       ;CaptionML=[ENU=Order;
                                                              ENG=Order];
                                                   Description=used to load content in a specified order }
    { 41  ;   ;New Order           ;Integer       ;CaptionML=[ENU=New Order;
                                                              ENG=New Order] }
    { 100 ;   ;Component Row Index ;Integer       ;CaptionML=[ENU=Component Row Index;
                                                              ENG=Component Row Index] }
    { 101 ;   ;Component Column Index;Integer     ;FieldClass=Normal;
                                                   CaptionML=[ENU=Component Column Index;
                                                              ENG=Component Column Index] }
    { 120 ;   ;DateTime Created    ;DateTime      ;CaptionML=[ENU=DateTime Created;
                                                              ENG=DateTime Created] }
    { 121 ;   ;DateTime Last Modified;DateTime    ;CaptionML=[ENU=DateTime Last Modified;
                                                              ENG=DateTime Last Modified] }
  }
  KEYS
  {
    {    ;Label Code,Page GroupGuid,PlaceholderID,Line No.;
                                                   Clustered=Yes }
    {    ;Label Code,Page GroupGuid,PlaceholderID,Order }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LabelRec@1040014 : Record 9021618;
      PageRec@1000000001 : Record 9021628;
      PageContent@1040010 : Record 9021630;
      PageContent1@1040000 : Record 9021630;
      PageContent2@1040001 : Record 9021630;
      PageContValue@1040003 : Record 9021635;
      PageContValue1@1040012 : Record 9021635;
      ContTypeAttrib@1040006 : Record 9021634;
      ContentType@1040007 : Record 9021633;
      LayoutCpt@1000000000 : Record 9021631;
      MultiLang@1190000005 : Record 9021603;
      MultiLang1@1190000007 : Record 9021603;
      OrderNo@1040002 : Integer;
      Text001@1040004 : TextConst 'ENU=Values are already assigned to the attributes, changing the Content Type will result in the loss of these values.\\Do you want to continue? (1);ENG=Values are already assigned to the attributes, changing the Content Type will result in the loss of these values.\\Do you want to continue? (1)';
      Text002@1040005 : TextConst 'ENU=Abandoned. (2);ENG=Abandoned. (2)';
      Text003@1040008 : TextConst 'ENU=You cannot change the label unless the Content Type is blank. (3);ENG=You cannot change the label unless the Content Type is blank. (3)';
      Text004@1040009 : TextConst 'ENU=You cannot manually change the line no. (4);ENG=You cannot manually change the line no. (4)';
      LineNo@1040011 : Integer;
      Text005@1040013 : TextConst 'ENU=Error...... Content Type and Description cannot both be blank. (5);ENG=Error...... Content Type and Description cannot both be blank. (5)';
      Text006@1040015 : TextConst 'ENU=You cannot do this as the label %1 is not used in the company %2.  (6);ENG=You cannot do this as the label %1 is not used in the company %2.  (6)';
      Text007@1000000003 : TextConst 'ENU=This change will apply to each instance of this Page Content (i.e. on all the pages created from this master page).\\Do you want to continue?;ENG=This change will apply to each instance of this Page Content (i.e. on all the pages created from this master page).\\Do you want to continue?';
      Text008@1000000004 : TextConst 'ENU=Operation aborted.;ENG=Operation aborted.';
      Text009@1190000006 : TextConst 'ENU=Cannot find entry in Multi Language Reference Table. (9);ENG=Cannot find entry in Multi Language Reference Table. (9)';

    PROCEDURE Reorder@1000000000(LabelCode@1000000000 : Code[10];PageGroupGuID@1000000003 : Integer;PlaceholderCode@1040000 : Code[10]);
    VAR
      i1@1000000001 : Integer;
      i2@1000000002 : Integer;
    BEGIN
      PageContent1.RESET;
      PageContent1.SETRANGE("Label Code",LabelCode);
      PageContent1.SETRANGE("Page GroupGuid",PageGroupGuID);
      PageContent1.SETRANGE(PlaceholderID,PlaceholderCode);
      PageContent1.SETCURRENTKEY("Label Code","Page GroupGuid",PlaceholderID,Order);
      i1 := 0;
      IF PageContent1.FIND('-') THEN
      REPEAT
        IF i1 = 0 THEN BEGIN
          i1 := 1;
          i2 := 1;
          PageContent2 := PageContent1;
          PageContent2."New Order" := 1;
        END ELSE BEGIN
          PageContent2 := PageContent1;
          i2 := i2 + 1;
          PageContent2."New Order" := i2;
        END;
        PageContent2.MODIFY;
      UNTIL PageContent1.NEXT = 0;

      PageContent1.RESET;
      PageContent1.SETRANGE("Label Code",LabelCode);
      PageContent1.SETRANGE("Page GroupGuid",PageGroupGuID);
      PageContent1.SETRANGE(PlaceholderID,PlaceholderCode);
      IF PageContent1.FIND('-') THEN
      REPEAT
        PageContent2 := PageContent1;
        PageContent2.Order := PageContent2."New Order";
        PageContent2.MODIFY;
      UNTIL PageContent1.NEXT = 0;
    END;

    PROCEDURE MoveUp@1000000001(VAR LocalRec@1000000000 : Record 9021630);
    BEGIN
      Reorder(LocalRec."Label Code",LocalRec."Page GroupGuid",LocalRec.PlaceholderID);
      PageContent1.GET(LocalRec."Label Code",LocalRec."Page GroupGuid",
                          LocalRec.PlaceholderID,LocalRec."Line No.");
      IF PageContent1.Order = 1 THEN
        EXIT;

      PageContent2.RESET;
      PageContent2.SETRANGE("Label Code",PageContent1."Label Code");
      PageContent2.SETRANGE("Page GroupGuid",PageContent1."Page GroupGuid");
      PageContent2.SETFILTER(Order,'<%1',PageContent1.Order);
      PageContent2.SETCURRENTKEY("Label Code","Page GroupGuid",PlaceholderID,Order);
      IF PageContent2.FINDLAST THEN BEGIN
        PageContent2.Order := PageContent1.Order;
        PageContent2."New Order" := PageContent1.Order;
        PageContent1.Order := PageContent1.Order - 1;
        PageContent1."New Order" := PageContent1.Order;
        PageContent2.MODIFY;
        PageContent1.MODIFY;
      END ELSE
        EXIT;
    END;

    PROCEDURE MoveDown@1000000002(VAR LocalRec@1000000000 : Record 9021630);
    BEGIN
      Reorder(LocalRec."Label Code",LocalRec."Page GroupGuid",LocalRec.PlaceholderID);
      PageContent1.GET(LocalRec."Label Code",LocalRec."Page GroupGuid",
                          LocalRec.PlaceholderID,LocalRec."Line No.");

      PageContent2.RESET;
      PageContent2.SETRANGE("Label Code",PageContent1."Label Code");
      PageContent2.SETRANGE("Page GroupGuid",PageContent1."Page GroupGuid");
      PageContent2.SETFILTER(Order,'>%1',PageContent1.Order);
      PageContent2.SETCURRENTKEY("Label Code","Page GroupGuid",PlaceholderID,Order);
      IF PageContent2.FINDFIRST THEN BEGIN
        PageContent2.Order := PageContent1.Order;
        PageContent2."New Order" := PageContent1.Order;
        PageContent1.Order := PageContent1.Order + 1;
        PageContent1."New Order" := PageContent1.Order;
        PageContent2.MODIFY;
        PageContent1.MODIFY;
      END ELSE
        EXIT;
    END;

    PROCEDURE CalcLineNo@1040000();
    BEGIN
      PageContent1.RESET;
      PageContent1.SETRANGE("Label Code","Label Code");
      PageContent1.SETRANGE("Page GroupGuid","Page GroupGuid");
      PageContent1.SETRANGE(PlaceholderID,PlaceholderID);
      PageContent1.SETCURRENTKEY("Label Code","Page GroupGuid",PlaceholderID,"Line No.");
      IF PageContent1.FINDLAST THEN
        "Line No." := PageContent1."Line No." + 1
      ELSE
        "Line No." := 1;
    END;

    PROCEDURE SetModifiedDateTime@1000000003();
    VAR
      Page@1000000000 : Record 9021628;
    BEGIN
      "DateTime Last Modified":=CURRENTDATETIME;
      IF Page.GET("Label Code","Page GroupGuid") THEN BEGIN
        Page.VALIDATE("DateTime Last Modified",CURRENTDATETIME);
        Page.MODIFY;
      END;
    END;

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - Multilanguage mods
      //DOC EC2013.3.10 MQ 01/01/2013 - Rewrote MoveUp and MoveDown to work with RTC
      //DOC EC2013.3.70 MQ 07/08/2013 - GW130807 change in Content Type Code
      //DOC EC2013.4.00 MQ 04/11/2013 - GW131104 RTC
      //DOC EC2013.4.00 MQ 21/11/2013 - GW131121 Add fields 32 and 34
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

