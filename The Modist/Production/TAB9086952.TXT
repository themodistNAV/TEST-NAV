OBJECT Table 9086952 nF. MenuSuite Timestamp
{
  OBJECT-PROPERTIES
  {
    Date=30/10/16;
    Time=11:18:43 AM;
    Version List=NF2016.17;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               NfRecordAuditToolkit.UpdateCreated(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               NfRecordAuditToolkit.UpdateModified(Rec."Modified By", Rec."Modified Date Time");
             END;

    CaptionML=[ENU=nForcement MenuSuite Timestamp;
               ENG=nForcement MenuSuite Timestamp];
  }
  FIELDS
  {
    { 1   ;   ;MenuSuite ID        ;Integer       ;CaptionML=[ENU=MenuSuite ID;
                                                              ENG=MenuSuite ID] }
    { 10  ;   ;Saved Date          ;Date          ;CaptionML=[ENU=Saved Date;
                                                              ENG=Saved Date] }
    { 11  ;   ;Saved Time          ;Time          ;CaptionML=[ENU=Saved Time;
                                                              ENG=Saved Time] }
    { 20  ;   ;MenuSuite Name      ;Text50        ;CaptionML=[ENU=MenuSuite Name;
                                                              ENG=MenuSuite Name] }
    { 30  ;   ;Current Date        ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Date WHERE (Type=CONST(MenuSuite),
                                                                                         ID=FIELD(MenuSuite ID)));
                                                   CaptionML=[ENU=Current Date;
                                                              ENG=Current Date];
                                                   Editable=No }
    { 31  ;   ;Current Time        ;Time          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Time WHERE (Type=CONST(MenuSuite),
                                                                                         ID=FIELD(MenuSuite ID)));
                                                   CaptionML=[ENU=Current Time;
                                                              ENG=Current Time];
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              NfRecordAuditToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              NfRecordAuditToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;MenuSuite ID                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NfRecordAuditToolkit@1000000001 : Codeunit 9086951;

    PROCEDURE IsUpToDate@1000000000() : Boolean;
    BEGIN
      ///<summary>Returning if the record is up to date</summary>
      ///<returns>Response</returns>

      EXIT((Rec."Saved Date" = Rec."Current Date") AND (Rec."Saved Time" = Rec."Current Time"));
    END;

    PROCEDURE Invalidate@1000000001(pInteraction@1000000000 : Boolean) : Boolean;
    VAR
      eConfirmation@1000000001 : TextConst 'ENU=Do you want to invalidate all MenuSuite timestamps? It will allow full MenuSuite -> Permissions update.;ENG=Do you want to invalidate all MenuSuite timestamps? It will allow full MenuSuite -> Permissions update.';
    BEGIN
      ///<summary>Invalidating all timestamps</summary>
      ///<param name="pInteraction">Interaction flag</param>
      ///<returns>TRUE = Invalicated, FALSE = Cancelled</returns>

      pInteraction := ((pInteraction) AND (GUIALLOWED()));
      IF (pInteraction) THEN
        IF ( NOT (CONFIRM(eConfirmation, TRUE))) THEN
          EXIT(FALSE);

      Rec.RESET();
      Rec.DELETEALL(TRUE);

      EXIT(TRUE);
    END;

    BEGIN
    {
      ///<summary>Table keeping timestamps of all menu suite objects of the system</summary>
      ///<remarks>nForcement needs this information to inform users whether anything changed in menus since last update
      ///If yes - update of menusuite objects to permission gropu permissions is proposed</remarks>
      ///<change version="NF2016.17" initials="JH" date="2016-07-10">Created</change>
    }
    END.
  }
}

