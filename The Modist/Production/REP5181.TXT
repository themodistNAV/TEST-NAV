OBJECT Report 5181 Relocate Attachments
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Relocate Attachments;
               ENG=Relocate Attachments];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NOT CONFIRM(Text000,TRUE) THEN
                     CurrReport.QUIT;
                 END;

    OnPreReport=BEGIN
                  RMSetup.GET;
                  NoOfRecords := Attachment.COUNT;
                  Window.OPEN(Text001);
                END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 5264;    ;DataItem;                    ;
               DataItemTable=Table5062;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=VAR
                                  FromDiskFileName@1000 : Text[250];
                                  ServerFileName@1001 : Text;
                                BEGIN
                                  LineCount := LineCount + 1;
                                  Window.UPDATE(1,ROUND(LineCount / NoOfRecords * 10000,1));

                                  // Copy DiskFile to DiskFile
                                  IF ("Storage Type" = "Storage Type"::"Disk File") AND
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                                  THEN BEGIN
                                    RMSetup.TESTFIELD("Attachment Storage Location");
                                    IF "Storage Pointer" <> RMSetup."Attachment Storage Location" THEN BEGIN
                                      FromDiskFileName := ConstDiskFileName;
                                      "Storage Pointer" := RMSetup."Attachment Storage Location";
                                      MODIFY;
                                      FileManagement.CopyServerFile(FromDiskFileName,ConstDiskFileName,FALSE); // Copy from UNC location to another UNC location
                                      COMMIT;
                                      FileManagement.DeleteServerFile(FromDiskFileName);
                                    END;
                                    CurrReport.SKIP;
                                  END;

                                  // Export Embedded Blob to Diskfile
                                  IF ("Storage Type" = "Storage Type"::Embedded) AND
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                                  THEN BEGIN
                                    RMSetup.TESTFIELD("Attachment Storage Location");
                                    CALCFIELDS(Attachment);
                                    IF Attachment.HASVALUE THEN BEGIN
                                      "Storage Pointer" := RMSetup."Attachment Storage Location";
                                      ServerFileName := ConstDiskFileName;
                                      ExportAttachmentToServerFile(ServerFileName); // Export blob to UNC location
                                      "Storage Type" := "Storage Type"::"Disk File";
                                      CLEAR(Attachment);
                                      MODIFY;
                                      COMMIT;
                                      CurrReport.SKIP;
                                    END;
                                  END;

                                  // Import DiskFile to Embedded Blob
                                  IF ("Storage Type" = "Storage Type"::"Disk File") AND
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::Embedded)
                                  THEN BEGIN
                                    FromDiskFileName := ConstDiskFileName;
                                    ImportAttachmentFromServerFile(ConstDiskFileName,FALSE,TRUE); // Import file from UNC location
                                    COMMIT;
                                    FileManagement.DeleteServerFile(FromDiskFileName);
                                    CurrReport.SKIP;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      Text000@1001 : TextConst 'ENU=Do you want to relocate existing attachments?;ENG=Do you want to relocate existing attachments?';
      FileManagement@1003 : Codeunit 419;
      Window@1002 : Dialog;
      Text001@1004 : TextConst 'ENU=Relocating attachments @1@@@@@@@@@@@@@;ENG=Relocating attachments @1@@@@@@@@@@@@@';
      NoOfRecords@1005 : Integer;
      LineCount@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

