OBJECT Table 9062773 Extension
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Modified DateTime",CURRENTDATETIME);
               VALIDATE("Modified By User",USERID);
             END;

    OnDelete=VAR
               ExtensionVersion@9062230 : Record 9062774;
             BEGIN
               ExtensionVersion.SETRANGE("Extension ID","Extension ID");
               IF ExtensionVersion.FINDSET THEN
                 ExtensionVersion.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Extension;
               ENG=Extension];
    LookupPageID=Page9063155;
    DrillDownPageID=Page9063155;
  }
  FIELDS
  {
    { 10  ;   ;Extension ID        ;GUID          ;CaptionML=[ENU=Extension ID;
                                                              ENG=Extension ID] }
    { 20  ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 30  ;   ;Publisher           ;Text250       ;CaptionML=[ENU=Publisher;
                                                              ENG=Publisher] }
    { 40  ;   ;Published           ;Boolean       ;CaptionML=[ENU=Published;
                                                              ENG=Published];
                                                   Editable=No }
    { 50  ;   ;Installed           ;Boolean       ;CaptionML=[ENU=Installed;
                                                              ENG=Installed];
                                                   Editable=No }
    { 60  ;   ;Published DateTime  ;DateTime      ;CaptionML=[ENU=Published DateTime;
                                                              ENG=Published DateTime];
                                                   Editable=No }
    { 70  ;   ;Published By User   ;Code60        ;CaptionML=[ENU=Published By User;
                                                              ENG=Published By User];
                                                   Editable=No }
    { 80  ;   ;Installed DateTime  ;DateTime      ;CaptionML=[ENU=Installed DateTime;
                                                              ENG=Installed DateTime];
                                                   Editable=No }
    { 90  ;   ;Installed By User   ;Code60        ;CaptionML=[ENU=Installed By User;
                                                              ENG=Installed By User];
                                                   Editable=No }
    { 100 ;   ;Modified DateTime   ;DateTime      ;CaptionML=[ENU=Modified DateTime;
                                                              ENG=Modified DateTime];
                                                   Editable=No }
    { 110 ;   ;Modified By User    ;Code60        ;CaptionML=[ENU=Modified By User;
                                                              ENG=Modified By User];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Extension ID                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetPublishedExtensions@9062230();
    VAR
      NAVApp@9062230 : Record 2000000160;
      Extension@9062231 : Record 9062773;
      ExtensionVersion@9062232 : Record 9062774;
      NAVAppInstalledApp@9062233 : Record 2000000153;
    BEGIN
      IF NAVApp.FINDSET THEN
      REPEAT
        IF NOT Extension.GET(NAVApp.ID) THEN BEGIN
          Extension.VALIDATE("Extension ID",NAVApp.ID);
          Extension.VALIDATE(Name,NAVApp.Name);
          Extension.VALIDATE(Publisher,NAVApp.Publisher);
          Extension.VALIDATE(Published,TRUE);
          Extension.INSERT(TRUE);
        END;
        IF NOT ExtensionVersion.GET(NAVApp.ID,NAVApp."Version Major",NAVApp."Version Minor",NAVApp."Version Build",NAVApp."Version Revision") THEN BEGIN
          ExtensionVersion.VALIDATE("Extension ID",NAVApp.ID);
          ExtensionVersion.VALIDATE("Version Major",NAVApp."Version Major");
          ExtensionVersion.VALIDATE("Version Minor",NAVApp."Version Minor");
          ExtensionVersion.VALIDATE("Version Build",NAVApp."Version Build");
          ExtensionVersion.VALIDATE("Version Revision",NAVApp."Version Revision");
          ExtensionVersion.VALIDATE(Description,NAVApp.Name);
          ExtensionVersion.VALIDATE(Installed,FALSE);
          ExtensionVersion.INSERT(TRUE);
        END ELSE BEGIN
          ExtensionVersion.VALIDATE(Installed,FALSE);
          ExtensionVersion.MODIFY(TRUE);
        END;
      UNTIL NAVApp.NEXT = 0;

      IF NAVAppInstalledApp.FINDSET THEN
      REPEAT
        Extension.GET(NAVAppInstalledApp."App ID");
        Extension.VALIDATE(Installed,TRUE);
        Extension.MODIFY(TRUE);

        ExtensionVersion.GET(NAVAppInstalledApp."App ID",NAVAppInstalledApp."Version Major",NAVAppInstalledApp."Version Minor",NAVAppInstalledApp."Version Build",NAVAppInstalledApp."Version Revision");
        ExtensionVersion.VALIDATE(Installed,TRUE);
        ExtensionVersion.MODIFY(TRUE);
      UNTIL NAVAppInstalledApp.NEXT = 0;

      Extension.RESET;
      NAVApp.RESET;

      Extension.SETRANGE(Published,TRUE);
      IF Extension.FINDSET THEN
      REPEAT
        NAVApp.SETRANGE(ID,Extension."Extension ID");
        IF NOT NAVApp.FINDSET THEN BEGIN
          GET(Extension."Extension ID");
          VALIDATE(Published,FALSE);
          VALIDATE(Installed,FALSE);
          MODIFY(TRUE);
        END;
      UNTIL Extension.NEXT = 0;
    END;

    PROCEDURE InstallLatestVersion@9062231();
    VAR
      PSExtensionQueries@9062230 : Codeunit 9062638;
      ExtensionVersion@9062231 : Record 9062774;
    BEGIN
      ExtensionVersion.SETRANGE("Extension ID","Extension ID");
      IF ExtensionVersion.FINDLAST THEN
        PSExtensionQueries.InstallExtension(ExtensionVersion);
    END;

    PROCEDURE Uninstall@9062232();
    VAR
      PSExtensionQueries@9062230 : Codeunit 9062638;
      ExtensionVersion@9062231 : Record 9062774;
    BEGIN
      ExtensionVersion.SETRANGE("Extension ID","Extension ID");
      ExtensionVersion.SETRANGE(Installed,TRUE);
      IF ExtensionVersion.FINDFIRST THEN
        PSExtensionQueries.UninstallExtension(ExtensionVersion);
    END;

    BEGIN
    {
      //DOC NA2016.10 AP 29/10/2015 - Created
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

