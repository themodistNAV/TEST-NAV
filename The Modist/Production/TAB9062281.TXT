OBJECT Table 9062281 E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               EMailLine.RESET();
               EMailLine.SETRANGE("E-Mail Entry No.", Rec."Entry No.");
               EMailLine.DELETEALL(TRUE);

               EMailAttachment.RESET();
               EMailAttachment.SETRANGE("E-Mail Entry No.", Rec."Entry No.");
               EMailAttachment.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=E-Mail;
               ENG=E-Mail];
    LookupPageID=Page9062337;
    DrillDownPageID=Page9062337;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ENG=Entry No.] }
    { 2   ;   ;ID                  ;Text250       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 3   ;   ;Subject             ;Text250       ;CaptionML=[ENU=Subject;
                                                              ENG=Subject] }
    { 4   ;   ;Sender E-Mail       ;Text250       ;CaptionML=[ENU=Sender E-Mail;
                                                              ENG=Sender E-Mail] }
    { 5   ;   ;Sender Name         ;Text250       ;CaptionML=[ENU=Sender Name;
                                                              ENG=Sender Name] }
    { 6   ;   ;BCC                 ;Text250       ;CaptionML=[ENU=BCC;
                                                              ENG=BCC] }
    { 7   ;   ;CC                  ;Text250       ;CaptionML=[ENU=CC;
                                                              ENG=CC] }
    { 8   ;   ;Creation Date Time  ;DateTime      ;CaptionML=[ENU=Creation Date Time;
                                                              ENG=Creation Date Time] }
    { 9   ;   ;Received Date TIme  ;DateTime      ;CaptionML=[ENU=Received Date TIme;
                                                              ENG=Received Date TIme] }
    { 10  ;   ;Sent Date Time      ;DateTime      ;CaptionML=[ENU=Sent Date Time;
                                                              ENG=Sent Date Time] }
    { 11  ;   ;Size                ;Integer       ;CaptionML=[ENU=Size;
                                                              ENG=Size] }
    { 12  ;   ;Body                ;BLOB          ;CaptionML=[ENU=Body;
                                                              ENG=Body] }
    { 13  ;   ;Recipients          ;Text250       ;CaptionML=[ENU=Recipients;
                                                              ENG=Recipients] }
    { 14  ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                Contact@1000000000 : Record 5050;
                                                              BEGIN
                                                                IF Contact.GET("Contact No.") THEN
                                                                  VALIDATE(Recipients,Contact."E-Mail");
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              ENG=Contact No.] }
    { 15  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              ENG=Contact Name];
                                                   Editable=No }
    { 16  ;   ;Create CRM Log      ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Create CRM Log;
                                                              ENG=Create CRM Log] }
    { 17  ;   ;CRM Log Entry No.   ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=CRM Log Entry No.;
                                                              ENG=CRM Log Entry No.] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Sender E-Mail,Sent Date Time             }
    {    ;Sender Name,Sent Date Time               }
    {    ;Sender E-Mail,Received Date TIme         }
    {    ;Sender Name,Received Date TIme           }
    {    ;Subject,Sent Date Time                   }
    {    ;Subject,Received Date TIme               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EMailLine@1000000001 : Record 9062282;
      EMailAttachment@1000000002 : Record 9062283;
      Blob@9062229 : Record 99008535;
      BlobToolkit@9062230 : Codeunit 9062242;
      FileSystem@1000000000 : Codeunit 9062264;

    PROCEDURE SaveBodyAs@1000000000();
    VAR
      xNoContent@1000000000 : TextConst 'ENU="E-mail with ""%1"" = %2 has no content saved.";ENG="E-mail with ""%1"" = %2 has no content saved."';
      lBlob@1000000001 : Record 99008535;
      lFileName@1000000002 : Text[1024];
      xFileName@1000000003 : TextConst 'ENU=E-mail %1.eml;ENG=E-mail %1.eml';
    BEGIN
      //DOC NA2015.1 - Saving e-mail content as

      Rec.CALCFIELDS(Body);
      IF ( NOT (Rec.Body.HASVALUE)) THEN
        IF (GUIALLOWED()) THEN
        BEGIN
          MESSAGE(xNoContent, Rec.FIELDCAPTION("Entry No."), Rec."Entry No.");
          EXIT;
        END
        ELSE
          ERROR(xNoContent, Rec.FIELDCAPTION("Entry No."), Rec."Entry No.");

      lBlob.Blob := Rec.Body;
      lFileName := STRSUBSTNO(xFileName, Rec."Entry No.");
      FileSystem.ExportBlobToFile(FileSystem.Client, lBlob, lFileName, '', '');
    END;

    PROCEDURE ShowInNotepad@1000000001();
    VAR
      lEndOfLine@1000000001 : Text[2];
      lText@1000000000 : BigText;
      lEMailLine@1000000002 : Record 9062282;
      lBlob@1000000003 : Record 99008535;
      lOutStream@1000000004 : OutStream;
      lFileName@1000000008 : Text[256];
      xFileName@1000000006 : TextConst 'ENU=E-Mail %1.txt;ENG=E-Mail %1.txt';
    BEGIN
      //DOC NA2015.1 - Showing the e-mail in notepad

      lEndOfLine := PADSTR('', 2, ' ');
      lEndOfLine[1] := 10;
      lEndOfLine[2] := 13;

      lEMailLine.RESET();
      lEMailLine.SETRANGE("E-Mail Entry No.", Rec."Entry No.");
      IF (lEMailLine.FINDSET()) THEN
      REPEAT
        lText.ADDTEXT(lEMailLine.GetLine());
        IF (lEMailLine."End of Line") THEN
          lText.ADDTEXT(lEndOfLine);
      UNTIL (lEMailLine.NEXT() = 0);

      lBlob.Blob.CREATEOUTSTREAM(lOutStream);
      lText.WRITE(lOutStream);

      lFileName := STRSUBSTNO(xFileName, Rec."Entry No.");
      FileSystem.ExportBlobToFile(FileSystem.Client, lBlob, lFileName, '', '');
    END;

    PROCEDURE GetPageCaption@9062229() : Text;
    VAR
      lPageCaption@9062229 : Text;
      eEmailId@9062230 : TextConst 'ENU=E-Mail %1;ENG=E-Mail %1';
    BEGIN
      //DOC NA2016.10 - Returning page caption
      //  <- The caption

      IF ((Rec."Sender Name" <> '') AND (Rec."Sender E-Mail" <> '')) THEN
        lPageCaption := STRSUBSTNO('%1 (%2)', Rec."Sender Name", Rec."Sender E-Mail")
      ELSE IF (Rec."Sender Name" <> '') THEN
        lPageCaption := Rec."Sender Name"
      ELSE
        lPageCaption := Rec."Sender E-Mail";

      IF (Rec.Subject <> '') THEN
      BEGIN
        IF (lPageCaption <> '') THEN
          lPageCaption := lPageCaption + ': ';
        lPageCaption := lPageCaption + Rec.Subject;
      END;

      IF (lPageCaption = '') THEN
        lPageCaption := STRSUBSTNO(eEmailId, Rec.ID);

      EXIT(lPageCaption);
    END;

    PROCEDURE GetBodyText@9062231(pCalcFields@9062229 : Boolean) : Text;
    BEGIN
      IF (pCalcFields) THEN
        Rec.CALCFIELDS(Body);
      Blob.Blob := Rec.Body;
      EXIT(BlobToolkit.BlobToText(Blob));
    END;

    PROCEDURE SetBodyText@9062230(pBodyText@9062229 : Text);
    BEGIN
      CLEAR(Blob);
      BlobToolkit.TextToBlob(pBodyText, Blob);
      Rec.VALIDATE(Body, Blob.Blob);
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 02/09/2012 - Created
      //DOC NA2016.10 JH 27/08/2015 - Upgraded to 2016
      //DOC NA2016.12 JH 18/12/2015 - Deletion of lines / attachments added
      //DOC NA2016.14 PC 03/02/2016 - CRM fields added
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

