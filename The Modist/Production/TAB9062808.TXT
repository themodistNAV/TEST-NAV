OBJECT Table 9062808 Enhanced View Dynamic Style
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF (Rec.ID = 0) THEN
                 Rec.VALIDATE(ID, FindNewId());
               IF (Rec.Sequence = 0) THEN
                 Rec.VALIDATE(Sequence, FindNewIndex());

               EnhViewMgt.InvalidateStyleCache("View Code", Type);
             END;

    OnDelete=BEGIN
               EnhViewMgt.InvalidateStyleCache("View Code", Type);
             END;

    CaptionML=[ENU=Enhanced View Dynamic Style;
               ENG=Enhanced View Dynamic Style];
  }
  FIELDS
  {
    { 1   ;   ;View Code           ;Code20        ;CaptionML=[ENU=View Code;
                                                              ENG=View Code] }
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ENG=Type];
                                                   OptionCaptionML=[ENU=Group Header,Cell,Grid Column;
                                                                    ENG=Group Header,Cell,Grid Column];
                                                   OptionString=Group Header,Cell,Grid Column }
    { 3   ;   ;Target Type         ;Integer       ;CaptionML=[ENU=Target Type;
                                                              ENG=Target Type] }
    { 4   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Sequence            ;Integer       ;OnValidate=BEGIN
                                                                IF (Sequence <> xRec.Sequence) THEN
                                                                  EnhViewMgt.InvalidateStyleCache("View Code", Type);
                                                              END;

                                                   CaptionML=[ENU=Sequence;
                                                              ENG=Sequence] }
    { 11  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 20  ;   ;Filter              ;Text250       ;OnValidate=BEGIN
                                                                IF (Filter <> xRec.Filter) THEN
                                                                  EnhViewMgt.InvalidateStyleCache("View Code", Type);
                                                              END;

                                                   OnLookup=BEGIN
                                                              FilterLookup();
                                                            END;

                                                   CaptionML=[ENU=Filter;
                                                              ENG=Filter] }
    { 21  ;   ;Colour Code         ;Code20        ;TableRelation="System Colour";
                                                   OnValidate=BEGIN
                                                                UpdateColourBitmap();
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ColorLookup("Colour Code")) THEN
                                                                VALIDATE("Colour Code");
                                                            END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 22  ;   ;Background Colour Code;Code20      ;TableRelation="System Colour";
                                                   OnValidate=BEGIN
                                                                UpdateBackgroundColourBitmap();
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ColorLookup("Background Colour Code")) THEN
                                                                VALIDATE("Background Colour Code");
                                                            END;

                                                   CaptionML=[ENU=Colour Code;
                                                              ENG=Colour Code] }
    { 40  ;   ;Css Class Name      ;Text50        ;CaptionML=[ENU=Css Class Name;
                                                              ENG=Css Class Name] }
    { 100 ;   ;Colour Bitmap       ;BLOB          ;CaptionML=[ENU=Colour Bitmap;
                                                              ENG=Colour Bitmap];
                                                   SubType=Bitmap }
    { 101 ;   ;Background Colour Bitmap;BLOB      ;CaptionML=[ENU=Background Colour Bitmap;
                                                              ENG=Background Colour Bitmap];
                                                   SubType=Bitmap }
  }
  KEYS
  {
    {    ;View Code,Type,Target Type,ID           ;Clustered=Yes }
    {    ;View Code,Type,Target Type,Sequence      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EnhViewMgt@1000000000 : Codeunit 9062679;

    PROCEDURE FindNewId@9062234() : Integer;
    VAR
      Style@9062229 : Record 9062808;
    BEGIN
      Style.RESET();
      Style.SETRANGE("View Code", "View Code");
      Style.SETRANGE(Type, Type);
      Style.SETRANGE("Target Type", "Target Type");
      IF (Style.FINDLAST()) THEN
        EXIT(Style.ID + 1);
      EXIT(1);
    END;

    PROCEDURE FindNewIndex@9062233() : Integer;
    VAR
      Style@1000000000 : Record 9062808;
    BEGIN
      Style.RESET();
      Style.SETCURRENTKEY("View Code", Type, "Target Type", Sequence);
      Style.SETRANGE("View Code", "View Code");
      Style.SETRANGE(Type, Type);
      Style.SETRANGE("Target Type", "Target Type");
      IF (Style.FINDLAST()) THEN
        EXIT(Style.ID + 1);
      EXIT(1);
    END;

    PROCEDURE InitialiseNewRecord@9062229();
    BEGIN
      Rec.VALIDATE(ID, FindNewId());
      Rec.VALIDATE(Sequence, FindNewIndex());
    END;

    PROCEDURE UpdateColourBitmap@9062230();
    VAR
      SystemColour@1000000000 : Record 9062336;
    BEGIN
      IF (SystemColour.GET(Rec."Colour Code")) THEN
      BEGIN
        SystemColour.CALCFIELDS(Bitmap);
        Rec."Colour Bitmap" := SystemColour.Bitmap;
      END
      ELSE
        CLEAR(Rec."Colour Bitmap");
    END;

    PROCEDURE UpdateBackgroundColourBitmap@1000000001();
    VAR
      SystemColour@1000000000 : Record 9062336;
    BEGIN
      IF (SystemColour.GET(Rec."Background Colour Code")) THEN
      BEGIN
        SystemColour.CALCFIELDS(Bitmap);
        Rec."Background Colour Bitmap" := SystemColour.Bitmap;
      END
      ELSE
        CLEAR(Rec."Background Colour Bitmap");
    END;

    PROCEDURE GetHtmlStyle@1000000000() : Text;
    VAR
      SystemColour@1000000002 : Record 9062336;
      HtmlColor@1000000001 : Text;
      StyleText@1000000000 : Text;
    BEGIN
      StyleText := '';

      IF ("Background Colour Code" <> '') THEN BEGIN
        HtmlColor := SystemColour.HtmlColourName("Background Colour Code");
        IF (HtmlColor <> '') THEN
          StyleText := StyleText + 'background-color:' + HtmlColor + ';';
      END;

      IF ("Colour Code" <> '') THEN BEGIN
        HtmlColor := SystemColour.HtmlColourName("Colour Code");
        IF (HtmlColor <> '') THEN
          StyleText := StyleText + 'color:' + HtmlColor + ';';
      END;

      EXIT(StyleText);
    END;

    LOCAL PROCEDURE ColorLookup@1000000002(VAR ColorCode@1000000000 : Code[20]) : Boolean;
    VAR
      SystemColour@1000000001 : Record 9062336;
      Selected@1000000002 : Boolean;
    BEGIN
      SystemColour.RESET();
      IF (SystemColour.GET(ColorCode)) THEN;
      Selected := (PAGE.RUNMODAL(0, SystemColour) = ACTION::LookupOK);
      IF (Selected) THEN
        ColorCode := SystemColour.Code;
      EXIT(Selected);
    END;

    LOCAL PROCEDURE FilterLookup@1000000003();
    VAR
      EnhView@1000000000 : Record 9062804;
      FieldLookup@1000000001 : Codeunit 9062237;
    BEGIN
      IF ("View Code" <> '') THEN BEGIN
        EnhView.GET("View Code");
        IF (EnhView."Cell Style Field No." <> 0) THEN BEGIN
          IF (FieldLookup.Lookup(EnhView."Source Table No.", EnhView."Cell Style Field No.", Rec.Filter)) THEN
            VALIDATE(Filter);
        END;
      END;
    END;

    BEGIN
    {
      //DOC NA2016.12 AZ 21/01/2016 - Created
      //DOC NA2016.15 AZ 12/04/2016 - Midifications and bugfixes
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

