OBJECT Page 9801 User Subform
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=User Permission Sets;
               ENG=User Permission Sets];
    SourceTable=Table2000000053;
    DelayedInsert=Yes;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       IF User."User Name" <> '' THEN
                         CurrPage.CAPTION := User."User Name";
                       PermissionEditable := ISNULLGUID("App ID");
                     END;

    OnNewRecord=BEGIN
                  IF User.GET("User Security ID") THEN;
                  PermissionEditable := ISNULLGUID("App ID");
                  CALCFIELDS("App Name","Role Name");
                END;

    OnInsertRecord=BEGIN
                     User.TESTFIELD("User Name");
                     PermissionEditable := ISNULLGUID("App ID");
                     CALCFIELDS("App Name","Role Name");
                   END;

    OnModifyRecord=BEGIN
                     PermissionEditable := ISNULLGUID("App ID");
                     CALCFIELDS("App Name","Role Name");
                   END;

    OnDeleteRecord=VAR
                     UserGroupAccessControl@1000 : Record 9002;
                   BEGIN
                     UserGroupAccessControl.SETFILTER("User Group Code",'<>%1','');
                     UserGroupAccessControl.SETRANGE("User Security ID","User Security ID");
                     UserGroupAccessControl.SETRANGE("Role ID","Role ID");
                     UserGroupAccessControl.SETRANGE("Company Name","Company Name");
                     IF UserGroupAccessControl.FINDFIRST THEN
                       ERROR(InUserGroupErr,UserGroupAccessControl."User Group Code");
                     PermissionEditable := ISNULLGUID("App ID");
                   END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=PermissionActions;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=Permissions;
                      ShortCutKey=Shift+Ctrl+p;
                      CaptionML=[ENU=Permissions;
                                 ENG=Permissions];
                      RunObject=Page 9803;
                      RunPageLink=Role ID=FIELD(Role ID);
                      Enabled=PermissionEditable;
                      Image=Permission }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=[ENU=User Permissions;
                           ENG=User Permissions];
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=<Permission Set>;
                CaptionML=[ENU=Permission Set;
                           ENG=Permission Set];
                SourceExpr="Role ID";
                OnValidate=VAR
                             AggregatePermissionSet@1000 : Record 2000000167;
                           BEGIN
                             // If the user used the lookup, skip validation
                             IF SkipValidation THEN BEGIN
                               SkipValidation := FALSE;
                               EXIT;
                             END;

                             // Get the Scope and App ID for a matching Role ID
                             AggregatePermissionSet.SETRANGE("Role ID","Role ID");
                             AggregatePermissionSet.FINDFIRST;

                             IF AggregatePermissionSet.COUNT > 1 THEN
                               ERROR(MultipleRoleIDErr,"Role ID");

                             Scope := AggregatePermissionSet.Scope;
                             "App ID" := AggregatePermissionSet."App ID";

                             CALCFIELDS("App Name","Role Name");

                             SkipValidation := FALSE; // re-enable validation
                           END;

                OnLookup=VAR
                           PermissionSetLookupRecord@1002 : Record 2000000167;
                           PermissionSetLookup@1001 : Page 9841;
                         BEGIN
                           PermissionSetLookup.LOOKUPMODE := TRUE;
                           IF PermissionSetLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             PermissionSetLookup.GETRECORD(PermissionSetLookupRecord);
                             Scope := PermissionSetLookupRecord.Scope;
                             "App ID" := PermissionSetLookupRecord."App ID";
                             "Role ID" := PermissionSetLookupRecord."Role ID";
                             CALCFIELDS("App Name","Role Name");
                             SkipValidation := TRUE;
                           END;
                         END;
                          }

    { 4   ;2   ;Field     ;
                Name=<Description>;
                DrillDown=No;
                CaptionML=[ENU=Description;
                           ENG=Description];
                SourceExpr="Role Name";
                Editable=FALSE }

    { 5   ;2   ;Field     ;
                Name=<Company>;
                CaptionML=[ENU=Company;
                           ENG=Company];
                SourceExpr="Company Name" }

    { 6   ;2   ;Field     ;
                DrillDown=No;
                CaptionML=[ENU=NAV App Name;
                           ENG=NAV App Name];
                SourceExpr="App Name";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      User@1000 : Record 2000000120;
      InUserGroupErr@1001 : TextConst '@@@="%1=a user group code, e.g. ADMIN or SALESDEPT";ENU=You cannot remove this permission set because it is included in user group %1.;ENG=You cannot remove this permission set because it is included in user group %1.';
      PermissionEditable@1002 : Boolean;
      MultipleRoleIDErr@1003 : TextConst '@@@=%1 will be replaced with a Role ID code value from the Permission Set table;ENU=The permission set %1 is defined multiple times in this context. Use the lookup button to select the relevant permission set.;ENG=The permission set %1 is defined multiple times in this context. Use the lookup button to select the relevant permission set.';
      SkipValidation@1004 : Boolean;

    BEGIN
    END.
  }
}

