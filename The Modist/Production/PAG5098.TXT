OBJECT Page 5098 To-do Card
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=To-do Card;
               ENG=To-do Card];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table5080;
    PageType=Card;
    OnInit=BEGIN
             "Calc. Due Date FromEnable" := TRUE;
             "Recurring Date IntervalEnable" := TRUE;
             "Completed ByEnable" := TRUE;
             "Attendees Accepted No.Enable" := TRUE;
             "No. of AttendeesEnable" := TRUE;
             MakePhoneCallEnable := TRUE;
             AllDayEventEnable := TRUE;
             LocationEnable := TRUE;
             DurationEnable := TRUE;
             EndingTimeEnable := TRUE;
             StartTimeEnable := TRUE;
             "Completed ByEditable" := TRUE;
             "Calc. Due Date FromEditable" := TRUE;
             RecurringDateIntervalEditable := TRUE;
             "Contact No.Editable" := TRUE;
           END;

    OnOpenPage=BEGIN
                 IF GETFILTER("No.") = '' THEN
                   CurrPage.EDITABLE := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       "Contact Company NameHideValue" := FALSE;
                       "Contact NameHideValue" := FALSE;
                       SwitchCardControls;
                       IF "No." <> "Organizer To-do No." THEN
                         CurrPage.EDITABLE := FALSE
                       ELSE
                         CurrPage.EDITABLE := TRUE;
                       SetRecurringEditable;
                       EnableFields;
                       ContactNoOnFormat(FORMAT("Contact No."));
                       ContactNameOnFormat;
                       ContactCompanyNameOnFormat;
                     END;

    OnModifyRecord=BEGIN
                     IF ("Team Code" = '') AND ("Salesperson Code" = '') THEN
                       ERROR(
                         Text000,TABLECAPTION,FIELDCAPTION("Salesperson Code"),FIELDCAPTION("Team Code"));

                     IF (Type = Type::Meeting) AND (NOT "All Day Event") THEN BEGIN
                       IF "Start Time" = 0T THEN
                         ERROR(Text002,TABLECAPTION,Type,FIELDCAPTION("Start Time"));
                       IF Duration = 0 THEN
                         ERROR(Text002,TABLECAPTION,Type,FIELDCAPTION(Duration));
                     END;
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 29      ;1   ;ActionGroup;
                      CaptionML=[ENU=To-&do;
                                 ENG=To-&do];
                      Image=Task }
      { 33      ;2   ;Action    ;
                      CaptionML=[ENU=Co&mment;
                                 ENG=Co&mment];
                      RunObject=Page 5072;
                      RunPageLink=Table Name=CONST(To-do),
                                  No.=FIELD(Organizer To-do No.),
                                  Sub No.=CONST(0);
                      Image=ViewComments }
      { 34      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Interaction Log E&ntries;
                                 ENG=Interaction Log E&ntries];
                      RunObject=Page 5076;
                      RunPageView=SORTING(To-do No.);
                      RunPageLink=To-do No.=FIELD(Organizer To-do No.);
                      Image=InteractionLog }
      { 69      ;2   ;Action    ;
                      CaptionML=[ENU=Postponed &Interactions;
                                 ENG=Postponed &Interactions];
                      RunObject=Page 5082;
                      RunPageView=SORTING(To-do No.);
                      RunPageLink=To-do No.=FIELD(Organizer To-do No.);
                      Image=PostponedInteractions }
      { 70      ;2   ;Action    ;
                      CaptionML=[ENU=A&ttendee Scheduling;
                                 ENG=A&ttendee Scheduling];
                      Image=ProfileCalender;
                      OnAction=BEGIN
                                 IF Type <> Type::Meeting THEN
                                   ERROR(Text003,FORMAT(Type));

                                 PAGE.RUNMODAL(PAGE::"Attendee Scheduling",Rec);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 30      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ENG=F&unctions];
                      Image=Action }
      { 35      ;2   ;Action    ;
                      CaptionML=[ENU=Assign Activities;
                                 ENG=Assign Activities];
                      Image=Allocate;
                      OnAction=VAR
                                 TempToDo@1001 : TEMPORARY Record 5080;
                               BEGIN
                                 TempToDo.AssignActivityFromToDo(Rec)
                               END;
                                }
      { 36      ;2   ;Action    ;
                      CaptionML=[ENU=Make &Phone Call;
                                 ENG=Make &Phone Call];
                      Image=Calls;
                      OnAction=VAR
                                 SegmentLine@1001 : TEMPORARY Record 5077;
                               BEGIN
                                 IF "Contact No." = '' THEN BEGIN
                                   IF (Type = Type::Meeting) AND ("Team Code" = '') THEN
                                     ERROR(Text005);
                                   ERROR(Text006);
                                 END;
                                 SegmentLine."To-do No." := "No.";
                                 SegmentLine."Contact No." := "Contact No.";
                                 SegmentLine."Contact Company No." := "Contact Company No.";
                                 SegmentLine."Campaign No." := "Campaign No.";
                                 SegmentLine.CreateCall;
                               END;
                                }
      { 31      ;1   ;Action    ;
                      CaptionML=[ENU=&Create To-do;
                                 ENG=&Create To-do];
                      Promoted=Yes;
                      Image=NewToDo;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempToDo@1001 : TEMPORARY Record 5080;
                               BEGIN
                                 TempToDo.CreateToDoFromToDo(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 2   ;2   ;Field     ;
                SourceExpr="No." }

    { 20  ;2   ;Field     ;
                SourceExpr=Description }

    { 67  ;2   ;Field     ;
                SourceExpr=Location;
                Enabled=LocationEnable }

    { 6   ;2   ;Field     ;
                SourceExpr="Salesperson Code";
                OnValidate=BEGIN
                             SalespersonCodeOnAfterValidate;
                           END;
                            }

    { 71  ;2   ;Field     ;
                SourceExpr="No. of Attendees";
                Enabled="No. of AttendeesEnable";
                OnDrillDown=BEGIN
                              MODIFY;
                              COMMIT;
                              PAGE.RUNMODAL(PAGE::"Attendee Scheduling",Rec);
                              GET("No.");
                              CurrPage.UPDATE;
                            END;
                             }

    { 73  ;2   ;Field     ;
                SourceExpr="Attendees Accepted No.";
                Enabled="Attendees Accepted No.Enable";
                OnDrillDown=BEGIN
                              MODIFY;
                              COMMIT;
                              PAGE.RUNMODAL(PAGE::"Attendee Scheduling",Rec);
                              GET("No.");
                              CurrPage.UPDATE;
                            END;
                             }

    { 10  ;2   ;Field     ;
                SourceExpr="Contact No.";
                Editable="Contact No.Editable";
                OnValidate=BEGIN
                             ContactNoOnAfterValidate;
                           END;

                OnLookup=VAR
                           Todo@1005 : Record 5080;
                           AttendeesScheduling@1004 : Page 5199;
                           Cont@1003 : Record 5050;
                         BEGIN
                           IF Type = Type::Meeting THEN BEGIN
                             Todo.SETRANGE("No.","No.");
                             AttendeesScheduling.SETTABLEVIEW(Todo);
                             AttendeesScheduling.RUNMODAL;
                           END ELSE BEGIN
                             IF Cont.GET("Contact No.") THEN;
                             IF PAGE.RUNMODAL(0,Cont) = ACTION::LookupOK THEN BEGIN
                               VALIDATE("Contact No.",Cont."No.");
                               CurrPage.UPDATE;
                             END;
                           END;
                         END;
                          }

    { 47  ;2   ;Field     ;
                SourceExpr="Contact Name";
                Editable=FALSE;
                HideValue="Contact NameHideValue" }

    { 49  ;2   ;Field     ;
                SourceExpr="Contact Company Name";
                Editable=FALSE;
                HideValue="Contact Company NameHideValue" }

    { 4   ;2   ;Field     ;
                SourceExpr="Team Code";
                OnValidate=BEGIN
                             TeamCodeOnAfterValidate;
                           END;
                            }

    { 76  ;2   ;Field     ;
                SourceExpr="Completed By";
                Enabled="Completed ByEnable";
                Editable="Completed ByEditable";
                OnValidate=BEGIN
                             SwitchCardControls
                           END;
                            }

    { 16  ;2   ;Field     ;
                SourceExpr=Status }

    { 18  ;2   ;Field     ;
                SourceExpr=Priority }

    { 12  ;2   ;Field     ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             TypeOnAfterValidate;
                           END;
                            }

    { 64  ;2   ;Field     ;
                Name=AllDayEvent;
                CaptionML=[ENU=All Day Event;
                           ENG=All Day Event];
                SourceExpr="All Day Event";
                Enabled=AllDayEventEnable;
                OnValidate=BEGIN
                             AllDayEventOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                Name=Date;
                SourceExpr=Date }

    { 56  ;2   ;Field     ;
                Name=StartTime;
                SourceExpr="Start Time";
                Enabled=StartTimeEnable }

    { 58  ;2   ;Field     ;
                Name=Duration;
                BlankZero=Yes;
                SourceExpr=Duration;
                Enabled=DurationEnable }

    { 63  ;2   ;Field     ;
                Name=EndingDate;
                CaptionML=[ENU=Ending Date;
                           ENG=Ending Date];
                SourceExpr="Ending Date" }

    { 61  ;2   ;Field     ;
                Name=EndingTime;
                CaptionML=[ENU=Ending Time;
                           ENG=Ending Time];
                SourceExpr="Ending Time";
                Enabled=EndingTimeEnable }

    { 26  ;2   ;Field     ;
                SourceExpr=Canceled;
                OnValidate=BEGIN
                             SwitchCardControls
                           END;
                            }

    { 22  ;2   ;Field     ;
                SourceExpr=Closed;
                OnValidate=BEGIN
                             SwitchCardControls
                           END;
                            }

    { 24  ;2   ;Field     ;
                SourceExpr="Date Closed" }

    { 1905354401;1;Group  ;
                CaptionML=[ENU=Related Activities;
                           ENG=Related Activities] }

    { 8   ;2   ;Field     ;
                SourceExpr="Campaign No.";
                OnValidate=BEGIN
                             CampaignNoOnAfterValidate;
                           END;
                            }

    { 51  ;2   ;Field     ;
                SourceExpr="Campaign Description";
                Editable=FALSE }

    { 45  ;2   ;Field     ;
                SourceExpr="Opportunity No.";
                OnValidate=BEGIN
                             OpportunityNoOnAfterValidate;
                           END;
                            }

    { 53  ;2   ;Field     ;
                SourceExpr="Opportunity Description";
                Editable=FALSE }

    { 1904441601;1;Group  ;
                CaptionML=[ENU=Recurring;
                           ENG=Recurring] }

    { 39  ;2   ;Field     ;
                SourceExpr=Recurring;
                OnValidate=BEGIN
                             RecurringOnPush;
                           END;
                            }

    { 41  ;2   ;Field     ;
                SourceExpr="Recurring Date Interval";
                Enabled="Recurring Date IntervalEnable";
                Editable=RecurringDateIntervalEditable }

    { 43  ;2   ;Field     ;
                SourceExpr="Calc. Due Date From";
                Enabled="Calc. Due Date FromEnable";
                Editable="Calc. Due Date FromEditable" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=TRUE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The %1 will always have either the %2 or %3 assigned.;ENG=The %1 will always have either the %2 or %3 assigned.';
      Text002@1002 : TextConst 'ENU=The %1 of the %2 type must always have the %3 assigned.;ENG=The %1 of the %2 type must always have the %3 assigned.';
      Text003@1005 : TextConst 'ENU=You cannot select attendees for a to-do of the ''%1'' type.;ENG=You cannot select attendees for a to-do of the ''%1'' type.';
      Text005@1007 : TextConst 'ENU=The Make Phone Call function for this to-do is available only in the Attendee Scheduling window.;ENG=The Make Phone Call function for this to-do is available only in the Attendee Scheduling window.';
      Text006@1008 : TextConst 'ENU=You must assign a contact to this to-do before you can use the Make Phone Call function.;ENG=You must assign a contact to this to-do before you can use the Make Phone Call function.';
      Text007@1009 : TextConst 'ENU=(Multiple);ENG=(Multiple)';
      "Contact NameHideValue"@19061599 : Boolean INDATASET;
      "Contact Company NameHideValue"@19040407 : Boolean INDATASET;
      "Contact No.Editable"@19030566 : Boolean INDATASET;
      RecurringDateIntervalEditable@19051941 : Boolean INDATASET;
      "Calc. Due Date FromEditable"@19052355 : Boolean INDATASET;
      "Completed ByEditable"@19071171 : Boolean INDATASET;
      StartTimeEnable@19008764 : Boolean INDATASET;
      EndingTimeEnable@19050392 : Boolean INDATASET;
      DurationEnable@19035217 : Boolean INDATASET;
      LocationEnable@19013618 : Boolean INDATASET;
      AllDayEventEnable@19049519 : Boolean INDATASET;
      MakePhoneCallEnable@19032886 : Boolean INDATASET;
      "No. of AttendeesEnable"@19000329 : Boolean INDATASET;
      "Attendees Accepted No.Enable"@19007606 : Boolean INDATASET;
      "Completed ByEnable"@19024761 : Boolean INDATASET;
      "Recurring Date IntervalEnable"@19019953 : Boolean INDATASET;
      "Calc. Due Date FromEnable"@19036769 : Boolean INDATASET;

    PROCEDURE SetRecurringEditable@1();
    BEGIN
      RecurringDateIntervalEditable := Recurring;
      "Calc. Due Date FromEditable" := Recurring;
    END;

    LOCAL PROCEDURE EnableFields@2();
    BEGIN
      "Recurring Date IntervalEnable" := Recurring;
      "Calc. Due Date FromEnable" := Recurring;

      IF NOT Recurring THEN BEGIN
        EVALUATE("Recurring Date Interval",'');
        CLEAR("Calc. Due Date From");
      END;

      IF Type = Type::Meeting THEN BEGIN
        StartTimeEnable := NOT "All Day Event";
        EndingTimeEnable := NOT "All Day Event";
        DurationEnable := NOT "All Day Event";
        LocationEnable := TRUE;
        AllDayEventEnable := TRUE;
      END ELSE BEGIN
        StartTimeEnable := FALSE;
        EndingTimeEnable := FALSE;
        LocationEnable := FALSE;
        DurationEnable := FALSE;
        AllDayEventEnable := FALSE;
      END;

      GetEndDateTime;
    END;

    LOCAL PROCEDURE SwitchCardControls@3();
    BEGIN
      IF Type = Type::Meeting THEN BEGIN
        "Contact No.Editable" := FALSE;
        MakePhoneCallEnable := FALSE;

        "No. of AttendeesEnable" := TRUE;
        "Attendees Accepted No.Enable" := TRUE;
      END ELSE BEGIN
        "Contact No.Editable" := TRUE;
        MakePhoneCallEnable := TRUE;

        "No. of AttendeesEnable" := FALSE;
        "Attendees Accepted No.Enable" := FALSE;
      END;
      IF "Team Code" = '' THEN
        "Completed ByEnable" := FALSE
      ELSE BEGIN
        "Completed ByEnable" := TRUE;
        "Completed ByEditable" := NOT Closed
      END
    END;

    LOCAL PROCEDURE TeamCodeOnAfterValidate@19070305();
    BEGIN
      SwitchCardControls;
      CALCFIELDS(
        "No. of Attendees",
        "Attendees Accepted No.",
        "Contact Name",
        "Contact Company Name",
        "Campaign Description",
        "Opportunity Description")
    END;

    LOCAL PROCEDURE ContactNoOnAfterValidate@19009577();
    BEGIN
      CALCFIELDS("Contact Name","Contact Company Name");
    END;

    LOCAL PROCEDURE TypeOnAfterValidate@19069045();
    BEGIN
      EnableFields;
    END;

    LOCAL PROCEDURE AllDayEventOnAfterValidate@19020274();
    BEGIN
      EnableFields;
    END;

    LOCAL PROCEDURE SalespersonCodeOnAfterValidate@19011896();
    BEGIN
      SwitchCardControls;
      CALCFIELDS(
        "No. of Attendees",
        "Attendees Accepted No.",
        "Contact Name",
        "Contact Company Name",
        "Campaign Description",
        "Opportunity Description");
    END;

    LOCAL PROCEDURE CampaignNoOnAfterValidate@19036822();
    BEGIN
      CALCFIELDS("Campaign Description");
    END;

    LOCAL PROCEDURE OpportunityNoOnAfterValidate@19076180();
    BEGIN
      CALCFIELDS("Opportunity Description");
    END;

    LOCAL PROCEDURE RecurringOnPush@19040619();
    BEGIN
      SetRecurringEditable;
    END;

    LOCAL PROCEDURE ContactNoOnFormat@19025756(Text@19019593 : Text[1024]);
    BEGIN
      IF Type = Type::Meeting THEN
        Text := Text007;
    END;

    LOCAL PROCEDURE ContactNameOnFormat@19032823();
    BEGIN
      IF Type = Type::Meeting THEN
        "Contact NameHideValue" := TRUE;
    END;

    LOCAL PROCEDURE ContactCompanyNameOnFormat@19053256();
    BEGIN
      IF Type = Type::Meeting THEN
        "Contact Company NameHideValue" := TRUE;
    END;

    BEGIN
    END.
  }
}

