OBJECT Report 6036 Create Contract Service Orders
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Contract Service Orders;
               ENG=Create Contract Service Orders];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ServMgtSetup.GET;
                   IF ServMgtSetup."Last Contract Service Date" <> 0D THEN
                     StartDate := ServMgtSetup."Last Contract Service Date" + 1;
                 END;

    OnPostReport=BEGIN
                   IF CreateServOrders = CreateServOrders::"Create Service Order" THEN BEGIN
                     ServMgtSetup.GET;
                     ServMgtSetup."Last Contract Service Date" := EndDate;
                     ServMgtSetup.MODIFY;

                     IF NOT HideDialog THEN
                       IF ServOrderCreated > 1 THEN
                         MESSAGE(Text000,ServOrderCreated)
                       ELSE
                         MESSAGE(Text001,ServOrderCreated)
                   END;
                 END;

  }
  DATASET
  {
    { 9952;    ;DataItem;                    ;
               DataItemTable=Table5965;
               DataItemTableView=WHERE(Contract Type=CONST(Contract),
                                       Change Status=CONST(Locked),
                                       Status=CONST(Signed));
               OnPreDataItem=BEGIN
                               IF CreateServOrders = CreateServOrders::"Print Only" THEN BEGIN
                                 CLEAR(ContrServOrdersTest);
                                 ContrServOrdersTest.InitVariables(StartDate,EndDate);
                                 ContrServOrdersTest.SETTABLEVIEW("Service Contract Header");
                                 ContrServOrdersTest.RUNMODAL;
                                 CurrReport.BREAK;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  VerifyServiceContractHeader;
                                END;

               ReqFilterFields=Contract No. }

    { 6062;1   ;DataItem;                    ;
               DataItemTable=Table5964;
               DataItemTableView=SORTING(Contract Type,Contract No.,Line No.)
                                 ORDER(Ascending)
                                 WHERE(Service Period=FILTER(<>''));
               OnPreDataItem=BEGIN
                               IF EndDate = 0D THEN
                                 ERROR(Text002);
                               IF EndDate < StartDate THEN
                                 ERROR(Text003);

                               IF StartDate <> 0D THEN BEGIN
                                 IF EndDate - StartDate + 1 > ServMgtSetup."Contract Serv. Ord.  Max. Days" THEN
                                   ERROR(
                                     Text004,
                                     ServMgtSetup.TABLECAPTION);
                               END;

                               SETRANGE("Next Planned Service Date",StartDate,EndDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Contract Expiration Date" <> 0D THEN BEGIN
                                    IF "Contract Expiration Date" <= "Next Planned Service Date" THEN
                                      CurrReport.SKIP;
                                  END ELSE
                                    IF ("Service Contract Header"."Expiration Date" <> 0D) AND
                                       ("Service Contract Header"."Expiration Date" <= "Next Planned Service Date")
                                    THEN
                                      CurrReport.SKIP;

                                  Cust.GET("Service Contract Header"."Bill-to Customer No.");
                                  IF Cust.Blocked = Cust.Blocked::All THEN
                                    CurrReport.SKIP;

                                  ServHeader.SETCURRENTKEY("Contract No.",Status,"Posting Date");
                                  ServHeader.SETRANGE("Document Type",ServHeader."Document Type"::Order);
                                  ServHeader.SETRANGE("Contract No.","Contract No.");
                                  ServHeader.SETRANGE(Status,ServHeader.Status::Pending);

                                  ServOrderExist := ServHeader.FINDFIRST;
                                  IF ServOrderExist THEN BEGIN
                                    ServItemLine.SETCURRENTKEY("Document Type","Document No.","Service Item No.");
                                    ServItemLine.SETRANGE("Document Type",ServHeader."Document Type");
                                    ServItemLine.SETRANGE("Document No.",ServHeader."No.");
                                    ServItemLine.SETRANGE("Contract No.","Contract No.");
                                    ServItemLine.SETRANGE("Contract Line No.","Line No.");
                                    IF ServItemLine.FINDFIRST THEN
                                      CurrReport.SKIP;
                                  END;
                                  CreateOrAddToServOrder;
                                END;

               DataItemLink=Contract Type=FIELD(Contract Type),
                            Contract No.=FIELD(Contract No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 5   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ENG=Starting Date];
                  SourceExpr=StartDate }

      { 1   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ENG=Ending Date];
                  SourceExpr=EndDate;
                  OnValidate=BEGIN
                               IF EndDate < StartDate THEN
                                 ERROR(Text003);
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=CreateServiceOrders;
                  CaptionML=[ENU=Action;
                             ENG=Action];
                  OptionCaptionML=[ENU=Create Service Order,Print Only;
                                   ENG=Create Service Order,Print Only];
                  SourceExpr=CreateServOrders }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 service orders were created.;ENG=%1 service orders were created.';
      Text001@1001 : TextConst 'ENU=%1 service order was created.;ENG=%1 service order was created.';
      Text002@1002 : TextConst 'ENU=You must fill in the ending date field.;ENG=You must fill in the ending date field.';
      Text003@1003 : TextConst 'ENU=The starting date is after the ending date.;ENG=The starting date is after the ending date.';
      Text004@1004 : TextConst 'ENU=The date range you have entered is a longer period than is allowed in the %1 table.;ENG=The date range you have entered is a longer period than is allowed in the %1 table.';
      ServMgtSetup@1006 : Record 5911;
      ServHeader@1007 : Record 5900;
      ServItemLine@1008 : Record 5901;
      Cust@1010 : Record 18;
      ServItem@1021 : Record 5940;
      RepairStatus@1022 : Record 5927;
      ContrServOrdersTest@1005 : Report 5988;
      ServOrderCreated@1011 : Integer;
      RecordNo@1012 : Integer;
      StartDate@1014 : Date;
      EndDate@1015 : Date;
      CreateServOrders@1016 : 'Create Service Order,Print Only';
      ServOrderExist@1018 : Boolean;
      HideDialog@1023 : Boolean;
      Text005@1009 : TextConst 'ENU=A service order cannot be created for contract no. %1 because customer no. %2 does not have a %3.;ENG=A service order cannot be created for contract no. %1 because customer no. %2 does not have a %3.';

    LOCAL PROCEDURE CreateOrAddToServOrder@2();
    BEGIN
      CLEAR(ServHeader);
      ServHeader.SETCURRENTKEY("Contract No.",Status,"Posting Date");
      ServHeader.SETRANGE("Document Type",ServHeader."Document Type"::Order);
      ServHeader.SETRANGE("Contract No.","Service Contract Header"."Contract No.");
      ServHeader.SETRANGE(Status,ServHeader.Status::Pending);
      ServHeader.SETFILTER("Order Date",'>=%1',"Service Contract Line"."Next Planned Service Date");
      IF NOT ServHeader.FINDFIRST THEN BEGIN
        CLEAR(ServHeader);
        ServHeader.INIT;
        ServHeader."Document Type" := ServHeader."Document Type"::Order;
        ServHeader.INSERT(TRUE);
        ServHeader.SetHideValidationDialog(TRUE);
        ServHeader."Contract No." := "Service Contract Header"."Contract No.";
        ServHeader.VALIDATE("Order Date","Service Contract Line"."Next Planned Service Date");
        ServHeader.VALIDATE("Customer No.","Service Contract Header"."Customer No.");
        ServHeader.VALIDATE("Bill-to Customer No.","Service Contract Header"."Bill-to Customer No.");
        ServHeader."Default Response Time (Hours)" := "Service Contract Header"."Response Time (Hours)";
        ServHeader.VALIDATE("Ship-to Code","Service Contract Header"."Ship-to Code");
        ServHeader."Service Order Type" := "Service Contract Header"."Service Order Type";
        ServHeader.VALIDATE("Currency Code","Service Contract Header"."Currency Code");
        ServHeader."Salesperson Code" := "Service Contract Header"."Salesperson Code";
        ServHeader."Max. Labor Unit Price" := "Service Contract Header"."Max. Labor Unit Price";
        ServHeader."Your Reference" := "Service Contract Header"."Your Reference";
        ServHeader."Service Zone Code" := "Service Contract Header"."Service Zone Code";
        ServHeader."Shortcut Dimension 1 Code" := "Service Contract Header"."Shortcut Dimension 1 Code";
        ServHeader."Shortcut Dimension 2 Code" := "Service Contract Header"."Shortcut Dimension 2 Code";
        ServHeader.VALIDATE("Service Order Type","Service Contract Header"."Service Order Type");
        ServHeader."Dimension Set ID" := "Service Contract Header"."Dimension Set ID";
        ServHeader.MODIFY(TRUE);

        ServOrderCreated := ServOrderCreated + 1;
      END;

      RecordNo := 0;
      CLEAR(ServItemLine);
      ServItemLine.SETRANGE("Document Type",ServHeader."Document Type");
      ServItemLine.SETRANGE("Document No.",ServHeader."No.");
      IF ServItemLine.FINDLAST THEN
        RecordNo := ServItemLine."Line No."
      ELSE
        RecordNo := 0;
      CLEAR(ServItemLine);
      ServItemLine.SETCURRENTKEY("Document Type","Document No.","Service Item No.");
      ServItemLine.SETRANGE("Document Type",ServHeader."Document Type");
      ServItemLine.SETRANGE("Document No.",ServHeader."No.");
      ServItemLine.SETRANGE("Contract No.","Service Contract Line"."Contract No.");
      ServItemLine.SETRANGE("Contract Line No.","Service Contract Line"."Line No.");
      IF NOT ServItemLine.FINDFIRST THEN BEGIN
        RecordNo := RecordNo + 10000;
        ServItemLine.INIT;
        ServItemLine.SetHideDialogBox(TRUE);
        ServItemLine."Document No." := ServHeader."No.";
        ServItemLine."Document Type" := ServHeader."Document Type";
        RepairStatus.RESET;
        RepairStatus.Initial := TRUE;
        ServItemLine."Repair Status Code" := RepairStatus.ReturnStatusCode(RepairStatus);
        ServItemLine."Line No." := RecordNo;
        IF "Service Contract Line"."Service Item No." <> '' THEN BEGIN
          ServItem.GET("Service Contract Line"."Service Item No.");
          ServItemLine.VALIDATE("Service Item No.",ServItem."No.");
          ServItemLine."Location of Service Item" := ServItem."Location of Service Item";
          ServItemLine.Priority := ServItem.Priority;
        END ELSE
          ServItemLine.Description := "Service Contract Line".Description;
        ServItemLine."Serial No." := "Service Contract Line"."Serial No.";
        ServItemLine."Item No." := "Service Contract Line"."Item No.";
        ServItemLine."Variant Code" := "Service Contract Line"."Variant Code";
        ServItemLine."Contract No." := "Service Contract Line"."Contract No.";
        ServItemLine."Contract Line No." := "Service Contract Line"."Line No.";
        ServItemLine.UpdateResponseTimeHours;
        ServItemLine.INSERT(TRUE);
      END;
    END;

    PROCEDURE SetHideDialog@3(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    PROCEDURE InitializeRequest@4(StartDateFrom@1000 : Date;EndDateFrom@1001 : Date;CreateServOrdersFrom@1002 : Option);
    BEGIN
      StartDate := StartDateFrom;
      EndDate := EndDateFrom;
      CreateServOrders := CreateServOrdersFrom;
    END;

    LOCAL PROCEDURE VerifyServiceContractHeader@1();
    VAR
      ShipToAddress@1000 : Record 222;
    BEGIN
      WITH "Service Contract Header" DO
        IF "Ship-to Code" <> '' THEN BEGIN
          IF NOT ShipToAddress.GET("Customer No.","Ship-to Code") THEN
            MESSAGE(Text005,"Contract No.","Customer No.","Ship-to Code");
          CurrReport.SKIP;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

