OBJECT Codeunit 9062669 Tree Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=17/12/15;
    Time=12:10:51 PM;
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PlusImageBlob@1000000000 : Record 99008535;
      PlusImageBlobPrepared@1000000001 : Boolean;
      MinusImageBlob@1000000003 : Record 99008535;
      MinusImageBlobPrepared@1000000002 : Boolean;
      BlobToolkit@1000000004 : Codeunit 9062242;

    LOCAL PROCEDURE "--- Hashing ---"@1000000001();
    BEGIN
    END;

    PROCEDURE IntegerToHash@1000000002(pInteger@1000000000 : Integer) : Text[6];
    VAR
      lHashTable@1000000001 : Text;
      lHashBase@1000000004 : Integer;
      lHash@1000000002 : Text[6];
      lInteger@1000000003 : Integer;
      lLeadingZeros@1000000008 : Integer;
    BEGIN
      //DOC NA2016.11 - Converting integer to 4-character hash
      //  -> pInteger: The integer
      //  <- The hash

      lHashTable := '0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ';
      lHashBase := STRLEN(lHashTable);
      lHash := '';
      lInteger := ABS(pInteger);

      WHILE (lInteger > 0) DO
      BEGIN
        lHash := COPYSTR(lHashTable, lInteger MOD lHashBase + 1, 1) + lHash;
        lInteger := lInteger DIV lHashBase;
      END;

      lLeadingZeros := MAXSTRLEN(lHash) - STRLEN(lHash);
      IF (lLeadingZeros > 0) THEN
        lHash := PADSTR('', lLeadingZeros, COPYSTR(lHashTable, 1, 1)) + lHash;

      EXIT(lHash);
    END;

    LOCAL PROCEDURE "--- Plus / Minus ---"@1000000000();
    BEGIN
    END;

    PROCEDURE GetPlusImage@1000000003(VAR pImage@1000000000 : Record 99008535);
    VAR
      lBase64Text@1000000001 : Text;
    BEGIN
      //DOC NA2016.11 - Returning plus image
      //  <> pImage: Target image (BLOB)

      GetImage('iVBORw0KGgoAAAANSUhEUgAAAAkAAAAJCAIAAABv85FHAAAAHUlEQVQImWNgwA/+YwPocmhsIuQwbaHcTJzuxAUA2VJ3ibpYwPcAAAAASUVORK5CYII=',
        PlusImageBlobPrepared, PlusImageBlob, pImage);
    END;

    PROCEDURE GetMinusImage@1000000004(VAR pImage@1000000000 : Record 99008535);
    VAR
      lBase64Text@1000000001 : Text;
    BEGIN
      //DOC NA2016.11 - Returning minus image
      //  <> pImage: Target image (BLOB)

      GetImage('iVBORw0KGgoAAAANSUhEUgAAAAkAAAAJCAIAAABv85FHAAAAF0lEQVQImWNgwA/+YwOUy2HaQkP7cAEAu8SDfdYV7g8AAAAASUVORK5CYII=',
        MinusImageBlobPrepared, MinusImageBlob, pImage);
    END;

    LOCAL PROCEDURE GetImage@1000000010(pBase64Text@1000000002 : Text;VAR pPrepared@1000000000 : Boolean;VAR pGlobalImage@1000000001 : Record 99008535;VAR pLocalImage@1000000003 : Record 99008535);
    BEGIN
      //DOC NA2016.11 - Returning image
      //  -> pBase64Text: Base64 text
      //  <> pPrepared: Target "Prepared" flag
      //  <> pGlobalImage: Target global image (BLOB)
      //  <> pLocalImage: Target local image (BLOB)

      IF ( NOT (pPrepared)) THEN
      BEGIN
        BlobToolkit.FromBase64Text(pBase64Text, pGlobalImage);
        pPrepared := TRUE;

      END;

      pLocalImage.Blob := pGlobalImage.Blob;
    END;

    BEGIN
    {
      //DOC NA2016.11 JH 24/11/2015 - Created
      //DOC NA2016.12 JH 17/12/2015 - Moved to a different ID
    }
    END.
  }
}

