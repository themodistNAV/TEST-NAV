OBJECT Report 9000 Add/Subtract Permission Set
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Add/Subtract Permission Set;
               ENG=Add/Subtract Permission Set];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF DestinationPermissionSet = '' THEN
                    ERROR(NoDestinationErr);
                  IF SourcePermissionSet = '' THEN
                    ERROR(NoSourceErr);
                END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;SourcePermission    ;
               DataItemTable=Table2000000005;
               DataItemTableView=SORTING(Role ID,Object Type,Object ID)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               SETRANGE("Role ID",SourcePermissionSet);
                             END;

               OnAfterGetRecord=VAR
                                  DestinationPermission@1000 : Record 2000000005;
                                BEGIN
                                  CASE SetOperation OF
                                    SetOperation::Include:
                                      IF DestinationPermission.GET(DestinationPermissionSet,"Object Type","Object ID") THEN BEGIN
                                        IF PermissionValueIsGreaterOrEqual("Read Permission",DestinationPermission."Read Permission") THEN
                                          DestinationPermission."Read Permission" := "Read Permission";
                                        IF PermissionValueIsGreaterOrEqual("Insert Permission",DestinationPermission."Insert Permission") THEN
                                          DestinationPermission."Insert Permission" := "Insert Permission";
                                        IF PermissionValueIsGreaterOrEqual("Modify Permission",DestinationPermission."Modify Permission") THEN
                                          DestinationPermission."Modify Permission" := "Modify Permission";
                                        IF PermissionValueIsGreaterOrEqual("Delete Permission",DestinationPermission."Delete Permission") THEN
                                          DestinationPermission."Delete Permission" := "Delete Permission";
                                        IF PermissionValueIsGreaterOrEqual("Execute Permission",DestinationPermission."Execute Permission") THEN
                                          DestinationPermission."Execute Permission" := "Execute Permission";
                                        DestinationPermission.MODIFY;
                                      END ELSE BEGIN
                                        DestinationPermission := SourcePermission;
                                        DestinationPermission."Role ID" := DestinationPermissionSet;
                                        DestinationPermission.INSERT;
                                      END;
                                    SetOperation::Exclude:
                                      BEGIN
                                        DestinationPermission.SETRANGE("Role ID",DestinationPermissionSet);
                                        DestinationPermission.SETRANGE("Object Type","Object Type");
                                        IF "Object ID" <> 0 THEN
                                          DestinationPermission.SETRANGE("Object ID","Object ID");
                                        IF DestinationPermission.FINDSET THEN
                                          REPEAT
                                            IF PermissionValueIsGreaterOrEqual("Read Permission",DestinationPermission."Read Permission") THEN
                                              DestinationPermission."Read Permission" := "Read Permission"::" ";
                                            IF PermissionValueIsGreaterOrEqual("Insert Permission",DestinationPermission."Insert Permission") THEN
                                              DestinationPermission."Insert Permission" := "Insert Permission"::" ";
                                            IF PermissionValueIsGreaterOrEqual("Modify Permission",DestinationPermission."Modify Permission") THEN
                                              DestinationPermission."Modify Permission" := "Modify Permission"::" ";
                                            IF PermissionValueIsGreaterOrEqual("Delete Permission",DestinationPermission."Delete Permission") THEN
                                              DestinationPermission."Delete Permission" := "Delete Permission"::" ";
                                            IF PermissionValueIsGreaterOrEqual("Execute Permission",DestinationPermission."Execute Permission") THEN
                                              DestinationPermission."Execute Permission" := "Execute Permission"::" ";
                                            DestinationPermission.MODIFY;
                                            IF (DestinationPermission."Read Permission" = "Read Permission"::" ") AND
                                               (DestinationPermission."Insert Permission" = "Read Permission"::" ") AND
                                               (DestinationPermission."Modify Permission" = "Read Permission"::" ") AND
                                               (DestinationPermission."Delete Permission" = "Read Permission"::" ") AND
                                               (DestinationPermission."Execute Permission" = "Read Permission"::" ")
                                            THEN
                                              DestinationPermission.DELETE;
                                          UNTIL DestinationPermission.NEXT = 0;
                                      END;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  Name=Parameter;
                  ContainerType=ContentArea }

      { 2   ;1   ;Field     ;
                  Name=DestinationPermissionSet;
                  CaptionML=[ENU=Destination;
                             ENG=Destination];
                  SourceExpr=DestinationPermissionSet;
                  TableRelation="Permission Set"."Role ID";
                  Editable=FALSE }

      { 4   ;1   ;Field     ;
                  Name=SetOperation;
                  CaptionML=[ENU=Operation;
                             ENG=Operation];
                  OptionCaptionML=[ENU=Include,Exclude;
                                   ENG=Include,Exclude];
                  SourceExpr=SetOperation }

      { 3   ;1   ;Field     ;
                  Name=SourcePermissionSet;
                  CaptionML=[ENU=Source;
                             ENG=Source];
                  SourceExpr=SourcePermissionSet;
                  TableRelation="Permission Set"."Role ID" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DestinationPermissionSet@1000 : Code[20];
      SourcePermissionSet@1001 : Code[20];
      SetOperation@1002 : 'Include,Exclude';
      NoDestinationErr@1003 : TextConst 'ENU=No destination permission set has been set.;ENG=No destination permission set has been set.';
      NoSourceErr@1004 : TextConst 'ENU=You must select a source permission set.;ENG=You must select a source permission set.';

    PROCEDURE SetDestination@3(NewDestination@1000 : Code[20]);
    BEGIN
      DestinationPermissionSet := NewDestination;
    END;

    LOCAL PROCEDURE PermissionValueIsGreaterOrEqual@5(Left@1000 : ' ,Yes,Indirect';Right@1001 : ' ,Yes,Indirect') : Boolean;
    BEGIN
      // Returns (Left >= Right)
      CASE Left OF
        Left::" ":
          EXIT(Right = Right::" ");
        Left::Yes:
          EXIT(TRUE);
        Left::Indirect:
          EXIT(Right <> Right::Yes);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

