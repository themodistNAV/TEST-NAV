OBJECT XMLport 1010 SEPA DD pain.008.001.02
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=SEPA DD pain.008.001.02;
               ENG=SEPA DD pain.008.001.02];
    Direction=Export;
    Encoding=UTF-8;
    Format/Evaluate=XML Format/Evaluate;
    DefaultNamespace=urn:iso:std:iso:20022:tech:xsd:pain.008.001.02;
    UseDefaultNamespace=Yes;
    OnPreXMLport=BEGIN
                   InitData;
                 END;

  }
  ELEMENTS
  {
    { [{2285A995-535F-411B-832A-FA909F065FE5}];  ;Document            ;Element ;Table   ;
                                                  SourceTable=Table1208;
                                                  Temporary=Yes }

    { [{0C77DC6B-460D-4901-AA0A-37714FD3C679}];1 ;CstmrDrctDbtInitn   ;Element ;Table   ;
                                                  VariableName=CompanyInformation;
                                                  SourceTable=Table79;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             IF NOT PaymentExportData.GetPreserveNonLatinCharacters THEN
                                                                               PaymentExportData.CompanyInformationConvertToLatin(CompanyInformation);
                                                                             CompanyInformationName := CompanyInformation.Name;
                                                                           END;
                                                                            }

    { [{7BE712FB-D525-4629-ABBE-29A62DEA528E}];2 ;GrpHdr              ;Element ;Text     }

    { [{019F1D07-65E3-4936-9BB6-A900D57D6352}];3 ;MsgId               ;Element ;Text    ;
                                                  VariableName=MessageID }

    { [{58EE8FF7-DF0C-4367-9576-8709E10CADD7}];3 ;CreDtTm             ;Element ;Text    ;
                                                  VariableName=CreatedDateTime }

    { [{F5B319A7-570C-4871-902F-D2C54B670924}];3 ;NbOfTxs             ;Element ;Text    ;
                                                  VariableName=NoOfTransfers }

    { [{F65349AB-F1EA-4491-9244-A35750346341}];3 ;CtrlSum             ;Element ;Text    ;
                                                  VariableName=ControlSum }

    { [{8029E0A7-9180-41BE-9626-79D95EC58988}];3 ;InitgPty            ;Element ;Text     }

    { [{5AA5785B-6704-4AA4-B431-B8C0B2D1EE9F}];4 ;Nm                  ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=CompanyInformation::Name }

    { [{B9061BEA-4033-4A41-9B23-05D8DC15B64D}];4 ;PstlAdr             ;Element ;Text     }

    { [{190EA500-44E1-4087-B9C1-92FF22B6C606}];5 ;StrtNm              ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=CompanyInformation::Address }

    { [{D1D3FFEE-6CEB-4D87-B587-A083BBE8B023}];5 ;PstCd               ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=CompanyInformation::Post Code }

    { [{343F2DED-3188-42C4-8031-FFE838D82180}];5 ;TwnNm               ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=CompanyInformation::City }

    { [{EFF6FA4A-0155-43FE-A99F-B10AF9C097CC}];5 ;Ctry                ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=CompanyInformation::Country/Region Code }

    { [{33320197-FB5D-4204-9D74-43DFD5FD8AFE}];4 ;Id                  ;Element ;Text     }

    { [{85867A47-03EA-4687-B62C-DC0B5BE37BD2}];5 ;OrgId               ;Element ;Text     }

    { [{56368BE3-89A1-4C1E-8D16-EDCD7D773101}];6 ;Othr                ;Element ;Text     }

    { [{E28D2DFB-D224-4E8C-957E-848BA37C70CD}];7 ;Id                  ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=CompanyInformation::VAT Registration No. }

    { [{881E2785-60C2-4292-9E70-89A5A25DBDB0}];2 ;PmtInf              ;Element ;Table   ;
                                                  VariableName=PaymentExportDataGroup;
                                                  SourceTable=Table1226;
                                                  Temporary=Yes }

    { [{22BF24A4-80D9-4C16-BCA9-298756242324}];3 ;PmtInfId            ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportDataGroup::Payment Information ID }

    { [{4698527A-A2DB-414D-A8C8-3E097826F29A}];3 ;PmtMtd              ;Element ;Text    ;
                                                  VariableName=PaymentMethodDD;
                                                  DataType=Code }

    { [{732FD377-66F5-4E47-8ED2-8636F4F6A97F}];3 ;BtchBookg           ;Element ;Field   ;
                                                  DataType=Boolean;
                                                  SourceField=PaymentExportDataGroup::SEPA Batch Booking }

    { [{C7E5B30F-8C2C-483D-9B9A-55C995A5CFD6}];3 ;NbOfTxs             ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=PaymentExportDataGroup::Line No. }

    { [{29F4F927-4D34-4DFE-921C-91D7121D36E7}];3 ;CtrlSum             ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=PaymentExportDataGroup::Amount }

    { [{9CA51BA0-46E2-46B3-A14C-0B28D61E299D}];3 ;PmtTpInf            ;Element ;Text     }

    { [{5E01D811-0DCD-4BC3-A4C9-7277DED06DCD}];4 ;InstrPrty           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::SEPA Instruction Priority Text }

    { [{1D5A56C5-3B10-42BC-A930-65DAE73CDB35}];4 ;SvcLvl              ;Element ;Text     }

    { [{5126454D-9F23-4FEB-A798-3D6EE6C3E628}];5 ;Cd                  ;Element ;Text    ;
                                                  VariableName=ServiceLevelCodeSEPA }

    { [{122A2289-BF78-4D13-9331-2392FEB6F879}];4 ;LclInstrm           ;Element ;Text     }

    { [{84028F13-6930-44C4-8CC8-313F64B54DB1}];5 ;Cd                  ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::SEPA Partner Type Text }

    { [{9CA9B66F-0926-45DF-9095-D48C6DFD677D}];4 ;SeqTp               ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::SEPA Direct Debit Seq. Text }

    { [{B0BE6C3E-1E1E-49E7-BC45-A217B1BAE1A4}];3 ;ReqdColltnDt        ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=PaymentExportDataGroup::Transfer Date }

    { [{EA586377-22E1-472F-81D1-AA037BFC4CE2}];3 ;Cdtr                ;Element ;Text     }

    { [{83929BC3-4FCC-4EF3-A25C-D2EF552EF242}];4 ;Nm                  ;Element ;Text    ;
                                                  VariableName=CompanyInformationName }

    { [{F172EC0F-A290-4E91-B133-668C58AC1864}];4 ;Id                  ;Element ;Text    ;
                                                  VariableName=CdrId }

    { [{27A2721B-0987-4B0C-AAA1-225CE63B5117}];5 ;OrgId               ;Element ;Text    ;
                                                  VariableName=CdrOrgId }

    { [{1F8D3AEF-276C-4213-9999-5E6939C61623}];6 ;BICOrBEI            ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::Sender Bank BIC }

    { [{78E436F1-D17D-4690-A133-62D87C7BB883}];3 ;CdtrAcct            ;Element ;Text     }

    { [{F78DE695-FD64-4EB2-9C45-6B43441E7B2F}];4 ;Id                  ;Element ;Text    ;
                                                  VariableName=CdtrAcctId }

    { [{91610F21-D553-4E31-B958-F851887B2B2B}];5 ;IBAN                ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportDataGroup::Sender Bank Account No. }

    { [{7B0760B1-B72B-4517-9D5F-9D31D0D2AFAC}];3 ;CdtrAgt             ;Element ;Text     }

    { [{1F4B6580-5D4B-41E8-8462-05CC622192E6}];4 ;FinInstnId          ;Element ;Text     }

    { [{0A1A7D86-9A7D-4721-9BC7-007745FFFF59}];5 ;BIC                 ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::Sender Bank BIC;
                                                  MinOccurs=Once;
                                                  MaxOccurs=Once }

    { [{8C630D87-F35B-4264-A22D-69C3AAC54B00}];3 ;ChrgBr              ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::SEPA Charge Bearer Text }

    { [{76BE8EC4-22B4-44E6-9A49-AA31E9826E4D}];3 ;CdtrSchmeId         ;Element ;Text     }

    { [{50BF3AE8-686B-4876-8549-44AA06942437}];4 ;Id                  ;Element ;Text    ;
                                                  VariableName=CdtrSchmeIdId }

    { [{D1C9E961-C73E-4D43-A1F1-E6A5E3CEA8AD}];5 ;PrvtId              ;Element ;Text     }

    { [{C20CE8D4-CDA7-4DA2-A470-1A6027B37B7D}];6 ;Othr                ;Element ;Text    ;
                                                  VariableName=CdtrSchmeIdOthr }

    { [{3FAD5003-45AE-413F-B92E-82F3C88E888D}];7 ;Id                  ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportDataGroup::Creditor No. }

    { [{C03E1D59-4E45-49CA-BDE6-25842D811318}];7 ;SchmeNm             ;Element ;Text     }

    { [{B240E02A-76D8-4D90-AF68-B1DFB76BFCAD}];8 ;Prtry               ;Element ;Text    ;
                                                  VariableName=SchmeNmPrtry }

    { [{CB41E260-025B-4954-8333-A61D2EE26EB5}];3 ;DrctDbtTxInf        ;Element ;Table   ;
                                                  VariableName=PaymentExportData;
                                                  SourceTable=Table1226;
                                                  LinkFields=Field34=FIELD(Field34),
                                                             Field104=FIELD(Field104),
                                                             Field82=FIELD(Field82),
                                                             Field122=FIELD(Field122),
                                                             Field125=FIELD(Field125),
                                                             Field107=FIELD(Field107),
                                                             Field109=FIELD(Field109);
                                                  LinkTable=PaymentExportDataGroup;
                                                  Temporary=Yes }

    { [{59B306AA-C7F0-42F6-9F6C-2A976CC19AB0}];4 ;PmtId               ;Element ;Text     }

    { [{A8E33846-70EB-4206-9737-7A99C35DD0C5}];5 ;InstrId             ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Payment Information ID }

    { [{8B27A404-9DBA-4290-9088-581F891EDE98}];5 ;EndToEndId          ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::End-to-End ID }

    { [{6DC0F9C3-E082-405A-91C7-E171AE430745}];4 ;PmtTpInf            ;Element ;Text    ;
                                                  VariableName=DrctDbtTxInfPmtTpInf }

    { [{951D6B08-09A6-4878-877B-0E5E2FD1C523}];5 ;InstrPrty           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::SEPA Instruction Priority Text }

    { [{351B1C68-E833-44B1-85CB-6C1DFD29A96A}];4 ;InstdAmt            ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=PaymentExportData::Amount }

    { [{9D65A070-5DE7-4133-BA49-035EA3CFE68A}];5 ;Ccy                 ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::Currency Code }

    { [{7DDDB4E2-82ED-4C3C-BFE1-C581AFB9BBC5}];4 ;DrctDbtTx           ;Element ;Text     }

    { [{F7001F79-02A6-4E0D-AC71-86760D8A33FD}];5 ;MndtRltdInf         ;Element ;Text     }

    { [{0E4F03B7-4311-499A-8F8B-7C7FBBF6281B}];6 ;MndtId              ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::SEPA Direct Debit Mandate ID }

    { [{12B1A3FC-CB51-421D-AC98-1D30C84DB108}];6 ;DtOfSgntr           ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=PaymentExportData::SEPA DD Mandate Signed Date }

    { [{D211EEAA-B9BF-4DCC-8452-3A6E83A0635A}];4 ;DbtrAgt             ;Element ;Text     }

    { [{9E2A7E02-455A-436C-93C5-14E4B81FCC21}];5 ;FinInstnId          ;Element ;Text    ;
                                                  VariableName=DbtrAgtFinInstnId }

    { [{D60708D9-2767-4321-9497-00BBF2F9ED6E}];6 ;BIC                 ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::Recipient Bank BIC }

    { [{054D4604-0120-4FD9-B0F2-813C7257F6B2}];4 ;Dbtr                ;Element ;Text     }

    { [{43234295-B25C-46E3-9975-0143C5D76861}];5 ;Nm                  ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Recipient Name }

    { [{B55FD31A-E201-4E7C-AD5A-1E317800A7EF}];5 ;PstlAdr             ;Element ;Text    ;
                                                  VariableName=DbtrPstlAdr }

    { [{E8D1E430-9121-469F-A3A7-0B363599FA86}];6 ;StrtNm              ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Recipient Address;
                                                  Export::OnBeforePassField=BEGIN
                                                                              IF PaymentExportData."Recipient Address" = '' THEN
                                                                                currXMLport.SKIP;
                                                                            END;
                                                                             }

    { [{7738E25C-F1DD-4DB3-B00E-7DBC31FDB21D}];6 ;PstCd               ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::Recipient Post Code;
                                                  Export::OnBeforePassField=BEGIN
                                                                              IF PaymentExportData."Recipient Post Code" = '' THEN
                                                                                currXMLport.SKIP;
                                                                            END;
                                                                             }

    { [{7C5858BA-7E5C-41CF-B5CC-B238674E3068}];6 ;TwnNm               ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Recipient City;
                                                  Export::OnBeforePassField=BEGIN
                                                                              IF PaymentExportData."Recipient City" = '' THEN
                                                                                currXMLport.SKIP;
                                                                            END;
                                                                             }

    { [{ACE832B9-075C-47D1-8ED6-20DB2E414F13}];6 ;Ctry                ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::Recipient Country/Region Code;
                                                  Export::OnBeforePassField=BEGIN
                                                                              IF PaymentExportData."Recipient Country/Region Code" = '' THEN
                                                                                currXMLport.SKIP;
                                                                            END;
                                                                             }

    { [{3099F921-808C-40CF-B33B-6000DD52F81A}];5 ;Id                  ;Element ;Text    ;
                                                  VariableName=DbtrId }

    { [{DCE7DDB0-B1E6-41EB-A149-7F6426576A5B}];6 ;OrgId               ;Element ;Text    ;
                                                  VariableName=DbtrOrgId }

    { [{A772B9E0-E9B0-40EF-8B4B-96BB934BBF43}];7 ;BICOrBEI            ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=PaymentExportData::Recipient Bank BIC }

    { [{18CED242-D652-4ECE-B27D-369AB0D1C205}];4 ;DbtrAcct            ;Element ;Text     }

    { [{FFF9E6F1-CEE4-4FE9-8FF0-8A1C08A0E628}];5 ;Id                  ;Element ;Text    ;
                                                  VariableName=DbtrAcctId }

    { [{0B407BE7-B0E8-4812-8924-0D6F8F3B12B7}];6 ;IBAN                ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Recipient Bank Acc. No. }

    { [{54F717C7-6E9E-4217-A054-8310317D0B9F}];4 ;RmtInf              ;Element ;Text     }

    { [{6F3D66BA-368F-4018-AC01-5F66B2E7E93B}];5 ;Ustrd               ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=PaymentExportData::Message to Recipient 1 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      NoDataToExportErr@1000 : TextConst '@@@="%1=Field;%2=Value;%3=Value";ENU=There is no data to export. Make sure the %1 field is not set to %2 or %3.;ENG=There is no data to export. Make sure the %1 field is not set to %2 or %3.';

    LOCAL PROCEDURE InitData@1();
    VAR
      DirectDebitCollectionEntry@1000 : Record 1208;
      DirectDebitCollection@1002 : Record 1207;
      SEPADDFillExportBuffer@1001 : Codeunit 1231;
      PaymentGroupNo@1003 : Integer;
    BEGIN
      SEPADDFillExportBuffer.FillExportBuffer("Direct Debit Collection Entry",PaymentExportData);
      PaymentMethodDD := 'DD';
      ServiceLevelCodeSEPA := 'SEPA';
      SchmeNmPrtry := 'SEPA';
      NoOfTransfers := FORMAT(PaymentExportData.COUNT);
      MessageID := PaymentExportData."Message ID";
      CreatedDateTime := FORMAT(CURRENTDATETIME,19,9);
      PaymentExportData.CALCSUMS(Amount);
      ControlSum := FORMAT(PaymentExportData.Amount,0,'<Precision,2:2><Standard Format,9>');

      PaymentExportData.SETCURRENTKEY(
        "Sender Bank BIC","SEPA Instruction Priority Text","Transfer Date",
        "SEPA Direct Debit Seq. Text","SEPA Partner Type Text","SEPA Batch Booking","SEPA Charge Bearer Text");

      IF NOT PaymentExportData.FINDSET THEN
        ERROR(NoDataToExportErr,DirectDebitCollectionEntry.FIELDCAPTION(Status),
          DirectDebitCollectionEntry.Status::Rejected,DirectDebitCollection.Status::Canceled);

      InitPmtGroup;
      REPEAT
        IF IsNewGroup THEN BEGIN
          InsertPmtGroup(PaymentGroupNo);
          InitPmtGroup;
        END;
        PaymentExportDataGroup."Line No." += 1;
        PaymentExportDataGroup.Amount += PaymentExportData.Amount;
      UNTIL PaymentExportData.NEXT = 0;
      InsertPmtGroup(PaymentGroupNo);
    END;

    LOCAL PROCEDURE IsNewGroup@4() : Boolean;
    BEGIN
      EXIT(
        (PaymentExportData."Sender Bank BIC" <> PaymentExportDataGroup."Sender Bank BIC") OR
        (PaymentExportData."SEPA Instruction Priority Text" <> PaymentExportDataGroup."SEPA Instruction Priority Text") OR
        (PaymentExportData."Transfer Date" <> PaymentExportDataGroup."Transfer Date") OR
        (PaymentExportData."SEPA Direct Debit Seq. Text" <> PaymentExportDataGroup."SEPA Direct Debit Seq. Text") OR
        (PaymentExportData."SEPA Partner Type Text" <> PaymentExportDataGroup."SEPA Partner Type Text") OR
        (PaymentExportData."SEPA Batch Booking" <> PaymentExportDataGroup."SEPA Batch Booking") OR
        (PaymentExportData."SEPA Charge Bearer Text" <> PaymentExportDataGroup."SEPA Charge Bearer Text"));
    END;

    LOCAL PROCEDURE InitPmtGroup@2();
    BEGIN
      PaymentExportDataGroup := PaymentExportData;
      PaymentExportDataGroup."Line No." := 0; // used for counting transactions within group
      PaymentExportDataGroup.Amount := 0; // used for summarizing transactions within group
    END;

    LOCAL PROCEDURE InsertPmtGroup@3(VAR PaymentGroupNo@1000 : Integer);
    BEGIN
      PaymentGroupNo += 1;
      PaymentExportDataGroup."Entry No." := PaymentGroupNo;
      PaymentExportDataGroup."Payment Information ID" :=
        COPYSTR(
          STRSUBSTNO('%1/%2',PaymentExportData."Message ID",PaymentGroupNo),
          1,MAXSTRLEN(PaymentExportDataGroup."Payment Information ID"));
      PaymentExportDataGroup.INSERT;
    END;

    BEGIN
    END.
  }
}

