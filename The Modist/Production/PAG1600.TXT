OBJECT Page 1600 Outlook Mail Engine
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Outlook Mail Engine;
               ENG=Outlook Mail Engine];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table5050;
    DelayedInsert=No;
    PageType=List;
    OnOpenPage=BEGIN
                 IF NOT GetDetailsFromFilters THEN
                   EXIT;

                 IF COUNT = 0 THEN
                   SetupNewContact;
               END;

    OnAfterGetRecord=VAR
                       Contact@1003 : Record 5050;
                     BEGIN
                       IF TempEmail <> '' THEN
                         IF COUNT = 1 THEN BEGIN
                           FINDFIRST;
                           IF Type = Type::Company THEN
                             RedirectContactToCompany("No.")
                           ELSE
                             RedirectContactToCompany("Company No.");
                         END ELSE BEGIN
                           Contact.SETFILTER("No.",CreateFilterString);
                           PAGE.RUN(PAGE::"Outlook Mail Engine",Contact);
                         END;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           RedirectContactToCompany("No.");
                         END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 4       ;1   ;Action    ;
                      Name=Create Contact;
                      CaptionML=[ENU=Create Contact;
                                 ENG=Create Contact];
                      Promoted=Yes;
                      RunPageMode=View;
                      OnAction=BEGIN
                                 Contact.INIT;
                                 Contact."E-Mail" := TempEmail;
                                 PAGE.RUNMODAL(PAGE::"Contact Card",Contact,"E-Mail");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Content;
                ContainerType=ContentArea }

    { 7   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 6   ;2   ;Field     ;
                SourceExpr="No." }

    { 5   ;2   ;Field     ;
                SourceExpr=Name }

    { 2   ;2   ;Field     ;
                SourceExpr="E-Mail" }

  }
  CODE
  {
    VAR
      Contact@1002 : Record 5050;
      Customer@1000 : Record 18;
      Vendor@1001 : Record 23;
      TempEmail@1003 : Text[80];
      TempName@1005 : Text;
      NewContactQst@1006 : TextConst '@@@="%1 = Name of contact";ENU=%1 does not exist in NAV. Would you like to add them as a contact now?;ENG=%1 does not exist in NAV. Would you like to add them as a contact now?';

    LOCAL PROCEDURE RedirectContactToCompany@22(ContactNo@1000 : Code[20]);
    VAR
      ContactBusinessRelation@1001 : Record 5054;
      MarketingSetup@1002 : Record 5079;
    BEGIN
      ContactBusinessRelation.SETRANGE("Contact No.",ContactNo);
      IF NOT ContactBusinessRelation.FINDFIRST THEN
        PAGE.RUN(PAGE::"Contact Card",Rec)
      ELSE BEGIN
        MarketingSetup.GET;
        IF ContactBusinessRelation."Business Relation Code" = MarketingSetup."Bus. Rel. Code for Customers" THEN BEGIN
          Customer.SETRANGE("No.",ContactBusinessRelation."No.");
          IF Customer.FINDFIRST THEN
            PAGE.RUN(PAGE::"Mini Customer Card",Customer);
        END ELSE
          IF ContactBusinessRelation."Business Relation Code" = MarketingSetup."Bus. Rel. Code for Vendors" THEN BEGIN
            Vendor.SETRANGE("No.",ContactBusinessRelation."No.");
            IF Vendor.FINDFIRST THEN
              PAGE.RUN(PAGE::"Mini Vendor Card",Vendor)
          END
      END;
    END;

    LOCAL PROCEDURE GetDetailsFromFilters@1() : Boolean;
    BEGIN
      IF GETFILTER("E-Mail") <> '' THEN
        TempEmail := GETRANGEMIN("E-Mail");

      IF TempEmail = '' THEN
        EXIT(FALSE);

      TempName := GETFILTER(Name);
      IF (STRLEN(TempName) > 3) AND (STRPOS(TempName,'@*') = 1) THEN
        TempName := COPYSTR(TempName,3,STRLEN(TempName) - 3);
      IF TempName = '' THEN
        TempName := TempEmail;

      CLEAR(Rec);
      SETRANGE("E-Mail",TempEmail);
      EXIT(TRUE);
    END;

    [TryFunction]
    LOCAL PROCEDURE SetupNewContact@6();
    BEGIN
      IF CONFIRM(STRSUBSTNO(NewContactQst,TempName)) THEN BEGIN
        Contact.INIT;
        Contact."E-Mail" := TempEmail;
        Contact.Name := COPYSTR(TempName,1,50);
        Contact.Type := Contact.Type::Person;
        Contact."No." := '';
        Contact."Search Name" := UPPERCASE(TempName);
        Contact.INSERT(TRUE);
        PAGE.RUN(PAGE::"Contact Card",Contact);
      END ELSE
        CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE CreateFilterString@2() FilterString : Text;
    BEGIN
      FilterString := '';
      FINDFIRST;
      REPEAT
        IF FilterString = '' THEN
          FilterString := "Company No."
        ELSE
          FilterString := FilterString + '|' + "Company No.";
      UNTIL NEXT = 0;
    END;

    BEGIN
    END.
  }
}

