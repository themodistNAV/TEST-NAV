OBJECT Table 9021711 Shipping Agent Service Price
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:09 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ShipAgentServPrice.RESET;
               ShipAgentServPrice.SETRANGE("Shipping Agent Code","Shipping Agent Code");
               ShipAgentServPrice.SETRANGE("Shipping Service Code","Shipping Service Code");
               ShipAgentServPrice.SETRANGE("Label Code","Label Code");
               IF ShipAgentServPrice.FINDLAST THEN
                 "Entry No." := ShipAgentServPrice."Entry No." + 1
               ELSE
                 "Entry No." := 1;

               //DOC EC2009.2.69 ShipAgentServ.GET("Label Code","Shipping Agent Code","Shipping Service Code");
               ShipAgentServ.GET("Shipping Agent Code","Shipping Service Code"); //DOC EC2009.2.69

               ShipAgentServ.TESTFIELD("Default Measure");
               IF Measure = Measure::" " THEN
                 Measure := ShipAgentServ."Default Measure";
               IF Measure = Measure::"Order Value" THEN
                 "Combine Items" := TRUE
               ELSE
                 "Combine Items" := ShipAgentServ."Combine Items";
               IF "Destination Country" = '' THEN
                 "Destination Country" := ShipAgentServ."Default Country";
               "Maximum Length" := ShipAgentServ."Maximum Length";
               "Maximum Weight" := ShipAgentServ."Maximum Weight";

               DateTimeInsertedModified:=CURRENTDATETIME;
             END;

    OnModify=BEGIN
               IF ItemShipPriceExists(TRUE) THEN
                 IF CONFIRM(Text001) THEN
                   DeleteItemShipPrices(TRUE)
                 ELSE
                   ERROR(Text003);

               DateTimeInsertedModified:=CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               UnsetVerify;
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=[ENU=Shipping Agent Service Price;
               ENG=Shipping Agent Service Price];
    LookupPageID=Page9021711;
    DrillDownPageID=Page9021711;
  }
  FIELDS
  {
    { 1   ;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   CaptionML=[ENU=Shipping Agent Code;
                                                              ENG=Shipping Agent Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Shipping Service Code;Code10       ;TableRelation="Shipping Service";
                                                   CaptionML=[ENU=Shipping Service Code;
                                                              ENG=Shipping Service Code];
                                                   NotBlank=Yes }
    { 10  ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ENG=Entry No.] }
    { 40  ;   ;DateTimeInsertedModified;DateTime  ;CaptionML=[ENU=DateTimeInsertedModified;
                                                              ENG=DateTimeInsertedModified] }
    { 60  ;   ;Include Tax In Calc Amount;Boolean ;CaptionML=[ENU=Include Tax In Calc Amount;
                                                              ENG=Include Tax In Calc Amount];
                                                   Description=GW add 130108 }
    { 61  ;   ;User Context Type   ;Option        ;OnValidate=BEGIN
                                                                //IF "User Context Type"="User Context Type"::"User Context Code" THEN
                                                                //  ERROR('Not yet implemented.\\Please ask MQ if you need this functionality.');

                                                                IF ("User Context Type" <> xRec."User Context Type") THEN
                                                                  "User Context Code" := '';
                                                              END;

                                                   CaptionML=[ENU=User Context Type;
                                                              ENG=User Context Type];
                                                   OptionCaptionML=[ENU=" ,User Type,Web Security Role";
                                                                    ENG=" ,User Type,Web Security Role"];
                                                   OptionString=[ ,User Type,Web Security Role];
                                                   Description=GW add 130108 }
    { 62  ;   ;User Context Code   ;Code20        ;TableRelation=IF (User Context Type=CONST(Web Security Role)) "Web Security Role".Code WHERE (Label Code=FIELD(Label Code));
                                                   OnValidate=VAR
                                                                TxtB2C@1000000000 : TextConst 'ENU=B2C;ENG=B2C';
                                                                TxtB2B@1000000001 : TextConst 'ENU=B2B;ENG=B2B';
                                                              BEGIN
                                                                IF "User Context Type" = "User Context Type"::" " THEN
                                                                  ERROR(Text007,FIELDCAPTION("User Context Type"),FIELDCAPTION("User Context Code"));

                                                                IF "User Context Type" = "User Context Type"::"User Type" THEN
                                                                 IF ("User Context Code"  <> TxtB2C) AND
                                                                    ("User Context Code"  <> TxtB2B) THEN
                                                                   ERROR(Text008,FIELDCAPTION("User Context Type"),FIELDCAPTION("User Context Code"));

                                                                //When the %1 is User Type then the %2 must be either B2B or B2C.
                                                              END;

                                                   CaptionML=[ENU=User Context Code;
                                                              ENG=User Context Code];
                                                   Description=GW add 130108 }
    { 64  ;   ;Handling Amount     ;Decimal       ;CaptionML=[ENU=Handling Amount;
                                                              ENG=Handling Amount];
                                                   Description=GW add 130108 }
    { 200 ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code] }
    { 202 ;   ;Measure             ;Option        ;OnValidate=BEGIN
                                                                //GW 101119 -
                                                                InternetSetup.GET;
                                                                IF InternetSetup."Advanced Shipping Mode" = FALSE THEN
                                                                  IF Measure <> Measure::"Order Value" THEN
                                                                    ERROR(Text004);
                                                                //GW 101119 +

                                                                IF Measure = Measure::"Order Value" THEN
                                                                  "Combine Items" := TRUE;
                                                              END;

                                                   CaptionML=[ENU=Measure;
                                                              ENG=Measure];
                                                   OptionCaptionML=[ENU=" ,Weight,Longest Dimension,Unit Volume,Order Value";
                                                                    ENG=" ,Weight,Longest Dimension,Unit Volume,Order Value"];
                                                   OptionString=[ ,Weight,Longest Dimension,Unit Volume,Order Value];
                                                   Description=GW 101119 Non Editable - why wouldnt it be?;
                                                   Editable=No }
    { 204 ;   ;Combine Items       ;Boolean       ;OnValidate=BEGIN
                                                                IF (Measure = Measure::"Order Value") AND
                                                                   ("Combine Items" = FALSE) THEN
                                                                  ERROR(Text002);

                                                                UnsetVerify;
                                                              END;

                                                   CaptionML=[ENU=Combine Items;
                                                              ENG=Combine Items] }
    { 208 ;   ;Upper Limit         ;Decimal       ;OnValidate=BEGIN
                                                                UnsetVerify;
                                                              END;

                                                   CaptionML=[ENU=Upper Limit;
                                                              ENG=Upper Limit];
                                                   MinValue=0 }
    { 210 ;   ;Lower Limit         ;Decimal       ;CaptionML=[ENU=Lower Limit;
                                                              ENG=Lower Limit];
                                                   Editable=No }
    { 211 ;   ;Verified            ;Boolean       ;CaptionML=[ENU=Verified;
                                                              ENG=Verified];
                                                   Editable=No }
    { 212 ;   ;Item Ship Prices OK ;Boolean       ;CaptionML=[ENU=Item Ship Prices OK;
                                                              ENG=Item Ship Prices OK];
                                                   Editable=No }
    { 220 ;   ;Destination Country ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Destination Country;
                                                              ENG=Destination Country] }
    { 222 ;   ;Destination Web Location;Code10    ;TableRelation="Web Location".Code WHERE (Label Code=FIELD(Label Code));
                                                   OnValidate=BEGIN
                                                                //GW 101119 -
                                                                InternetSetup.GET;
                                                                IF InternetSetup."Advanced Shipping Mode" = FALSE THEN
                                                                  IF "Destination Web Location" <> '' THEN
                                                                    ERROR(Text005);
                                                                //GW 101119 +

                                                                IF "Destination Web Location" = '' THEN BEGIN
                                                                  "Destination Country" := '';
                                                                END ELSE BEGIN
                                                                  WebLoc.RESET;
                                                                  WebLoc.SETRANGE(Code,"Destination Web Location");
                                                                  IF WebLoc.COUNT = 1 THEN BEGIN
                                                                    WebLoc.FINDFIRST;
                                                                    WebLoc.TESTFIELD("Country Code");
                                                                    "Destination Country" := WebLoc."Country Code";
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Destination Web Location;
                                                              ENG=Destination Web Location] }
    { 224 ;   ;Maximum Length      ;Decimal       ;OnValidate=BEGIN
                                                                UnsetVerify;
                                                              END;

                                                   CaptionML=[ENU=Maximum Length;
                                                              ENG=Maximum Length] }
    { 226 ;   ;Maximum Weight      ;Decimal       ;OnValidate=BEGIN
                                                                UnsetVerify;
                                                              END;

                                                   CaptionML=[ENU=Maximum Weight;
                                                              ENG=Maximum Weight] }
    { 230 ;   ;Price               ;Decimal       ;OnValidate=BEGIN
                                                                UnsetVerify;
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              ENG=Price] }
    { 240 ;   ;No. of Item Ship Prices;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Item Ship Prices" WHERE (Label Code=FIELD(Label Code),
                                                                                               Shipping Service Code=FIELD(Shipping Service Code),
                                                                                               Shipping Agent Code=FIELD(Shipping Agent Code),
                                                                                               Measure=FIELD(Measure),
                                                                                               Upper Limit=FIELD(Upper Limit),
                                                                                               Destination Country=FIELD(Destination Country),
                                                                                               Destination Web Location=FIELD(Destination Web Location)));
                                                   CaptionML=[ENU=No. of Item Ship Prices;
                                                              ENG=No. of Item Ship Prices];
                                                   Description=flowfield }
  }
  KEYS
  {
    {    ;Label Code,Shipping Agent Code,Shipping Service Code,Entry No. }
    {    ;Label Code,Shipping Agent Code,Shipping Service Code,User Context Code,Destination Country,Destination Web Location,Measure,Upper Limit;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ShipAgentServPrice@1000000000 : Record 9021711;
      ShipAgentServPrice1@1000000007 : Record 9021711;
      ShipAgentServ@1000000002 : Record 5790;
      ItemShipPrice@1000000001 : Record 9021712;
      Text000@1000000003 : TextConst 'ENU=You cannot rename a Shipping Agent Service Price.  Please delete and reinsert.;ENG=You cannot rename a Shipping Agent Service Price.  Please delete and reinsert.';
      Text001@1000000004 : TextConst 'ENU=You cannot change the Shipping Agent Service Price as Item Ship Prices exist.\\Do you want to delete the Item Ship Prices?;ENG=You cannot change the Shipping Agent Service Price as Item Ship Prices exist.\\Do you want to delete the Item Ship Prices?';
      Text002@1000000005 : TextConst 'ENU=Combine Items must be Yes if Measure is Order Value.;ENG=Combine Items must be Yes if Measure is Order Value.';
      WebLoc@1000000006 : Record 9021692;
      Text003@1000000008 : TextConst 'ENU=Change Abandonned.;ENG=Change Abandonned.';
      Text004@1000000009 : TextConst 'ENU=The Default Measure can only be Order Value unless you are using the Advanced Shipping Module.;ENG=The Default Measure can only be Order Value unless you are using the Advanced Shipping Module.';
      InternetSetup@1000000010 : Record 9021609;
      Text005@1000000011 : TextConst 'ENU=The Destination Web Location must be blank unless you are using the Advanced Shipping Module.;ENG=The Destination Web Location must be blank unless you are using the Advanced Shipping Module.';
      Text006@1000000012 : TextConst 'ENU=You cannot have more than one entry with the same Label, Ship Agent, Ship Service, Country, Web Loc., Measure and Upper Limit.;ENG=You cannot have more than one entry with the same Label, Ship Agent, Ship Service, Country, Web Loc., Measure and Upper Limit.';
      Text007@1000000013 : TextConst 'ENU=Please select a %1 before assigning a %2.;ENG=Please select a %1 before assigning a %2.';
      Text008@1000000014 : TextConst 'ENU=When the %1 is User Type then the %2 must be either B2B or B2C.;ENG=When the %1 is User Type then the %2 must be either B2B or B2C.';

    PROCEDURE ItemShipPriceExists@1000000000(AllUpperLimits@1000000000 : Boolean) : Boolean;
    BEGIN
      ItemShipPrice.RESET;
      ItemShipPrice.SETRANGE("Shipping Service Code","Shipping Service Code");
      ItemShipPrice.SETRANGE("Shipping Agent Code","Shipping Agent Code");
      ItemShipPrice.SETRANGE("Label Code","Label Code");
      ItemShipPrice.SETRANGE("Destination Country","Destination Country");
      ItemShipPrice.SETRANGE("Destination Web Location","Destination Web Location");
      ItemShipPrice.SETRANGE("User Context Code","User Context Code"); //DOC EC2013.3.40
      ItemShipPrice.SETRANGE(Measure,Measure);
      IF AllUpperLimits = FALSE THEN
        ItemShipPrice.SETRANGE("Upper Limit","Upper Limit");
      ItemShipPrice.SETRANGE("Ship Price Manually Set",FALSE);
      IF ItemShipPrice.FINDFIRST THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE DeleteItemShipPrices@1000000002(AllUpperLimits@1000000000 : Boolean);
    BEGIN
      ItemShipPrice.RESET;
      ItemShipPrice.SETRANGE("Shipping Service Code","Shipping Service Code");
      ItemShipPrice.SETRANGE("Shipping Agent Code","Shipping Agent Code");
      ItemShipPrice.SETRANGE("Label Code","Label Code");
      ItemShipPrice.SETRANGE("Destination Country","Destination Country");
      ItemShipPrice.SETRANGE("Destination Web Location","Destination Web Location");
      ItemShipPrice.SETRANGE("User Context Code","User Context Code"); //DOC EC2013.3.40
      ItemShipPrice.SETRANGE(Measure,Measure);
      IF AllUpperLimits = FALSE THEN
        ItemShipPrice.SETRANGE("Upper Limit","Upper Limit");
      ItemShipPrice.SETRANGE("Ship Price Manually Set",FALSE);
      ItemShipPrice.DELETEALL;
    END;

    PROCEDURE UnsetVerify@1000000001();
    BEGIN
      IF "Label Code" = '' THEN
        ERROR('label code is blank');

      IF ItemShipPriceExists(TRUE) THEN
        IF CONFIRM(Text001) THEN
          DeleteItemShipPrices(TRUE)
        ELSE
          ERROR(Text003);

      ShipAgentServPrice.RESET;

      //DOC EC2013.3.40 -
      //ShipAgentServPrice.SETCURRENTKEY("Label Code","Shipping Agent Code","Shipping Service Code",
      //                                 "Destination Country","Destination Web Location",Measure,"Upper Limit");

      ShipAgentServPrice.SETCURRENTKEY("Label Code","Shipping Agent Code","Shipping Service Code",
                         "User Context Code","Destination Country","Destination Web Location",Measure,"Upper Limit");
      //DOC EC2013.3.40 +


      ShipAgentServPrice.SETRANGE("Label Code","Label Code");
      ShipAgentServPrice.SETRANGE("Shipping Agent Code","Shipping Agent Code");
      ShipAgentServPrice.SETRANGE("Shipping Service Code","Shipping Service Code");
      ShipAgentServPrice.SETRANGE("User Context Code","User Context Code");  //DOC EC2013.3.40
      ShipAgentServPrice.SETRANGE("Destination Country","Destination Country");
      ShipAgentServPrice.SETRANGE("Destination Web Location","Destination Web Location");
      ShipAgentServPrice.SETRANGE("Upper Limit","Upper Limit");
      ShipAgentServPrice.SETRANGE(Measure,Measure);
      ShipAgentServPrice.SETFILTER("Entry No.",'<>%1',"Entry No.");
      IF ShipAgentServPrice.FINDFIRST THEN
        ERROR(Text006);
      ShipAgentServPrice.SETRANGE("Upper Limit");
      IF ShipAgentServPrice.FIND('-') THEN
      REPEAT
        {
        IF (ShipAgentServPrice."Label Code" = "Label Code") AND
           (ShipAgentServPrice."Shipping Agent Code" = "Shipping Agent Code") AND
           (ShipAgentServPrice."Shipping Service Code" = "Shipping Service Code") AND
           (ShipAgentServPrice."Destination Country" = "Destination Country") AND
           (ShipAgentServPrice."Destination Web Location" = "Destination Web Location") AND
           (ShipAgentServPrice.Measure = Measure) AND
           (ShipAgentServPrice."Upper Limit" = "Upper Limit") THEN BEGIN
          //do nothing
        END ELSE BEGIN
        }
          ShipAgentServPrice1 := ShipAgentServPrice;
          ShipAgentServPrice1."Lower Limit" := 0;
          ShipAgentServPrice1.Verified := FALSE;
          ShipAgentServPrice1."Item Ship Prices OK" := FALSE;
          ShipAgentServPrice1.DateTimeInsertedModified := CURRENTDATETIME;
          ShipAgentServPrice1.MODIFY;
        //END;
      UNTIL ShipAgentServPrice.NEXT = 0;
      //DOC EC2009.2.69 ShipAgentServ.GET("Label Code","Shipping Agent Code","Shipping Service Code");
      ShipAgentServ.GET("Shipping Agent Code","Shipping Service Code"); //DOC EC2009.2.69
      ShipAgentServ.VALIDATE("Price Lower Limits Set",FALSE);

      ShipAgentServ.MODIFY;
      Verified := FALSE;
      "Lower Limit" := 0;
      "Item Ship Prices OK" := FALSE;
    END;

    BEGIN
    {
      //DOC EC2009.2.69 MQ 14/09/2011 - GW110914 Code ammendment now key in Shipping Agent Services is returned to std.
      //DOC EC2013.3.40 MQ 08/01/2013 - GW130108 Add fields 60, 61, 62 and 64 and enhance code
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

