OBJECT Codeunit 5306 Outlook Synch. Export Schema
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      OSynchEntity@1005 : Record 5300;
      OSynchField@1004 : Record 5304;
      SortedEntitiesBuffer@1000 : TEMPORARY Record 5306;
      OSynchSetupDetail@1006 : Record 5310;
      OSynchTypeConversion@1007 : Codeunit 5302;
      OsynchOutlookMgt@1011 : Codeunit 5304;
      XMLWriter@1002 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter";
      Text001@1003 : TextConst 'ENU=The synchronization failed because the synchronization data could not be sent from Microsoft Dynamics NAV. Try again later and if the problem persists contact your system administrator.;ENG=The synchronisation failed because the synchronisation data could not be sent from Microsoft Dynamics NAV. Try again later and if the problem persists contact your system administrator.';
      Text002@1008 : TextConst 'ENU=The synchronization failed because the %1 user has no entities to synchronize. Try again later and if the problem persists contact your system administrator.;ENG=The synchronisation failed because the %1 user has no entities to synchronise. Try again later and if the problem persists contact your system administrator.';
      Text003@1009 : TextConst 'ENU=The synchronization failed because the synchronization data from Microsoft Outlook cannot be processed. Try again later and if the problem persists contact your system administrator.;ENG=The synchronisation failed because the synchronisation data from Microsoft Outlook cannot be processed. Try again later and if the problem persists contact your system administrator.';

    PROCEDURE Export@2(UserID@1000 : Text[50];VAR XMLMessage@1001 : Text);
    VAR
      OsynchNAVMgt@1002 : Codeunit 5301;
    BEGIN
      OsynchNAVMgt.GetSortedEntities(UserID,SortedEntitiesBuffer,TRUE);
      SendMappingScheme(UserID,XMLMessage);
    END;

    PROCEDURE SendMappingScheme@36(UserID@1002 : Code[50];VAR XMLMessage@1000 : Text);
    VAR
      XMLTextReader@1003 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextReader";
      TempDateTime@1007 : DateTime;
      OutlookCheckSum@1005 : Text[250];
      CurrentCheckSum@1006 : Text[250];
    BEGIN
      IF NOT SortedEntitiesBuffer.FIND('-') THEN
        ERROR(Text002,UserID);

      CLEAR(XMLTextReader);
      XMLTextReader := XMLTextReader.XmlTextReader;
      TempDateTime := OSynchTypeConversion.LocalDT2UTC(CURRENTDATETIME);

      IF NOT XMLTextReader.LoadXml(XMLMessage) THEN
        ERROR(Text003);

      IF ISNULL(XMLWriter) THEN
        XMLWriter := XMLWriter.XmlTextWriter;

      XMLWriter.WriteStartDocument;
      XMLWriter.WriteStartElement('Schema');
      XMLWriter.WriteAttribute('StartSynchTime',FORMAT(OSynchTypeConversion.SetDateTimeFormat(TempDateTime)));

      REPEAT
        OSynchEntity.GET(SortedEntitiesBuffer.Name);
        IF OSynchEntity."Outlook Item" <> '' THEN
          ComposeSynchEntityNode(OSynchEntity,UserID);
      UNTIL SortedEntitiesBuffer.NEXT = 0;

      XMLWriter.WriteEndElement;
      XMLWriter.WriteEndDocument;

      OutlookCheckSum := GetOutlookSchemaCheckSum(XMLTextReader);
      IF OutlookCheckSum <> '' THEN
        CurrentCheckSum := GetCurrentSchemaCheckSum(XMLWriter);

      IF (OutlookCheckSum <> '') AND (OutlookCheckSum = CurrentCheckSum) THEN
        PutCurrentSchemaCheckSum(XMLWriter,CurrentCheckSum,TempDateTime);

      XMLMessage := XMLWriter.ToString;

      IF STRLEN(XMLMessage) = 0 THEN BEGIN
        CLEAR(XMLWriter);
        ERROR(Text001);
      END;

      CLEAR(XMLWriter);
    END;

    LOCAL PROCEDURE ComposeSynchEntityNode@35(OSynchEntity1@1000 : Record 5300;UserID@1003 : Code[50]);
    VAR
      OSynchEntityElement@1002 : Record 5301;
      TempOSynchField@1004 : TEMPORARY Record 5304;
      OSynchUserSetup@1001 : Record 5305;
      IntVar@1005 : Integer;
    BEGIN
      IF ISNULL(XMLWriter) THEN
        EXIT;

      OSynchEntity1.TESTFIELD(Code);
      OSynchUserSetup.GET(UserID,OSynchEntity1.Code);
      IntVar := OSynchUserSetup."Synch. Direction";

      XMLWriter.WriteStartElement('SynchEntity');
      XMLWriter.WriteAttribute('Code',OSynchEntity1.Code);
      XMLWriter.WriteAttribute('Description',OSynchEntity1.Description);
      XMLWriter.WriteAttribute('OutlookItemType',OSynchEntity1."Outlook Item");
      XMLWriter.WriteAttribute('SynchDirection',FORMAT(IntVar));

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntity1.Code);
      OSynchField.SETRANGE("Element No.",0);
      OSynchField.SETFILTER("Outlook Object",'<>%1','');
      OSynchField.SETFILTER("Outlook Property",'<>%1','');
      IF OSynchField.FIND('-') THEN
        REPEAT
          TempOSynchField.RESET;
          TempOSynchField.SETRANGE("Synch. Entity Code",OSynchField."Synch. Entity Code");
          TempOSynchField.SETRANGE("Element No.",OSynchField."Element No.");
          TempOSynchField.SETRANGE("Outlook Property",OSynchField."Outlook Property");
          IF NOT TempOSynchField.FIND('-') THEN BEGIN
            TempOSynchField.INIT;
            TempOSynchField := OSynchField;
            TempOSynchField.INSERT;
          END;
        UNTIL OSynchField.NEXT = 0;

      TempOSynchField.RESET;
      IF TempOSynchField.FIND('-') THEN
        REPEAT
          ComposeFieldNode(TempOSynchField);
        UNTIL TempOSynchField.NEXT = 0;

      OSynchSetupDetail.RESET;
      OSynchSetupDetail.SETCURRENTKEY("Table No.");
      OSynchSetupDetail.SETRANGE("Synch. Entity Code",OSynchEntity1.Code);
      OSynchSetupDetail.SETRANGE("User ID",UserID);
      IF OSynchSetupDetail.FIND('-') THEN
        REPEAT
          OSynchEntityElement.GET(OSynchSetupDetail."Synch. Entity Code",OSynchSetupDetail."Element No.");
          IF OSynchEntityElement."Outlook Collection" <> '' THEN
            ComposeCollectionNode(OSynchEntityElement);
        UNTIL OSynchSetupDetail.NEXT = 0;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeFieldNode@34(OSynchFieldIn@1000 : Record 5304);
    VAR
      Field@1001 : Record 2000000041;
      RecRef@1003 : RecordRef;
      FldRef@1002 : FieldRef;
    BEGIN
      IF ISNULL(XMLWriter) THEN
        EXIT;

      OSynchFieldIn.TESTFIELD("Synch. Entity Code");
      OSynchFieldIn.TESTFIELD("Line No.");

      XMLWriter.WriteStartElement('Field');
      IF OSynchFieldIn."Outlook Property" <> '' THEN
        XMLWriter.WriteAttribute('Name',OSynchFieldIn."Outlook Property");

      IF OSynchFieldIn."Read-Only Status" <> OSynchFieldIn."Read-Only Status"::" " THEN BEGIN
        RecRef.GETTABLE(OSynchFieldIn);
        FldRef := RecRef.FIELD(OSynchFieldIn.FIELDNO("Read-Only Status"));

        XMLWriter.WriteAttribute('Read-OnlyStatus',SELECTSTR(OSynchFieldIn."Read-Only Status" + 1,FldRef.OPTIONSTRING));
        RecRef.CLOSE
      END;

      IF OSynchFieldIn."User-Defined" THEN
        XMLWriter.WriteAttribute('User-Defined',OSynchTypeConversion.SetBoolFormat(OSynchFieldIn."User-Defined"));

      IF OSynchFieldIn."Search Field" THEN
        XMLWriter.WriteAttribute('SearchKey',OSynchTypeConversion.SetBoolFormat(OSynchFieldIn."Search Field"));

      IF OSynchFieldIn."Table No." = 0 THEN
        Field.GET(OSynchFieldIn."Master Table No.",OSynchFieldIn."Field No.")
      ELSE
        Field.GET(OSynchFieldIn."Table No.",OSynchFieldIn."Field No.");

      IF Field.Type = Field.Type::BLOB THEN
        XMLWriter.WriteAttribute('Base64',OSynchTypeConversion.SetBoolFormat(TRUE));

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeCollectionNode@33(OSynchEntityElementIn@1000 : Record 5301);
    VAR
      TempOSynchField@1002 : TEMPORARY Record 5304;
      OSynchDependency@1001 : Record 5311;
    BEGIN
      IF ISNULL(XMLWriter) THEN
        EXIT;

      OSynchEntityElementIn.TESTFIELD("Synch. Entity Code");
      OSynchEntityElementIn.TESTFIELD("Element No.");

      XMLWriter.WriteStartElement('Collection');
      XMLWriter.WriteAttribute('Name',OSynchEntityElementIn."Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
      OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
      OSynchField.SETFILTER("Outlook Object",'<>%1','');
      OSynchField.SETFILTER("Outlook Property",'<>%1','');
      IF OSynchField.FIND('-') THEN
        REPEAT
          TempOSynchField.RESET;
          TempOSynchField.SETRANGE("Synch. Entity Code",OSynchField."Synch. Entity Code");
          TempOSynchField.SETRANGE("Element No.",OSynchField."Element No.");
          TempOSynchField.SETRANGE("Outlook Property",OSynchField."Outlook Property");
          IF NOT TempOSynchField.FIND('-') THEN BEGIN
            TempOSynchField.INIT;
            TempOSynchField := OSynchField;
            TempOSynchField.INSERT;
          END;
        UNTIL OSynchField.NEXT = 0;

      TempOSynchField.RESET;
      IF TempOSynchField.FIND('-') THEN
        REPEAT
          ComposeFieldNode(TempOSynchField);
        UNTIL TempOSynchField.NEXT = 0;

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code",TempOSynchField."Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.",TempOSynchField."Element No.");
      IF OSynchDependency.FINDFIRST THEN BEGIN
        TempOSynchField.SETRANGE("Search Field",TRUE);
        IF TempOSynchField.FIND('-') THEN
          REPEAT
            ComposeSearchFieldNode(TempOSynchField,OSynchDependency);
          UNTIL TempOSynchField.NEXT = 0;
      END;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeSearchFieldNode@32(OSynchFieldIn@1000 : Record 5304;OSynchDependency@1004 : Record 5311);
    VAR
      OSynchField1@1002 : Record 5304;
      SearchKeyBuffer@1001 : TEMPORARY Record 5306;
    BEGIN
      IF ISNULL(XMLWriter) THEN
        EXIT;

      XMLWriter.WriteStartElement('SearchKey');
      XMLWriter.WriteAttribute('Field',OSynchFieldIn."Outlook Property");

      IF OSynchDependency.FIND('-') THEN
        REPEAT
          OSynchField.RESET;
          OSynchField.SETRANGE("Synch. Entity Code",OSynchFieldIn."Synch. Entity Code");
          OSynchField.SETRANGE("Element No.",OSynchFieldIn."Element No.");
          OSynchField.SETRANGE("Outlook Property",OSynchFieldIn."Outlook Property");
          IF OSynchField.FIND('-') THEN
            REPEAT
              OSynchEntity.GET(OSynchDependency."Depend. Synch. Entity Code");
              OSynchField1.RESET;
              OSynchField1.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
              OSynchField1.SETRANGE("Element No.",0);
              IF OSynchField."Table No." = 0 THEN
                IF OSynchField."Master Table No." = OSynchEntity."Table No." THEN
                  OSynchField1.SETRANGE("Table No.",0)
                ELSE
                  OSynchField1.SETRANGE("Table No.",OSynchField."Master Table No.")
              ELSE
                IF OSynchField."Table No." = OSynchEntity."Table No." THEN
                  OSynchField1.SETRANGE("Table No.",0)
                ELSE
                  OSynchField1.SETRANGE("Table No.",OSynchField."Table No.");

              OSynchField1.SETRANGE("Field No.",OSynchField."Field No.");
              IF OSynchField1.FINDFIRST THEN BEGIN
                SearchKeyBuffer.RESET;
                SearchKeyBuffer.SETRANGE(Name,OSynchEntity.Code);
                IF NOT SearchKeyBuffer.FINDFIRST THEN BEGIN
                  XMLWriter.WriteStartElement('Entity');
                  XMLWriter.WriteAttribute('Name',OSynchEntity.Code);
                  XMLWriter.WriteAttribute('Field',OSynchField1."Outlook Property");
                  XMLWriter.WriteEndElement;

                  SearchKeyBuffer.INIT;
                  SearchKeyBuffer."Entry No." := SearchKeyBuffer."Entry No." + 1;
                  SearchKeyBuffer.Name := OSynchEntity.Code;
                  SearchKeyBuffer.INSERT;
                END;
              END;
            UNTIL OSynchField.NEXT = 0;
        UNTIL OSynchDependency.NEXT = 0;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE GetOutlookSchemaCheckSum@1(XmlTextReader@1000 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextReader") CheckSum : Text[250];
    VAR
      Container@1003 : Text;
      RootIterator@1002 : Text[38];
    BEGIN
      IF ISNULL(XmlTextReader) THEN
        ERROR(Text003);

      IF XmlTextReader.RootLocalName <> 'Schema' THEN
        ERROR(Text003);

      IF XmlTextReader.SelectElements(RootIterator,'child::CheckSum') > 0 THEN BEGIN
        Container := OsynchOutlookMgt.ConvertValueFromBase64(XmlTextReader.GetValue(RootIterator));
        CheckSum := COPYSTR(Container,1,250);
      END;
    END;

    LOCAL PROCEDURE GetCurrentSchemaCheckSum@37(VAR XMLTextWriter@1003 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter") CheckSum : Text[250];
    VAR
      XmlTextReader@1002 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextReader";
      Container@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      CarriageChar@1004 : Char;
      ReturnChar@1001 : Char;
    BEGIN
      XmlTextReader := XmlTextReader.XmlTextReader;
      XmlTextReader.Initialize(XMLTextWriter);
      XmlTextReader.XPathNavigator.MoveToRoot;
      Container := XmlTextReader.XPathNavigator.InnerXml;
      // Remove the first line of the container (contains timestamp)
      Container := Container.Substring(Container.IndexOf('<SynchEntity'));
      // Remove all \r from \r\n as the recipent over the wire will only recieve \n.
      CarriageChar := 13;
      ReturnChar := 10;
      Container := Container.Replace(FORMAT(CarriageChar) + FORMAT(ReturnChar),FORMAT(ReturnChar));
      CheckSum := OsynchOutlookMgt.ComputeHash(Container);
      CLEAR(XmlTextReader);
    END;

    LOCAL PROCEDURE PutCurrentSchemaCheckSum@3(VAR XMLTextWriter@1003 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter";CheckSumText@1002 : Text[250];StartDateTime@1000 : DateTime);
    VAR
      TempOSynchLink@1001 : TEMPORARY Record 5302;
      OutStrm@1004 : OutStream;
      EntryIDContainer@1005 : Text;
    BEGIN
      CLEAR(XMLTextWriter);
      CLEAR(EntryIDContainer);

      XMLTextWriter := XMLTextWriter.XmlTextWriter;

      XMLTextWriter.WriteStartDocument;
      XMLTextWriter.WriteStartElement('Schema');
      XMLWriter.WriteAttribute('StartSynchTime',FORMAT(OSynchTypeConversion.SetDateTimeFormat(StartDateTime)));

      XMLTextWriter.WriteStartElement('CheckSum');
      IF CheckSumText <> '' THEN BEGIN
        CLEAR(TempOSynchLink);
        CLEAR(OutStrm);
        TempOSynchLink.RESET;
        TempOSynchLink.INIT;
        TempOSynchLink."Outlook Entry ID".CREATEOUTSTREAM(OutStrm);
        TempOSynchLink.INSERT;
        IF OutStrm.WRITETEXT(CheckSumText,STRLEN(CheckSumText)) > 0 THEN BEGIN
          TempOSynchLink.CALCFIELDS("Outlook Entry ID");
          IF TempOSynchLink.GetEntryID(EntryIDContainer) THEN
            XMLTextWriter.WriteElementTextContent(OsynchOutlookMgt.ConvertValueToBase64(EntryIDContainer));
        END;
      END;
      XMLTextWriter.WriteEndElement;
      XMLTextWriter.WriteEndElement;
    END;

    BEGIN
    END.
  }
}

