OBJECT Table 9062443 Panel Property Field
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Parent Table ID");
               Rec.TESTFIELD("Parent Code");
               Rec.TESTFIELD("Panel Property ID");
               CheckModifyPermission();
               IF (Rec.ID = 0) THEN
                 Rec.VALIDATE(ID, NewId());
               IF (PanelToolkit.PanelTable(Rec."Parent Table ID")) THEN
                 PanelTimestampToolkit.Add(Rec."Parent Code");
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               CheckModifyPermission();
               IF (PanelToolkit.PanelTable(Rec."Parent Table ID")) THEN
                 PanelTimestampToolkit.Add(Rec."Parent Code");
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CheckModifyPermission();
               IF (PanelToolkit.PanelTable(Rec."Parent Table ID")) THEN
                 PanelTimestampToolkit.Add(Rec."Parent Code");
             END;

    OnRename=BEGIN
               CheckModifyPermission();
             END;

    CaptionML=[ENU=Panel Property Field;
               ENG=Panel Property Field];
  }
  FIELDS
  {
    { 1   ;   ;Parent Table ID     ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Parent Table ID;
                                                              ENG=Parent Table ID] }
    { 2   ;   ;Parent Code         ;Code20        ;TableRelation=Panel;
                                                   CaptionML=[ENU=Parent Code;
                                                              ENG=Parent Code] }
    { 3   ;   ;Parent ID           ;Integer       ;CaptionML=[ENU=Parent ID;
                                                              ENG=Parent ID] }
    { 4   ;   ;Parent ID 2         ;Integer       ;CaptionML=[ENU=Parent ID 2;
                                                              ENG=Parent ID 2] }
    { 7   ;   ;Panel Property ID   ;Integer       ;CaptionML=[ENU=Panel Property ID;
                                                              ENG=Panel Property ID] }
    { 8   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF (Rec."Panel Property ID" <> xRec."Panel Property ID") THEN
                                                                  Rec.VALIDATE("Field ID", 0);
                                                                Rec.CALCFIELDS("Table Name");
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 11  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name];
                                                   Editable=No }
    { 12  ;   ;Field ID            ;Integer       ;TableRelation=IF (Field Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(Table ID))
                                                                 ELSE IF (Field Type=CONST(Enhanced Field)) "Enhanced Field Definition"."Enhanced Field No." WHERE (Table No.=FIELD(Table ID));
                                                   OnValidate=BEGIN
                                                                Rec.CALCFIELDS("Field Name");
                                                              END;

                                                   CaptionML=[ENU=Field ID;
                                                              ENG=Field ID] }
    { 13  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table ID),
                                                                                             No.=FIELD(Field ID)));
                                                   CaptionML=[ENU=Field Name;
                                                              ENG=Field Name];
                                                   Editable=No }
    { 14  ;   ;Field Index         ;Integer       ;OnValidate=VAR
                                                                eIndexUsedAlready@1000000000 : TextConst 'ENU=Field index %1 has already been used for field "%2". It must be unique.;ENG=Field index %1 has already been used for field "%2". It must be unique.';
                                                                lPanelPropertyField@1000000001 : Record 9062443;
                                                              BEGIN
                                                                IF (Rec."Field Index" <> xRec."Field Index") THEN
                                                                  IF (Rec."Field Index" <> 0) THEN
                                                                  BEGIN
                                                                    lPanelPropertyField.RESET();
                                                                    lPanelPropertyField.SETRANGE("Parent Table ID", Rec."Parent Table ID");
                                                                    lPanelPropertyField.SETRANGE("Parent Code", Rec."Parent Code");
                                                                    lPanelPropertyField.SETRANGE("Parent ID", Rec."Parent ID");
                                                                    lPanelPropertyField.SETRANGE("Parent ID 2", Rec."Parent ID 2");
                                                                    lPanelPropertyField.SETRANGE("Panel Property ID", Rec."Panel Property ID");
                                                                    lPanelPropertyField.SETRANGE("Field Index", Rec."Field Index");
                                                                    IF (lPanelPropertyField.FINDFIRST()) THEN
                                                                      ERROR(eIndexUsedAlready, Rec."Field Index", lPanelPropertyField."Field Name");
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Field Index;
                                                              ENG=Field Index];
                                                   MinValue=0;
                                                   MaxValue=10 }
    { 15  ;   ;Field Type          ;Option        ;CaptionML=[ENU=Line Type;
                                                              ENG=Line Type];
                                                   OptionCaptionML=[ENU=Field,Enhanced Field;
                                                                    ENG=Field,Enhanced Field];
                                                   OptionString=Field,Enhanced Field }
    { 16  ;   ;Enhanced Field Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Enhanced Field Definition".Name WHERE (Table No.=FIELD(Table ID),
                                                                                                              Enhanced Field No.=FIELD(Field ID)));
                                                   CaptionML=[ENU=Enhanced Field Name;
                                                              ENG=Enhanced Field Name];
                                                   Editable=No }
    { 17  ;   ;Enhanced Field Type ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Enhanced Field Definition".Type WHERE (Table No.=FIELD(Table ID),
                                                                                                              Enhanced Field No.=FIELD(Field ID)));
                                                   CaptionML=[ENU=Enhanced Field Type;
                                                              ENG=Enhanced Field Type];
                                                   OptionCaptionML=[ENU=Text,Code,Integer,Decimal,Date,Time;
                                                                    ENG=Text,Code,Integer,Decimal,Date,Time];
                                                   OptionString=Text,Code,Integer,Decimal,Date,Time;
                                                   Editable=No }
    { 20  ;   ;Parent Table Name   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Parent Table ID)));
                                                   CaptionML=[ENU=Parent Table Name;
                                                              ENG=Parent Table Name];
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Parent Table ID,Parent Code,Parent ID,Parent ID 2,Panel Property ID,ID;
                                                   Clustered=Yes }
    {    ;Parent Table ID,Parent Code,Parent ID,Parent ID 2,Panel Property ID,Field Index }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Panel@1000000006 : Record 9062363;
      PanelProperty@1000000005 : Record 9062433;
      EnhancedPermissionsMgt@1000000007 : Codeunit 9062277;
      PanelTimestampToolkit@1000000001 : Codeunit 9062385;
      PanelToolkit@1000000004 : Codeunit 9062320;
      TnpToolkit@1000000000 : Codeunit 9062230;
      tBlob@1000000003 : TextConst 'ENU=BLOB;ENG=BLOB';

    PROCEDURE "--- Initialisation ---"@1000000003();
    BEGIN
    END;

    PROCEDURE NewId@1000000000() : Integer;
    VAR
      lPanelPropertyField@1000000000 : Record 9062443;
    BEGIN
      //DOC NA2015.1 - Returning new ID
      //  <- New ID

      Rec.TESTFIELD("Parent Table ID");
      Rec.TESTFIELD("Parent Code");
      Rec.TESTFIELD("Panel Property ID");

      lPanelPropertyField.RESET();
      lPanelPropertyField.SETRANGE("Parent Table ID", Rec."Parent Table ID");
      lPanelPropertyField.SETRANGE("Parent Code", Rec."Parent Code");
      lPanelPropertyField.SETRANGE("Parent ID", Rec."Parent ID");
      lPanelPropertyField.SETRANGE("Parent ID 2", Rec."Parent ID 2");
      lPanelPropertyField.SETRANGE("Panel Property ID", Rec."Panel Property ID");
      IF (lPanelPropertyField.FINDLAST()) THEN
        EXIT(lPanelPropertyField.ID + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE NewFieldIndex@1000000002() : Integer;
    VAR
      lPanelPropertyField@1000000001 : Record 9062443;
    BEGIN
      //DOC NA2015.1 - Returning new field index
      //  <- New index

      lPanelPropertyField.RESET();
      lPanelPropertyField.SETCURRENTKEY("Parent Table ID", "Parent Code", "Parent ID", "Parent ID 2", "Panel Property ID", "Field Index");
      lPanelPropertyField.SETRANGE("Parent Table ID", Rec."Parent Table ID");
      lPanelPropertyField.SETRANGE("Parent Code", Rec."Parent Code");
      lPanelPropertyField.SETRANGE("Parent ID", Rec."Parent ID");
      lPanelPropertyField.SETRANGE("Parent ID 2", Rec."Parent ID 2");
      lPanelPropertyField.SETRANGE("Panel Property ID", Rec."Panel Property ID");
      IF (lPanelPropertyField.FINDLAST()) THEN
        EXIT(lPanelPropertyField."Field Index" + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE NewRecord@1000000001();
    BEGIN
      //DOC NA2015.1 - Initialising the record

      IF ( NOT (PanelProperty.GET(Rec."Parent Table ID", Rec."Parent Code", Rec."Parent ID", Rec."Parent ID 2", Rec."Panel Property ID"))) THEN
        CLEAR(PanelProperty);

      Rec.VALIDATE("Table ID", PanelProperty."Table ID");
      Rec.VALIDATE("Field Index", NewFieldIndex());
    END;

    LOCAL PROCEDURE "--- General ---"@1000000004();
    BEGIN
    END;

    LOCAL PROCEDURE CheckModifyPermission@1000000041();
    BEGIN
      //DOC NA2016.15 - Checking modify permission (given to the panel)

      IF ( NOT (PanelToolkit.PanelTable(Rec."Parent Table ID"))) THEN
        EXIT;
      IF ( NOT (Panel.GET(Rec."Parent Code"))) THEN
        EXIT;
      EnhancedPermissionsMgt.CheckPanelModifyPermission(Panel, USERID());
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 28/10/2013 - Created
      //DOC NA2015.1  JH 15/12/2013 - Properties and sources integrated
      //DOC NA2015.1  JH 25/04/2014 - Tile panels 3
      //DOC NA2016.1  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 05/05/2016 - Enhanced permissions
    }
    END.
  }
}

