OBJECT Codeunit 6 Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00 PM;
    Version List=NAVW13.00,NAVGB6.00;
  }
  PROPERTIES
  {
    TableNo=10560;
    OnRun=BEGIN
            AccountingPeriod.COPY(Rec);
            Code;
            Rec := AccountingPeriod;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;ENG=You must create a new fiscal year before you can close the old year.';
      Text002@1001 : TextConst 'ENU="This function closes the fiscal year from %1 to %2. ";ENG="This function closes the fiscal year from %1 to %2. "';
      Text003@1002 : TextConst 'ENU=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\;ENG=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\';
      Text004@1003 : TextConst 'ENU=Do you want to close the fiscal year?;ENG=Do you want to close the fiscal year?';
      AccountingPeriod@1004 : Record 10560;
      AccountingPeriod2@1005 : Record 10560;
      AccountingPeriod3@1006 : Record 10560;
      FiscalYearStartDate@1007 : Date;
      FiscalYearEndDate@1008 : Date;
      Text1041000@1041000 : TextConst 'ENU=%1 must be %2;ENG=%1 must be %2';

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH AccountingPeriod DO BEGIN
        IF Closed THEN
          EXIT;
        IF "Period Type" <> "Period Type"::Year THEN BEGIN
          "Period Type" := "Period Type"::Year;
          ERROR(
            Text1041000,
            FIELDCAPTION("Period Type"),"Period Type");
        END;
        TESTFIELD("Period Start");
        TESTFIELD("Period End");
        AccountingPeriod2 := AccountingPeriod;
        AccountingPeriod2.SETRANGE("Period Type","Period Type"::Year);
        IF AccountingPeriod2.NEXT(-1) <> 0 THEN
          AccountingPeriod2.TESTFIELD(Closed,TRUE);
        AccountingPeriod2 := AccountingPeriod;
        IF AccountingPeriod2.NEXT = 0 THEN
          ERROR(Text001);

        IF NOT
           CONFIRM(
             Text002 +
             Text003 +
             Text004,FALSE,
             "Period Start",NORMALDATE("Period End"))
        THEN
          EXIT;

        RESET;
        SETRANGE("Period Start","Period Start","Period End");
        MODIFYALL(Closed,TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

