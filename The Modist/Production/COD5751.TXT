OBJECT Codeunit 5751 Get Source Doc. Inbound
{
  OBJECT-PROPERTIES
  {
    Date=13/08/18;
    Time=[ 1:31:04 PM];
    Modified=Yes;
    Version List=NAVW19.00,TM3003;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GetSourceDocuments@1000 : Report 5753;
      GetSourceDocuments2G@1000000000 : Report 50034;

    LOCAL PROCEDURE CreateWhseReceiptHeaderFromWhseRequest@7(VAR WarehouseRequest@1000 : Record 5765) : Boolean;
    BEGIN
      IF WarehouseRequest.ISEMPTY THEN
        EXIT(FALSE);

      CLEAR(GetSourceDocuments);
      GetSourceDocuments.USEREQUESTPAGE(FALSE);
      GetSourceDocuments.SETTABLEVIEW(WarehouseRequest);
      GetSourceDocuments.SetHideDialog(TRUE);
      GetSourceDocuments.RUNMODAL;

      EXIT(TRUE);
    END;

    PROCEDURE GetInboundDocs@1(WhseReceiptHeader@1000 : Record 7316);
    VAR
      WhseGetSourceFilterRec@1001 : Record 5771;
      WhseSourceFilterSelection@1002 : Page 5784;
    BEGIN
      WhseReceiptHeader.FIND;
      WhseSourceFilterSelection.SetOneCreatedReceiptHeader(WhseReceiptHeader);
      WhseGetSourceFilterRec.FILTERGROUP(2);
      WhseGetSourceFilterRec.SETRANGE(Type,WhseGetSourceFilterRec.Type::Inbound);
      WhseGetSourceFilterRec.FILTERGROUP(0);
      WhseSourceFilterSelection.SETTABLEVIEW(WhseGetSourceFilterRec);
      WhseSourceFilterSelection.RUNMODAL;
    END;

    PROCEDURE GetSingleInboundDoc@2(WhseReceiptHeader@1000 : Record 7316);
    VAR
      WhseRqst@1001 : Record 5765;
      SourceDocSelection@1003 : Page 5793;
    BEGIN
      CLEAR(GetSourceDocuments);
      WhseReceiptHeader.FIND;

      WhseRqst.FILTERGROUP(2);
      WhseRqst.SETRANGE(Type,WhseRqst.Type::Inbound);
      WhseRqst.SETRANGE("Location Code",WhseReceiptHeader."Location Code");
      WhseRqst.FILTERGROUP(0);
      WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
      WhseRqst.SETRANGE("Completely Handled",FALSE);

      SourceDocSelection.LOOKUPMODE(TRUE);
      SourceDocSelection.SETTABLEVIEW(WhseRqst);
      IF SourceDocSelection.RUNMODAL <> ACTION::LookupOK THEN
        EXIT;
      SourceDocSelection.GetResult(WhseRqst);

      GetSourceDocuments.SetOneCreatedReceiptHeader(WhseReceiptHeader);
      GetSourceDocuments.USEREQUESTPAGE(FALSE);
      GetSourceDocuments.SETTABLEVIEW(WhseRqst);
      GetSourceDocuments.RUNMODAL;
    END;

    PROCEDURE CreateFromPurchOrder@3(PurchHeader@1000 : Record 38);
    BEGIN
      ShowDialog(CreateFromPurchOrderHideDialog(PurchHeader));
    END;

    PROCEDURE CreateFromPurchOrderHideDialog@15(PurchHeader@1000 : Record 38) : Boolean;
    VAR
      WhseRqst@1001 : Record 5765;
    BEGIN
      FindWarehouseRequestForPurchaseOrder(WhseRqst,PurchHeader);
      EXIT(CreateWhseReceiptHeaderFromWhseRequest(WhseRqst));
    END;

    PROCEDURE CreateFromSalesReturnOrder@5(SalesHeader@1000 : Record 36);
    BEGIN
      ShowDialog(CreateFromSalesReturnOrderHideDialog(SalesHeader));
    END;

    PROCEDURE CreateFromSalesReturnOrderHideDialog@16(SalesHeader@1000 : Record 36) : Boolean;
    VAR
      WhseRqst@1001 : Record 5765;
    BEGIN
      FindWarehouseRequestForSalesReturnOrder(WhseRqst,SalesHeader);
      EXIT(CreateWhseReceiptHeaderFromWhseRequest(WhseRqst));
    END;

    PROCEDURE CreateFromInbndTransferOrder@4(TransHeader@1000 : Record 5740);
    BEGIN
      ShowDialog(CreateFromInbndTransferOrderHideDialog(TransHeader));
    END;

    PROCEDURE CreateFromInbndTransferOrderHideDialog@17(TransHeader@1000 : Record 5740) : Boolean;
    VAR
      WhseRqst@1001 : Record 5765;
    BEGIN
      FindWarehouseRequestForInbndTransferOrder(WhseRqst,TransHeader);
      EXIT(CreateWhseReceiptHeaderFromWhseRequest(WhseRqst));
    END;

    PROCEDURE GetSingleWhsePutAwayDoc@10(CurrentWkshTemplateName@1008 : Code[10];CurrentWkshName@1004 : Code[10];LocationCode@1000 : Code[10]);
    VAR
      WhsePutAwayRqst@1001 : Record 7324;
      WhsePutAwayDocSelection@1003 : Page 7334;
      GetWhseSourceDocuments@1007 : Report 7306;
    BEGIN
      WhsePutAwayRqst.FILTERGROUP(2);
      WhsePutAwayRqst.SETRANGE("Completely Put Away",FALSE);
      WhsePutAwayRqst.SETRANGE("Location Code",LocationCode);
      WhsePutAwayRqst.FILTERGROUP(0);

      WhsePutAwayDocSelection.LOOKUPMODE(TRUE);
      WhsePutAwayDocSelection.SETTABLEVIEW(WhsePutAwayRqst);
      IF WhsePutAwayDocSelection.RUNMODAL <> ACTION::LookupOK THEN
        EXIT;

      WhsePutAwayDocSelection.GetResult(WhsePutAwayRqst);

      GetWhseSourceDocuments.SetWhseWkshName(
        CurrentWkshTemplateName,CurrentWkshName,LocationCode);

      GetWhseSourceDocuments.USEREQUESTPAGE(FALSE);
      GetWhseSourceDocuments.SETTABLEVIEW(WhsePutAwayRqst);
      GetWhseSourceDocuments.RUNMODAL;
    END;

    LOCAL PROCEDURE GetRequireReceiveRqst@6(VAR WhseRqst@1002 : Record 5765);
    VAR
      Location@1000 : Record 14;
      LocationCode@1001 : Text;
    BEGIN
      IF WhseRqst.FINDSET THEN BEGIN
        REPEAT
          IF Location.RequireReceive(WhseRqst."Location Code") THEN
            LocationCode += WhseRqst."Location Code" + '|';
        UNTIL WhseRqst.NEXT = 0;
        IF LocationCode <> '' THEN
          LocationCode := COPYSTR(LocationCode,1,STRLEN(LocationCode) - 1);
        WhseRqst.SETFILTER("Location Code",LocationCode);
      END;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForPurchaseOrder@8(VAR WhseRqst@1000 : Record 5765;PurchHeader@1001 : Record 38);
    BEGIN
      WITH PurchHeader DO BEGIN
        TESTFIELD(Status,Status::Released);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Inbound);
        WhseRqst.SETRANGE("Source Type",DATABASE::"Purchase Line");
        WhseRqst.SETRANGE("Source Subtype","Document Type");
        WhseRqst.SETRANGE("Source No.","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireReceiveRqst(WhseRqst);
      END;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForSalesReturnOrder@9(VAR WhseRqst@1000 : Record 5765;SalesHeader@1001 : Record 36);
    BEGIN
      WITH SalesHeader DO BEGIN
        TESTFIELD(Status,Status::Released);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Inbound);
        WhseRqst.SETRANGE("Source Type",DATABASE::"Sales Line");
        WhseRqst.SETRANGE("Source Subtype","Document Type");
        WhseRqst.SETRANGE("Source No.","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireReceiveRqst(WhseRqst);
      END;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForInbndTransferOrder@12(VAR WhseRqst@1000 : Record 5765;TransHeader@1001 : Record 5740);
    BEGIN
      WITH TransHeader DO BEGIN
        TESTFIELD(Status,Status::Released);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Inbound);
        WhseRqst.SETRANGE("Source Type",DATABASE::"Transfer Line");
        WhseRqst.SETRANGE("Source Subtype",1);
        WhseRqst.SETRANGE("Source No.","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireReceiveRqst(WhseRqst);
      END;
    END;

    LOCAL PROCEDURE OpenWarehouseReceiptPage@14();
    VAR
      WarehouseReceiptHeader@1000 : Record 7316;
    BEGIN
      GetSourceDocuments.GetLastReceiptHeader(WarehouseReceiptHeader);
      PAGE.RUN(PAGE::"Warehouse Receipt",WarehouseReceiptHeader);
    END;

    LOCAL PROCEDURE ShowDialog@11(WhseReceiptCreated@1000 : Boolean);
    BEGIN
      GetSourceDocuments.ShowReceiptDialog;
      IF WhseReceiptCreated THEN
        OpenWarehouseReceiptPage;
    END;

    LOCAL PROCEDURE __TM3003__@1000000004();
    BEGIN
    END;

    PROCEDURE CreateFromPurchOrderCopy@1000000002(PurchHeader@1000 : Record 38);
    BEGIN
      //Doc TM3003 ST 03/07/2018 -
      ShowDialogCopy(CreateFromPurchOrderHideDialogCopy(PurchHeader));
      //Doc TM3003 ST 03/07/2018 +
    END;

    PROCEDURE CreateFromPurchOrderHideDialogCopy@1000000000(PurchHeader@1000 : Record 38) : Boolean;
    VAR
      WhseRqst@1001 : Record 5765;
    BEGIN
      //Doc TM3003 ST 03/07/2018 -
      FindWarehouseRequestForPurchaseOrder(WhseRqst,PurchHeader);
      EXIT(CreateWhseReceiptHeaderFromWhseRequestCopy(WhseRqst));
      //Doc TM3003 ST 03/07/2018 +
    END;

    LOCAL PROCEDURE CreateWhseReceiptHeaderFromWhseRequestCopy@1000000001(VAR WarehouseRequest@1000 : Record 5765) : Boolean;
    BEGIN
      //Doc TM3003 ST 03/07/2018 -
      IF WarehouseRequest.ISEMPTY THEN
        EXIT(FALSE);

      CLEAR(GetSourceDocuments2G);
      GetSourceDocuments2G.USEREQUESTPAGE(FALSE);
      GetSourceDocuments2G.SETTABLEVIEW(WarehouseRequest);
      GetSourceDocuments2G.SetHideDialog(TRUE);
      GetSourceDocuments2G.RUNMODAL;
      EXIT(TRUE);
      //Doc TM3003 ST 03/07/2018 +
    END;

    LOCAL PROCEDURE ShowDialogCopy@1000000003(WhseReceiptCreated@1000 : Boolean);
    BEGIN
      //Doc TM3003 ST 03/07/2018 -
      GetSourceDocuments2G.ShowReceiptDialog;
      IF WhseReceiptCreated THEN
        OpenWarehouseReceiptPageCopy;
      //Doc TM3003 ST 03/07/2018 +
    END;

    LOCAL PROCEDURE OpenWarehouseReceiptPageCopy@1000000005();
    VAR
      WarehouseReceiptHeader@1000 : Record 7316;
    BEGIN
      //Doc TM3003 ST 03/07/2018 -
      GetSourceDocuments2G.GetLastReceiptHeader(WarehouseReceiptHeader);
      PAGE.RUN(PAGE::"Warehouse Receipt",WarehouseReceiptHeader);
      //Doc TM3003 ST 03/07/2018 +
    END;

    BEGIN
    {
      //Doc TM3003 ST 03/07/2018 - Added new functions.
    }
    END.
  }
}

