OBJECT Table 9062628 Criteria Template
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(Code);
               CreateCriteria();
               mTnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               mTnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               mCriteriaManagement.DeleteCriteria("Criteria ID");
             END;

    CaptionML=[ENU=Criteria Template;
               ENG=Criteria Template];
    LookupPageID=Page9062890;
    DrillDownPageID=Page9062890;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                mTnpToolkit.CodeToName(Rec.Code, Rec.Name);
                                                                Rec.VALIDATE(Name);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 10  ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                UpdateCriteria(Rec.FIELDNO(Name));
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 11  ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                UpdateCriteria(Rec.FIELDNO("Table ID"));
                                                                Rec.CALCFIELDS("Table Name");
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 12  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name];
                                                   Editable=No }
    { 13  ;   ;Criteria ID         ;Integer       ;TableRelation=Criteria;
                                                   CaptionML=[ENU=Criteria ID;
                                                              ENG=Criteria ID] }
    { 14  ;   ;Description         ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Criteria.Description WHERE (ID=FIELD(Criteria ID)));
                                                   CaptionML=[ENU=Description;
                                                              ENG=Description];
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              mTnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              mTnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      mCriteria@1000000002 : Record 9062626;
      mCriteriaTemplate@1000000004 : Record 9062628;
      mCriteriaManagement@1000000001 : Codeunit 9062462;
      mTnpToolkit@1000000000 : Codeunit 9062230;
      mSystemActivity@1000000003 : Boolean;

    PROCEDURE SetSystemActivity@1000000002(pSystemActivity@1000000000 : Boolean);
    BEGIN
      //DOC NA2015.3 - Setting system activity flag

      mSystemActivity := pSystemActivity;
    END;

    LOCAL PROCEDURE CreateCriteria@1000000004();
    BEGIN
      //DOC NA2015.3 - Creating criteria

      Rec."Criteria ID" := mCriteriaManagement.CreateCriteriaAttached(DATABASE::"Criteria Template", Rec.GETPOSITION(FALSE),
        Rec.FIELDNO("Criteria ID"), FALSE);

      UpdateCriteria(Rec.FIELDNO(Name));
      UpdateCriteria(Rec.FIELDNO("Table ID"));
    END;

    PROCEDURE UpdateCriteria@1000000001(pFieldID@1000000000 : Integer);
    BEGIN
      //DOC NA2015.3 - Updating criteria
      //  -> pFieldID: Field ID

      IF ((mSystemActivity) OR ("Criteria ID" = 0)) THEN
        EXIT;

      IF (mCriteria.GET("Criteria ID")) THEN
      BEGIN
        mCriteria.SetSystemActivity(TRUE);
        CASE (pFieldID) OF
          Rec.FIELDNO(Name):
            mCriteria.VALIDATE(Name, Rec.Name);
          Rec.FIELDNO("Table ID"):
            mCriteria.VALIDATE("Table ID", Rec."Table ID");
        END;
        mCriteria.MODIFY(TRUE);
        mCriteria.SetSystemActivity(FALSE);
      END;
    END;

    PROCEDURE ShowCriteria@1000000000();
    BEGIN
      //DOC NA2015.3 - Showing criteria

      mCriteriaManagement.ShowCriteria("Criteria ID");
    END;

    PROCEDURE AddFields@1000000005();
    BEGIN
      //DOC NA2015.3 - Adding fields

      Rec.TESTFIELD("Criteria ID");
      mCriteriaManagement.AddCriteriaFields(Rec."Criteria ID");
    END;

    BEGIN
    {
      //DOC NA2015.3  JH 17/09/2014 - Created
      //DOC NA2016.3  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.12 JH 27/12/2015 - Criteria references changed
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

