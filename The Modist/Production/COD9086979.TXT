OBJECT Codeunit 9086979 nF. BLOB Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=30/10/16;
    Time=12:09:12 PM;
    Version List=NF2016.17;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      tBase64Chars@1000000000 : TextConst 'ENU=ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/;ENG=ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/';

    PROCEDURE "--- Text ---"@1000000014();
    BEGIN
    END;

    PROCEDURE TextToBlob@1000000007(pText@1000000000 : Text;VAR pBlob@1000000001 : Record 99008535);
    VAR
      lOutStream@1000000002 : OutStream;
    BEGIN
      ///<summary>Writing text to BLOB</summary>
      ///<param name="pText">The text</param>
      ///<param name="pBlob">Target BLOB</param>

      CLEAR(pBlob);
      pBlob.Blob.CREATEOUTSTREAM(lOutStream);
      lOutStream.WRITETEXT(pText);
    END;

    PROCEDURE BlobToText@1000000017(VAR pBlob@1000000000 : Record 99008535) : Text;
    VAR
      lText@1000000001 : Text;
      lTextPart@1000000002 : Text[1024];
      lBigText@1000000003 : BigText;
      lBigTextSteps@1000000004 : Integer;
      lBigTextStep@1000000005 : Integer;
      lInStream@1000000006 : InStream;
    BEGIN
      ///<summary>Writing BLOB to text</summary>
      ///<param name="pBlob">Source BLOB</param>
      ///<returns>Text</returns>

      CLEAR(lText);
      pBlob.Blob.CREATEINSTREAM(lInStream);
      lBigText.READ(lInStream);
      lBigTextSteps := ROUND(lBigText.LENGTH / 1024, 1, '>');
      FOR lBigTextStep := 1 TO lBigTextSteps DO
      BEGIN
        lBigText.GETSUBTEXT(lTextPart, (lBigTextStep - 1) * 1024 + 1, 1024);
        lText := lText + lTextPart;
      END;

      EXIT(lText);
    END;

    LOCAL PROCEDURE "--- Big Text ---"@1000000021();
    BEGIN
    END;

    PROCEDURE BigTextToBlob@1000000023(pBigText@1000000000 : BigText;VAR pBlob@1000000001 : Record 99008535);
    VAR
      lOutStream@1000000002 : OutStream;
    BEGIN
      ///<summary>Writing big text to BLOB</summary>
      ///<param name="pBigText">Source big text</param>
      ///<param name="pBlob">Destingation BLOB</param>

      CLEAR(pBlob);
      pBlob.Blob.CREATEOUTSTREAM(lOutStream);
      pBigText.WRITE(lOutStream);
    END;

    PROCEDURE BlobToBigText@1000000022(VAR pBlob@1000000000 : Record 99008535;VAR pBigText@1000000007 : BigText);
    VAR
      lInStream@1000000001 : InStream;
    BEGIN
      ///<summary>Writing BLOB to big text</summary>
      ///<param name="pBlob">Source BLOB</param>
      ///<param name="pBigText">Destination big text</param>

      CLEAR(pBigText);
      pBlob.Blob.CREATEINSTREAM(lInStream);
      pBigText.READ(lInStream);
    END;

    BEGIN
    {
      ///<summary>Library containing BLOB-related functions</summary>
      ///<change version="NF2016.17" initials="JH" date="2016-10-27">Created</change>
    }
    END.
  }
}

