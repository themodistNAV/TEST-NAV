OBJECT Codeunit 9062592 AutoNAV Task Queue Start Cdu.
{
  OBJECT-PROPERTIES
  {
    Date=17/12/15;
    Time=11:34:57 AM;
    Version List=NA2016.12;
  }
  PROPERTIES
  {
    TableNo=9062734;
    OnRun=BEGIN
            IF "User Language ID" <> 0 THEN
              GLOBALLANGUAGE("User Language ID");

            CASE "Object Type to Run" OF
              "Object Type to Run"::Codeunit:
                CODEUNIT.RUN("Object ID to Run",Rec);
              "Object Type to Run"::Report:
                RunReport("Object ID to Run",Rec);
            END;

            // Commit any remaining transactions from the target codeunit\report. This is necessary due
            // to buffered record insertion which may not have surfaced errors in CODEUNIT.RUN above.
            COMMIT;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE RunReport@1(ReportID@1000 : Integer;VAR AutoNavTask@1006 : Record 9062734);
    VAR
      ReportInbox@1003 : Record 477;
      RecRef@1002 : RecordRef;
      OutStr@1004 : OutStream;
      RunOnRec@1001 : Boolean;
    BEGIN
      ReportInbox.INIT;
      ReportInbox."User ID" := USERID;
      ReportInbox."Report ID" := ReportID;
      ReportInbox.Description := AutoNavTask.Description;
      ReportInbox."Report Output".CREATEOUTSTREAM(OutStr);
      RunOnRec := RecRef.GET(AutoNavTask."Record ID to Process");
      IF RunOnRec THEN
        RecRef.SETRECFILTER;

      CASE AutoNavTask."Report Output Type" OF
        AutoNavTask."Report Output Type"::"None (Processing only)":
          IF RunOnRec THEN
            REPORT.EXECUTE(ReportID,AutoNavTask.GetReportParameters,RecRef)
          ELSE
            REPORT.EXECUTE(ReportID,AutoNavTask.GetReportParameters);
        AutoNavTask."Report Output Type"::Print:
          IF RunOnRec THEN
            REPORT.PRINT(ReportID,AutoNavTask.GetReportParameters,AutoNavTask."Printer Name",RecRef)
          ELSE
            REPORT.PRINT(ReportID,AutoNavTask.GetReportParameters,AutoNavTask."Printer Name");
        AutoNavTask."Report Output Type"::PDF:
          BEGIN
            IF RunOnRec THEN
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Pdf,OutStr,RecRef)
            ELSE
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Pdf,OutStr);
            ReportInbox."Output Type" := ReportInbox."Output Type"::PDF;
          END;
        AutoNavTask."Report Output Type"::Word:
          BEGIN
            IF RunOnRec THEN
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Word,OutStr,RecRef)
            ELSE
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Word,OutStr);
            ReportInbox."Output Type" := ReportInbox."Output Type"::Word;
          END;
        AutoNavTask."Report Output Type"::Excel:
          BEGIN
            IF RunOnRec THEN
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Excel,OutStr,RecRef)
            ELSE
              REPORT.SAVEAS(ReportID,AutoNavTask.GetReportParameters,REPORTFORMAT::Excel,OutStr);
            ReportInbox."Output Type" := ReportInbox."Output Type"::Excel;
          END;
      END;

      CASE AutoNavTask."Report Output Type" OF
        AutoNavTask."Report Output Type"::"None (Processing only)":
          BEGIN
            AutoNavTask."Notify On Success" := TRUE;
            AutoNavTask.MODIFY;
          END;
        AutoNavTask."Report Output Type"::Print:
          ;
        ELSE BEGIN
          ReportInbox."Created Date-Time" := ROUNDDATETIME(CURRENTDATETIME,60000);
          ReportInbox.INSERT(TRUE);
        END;
      END;
      COMMIT;
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 17/12/2015 - Updated from standard
    }
    END.
  }
}

