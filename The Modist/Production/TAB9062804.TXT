OBJECT Table 9062804 Enhanced View
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=VAR
               ControlTemplate@1000000000 : Record 9062799;
               InitEnhancedViews@1000000001 : Codeunit 9062691;
             BEGIN
               IF ("Control Template Code" = '') THEN
                 VALIDATE("Control Template Code",
                   HtmlControlTemplMgt.GetDefaultTemplate(ControlTemplate."Control Type"::"Enhanced View"));

               IF ("Control Template Code" = '') THEN BEGIN
                 InitEnhancedViews.InitializeEnhancedViews(FALSE);
                 VALIDATE("Control Template Code",
                   HtmlControlTemplMgt.GetDefaultTemplate(ControlTemplate."Control Type"::"Enhanced View"));
               END;

               IF (NOT(Rec.ISTEMPORARY)) THEN
                 EnhViewMgt.CreateUpdateDefaultFields(Rec);
             END;

    OnDelete=BEGIN
               IF (NOT(Rec.ISTEMPORARY)) THEN BEGIN
                 EnhViewColumn.RESET();
                 EnhViewColumn.SETCURRENTKEY("View Code");
                 EnhViewColumn.SETRANGE("View Code", Code);
                 EnhViewColumn.DELETEALL(TRUE);

                 //clear default group and totals fields before calling delete with trigger
                 EnhViewField.RESET();
                 EnhViewField.SETRANGE("View Code", Code);
                 EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                 EnhViewField.SETRANGE("Default Group", TRUE);
                 EnhViewField.MODIFYALL("Default Group", FALSE);
                 EnhViewField.SETRANGE("Default Group");
                 EnhViewField.SETRANGE("Default Totals", TRUE);
                 EnhViewField.MODIFYALL("Default Totals", FALSE);

                 //delete records
                 EnhViewField.RESET();
                 EnhViewField.SETRANGE("View Code", Code);
                 EnhViewField.DELETEALL(TRUE);

                 EnhViewStyleCache.RESET();
                 EnhViewStyleCache.SETRANGE("View Code", Code);
                 EnhViewStyleCache.DELETEALL();

                 EnhViewStyle.RESET();
                 EnhViewStyle.SETRANGE("View Code", Code);
                 EnhViewStyle.DELETEALL(TRUE);

                 CriteriaMgt.DeleteCriteria("Source Filter Criteria Id");
               END;
             END;

    CaptionML=[ENU=Enhanced View;
               ENG=Enhanced View];
    LookupPageID=Page9063222;
    DrillDownPageID=Page9063222;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 3   ;   ;Read Only           ;Boolean       ;CaptionML=[ENU=Read Only;
                                                              ENG=Read Only] }
    { 9   ;   ;Fixed Columns       ;Boolean       ;CaptionML=[ENU=Fixed Columns;
                                                              ENG=Fixed Columns] }
    { 10  ;   ;Control Template Code;Code50       ;TableRelation="Html Control Template" WHERE (Control Type=CONST(Enhanced View));
                                                   OnValidate=VAR
                                                                HtmlControlView@1000000000 : Record 9062800;
                                                              BEGIN
                                                                IF ("Control Template Code" <> xRec."Control Template Code") THEN
                                                                  VALIDATE("Control View Code", HtmlControlTemplMgt.GetDefaultView("Control Template Code"));
                                                              END;

                                                   CaptionML=[ENU=Control Template Code;
                                                              ENG=Control Template Code] }
    { 11  ;   ;Control View Code   ;Code20        ;TableRelation="Html Control View".Code WHERE (Template Code=FIELD(Control Template Code));
                                                   CaptionML=[ENU=Control View Code;
                                                              ENG=Control View Code] }
    { 12  ;   ;View Mode           ;Option        ;CaptionML=[ENU=View Mode;
                                                              ENG=View Mode];
                                                   OptionCaptionML=[ENU=Lane,Grid,Map,Chart;
                                                                    ENG=Lane,Grid,Map,Chart];
                                                   OptionString=Lane,Grid,Map,Chart }
    { 13  ;   ;Group By Field Type ;Option        ;OnValidate=BEGIN
                                                                IF ("Group By Field Type" <> xRec."Group By Field Type") THEN
                                                                  VALIDATE("Group By Field No.", 0);
                                                              END;

                                                   CaptionML=[ENU=Group By Field Type;
                                                              ENG=Group By Field Type];
                                                   OptionCaptionML=[ENU=" ,Field,Enhanced Field";
                                                                    ENG=" ,Field,Enhanced Field"];
                                                   OptionString=[ ,Field,Enhanced Field] }
    { 14  ;   ;Group By Field No.  ;Integer       ;OnValidate=VAR
                                                                EnhViewField@1000000000 : Record 9062807;
                                                              BEGIN
                                                                EnhViewMgt.ValidateFieldNo(Rec, "Group By Field Type", "Group By Field No.", "Group By Field Name", GridFieldType::Group);

                                                                IF (NOT(Rec.ISTEMPORARY)) THEN BEGIN
                                                                  EnhViewField.RESET();
                                                                  EnhViewField.SETRANGE("View Code", Code);
                                                                  EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                                                                  EnhViewField.SETRANGE("Default Group", TRUE);
                                                                  IF (EnhViewField.FINDFIRST()) THEN BEGIN
                                                                    IF (EnhViewField."Field Type" <> Rec."Group By Field Type") OR (EnhViewField."Field No." <> "Group By Field No.") THEN BEGIN
                                                                      EnhViewField."Default Group" := FALSE;
                                                                      EnhViewField.MODIFY();
                                                                    END;
                                                                  END;

                                                                  EnhViewField.RESET();
                                                                  EnhViewField.SETRANGE("View Code", Code);
                                                                  EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                                                                  EnhViewField.SETRANGE("Default Group", FALSE);
                                                                  EnhViewField.SETRANGE("Field Type", "Group By Field Type");
                                                                  EnhViewField.SETRANGE("Field No.", "Group By Field No.");
                                                                  IF (EnhViewField.FINDFIRST()) THEN BEGIN
                                                                    EnhViewField."Default Group" := TRUE;
                                                                    EnhViewField.MODIFY();
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              EnhViewMgt.LookupField(Rec, "Group By Field Type", "Group By Field No.", "Group By Field Name", GridFieldType::Group);
                                                              VALIDATE("Group By Field No.");
                                                            END;

                                                   CaptionML=[ENU=Group By Field No.;
                                                              ENG=Group By Field No.] }
    { 15  ;   ;Group By Field Name ;Text50        ;OnValidate=BEGIN
                                                                EnhViewMgt.ValidateFieldName(Rec, "Group By Field Type", "Group By Field No.", "Group By Field Name", GridFieldType::Group);
                                                                VALIDATE("Group By Field No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              EnhViewMgt.LookupField(Rec, "Group By Field Type", "Group By Field No.", "Group By Field Name", GridFieldType::Group);
                                                              VALIDATE("Group By Field No.");
                                                            END;

                                                   CaptionML=[ENU=Group By Field Name;
                                                              ENG=Group By Field Name] }
    { 16  ;   ;Totals Field Type   ;Option        ;OnValidate=BEGIN
                                                                IF ("Totals Field Type" <> xRec."Totals Field Type") THEN
                                                                  VALIDATE("Totals Field No.", 0);
                                                              END;

                                                   CaptionML=[ENU=Totals Field Type;
                                                              ENG=Totals Field Type];
                                                   OptionCaptionML=[ENU=" ,Field,Enhanced Field";
                                                                    ENG=" ,Field,Enhanced Field"];
                                                   OptionString=[ ,Field,Enhanced Field] }
    { 17  ;   ;Totals Field No.    ;Integer       ;OnValidate=BEGIN
                                                                EnhViewMgt.ValidateFieldNo(Rec, "Totals Field Type", "Totals Field No.", "Totals Field Name", GridFieldType::Totals);

                                                                IF (NOT(Rec.ISTEMPORARY)) THEN BEGIN
                                                                  EnhViewField.RESET();
                                                                  EnhViewField.SETRANGE("View Code", Code);
                                                                  EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                                                                  EnhViewField.SETRANGE("Default Totals", TRUE);
                                                                  IF (EnhViewField.FINDFIRST()) THEN BEGIN
                                                                    IF (EnhViewField."Field Type" <> Rec."Totals Field Type") OR (EnhViewField."Field No." <> "Totals Field No.") THEN BEGIN
                                                                      EnhViewField."Default Totals" := FALSE;
                                                                      EnhViewField.MODIFY();
                                                                    END;
                                                                  END;

                                                                  EnhViewField.RESET();
                                                                  EnhViewField.SETRANGE("View Code", Code);
                                                                  EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                                                                  EnhViewField.SETRANGE("Default Totals", FALSE);
                                                                  EnhViewField.SETRANGE("Field Type", "Totals Field Type");
                                                                  EnhViewField.SETRANGE("Field No.", "Totals Field No.");
                                                                  IF (EnhViewField.FINDFIRST()) THEN BEGIN
                                                                    EnhViewField."Default Totals" := TRUE;
                                                                    EnhViewField.MODIFY();
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              EnhViewMgt.LookupField(Rec, "Totals Field Type", "Totals Field No.", "Totals Field Name", GridFieldType::Totals);
                                                              VALIDATE("Totals Field No.");
                                                            END;

                                                   CaptionML=[ENU=Totals Field No.;
                                                              ENG=Totals Field No.] }
    { 18  ;   ;Totals Field Name   ;Text30        ;OnValidate=BEGIN
                                                                EnhViewMgt.ValidateFieldName(Rec, "Totals Field Type", "Totals Field No.", "Totals Field Name", GridFieldType::Totals);
                                                                VALIDATE("Totals Field No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              EnhViewMgt.LookupField(Rec, "Totals Field Type", "Totals Field No.", "Totals Field Name", GridFieldType::Totals);
                                                              VALIDATE("Totals Field No.");
                                                            END;

                                                   CaptionML=[ENU=Totals Field Name;
                                                              ENG=Totals Field Name] }
    { 20  ;   ;Source Table No.    ;Integer       ;OnValidate=BEGIN
                                                                IF ("Source Table No." <> xRec."Source Table No.") THEN BEGIN
                                                                  VALIDATE("Cell Style Field No.", 0);
                                                                  VALIDATE("Group By Field No.", 0);
                                                                  VALIDATE("Totals Field No.", 0);

                                                                  IF (NOT(Rec.ISTEMPORARY)) THEN BEGIN
                                                                    EnhViewField.RESET();
                                                                    EnhViewField.SETRANGE("View Code", Code);
                                                                    EnhViewField.SETRANGE(Type, EnhViewField.Type::Cell);
                                                                    EnhViewField.MODIFYALL("Table No.", "Source Table No.", TRUE);

                                                                    EnhViewField.SETRANGE(Type, EnhViewField.Type::"Grid Column");
                                                                    EnhViewField.DELETEALL(TRUE);

                                                                    IF ("Source Filter Criteria Id" <> 0) THEN BEGIN
                                                                      Criteria.GET("Source Filter Criteria Id");
                                                                      Criteria.VALIDATE("Table ID", "Source Table No.");
                                                                      Criteria.MODIFY();
                                                                    END;

                                                                    EnhViewMgt.CreateUpdateDefaultFields(Rec);
                                                                  END;
                                                                END;
                                                                "Source Table Name" := ObjectFieldToolkit.GetTableName("Source Table No.");
                                                              END;

                                                   CaptionML=[ENU=Source Table No.;
                                                              ENG=Source Table No.] }
    { 24  ;   ;Source View Text    ;Text250       ;CaptionML=[ENU=Source View Text;
                                                              ENG=Source View Text] }
    { 25  ;   ;Source Filter Criteria Id;Integer  ;CaptionML=[ENU=Source Filter Criteria Id;
                                                              ENG=Source Filter Criteria Id] }
    { 26  ;   ;Source Filter Criteria Desc.;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Criteria.Description WHERE (ID=FIELD(Source Filter Criteria Id)));
                                                   OnLookup=BEGIN
                                                              EditSourceCriteria();
                                                            END;

                                                   CaptionML=[ENU=Source Filter Criteria Desc.;
                                                              ENG=Source Filter Criteria Desc.];
                                                   Editable=No }
    { 27  ;   ;Cell Style Field Type;Option       ;OnValidate=BEGIN
                                                                IF ("Cell Style Field Type" <> xRec."Cell Style Field Type") THEN
                                                                  VALIDATE("Cell Style Field No.", 0);
                                                              END;

                                                   CaptionML=[ENU=Cell Style Field Type;
                                                              ENG=Cell Style Field Type];
                                                   OptionCaptionML=[ENU=" ,Field,Enhanced Field";
                                                                    ENG=" ,Field,Enhanced Field"];
                                                   OptionString=[ ,Field,Enhanced Field] }
    { 28  ;   ;Cell Style Field No.;Integer       ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateFieldIdAdv("Source Table No.", "Cell Style Field Type", "Cell Style Field No.");
                                                                "Cell Style Field Name" := ObjectFieldToolkit.GetFieldNameAdv("Source Table No.", "Cell Style Field Type", "Cell Style Field No.");
                                                                IF ("Cell Style Field No." <> xRec."Cell Style Field No.") THEN
                                                                  EnhViewMgt.InvalidateStyleCache(Code, EnhViewStyle.Type::Cell);
                                                              END;

                                                   OnLookup=BEGIN
                                                              ObjectFieldToolkit.FieldLookupAdv("Source Table No.", "Cell Style Field Type", "Cell Style Field No.");
                                                              VALIDATE("Cell Style Field No.");
                                                            END;

                                                   CaptionML=[ENU=Cell Style Field No.;
                                                              ENG=Cell Style Field No.] }
    { 29  ;   ;Use Style Cache     ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF ("Use Style Cache" <> xRec."Use Style Cache") THEN
                                                                  EnhViewMgt.InvalidateStyleCache(Code, EnhViewStyle.Type::Cell);
                                                              END;

                                                   CaptionML=[ENU=Use Style Cache;
                                                              ENG=Use Style Cache] }
    { 32  ;   ;Max. No. of Lane/Grid Entries;Integer;
                                                   InitValue=300;
                                                   CaptionML=[ENU=Max. No. of Lane/Grid Entries;
                                                              ENG=Max. No. of Lane/Grid Entries] }
    { 33  ;   ;Max. No. of Map Entries;Integer    ;InitValue=100;
                                                   CaptionML=[ENU=Max. No. of Map Entries;
                                                              ENG=Max. No. of Map Entries] }
    { 34  ;   ;Max. No. of Chart Entries;Integer  ;InitValue=20;
                                                   CaptionML=[ENU=Max. No. of Chart Entries;
                                                              ENG=Max. No. of Chart Entries] }
    { 37  ;   ;Chart Type          ;Text30        ;CaptionML=[ENU=Chart Type;
                                                              ENG=Chart Type] }
    { 38  ;   ;No. of Grid Fields  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Enhanced View Field" WHERE (View Code=FIELD(Code),
                                                                                                  Type=CONST(Grid Column)));
                                                   CaptionML=[ENU=No. of Grid Fields;
                                                              ENG=No. of Grid Fields];
                                                   Editable=No }
    { 39  ;   ;Entry Details Page Id;Integer      ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Page));
                                                   CaptionML=[ENU=Entry Details Page Id;
                                                              ENG=Entry Details Page Id] }
    { 40  ;   ;Source Table Name   ;Text80        ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateTableName("Source Table No.", "Source Table Name", FALSE);
                                                                VALIDATE("Source Table No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF (ObjectFieldToolkit.TableLookup("Source Table No.")) THEN
                                                                VALIDATE("Source Table No.");
                                                            END;

                                                   CaptionML=[ENU=Source Table Name;
                                                              ENG=Source Table Name] }
    { 43  ;   ;Cell Style Field Name;Text50       ;OnValidate=BEGIN
                                                                ObjectFieldToolkit.ValidateFieldNameAdv("Source Table No.", "Cell Style Field Type", "Cell Style Field No.", "Cell Style Field Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ObjectFieldToolkit.FieldLookupAdv("Source Table No.", "Cell Style Field Type", "Cell Style Field No.");
                                                              VALIDATE("Cell Style Field No.");
                                                            END;

                                                   CaptionML=[ENU=Cell Style Field Name;
                                                              ENG=Cell Style Field Name] }
    { 44  ;   ;Entry Details Page Name;Text80     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Page),
                                                                                                  Object ID=FIELD(Entry Details Page Id)));
                                                   CaptionML=[ENU=Entry Details Page Name;
                                                              ENG=Entry Details Page Name];
                                                   Editable=No }
    { 50  ;   ;Lane View Column Width;Integer     ;CaptionML=[ENU=Lane View Column Width;
                                                              ENG=Lane View Column Width] }
    { 51  ;   ;Background Image Url;Text100       ;CaptionML=[ENU=Background Image Url;
                                                              ENG=Background Image Url] }
    { 52  ;   ;Show on Role Centre ;Boolean       ;CaptionML=[ENU=Show on Role Centre;
                                                              ENG=Show on Role Centre] }
    { 53  ;   ;Hide Menu Bar       ;Boolean       ;CaptionML=[ENU=Hide Menu Bar;
                                                              ENG=Hide Menu Bar] }
    { 100 ;   ;View Mode Filter    ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=View Mode Filter;
                                                              ENG=View Mode Filter];
                                                   OptionCaptionML=[ENU=Lane,Grid,Map,Chart;
                                                                    ENG=Lane,Grid,Map,Chart];
                                                   OptionString=Lane,Grid,Map,Chart }
    { 101 ;   ;Chart Type Filter   ;Text30        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Chart Type Filter;
                                                              ENG=Chart Type Filter] }
    { 102 ;   ;Field 1 No. Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 1 No. Filter;
                                                              ENG=Field 1 No. Filter] }
    { 103 ;   ;Field 1 Value Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 1 Value Filter;
                                                              ENG=Field 1 Value Filter] }
    { 104 ;   ;Field 2 No. Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 2 No. Filter;
                                                              ENG=Field 2 No. Filter] }
    { 105 ;   ;Field 2 Value Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 2 Value Filter;
                                                              ENG=Field 2 Value Filter] }
    { 106 ;   ;Field 3 No. Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 3 No. Filter;
                                                              ENG=Field 3 No. Filter] }
    { 107 ;   ;Field 3 Value Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 3 Value Filter;
                                                              ENG=Field 3 Value Filter] }
    { 108 ;   ;Field 4 No. Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 4 No. Filter;
                                                              ENG=Field 4 No. Filter] }
    { 109 ;   ;Field 4 Value Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 4 Value Filter;
                                                              ENG=Field 4 Value Filter] }
    { 110 ;   ;Field 5 No. Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 5 No. Filter;
                                                              ENG=Field 5 No. Filter] }
    { 111 ;   ;Field 5 Value Filter;Text250       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Field 5 Value Filter;
                                                              ENG=Field 5 Value Filter] }
    { 112 ;   ;Hide Menu Bar Filter;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Hide Menu Bar Filter;
                                                              ENG=Hide Menu Bar Filter] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EnhViewColumn@1000000000 : Record 9062805;
      EnhViewField@1000000001 : Record 9062807;
      EnhViewStyle@1000000006 : Record 9062808;
      EnhViewStyleCache@1000000007 : Record 9062812;
      Criteria@1000000004 : Record 9062626;
      ObjectFieldToolkit@1000000002 : Codeunit 9062343;
      CriteriaMgt@1000000003 : Codeunit 9062462;
      EnhViewMgt@1000000005 : Codeunit 9062679;
      HtmlControlTemplMgt@1000000009 : Codeunit 9062686;
      GridFieldType@1000000008 : 'All,Group,Totals';

    PROCEDURE EditSourceCriteria@1000000003();
    BEGIN
      CriteriaMgt.EditCriteriaAttached("Source Filter Criteria Id", "Source Table No.",
        DATABASE::"Enhanced View", GETPOSITION(FALSE), FIELDNO("Source Filter Criteria Id"),
        TRUE);
      MODIFY();
      CALCFIELDS("Source Filter Criteria Desc.");
    END;

    PROCEDURE ValidateDetailsPageName@1000000023(PageName@1000000001 : Text[50]);
    VAR
      lResult@1000000000 : Boolean;
    BEGIN
      ObjectFieldToolkit.ValidatePageName(Rec."Entry Details Page Id", PageName, FALSE);
      Rec.VALIDATE("Entry Details Page Id");
    END;

    PROCEDURE DetailsPageLookup@1000000020() : Boolean;
    VAR
      RetVal@1000000000 : Boolean;
    BEGIN
      RetVal := ObjectFieldToolkit.PageLookup(Rec."Entry Details Page Id");
      IF (RetVal) THEN
        Rec.VALIDATE("Entry Details Page Id");
      EXIT(RetVal);
    END;

    BEGIN
    {
      //DOC NA2016.12 AZ 21/01/2016 - Created
      //DOC NA2016.12 AZ 29/01/2016 - Control Type added to Html Control Template table
      //DOC NA2016.14 AZ 15/02/2016 - Initialize enhanced views from the content server
      //DOC NA2016.14 AZ 26/02/2016 - Grid View added, bugfixes
      //DOC NA2016.15 AZ 12/04/2016 - Midifications and bugfixes, Quick Reports support added
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

