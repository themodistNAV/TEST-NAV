OBJECT Table 11102058 OM - Where Used Object Line
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnDelete=VAR
               WhereUsedCrossTable@1000 : Record 11102061;
             BEGIN
               WhereUsedCrossTable.SETCURRENTKEY("Object Entry No.");
               WhereUsedCrossTable.SETRANGE("Object Entry No.", "Entry No.");
               WhereUsedCrossTable.DELETEALL(TRUE);
             END;

    CaptionML=[DEU=Where Used Object Line;
               ENU=Where Used Object Line;
               ESP=Where Used Object Line;
               FRA=Where Used Object Line;
               NLD=Waar-gebruikt objectregel;
               DEA=Where Used Object Line];
    LookupPageID=Page11102090;
    DrillDownPageID=Page11102090;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DEU=Entry No.;
                                                              ENU=Entry No.;
                                                              ESP=Entry No.;
                                                              FRA=Entry No.;
                                                              NLD=Volgnummer;
                                                              DEA=Entry No.] }
    { 2   ;   ;Object Type         ;Option        ;CaptionML=[DEU=Object Type;
                                                              ENU=Object Type;
                                                              ESP=Object Type;
                                                              FRA=Object Type;
                                                              NLD=Objecttype;
                                                              DEA=Object Type];
                                                   OptionCaptionML=[DEU=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    ENU=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    ESP=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    FRA=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    NLD=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                                    DEA=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query];
                                                   OptionString=,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query;
                                                   BlankZero=Yes }
    { 3   ;   ;Object No.          ;Integer       ;TableRelation=Object.ID WHERE (Type=FIELD(Object Type),
                                                                                  Company Name=FILTER(''));
                                                   CaptionML=[DEU=Object No.;
                                                              ENU=Object No.;
                                                              ESP=Object No.;
                                                              FRA=Object No.;
                                                              NLD=Objectnr.;
                                                              DEA=Object No.];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 4   ;   ;Enabled             ;Boolean       ;OnValidate=VAR
                                                                WhereUsedObjectLine@1000 : Record 11102058;
                                                                Field@1001 : Record 2000000041;
                                                                WhereUsedMgt@1002 : Codeunit 11102045;
                                                              BEGIN
                                                                CASE Type OF

                                                                  Type::Key :
                                                                    BEGIN
                                                                      WhereUsedObjectLine := Rec;
                                                                      WhereUsedMgt.ToggleKeyEnabled(WhereUsedObjectLine);
                                                                    END;

                                                                  Type::Field :
                                                                    BEGIN
                                                                      Field.GET("Object No.", Id);
                                                                      Field.Enabled := Enabled;
                                                                      Field.MODIFY;
                                                                    END;

                                                                  ELSE
                                                                    FIELDERROR(Type);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Enabled;
                                                              ENU=Enabled;
                                                              ESP=Enabled;
                                                              FRA=Enabled;
                                                              NLD=Ingeschakeld;
                                                              DEA=Enabled] }
    { 5   ;   ;Type                ;Option        ;CaptionML=[DEU=Type;
                                                              ENU=Type;
                                                              ESP=Type;
                                                              FRA=Type;
                                                              NLD=Soort;
                                                              DEA=Type];
                                                   OptionCaptionML=[DEU=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value;
                                                                    ENU=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value;
                                                                    ESP=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value;
                                                                    FRA=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value;
                                                                    NLD=Object,Trigger,Sleutel,Sum Index Field,Veld,Globale functie,Lokale functie,Globale variabele,Lokale variabele,Parameter,Return Value;
                                                                    DEA=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value];
                                                   OptionString=Object,Trigger,Key,Sum Index Field,Field,Global Function,Local Function,Global Variable,Local Variable,Parameter,Return Value;
                                                   Editable=No }
    { 6   ;   ;Id                  ;Integer       ;CaptionML=[DEU=Id;
                                                              ENU=Id;
                                                              ESP=Id;
                                                              FRA=Id;
                                                              NLD=Id;
                                                              DEA=Id];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 7   ;   ;Name                ;Text250       ;CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              ESP=Name;
                                                              FRA=Name;
                                                              NLD=Naam;
                                                              DEA=Name];
                                                   Editable=No }
    { 8   ;   ;No. of Times Used   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Where Used Cross Table" WHERE (Object Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[DEU=No. of Times Used;
                                                              ENU=No. of Times Used;
                                                              ESP=No. of Times Used;
                                                              FRA=No. of Times Used;
                                                              NLD=Aantal keer gebruikt;
                                                              DEA=No. of Times Used];
                                                   Editable=No }
    { 9   ;   ;With Validation     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Where Used Cross Table" WHERE (Object Entry No.=FIELD(Entry No.),
                                                                                                          Validation=CONST(Yes)));
                                                   CaptionML=[DEU=With Validation;
                                                              ENU=With Validation;
                                                              ESP=With Validation;
                                                              FRA=With Validation;
                                                              NLD=Met validatie;
                                                              DEA=With Validation];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;Filled              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("OM - Where Used Cross Table" WHERE (Object Entry No.=FIELD(Entry No.),
                                                                                                          Validation=FILTER(No|Yes)));
                                                   CaptionML=[DEU=Filled;
                                                              ENU=Filled;
                                                              ESP=Filled;
                                                              FRA=Filled;
                                                              NLD=Gevuld;
                                                              DEA=Filled];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 11  ;   ;Parent No.          ;Integer       ;TableRelation="OM - Where Used Object Line";
                                                   CaptionML=[DEU=Parent No.;
                                                              ENU=Parent No.;
                                                              ESP=Parent No.;
                                                              FRA=Parent No.;
                                                              NLD=Oudernr.;
                                                              DEA=Parent No.];
                                                   BlankZero=Yes }
    { 12  ;   ;C/AL Line No.       ;Integer       ;CaptionML=[DEU=C/AL Line No.;
                                                              ENU=C/AL Line No.;
                                                              ESP=C/AL Line No.;
                                                              FRA=C/AL Line No.;
                                                              NLD=C/AL Regelnr.;
                                                              DEA=C/AL Line No.] }
    { 13  ;   ;No. of Variables    ;Integer       ;CaptionML=[DEU=No. of Variables;
                                                              ENU=No. of Variables;
                                                              ESP=No. of Variables;
                                                              FRA=No. of Variables;
                                                              NLD=Aantal variabelen;
                                                              DEA=No. of Variables];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 14  ;   ;No. of Parameters   ;Integer       ;CaptionML=[DEU=No. of Parameters;
                                                              ENU=No. of Parameters;
                                                              ESP=No. of Parameters;
                                                              FRA=No. of Parameters;
                                                              NLD=Aantal parameters;
                                                              DEA=No. of Parameters];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 15  ;   ;Used                ;Boolean       ;CaptionML=[DEU=Used;
                                                              ENU=Used;
                                                              ESP=Used;
                                                              FRA=Used;
                                                              NLD=Gebruikt;
                                                              DEA=Used] }
    { 16  ;   ;Object Name         ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=FIELD(Object Type),
                                                                                         Company Name=FILTER(''),
                                                                                         ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Object Name;
                                                              ENU=Object Name;
                                                              ESP=Object Name;
                                                              FRA=Object Name;
                                                              NLD=Objectnaam;
                                                              DEA=Object Name];
                                                   Editable=No }
    { 17  ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object No.)));
                                                   CaptionML=[DEU=Object Caption;
                                                              ENU=Object Caption;
                                                              ESP=Object Caption;
                                                              FRA=Object Caption;
                                                              NLD=Object caption;
                                                              DEA=Object Caption];
                                                   Editable=No }
    { 19  ;   ;Name to Display     ;Text250       ;CaptionML=[DEU=Name to Display;
                                                              ENU=Name to Display;
                                                              ESP=Name to Display;
                                                              FRA=Name to Display;
                                                              NLD=Naam te tonen;
                                                              DEA=Name to Display] }
    { 20  ;   ;Type Name           ;Text30        ;CaptionML=[DEU=Type Name;
                                                              ENU=Type Name;
                                                              ESP=Type Name;
                                                              FRA=Type Name;
                                                              NLD=Soortnaam;
                                                              DEA=Type Name];
                                                   Editable=No }
    { 21  ;   ;No. of Return Values;Integer       ;CaptionML=[DEU=No. of Return Values;
                                                              ENU=No. of Return Values;
                                                              ESP=No. of Return Values;
                                                              FRA=No. of Return Values;
                                                              NLD=Aantal returnwaarden;
                                                              DEA=No. of Return Values];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;Sub Type            ;Text30        ;CaptionML=[DEU=Sub Type;
                                                              ENU=Sub Type;
                                                              ESP=Sub Type;
                                                              FRA=Sub Type;
                                                              NLD=Subtype;
                                                              DEA=Sub Type] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Object Type,Object No.,Type,Id           }
    {    ;Object Type,Object No.,C/AL Line No.     }
    {    ;Object Type,Object No.,Type,C/AL Line No. }
    {    ;Parent No.,Type,Id                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE DisplayKey@1() ExitValue : Text;
    VAR
      Fld@1000 : Record 2000000041;
      WhereUsedMgt@1001 : Codeunit 11102045;
      FieldNoString@1002 : Text;
      KeyString@1003 : Text;
      FieldNo2@1004 : Integer;
      i@1005 : Integer;
    BEGIN
      KeyString := "Name to Display" + ', ';
      i := 1;
      WHILE WhereUsedMgt.SelectStr2(i, KeyString) <> ' ' DO BEGIN
        FieldNoString := WhereUsedMgt.SelectStr2(i, KeyString);
        IF EVALUATE(FieldNo2, FieldNoString) THEN
          IF Fld.GET("Object No.", FieldNo2) THEN
            ExitValue += ',' + Fld.FieldName;
        i += 1;
      END;

      ExitValue := COPYSTR(ExitValue, 2);
    END;

    PROCEDURE NoOfSumIndexFields@2() ExitValue : Text;
    VAR
      WhereUsedObjectLine@1000 : Record 11102058;
    BEGIN
      IF Type = Type::Key THEN BEGIN
        WhereUsedObjectLine.SETCURRENTKEY("Object Type", "Object No.", Type, Id);
        WhereUsedObjectLine.SETRANGE("Object Type", "Object Type");
        WhereUsedObjectLine.SETRANGE("Object No.", "Object No.");
        WhereUsedObjectLine.SETRANGE(Type, Type::"Sum Index Field");
        WhereUsedObjectLine.SETRANGE(Id, Id * 100, Id * 100 + 99);
        IF WhereUsedObjectLine.FINDSET THEN BEGIN
          REPEAT
            ExitValue += ',' + WhereUsedObjectLine.Name;
          UNTIL WhereUsedObjectLine.NEXT = 0;
          ExitValue := COPYSTR(ExitValue, 2);
        END;
      END;
    END;

    PROCEDURE AssistEditSumIndexFields@3();
    VAR
      WhereUsedObjectLine@1000 : Record 11102058;
      WhereUsedObjectLines@1001 : Page 11102090;
    BEGIN
      IF Type = Type::Key THEN BEGIN
        WhereUsedObjectLine.SETCURRENTKEY("Object Type", "Object No.", Type, Id);
        WhereUsedObjectLine.SETRANGE("Object Type", "Object Type");
        WhereUsedObjectLine.SETRANGE("Object No.", "Object No.");
        WhereUsedObjectLine.SETRANGE(Type, Type::"Sum Index Field");
        WhereUsedObjectLine.SETRANGE(Id, Id * 100, Id * 100 + 99);

        WhereUsedObjectLines.SETTABLEVIEW(WhereUsedObjectLine);
        WhereUsedObjectLines.RUNMODAL;
      END;
    END;

    PROCEDURE DrillDownNoOfTimesUsed@4(Type@1000 : ' ,Filled,Validated');
    VAR
      TmpWhereUsedCrossTable@1001 : TEMPORARY Record 11102061;
      WhereUsedCrossTable@1002 : Record 11102061;
    BEGIN
      TmpWhereUsedCrossTable.FILTERGROUP(2);
      TmpWhereUsedCrossTable.SETRANGE("Object Entry No.", "Entry No.");
      CASE Type OF

        Type::Filled :
          TmpWhereUsedCrossTable.SETFILTER(
            Validation, '%1|%2',
            WhereUsedCrossTable.Validation::No, WhereUsedCrossTable.Validation::Yes);

        Type::Validated :
          TmpWhereUsedCrossTable.SETRANGE(Validation, WhereUsedCrossTable.Validation::Yes);
      END;
      TmpWhereUsedCrossTable.FILTERGROUP(0);

      PAGE.RUN(PAGE::"OM - Where Used In", TmpWhereUsedCrossTable);
    END;

    PROCEDURE DrillDownVariables@5(DrillDownType@1000 : ',,,,,,,,Local Variable,Parameter,Return Value');
    VAR
      WhereUsedObject@1001 : Record 11102057;
      WhereUsedObjectLine@1002 : Record 11102058;
      WhereUsedObjectLines@1003 : Page 11102090;
    BEGIN
      WhereUsedObject.GET("Object Type", "Object No.");
      WhereUsedObject.CalculateVariableUsage;

      WhereUsedObjectLine.SETCURRENTKEY("Parent No.", Type, Id);
      WhereUsedObjectLine.SETRANGE("Parent No.", "Entry No.");
      WhereUsedObjectLine.SETRANGE(Type, DrillDownType);
      WhereUsedObjectLines.SETTABLEVIEW(WhereUsedObjectLine);
      WhereUsedObjectLines.RUN;
    END;

    PROCEDURE ShowCode@6();
    VAR
      Setup@1000 : Record 11102035;
      CALHistoryObject@1001 : Record 11102052;
      CALHistoryLine@1002 : Record 11102053;
      TmpHighLightedLine@1003 : TEMPORARY Record 11102053;
      CALHistoryLines@1004 : Page 11102073;
      NavgateEditor@1005 : Page 11175819;
    BEGIN
      CALHistoryObject.GoToLastRecord("Object Type", "Object No.");

      Setup.CustomGet;

      CASE Setup.Editor OF

        Setup.Editor::"Object Manager" :
          BEGIN

            CALHistoryLines.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");

            IF CALHistoryLine.GET(CALHistoryObject."Entry No.", "C/AL Line No.", 1) THEN BEGIN
              TmpHighLightedLine := CALHistoryLine;
              IF TmpHighLightedLine.INSERT THEN
                ;
              CALHistoryLines.SetHighLightedLines(TmpHighLightedLine);
            END;

            CALHistoryLines.SetFromWhereUsed(TRUE);
            CALHistoryLines.RUN;

          END;

        Setup.Editor::Navgate :
          BEGIN
            NavgateEditor.SetCALHistoryEntryNo(CALHistoryObject."Entry No.");
            NavgateEditor.SetWhereUsedEntryNo("Entry No.", '');
            NavgateEditor.GotoPosition("C/AL Line No.", 1);

            NavgateEditor.RUN;
          END;

      END;
    END;

    PROCEDURE CanBeLocal@7() : Boolean;
    VAR
      CALHistoryObject@1000 : Record 11102052;
      CALHistoryLine@1001 : Record 11102053;
      WhereUsedIn@1002 : Record 11102060;
      WhereUsedCrossTable@1003 : Record 11102061;
      CALCode@1004 : Text;
    BEGIN
      IF Type <> Type::"Global Function" THEN
        EXIT(FALSE);

      WhereUsedCrossTable.SETCURRENTKEY("Object Entry No.");
      WhereUsedCrossTable.SETRANGE("Object Entry No.", "Entry No.");
      IF WhereUsedCrossTable.FINDSET THEN
        REPEAT
          IF NOT WhereUsedIn.GET(WhereUsedCrossTable."In Entry No.") THEN
            EXIT(FALSE);

          IF (WhereUsedIn."Object Type" <> "Object Type") OR
             (WhereUsedIn."Object No." <> "Object No.")
          THEN
            EXIT(FALSE);

          CALHistoryObject.GoToLastRecord("Object Type", "Object No.");

          CALCode := '';
          CALHistoryLine.SETRANGE("Entry No.", CALHistoryObject."Entry No.");
          CALHistoryLine.SETRANGE("Line No.", WhereUsedCrossTable."C/AL Code Line No.");
          IF CALHistoryLine.FINDSET THEN
            REPEAT
              IF STRLEN(CALCode + CALHistoryLine.Code) <= 1024 THEN
                CALCode += CALHistoryLine.Code;
            UNTIL CALHistoryLine.NEXT = 0;
          CALCode := LOWERCASE(CALCode);

          IF STRPOS(CALCode, '.' + LOWERCASE(Name)) > 0 THEN
            EXIT(FALSE);

          IF STRPOS(CALCode, '."' + LOWERCASE(Name)) > 0 THEN
            EXIT(FALSE);

        UNTIL WhereUsedCrossTable.NEXT = 0
      ELSE
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE LookupSubType@8();
    VAR
      Object@1000 : Record 2000000001;
      Mgt@1001 : Codeunit 11102035;
    BEGIN
      IF "Type Name" = 'Record' THEN
        Object.Type := Object.Type::Table
      ELSE
        IF NOT EVALUATE(Object.Type, "Type Name") THEN
          EXIT;

      IF NOT EVALUATE(Object.ID, "Sub Type") THEN
        EXIT;

      Mgt.LookupObjectNo(Object.Type, "Sub Type");
    END;

    PROCEDURE FunctionName@9() : Text;
    VAR
      WhereUsedObjectLine@1000 : Record 11102058;
    BEGIN
      IF Type IN [Type::"Global Variable", Type::"Local Variable", Type::Parameter, Type::"Return Value"] THEN BEGIN
        WhereUsedObjectLine.SETCURRENTKEY("Object Type", "Object No.", "C/AL Line No.");
        WhereUsedObjectLine.SETRANGE("Object Type", "Object Type");
        WhereUsedObjectLine.SETRANGE("Object No.", "Object No.");
        WhereUsedObjectLine.SETFILTER(Type, '%1|%2|%3|%4',
          WhereUsedObjectLine.Type::Trigger,
          WhereUsedObjectLine.Type::Field,
          WhereUsedObjectLine.Type::"Global Function",
          WhereUsedObjectLine.Type::"Local Function");

        WhereUsedObjectLine.SETFILTER("C/AL Line No.", '<=%1', "C/AL Line No.");
        IF WhereUsedObjectLine.FINDLAST THEN
          EXIT(WhereUsedObjectLine.Name);
      END;
    END;

    BEGIN
    END.
  }
}

