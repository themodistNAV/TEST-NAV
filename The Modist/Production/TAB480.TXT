OBJECT Table 480 Dimension Set Entry
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 480=ri,
                TableData 481=rim;
    OnInsert=BEGIN
               IF DimVal.GET("Dimension Code","Dimension Value Code") THEN
                 "Dimension Value ID" := DimVal."Dimension Value ID"
               ELSE
                 "Dimension Value ID" := 0;
             END;

    OnModify=BEGIN
               IF DimVal.GET("Dimension Code","Dimension Value Code") THEN
                 "Dimension Value ID" := DimVal."Dimension Value ID"
               ELSE
                 "Dimension Value ID" := 0;
             END;

    CaptionML=[ENU=Dimension Set Entry;
               ENG=Dimension Set Entry];
    LookupPageID=Page479;
    DrillDownPageID=Page479;
  }
  FIELDS
  {
    { 1   ;   ;Dimension Set ID    ;Integer       ;CaptionML=[ENU=Dimension Set ID;
                                                              ENG=Dimension Set ID] }
    { 2   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                                IF "Dimension Code" <> xRec."Dimension Code" THEN BEGIN
                                                                  "Dimension Value Code" := '';
                                                                  "Dimension Value ID" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              ENG=Dimension Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);

                                                                DimVal.GET("Dimension Code","Dimension Value Code");
                                                                "Dimension Value ID" := DimVal."Dimension Value ID";
                                                              END;

                                                   CaptionML=[ENU=Dimension Value Code;
                                                              ENG=Dimension Value Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Dimension Value ID  ;Integer       ;CaptionML=[ENU=Dimension Value ID;
                                                              ENG=Dimension Value ID] }
    { 5   ;   ;Dimension Name      ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Dimension.Name WHERE (Code=FIELD(Dimension Code)));
                                                   CaptionML=[ENU=Dimension Name;
                                                              ENG=Dimension Name];
                                                   Editable=No }
    { 6   ;   ;Dimension Value Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Dimension Code=FIELD(Dimension Code),
                                                                                                    Code=FIELD(Dimension Value Code)));
                                                   CaptionML=[ENU=Dimension Value Name;
                                                              ENG=Dimension Value Name];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Dimension Set ID,Dimension Code         ;Clustered=Yes }
    {    ;Dimension Value ID                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimVal@1000 : Record 349;
      DimMgt@1001 : Codeunit 408;

    PROCEDURE GetDimensionSetID@3(VAR DimSetEntry@1000 : Record 480) : Integer;
    VAR
      DimSetEntry2@1001 : Record 480;
      DimSetTreeNode@1002 : Record 481;
      Found@1003 : Boolean;
    BEGIN
      DimSetEntry2.COPY(DimSetEntry);
      IF DimSetEntry."Dimension Set ID" > 0 THEN
        DimSetEntry.SETRANGE("Dimension Set ID",DimSetEntry."Dimension Set ID");

      DimSetEntry.SETCURRENTKEY("Dimension Value ID");
      DimSetEntry.SETFILTER("Dimension Code",'<>%1','');
      DimSetEntry.SETFILTER("Dimension Value Code",'<>%1','');

      IF NOT DimSetEntry.FINDSET THEN
        EXIT(0);

      Found := TRUE;
      DimSetTreeNode."Dimension Set ID" := 0;
      REPEAT
        DimSetEntry.TESTFIELD("Dimension Value ID");
        IF Found THEN
          IF NOT DimSetTreeNode.GET(DimSetTreeNode."Dimension Set ID",DimSetEntry."Dimension Value ID") THEN BEGIN
            Found := FALSE;
            DimSetTreeNode.LOCKTABLE;
          END;
        IF NOT Found THEN BEGIN
          DimSetTreeNode."Parent Dimension Set ID" := DimSetTreeNode."Dimension Set ID";
          DimSetTreeNode."Dimension Value ID" := DimSetEntry."Dimension Value ID";
          DimSetTreeNode."Dimension Set ID" := 0;
          DimSetTreeNode."In Use" := FALSE;
          IF NOT DimSetTreeNode.INSERT(TRUE) THEN
            DimSetTreeNode.GET(DimSetTreeNode."Parent Dimension Set ID",DimSetTreeNode."Dimension Value ID");
        END;
      UNTIL DimSetEntry.NEXT = 0;
      IF NOT DimSetTreeNode."In Use" THEN BEGIN
        IF Found THEN BEGIN
          DimSetTreeNode.LOCKTABLE;
          DimSetTreeNode.GET(DimSetTreeNode."Parent Dimension Set ID",DimSetTreeNode."Dimension Value ID");
        END;
        DimSetTreeNode."In Use" := TRUE;
        DimSetTreeNode.MODIFY;
        InsertDimSetEntries(DimSetEntry,DimSetTreeNode."Dimension Set ID");
      END;

      DimSetEntry.COPY(DimSetEntry2);

      EXIT(DimSetTreeNode."Dimension Set ID");
    END;

    LOCAL PROCEDURE InsertDimSetEntries@13(VAR DimSetEntry@1000 : Record 480;NewID@1001 : Integer);
    VAR
      DimSetEntry2@1002 : Record 480;
    BEGIN
      DimSetEntry2.LOCKTABLE;
      IF DimSetEntry.FINDSET THEN
        REPEAT
          DimSetEntry2 := DimSetEntry;
          DimSetEntry2."Dimension Set ID" := NewID;
          DimSetEntry2.INSERT;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

