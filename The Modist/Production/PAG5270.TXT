OBJECT Page 5270 CRM Coupling Resource
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Microsoft Dynamics CRM Product Coupling;
               ENG=Microsoft Dynamics CRM Product Coupling];
    SourceTable=Table156;
    PageType=StandardDialog;
    OnInit=BEGIN
             SynchAction := SynchAction::PushToCRM;
             SetPushActionCheckboxes;
           END;

    OnAfterGetRecord=VAR
                       CRMIntegrationRecord@1000 : Record 5331;
                       CRMID@1001 : GUID;
                     BEGIN
                       IF CRMIntegrationRecord.FindIDFromRecordID(RECORDID,CRMID) THEN BEGIN
                         CRMProduct.GET(CRMID);
                         OriginalCRMProduct := CRMProduct;
                         SynchAction := SynchAction::DoNotSynch;
                         SetPushActionCheckboxes;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=Coupling;
                           ENG=Coupling];
                GroupType=Group;
                Layout=Rows }

    { 3   ;2   ;Group     ;
                CaptionML=[ENU=Dynamics NAV;
                           ENG=Dynamics NAV];
                GroupType=Group }

    { 4   ;3   ;Field     ;
                CaptionML=[ENU=Resource;
                           ENG=Resource];
                SourceExpr="No.";
                Editable=FALSE }

    { 5   ;3   ;Field     ;
                Name=SynchActionControl;
                CaptionML=[ENU=Synchronize After Coupling;
                           ENG=Synchronise After Coupling];
                OptionCaptionML=[ENU=No,Yes - Use the Dynamics NAV data;
                                 ENG=No,Yes - Use the Dynamics NAV data];
                SourceExpr=SynchAction;
                Enabled=NOT CreateNewInCRM;
                Editable=NOT CreateNewInCRM;
                OnValidate=BEGIN
                             SetPushActionCheckboxes;
                           END;
                            }

    { 6   ;2   ;Group     ;
                CaptionML=[ENU=Dynamics CRM;
                           ENG=Dynamics CRM];
                GroupType=Group }

    { 7   ;3   ;Field     ;
                Name=CRM Product ProductNumber;
                CaptionML=[ENU=Product;
                           ENG=Product];
                SourceExpr=CRMProduct.ProductNumber;
                Enabled=NOT CreateNewInCRM;
                OnValidate=VAR
                             ManualCRMProduct@1000 : Record 5348;
                             PreviouslySelectedCRMProduct@1001 : Record 5348;
                             CRMProductFound@1002 : Boolean;
                           BEGIN
                             PreviouslySelectedCRMProduct := CRMProduct;
                             ManualCRMProduct.SETRANGE(ProductNumber,CRMProduct.ProductNumber);

                             IF ManualCRMProduct.FINDFIRST THEN
                               CRMProductFound := TRUE
                             ELSE BEGIN
                               ManualCRMProduct.SETCURRENTKEY(ProductNumber);
                               ManualCRMProduct.SETFILTER(ProductNumber,'%1',CRMProduct.ProductNumber + '*');
                               IF ManualCRMProduct.FINDFIRST THEN
                                 CRMProductFound := TRUE;
                             END;

                             IF CRMProductFound THEN BEGIN
                               CRMProduct := ManualCRMProduct;
                               IF PreviouslySelectedCRMProduct.ProductId <> CRMProduct.ProductId THEN
                                 HandleNewCRMProduct;
                             END ELSE
                               ERROR(STRSUBSTNO(NoSuchCRMRecordErr,CRMProduct.ProductNumber));
                           END;

                OnLookup=VAR
                           PreviouslySelectedCRMProduct@1002 : Record 5348;
                           CRMProductList@1000 : Page 5348;
                         BEGIN
                           CRMProductList.SETRECORD(CRMProduct);
                           CRMProductList.LOOKUPMODE(TRUE);
                           PreviouslySelectedCRMProduct := CRMProduct;

                           IF CRMProductList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             CRMProductList.GETRECORD(CRMProduct);
                             IF PreviouslySelectedCRMProduct.ProductId <> CRMProduct.ProductId THEN
                               HandleNewCRMProduct;
                           END;
                         END;
                          }

    { 8   ;3   ;Field     ;
                Name=CreateNewControl;
                CaptionML=[ENU=Create New Product;
                           ENG=Create New Product];
                SourceExpr=CreateNewInCRM;
                OnValidate=BEGIN
                             IF CreateNewInCRM THEN BEGIN
                               SavedCRMProduct := CRMProduct;
                               SavedSynchAction := SynchAction;
                               SynchAction := SynchAction::PushToCRM;
                               CRMProduct.INIT;
                             END ELSE BEGIN
                               CRMProduct := SavedCRMProduct;
                               SynchAction := SavedSynchAction;
                             END;

                             SetPushActionCheckboxes;
                           END;
                            }

    { 9   ;1   ;Group     ;
                CaptionML=[ENU=Coupled Records;
                           ENG=Coupled Records];
                Editable=FALSE;
                GroupType=Group }

    { 10  ;2   ;Group     ;
                GroupType=FixedLayout }

    { 11  ;3   ;Group     ;
                CaptionML=[ENU=Dynamics NAV Resource;
                           ENG=Dynamics NAV Resource];
                GroupType=Group;
                Layout=Columns }

    { 12  ;4   ;Field     ;
                Name="No.2";
                CaptionML=[ENU=No.;
                           ENG=No.];
                SourceExpr="No." }

    { 13  ;4   ;Field     ;
                Name=Name2;
                CaptionML=[ENU=Name;
                           ENG=Name];
                SourceExpr=Name }

    { 14  ;4   ;Field     ;
                SourceExpr="Unit Price" }

    { 15  ;4   ;Field     ;
                SourceExpr="Unit Cost" }

    { 16  ;4   ;Field     ;
                SourceExpr="Direct Unit Cost" }

    { 17  ;4   ;Field     ;
                Name=PushToCRMControl;
                CaptionML=[ENU=Use for Synchronization;
                           ENG=Use for Synchronisation];
                SourceExpr=PushToCRM }

    { 18  ;3   ;Group     ;
                CaptionML=[ENU=Dynamics CRM Product;
                           ENG=Dynamics CRM Product];
                GroupType=Group }

    { 19  ;4   ;Field     ;
                SourceExpr=CRMProduct.ProductNumber }

    { 20  ;4   ;Field     ;
                SourceExpr=CRMProduct.Name }

    { 21  ;4   ;Field     ;
                SourceExpr=CRMProduct.Price }

    { 22  ;4   ;Field     ;
                SourceExpr=CRMProduct.StandardCost }

    { 23  ;4   ;Field     ;
                SourceExpr=CRMProduct.CurrentCost }

    { 24  ;4   ;Field     ;
                Name=PushToNAVControl;
                SourceExpr=PushToNAV }

  }
  CODE
  {
    VAR
      CRMProduct@1001 : Record 5348;
      OriginalCRMProduct@1010 : Record 5348;
      IntegrationTableMapping@1009 : Record 5335;
      SavedCRMProduct@1007 : Record 5348;
      CreateNewInCRM@1000 : Boolean;
      PushToCRM@1003 : Boolean;
      PushToNAV@1004 : Boolean;
      InitialSynchDisabledErr@1006 : TextConst 'ENU=No initial synchronization direction was specified because initial synchronization was disabled.;ENG=No initial synchronisation direction was specified because initial synchronisation was disabled.';
      NoSuchCRMRecordErr@1011 : TextConst '@@@="%1 = The Product ID entered by the user";ENU=Product with ID %1 does not exist in Microsoft Dynamics CRM.;ENG=Product with ID %1 does not exist in Microsoft Dynamics CRM.';
      SynchAction@1012 : 'DoNotSynch,PushToCRM';
      SavedSynchAction@1013 : Integer;

    LOCAL PROCEDURE SetPushActionCheckboxes@14();
    BEGIN
      IF SynchAction = SynchAction::DoNotSynch THEN BEGIN
        PushToCRM := FALSE;
        PushToNAV := FALSE;
      END ELSE BEGIN
        PushToCRM := SynchAction = SynchAction::PushToCRM;
        PushToNAV := NOT PushToCRM;
      END
    END;

    LOCAL PROCEDURE HandleNewCRMProduct@7();
    VAR
      CRMIntegrationRecord@1001 : Record 5331;
    BEGIN
      IF OriginalCRMProduct.ProductId <> CRMProduct.ProductId THEN
        CRMIntegrationRecord.AssertRecordIDCanBeCoupled(RECORDID,CRMProduct.ProductId);
    END;

    PROCEDURE GetCRMID@6() : GUID;
    BEGIN
      EXIT(CRMProduct.ProductId);
    END;

    PROCEDURE GetCreateNewInCRM@19() : Boolean;
    BEGIN
      EXIT(CreateNewInCRM);
    END;

    PROCEDURE GetPerformInitialSynchronization@3() : Boolean;
    BEGIN
      EXIT(SynchAction <> SynchAction::DoNotSynch);
    END;

    PROCEDURE GetInitialSynchronizationDirection@4() : Integer;
    BEGIN
      IF SynchAction = SynchAction::DoNotSynch THEN
        ERROR(InitialSynchDisabledErr);

      EXIT(IntegrationTableMapping.Direction::ToIntegrationTable);
    END;

    BEGIN
    END.
  }
}

