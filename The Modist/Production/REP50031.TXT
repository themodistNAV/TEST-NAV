OBJECT Report 50031 Correct Item Ledger Entry
{
  OBJECT-PROPERTIES
  {
    Date=14/05/18;
    Time=[ 3:49:20 PM];
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ItemLedgerEntry.ISEMPTY THEN
                    ERROR(Text001);

                  IF (JournalBatch = '') OR (JournalTemplate = '') THEN
                    ERROR(Text002);

                  ItemLedgerEntry.FINDSET;
                  REPEAT
                    ItemJournalLine.RESET;
                    ItemJournalLine.INIT;
                    ItemJournalLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
                    ItemJournalLine.SETRANGE("Journal Batch Name",JournalBatch);
                    ItemJournalLine.SETRANGE("Journal Template Name",JournalTemplate);
                    IF ItemJournalLine.FINDLAST THEN
                      LineNo := ItemJournalLine."Line No." + 10000
                    ELSE
                      LineNo := 10000;

                    ItemJournalLine.INIT;
                    ItemJournalLine.VALIDATE("Journal Template Name",JournalTemplate);
                    ItemJournalLine.VALIDATE("Journal Batch Name",JournalBatch);
                    ItemJournalLine.VALIDATE("Document No.",COPYSTR('R-' + ItemLedgerEntry."Document No.",1,20));
                    ItemJournalLine.VALIDATE("Line No.",LineNo);
                    ItemJournalLine.VALIDATE("Posting Date",ItemLedgerEntry."Posting Date");

                    IF ItemLedgerEntry."Entry Type" = ItemLedgerEntry."Entry Type"::"Positive Adjmt." THEN
                      ItemJournalLine.VALIDATE("Entry Type",ItemJournalLine."Entry Type"::"Negative Adjmt.")
                    ELSE
                      ItemJournalLine.VALIDATE("Entry Type",ItemJournalLine."Entry Type"::"Positive Adjmt.");

                    ItemJournalLine.VALIDATE("Item No.",ItemLedgerEntry."Item No.");
                    ItemJournalLine.VALIDATE("Variant Code",ItemLedgerEntry."Variant Code");
                    ItemJournalLine.VALIDATE("Location Code",ItemLedgerEntry."Location Code");
                    ItemJournalLine.VALIDATE(Quantity,ABS(ItemLedgerEntry.Quantity));
                    ItemJournalLine.INSERT(TRUE);
                    LineNo += 10000;
                  UNTIL ItemLedgerEntry.NEXT=0;

                  //MESSAGE(Text004);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=ContentArea1;
                  ContainerType=ContentArea }

      { 1000000001;1;Group  ;
                  Name=Options;
                  GroupType=Group }

      { 1000000002;2;Field  ;
                  Name=JournalTemplate;
                  CaptionML=ENU=Journal Template;
                  SourceExpr=JournalTemplate;
                  TableRelation="Item Journal Template" }

      { 1000000003;2;Field  ;
                  Name=JournalBatch;
                  CaptionML=ENU=Journal Batch;
                  SourceExpr=JournalBatch;
                  OnLookup=BEGIN
                             IF JournalTemplate = '' THEN
                               ERROR(Text003);
                             ItemJournalBatch.RESET;
                             ItemJournalBatch.SETRANGE("Journal Template Name",JournalTemplate);
                             IF PAGE.RUNMODAL(0,ItemJournalBatch) = ACTION::LookupOK THEN
                               JournalBatch := ItemJournalBatch.Name;
                           END;
                            }

      { 1000000004;2;Field  ;
                  Name=ItemLedgerEntryNo;
                  CaptionML=ENU=Entry No.;
                  SourceExpr=ItemLedgerEntryNo;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ItemLedgerEntry@1000000004 : Record 32;
      ItemJournalLine@1000000002 : Record 83;
      ItemJournalBatch@1000000011 : Record 233;
      ItemJnlMgt@1000000009 : Codeunit 240;
      JournalTemplate@1000000000 : Code[10];
      JournalBatch@1000000001 : Code[10];
      ItemLedgerEntryNo@1000000003 : Text;
      Text000@1000000005 : TextConst 'ENU=Only Positive Adj. and Negative Adj. are allowed to be reversed.';
      Text001@1000000006 : TextConst 'ENU=This process is only initiated from Item Ledger Entry Screen.';
      Text002@1000000007 : TextConst 'ENU=Journal Template and Journal Batch Must be specified.';
      LineNo@1000000008 : Integer;
      JrnlSelected@1000000010 : Boolean;
      Text003@1000000012 : TextConst 'ENU=Journal Template is missing.';
      Text004@1000000013 : TextConst 'ENU=Item Journal Created';

    PROCEDURE SetItemLedgerEntryNo@1000000003(pEntryNo@1000000000 : Integer);
    BEGIN
      CLEAR(ItemLedgerEntry);
      ItemLedgerEntry.GET(pEntryNo);
      IF (ItemLedgerEntry."Entry Type" <> ItemLedgerEntry."Entry Type"::"Negative Adjmt.") AND
         (ItemLedgerEntry."Entry Type" <> ItemLedgerEntry."Entry Type"::"Positive Adjmt.")
      THEN
        ERROR(Text000);
      //ItemLedgerEntryNo := ItemLedgerEntry."Entry No.";
    END;

    PROCEDURE SetItemLedgerEntries@1000000000(VAR pItemLedgerEntry@1000000000 : Record 32);
    BEGIN
      CLEAR(ItemLedgerEntry);
      ItemLedgerEntry.COPY(pItemLedgerEntry);
      ItemLedgerEntry.FINDFIRST;
      ItemLedgerEntryNo := FORMAT(ItemLedgerEntry."Entry No.");
      ItemLedgerEntry.FINDLAST;
      ItemLedgerEntryNo += '..' + FORMAT(ItemLedgerEntry."Entry No.");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

