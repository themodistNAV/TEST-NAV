OBJECT Page 11102092 OM - Renumber Objects
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Renumber Objects;
               ENU=Renumber Objects;
               ESP=Renumber Objects;
               FRA=Renumber Objects;
               NLD=Objecten omnummeren;
               DEA=Renumber Objects];
    SaveValues=Yes;
    SourceTable=Table11102063;
    DelayedInsert=Yes;
    PageType=Worksheet;
    ShowFilter=No;
    OnInit=BEGIN
             TargetFileNameEnabled := TRUE;
             SourceFileNameEnabled := TRUE;
             WebClient := FALSE;
           END;

    OnOpenPage=VAR
                 WhereUsedStatus@1000 : Codeunit 11102047;
                 FileHandler@1001 : Codeunit 11102097;
               BEGIN
                 IF NOT FormInitialized THEN BEGIN
                   RunType := RunType::"Update Text File";
                   KeepDeltaEqual := TRUE;
                   FormInitialized := TRUE;
                 END;

                 UpdateReferencingObjects := TRUE;
                 SetModifyFlag := TRUE;

                 WebClient := FileHandler.IsWebClient;
                 IF WebClient THEN
                   SourceFileName := '';

                 WhereUsedStatus.WhereUsedUpToDate;
                 RefreshNoOfInvolvedObjects := TRUE;

                 SetEnabled;
               END;

    OnAfterGetRecord=BEGIN
                       IF "Object No." = 0 THEN
                         ObjectNo := ''
                       ELSE
                         ObjectNo := FORMAT("Object No.");

                       SetLineStyle;

                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  "Object Type" := xRec."Object Type";
                  ObjectNo := '';

                  OnAfterGetCurrRecord;
                END;

    OnInsertRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    OnModifyRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     RefreshNoOfInvolvedObjects := TRUE;
                   END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      CaptionML=[DEU=&Start;
                                 ENU=&Start;
                                 ESP=&Start;
                                 FRA=&Start;
                                 NLD=&Starten;
                                 DEA=&Start];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 RenumberObjectMgt@1000 : Codeunit 11102048;
                                 FileHandler@1001 : Codeunit 11102097;
                                 FileToCreate@1002 : Text;
                                 ServerExportFileName@1003 : Text;
                                 ServerSourceFileName@1004 : Text;
                                 ctDefaultFileName@1005 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctEnterFileName@1006 : TextConst 'DEU=No file name entered.;ENU=No file name entered.;ESP=No file name entered.;FRA=No file name entered.;NLD=Geen bestandsnaam opgegeven.;DEA=No file name entered.';
                               BEGIN
                                 CASE RunType OF

                                   RunType::Export :
                                     BEGIN
                                       IF NOT WebClient THEN BEGIN
                                         IF ExportFileName = '' THEN
                                           ERROR(ctEnterFileName);
                                         FileHandler.QueryReplace(ExportFileName);
                                       END;
                                       FileToCreate := ExportFileName;
                                       ServerExportFileName := FileHandler.CreateServerFileName(ExportFileName, ctDefaultFileName);
                                     END;

                                   RunType::"Update Text File" :
                                     BEGIN
                                       IF SourceFileName = '' THEN
                                         ERROR(ctEnterFileName);

                                       FileToCreate := FileHandler.CheckUpdateExisting(SourceFileName, ExportFileName);
                                       ServerExportFileName := FileHandler.CreateServerFileName(FileToCreate, ctDefaultFileName);
                                       ServerSourceFileName := FileHandler.UploadToServer(SourceFileName, '');
                                     END;

                                 END;

                                 IF RunType = RunType::"Update in DB" THEN
                                   RenumberObjectMgt.SetRunType(3)
                                 ELSE
                                   RenumberObjectMgt.SetRunType(RunType);

                                 RenumberObjectMgt.SetSourceFileName(ServerSourceFileName);
                                 RenumberObjectMgt.SetExportFileName(ServerExportFileName);
                                 RenumberObjectMgt.SetUpdateReferencingObjects(UpdateReferencingObjects);
                                 RenumberObjectMgt.SetSetModifyFlag(SetModifyFlag);
                                 RenumberObjectMgt.RUN(Rec);

                                 IF RunType = RunType::"Update Text File" THEN
                                   FileHandler.FinalizeUpdateExisting(SourceFileName);

                                 IF RunType IN [RunType::Export, RunType::"Update Text File"] THEN
                                   FileHandler.DownloadToClient(ServerExportFileName, FileToCreate, ctDefaultFileName);

                                 RenumberObjectMgt.ShowSucceedMessage;
                               END;
                                }
      { 3       ;1   ;ActionGroup;
                      CaptionML=[DEU=Add Objects;
                                 ENU=Add Objects;
                                 ESP=Add Objects;
                                 FRA=Add Objects;
                                 NLD=F&uncties;
                                 DEA=Add Objects] }
      { 4       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+G;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add &Objects;
                                 ENU=Add &Objects;
                                 ESP=Add &Objects;
                                 FRA=Add &Objects;
                                 NLD=&Objecten toevoegen;
                                 DEA=Add &Objects];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AddRenObjectLines@1000 : Report 11102041;
                               BEGIN
                                 IF KeepDeltaEqual THEN
                                   AddRenObjectLines.SetDelta(Delta);

                                 AddRenObjectLines.RUNMODAL;
                                 IF FINDFIRST THEN
                                   ;
                                 CurrPage.UPDATE(FALSE);

                                 RefreshNoOfInvolvedObjects := TRUE;
                               END;
                                }
      { 5       ;2   ;Separator  }
      { 6       ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add Objects from &Source File;
                                 ENU=Add Objects from &Source File;
                                 ESP=Add Objects from &Source File;
                                 FRA=Add Objects from &Source File;
                                 NLD=Objecten toevoegen vanaf &bronbestand;
                                 DEA=Add Objects from &Source File];
                      Image=Add;
                      OnAction=VAR
                                 TmpCALHistoryObject@1000 : TEMPORARY Record 11102052;
                                 TmpCALHistoryLine@1001 : TEMPORARY Record 11102053;
                                 ObjectToRenumber@1002 : Record 11102063;
                                 Progressbar@1003 : Codeunit 11102044;
                                 SourceControl@1004 : Codeunit 11102050;
                                 FileHandler@1005 : Codeunit 11102097;
                                 oFile@1006 : File;
                                 ServerSourceFileName@1007 : Text;
                                 ctDefaultFileName@1008 : TextConst 'DEU=objects.txt;ENU=objects.txt;ESP=objects.txt;FRA=objects.txt;NLD=objects.txt;DEA=objects.txt';
                                 ctReadingObjects@1009 : TextConst 'DEU=Reading objects...;ENU=Reading objects...;ESP=Reading objects...;FRA=Reading objects...;NLD=Objecten lezen...;DEA=Reading objects...';
                               BEGIN
                                 Progressbar.Open(ctReadingObjects, 1);

                                 ServerSourceFileName := FileHandler.UploadToServer(SourceFileName, ctDefaultFileName);

                                 oFile.OPEN(ServerSourceFileName);
                                 Progressbar.BarSize := oFile.LEN;
                                 oFile.CLOSE;

                                 SourceControl.ImportTextFile2(ServerSourceFileName, 1, Progressbar, 0, TmpCALHistoryObject, TmpCALHistoryLine, TRUE);

                                 Progressbar.Close;

                                 TmpCALHistoryObject.SETFILTER("Object Type", '<>%1', TmpCALHistoryObject."Object Type"::MenuSuite);
                                 IF TmpCALHistoryObject.FINDSET THEN
                                   REPEAT
                                     IF NOT ObjectToRenumber.GET(TmpCALHistoryObject."Object Type", TmpCALHistoryObject."Object No.") THEN BEGIN
                                       ObjectToRenumber."Object Type" := TmpCALHistoryObject."Object Type";
                                       ObjectToRenumber."Object No." := TmpCALHistoryObject."Object No.";
                                       IF KeepDeltaEqual THEN
                                         ObjectToRenumber.VALIDATE("New Number", TmpCALHistoryObject."Object No." + Delta)
                                       ELSE
                                         ObjectToRenumber.VALIDATE("New Number", TmpCALHistoryObject."Object No.");
                                       ObjectToRenumber.INSERT;
                                     END;

                                   UNTIL TmpCALHistoryObject.NEXT = 0;

                                 IF FINDFIRST THEN
                                   ;
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add Objects from &Project;
                                 ENU=Add Objects from &Project;
                                 ESP=Add Objects from &Project;
                                 FRA=Add Objects from &Project;
                                 NLD=Objecten toevoegen vanaf &project;
                                 DEA=Add Objects from &Project];
                      Image=Journal;
                      OnAction=BEGIN
                                 AddProjects;
                               END;
                                }
      { 8       ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Add Objects from &Transport;
                                 ENU=Add Objects from &Transport;
                                 ESP=Add Objects from &Transport;
                                 FRA=Add Objects from &Transport;
                                 NLD=Objecten toevoegen vanaf &transport;
                                 DEA=Add Objects from &Transport];
                      Image=Travel;
                      OnAction=BEGIN
                                 AddTransports;
                               END;
                                }
      { 9       ;1   ;ActionGroup;
                      CaptionML=[DEU=Definition File;
                                 ENU=Definition File;
                                 ESP=Definition File;
                                 FRA=Definition File;
                                 NLD=Definitiebestand;
                                 DEA=Definition File] }
      { 10      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Export &Definition to File;
                                 ENU=Export &Definition to File;
                                 ESP=Export &Definition to File;
                                 FRA=Export &Definition to File;
                                 NLD=Definitie &exporteren;
                                 DEA=Export &Definition to File];
                      Image=Export;
                      OnAction=VAR
                                 RenumberObjectMgt@1000 : Codeunit 11102048;
                                 CommonDialog@1001 : Codeunit 11102059;
                                 FileHandler@1002 : Codeunit 11102097;
                                 FileName@1003 : Text;
                                 ServerFileName@1004 : Text;
                                 ctDefaultFileName@1005 : TextConst 'DEU=definition.txt;ENU=definition.txt;ESP=definition.txt;FRA=definition.txt;NLD=definition.txt;DEA=definition.txt';
                                 ctExtensions@1006 : TextConst 'DEU=Renumber Objects Definition File (*.txt)|*.txt;ENU=Renumber Objects Definition File (*.txt)|*.txt;ESP=Renumber Objects Definition File (*.txt)|*.txt;FRA=Renumber Objects Definition File (*.txt)|*.txt;NLD=Renumber Objects Definition File (*.txt)|*.txt;DEA=Renumber Objects Definition File (*.txt)|*.txt';
                               BEGIN
                                 CommonDialog.AddExtension(ctExtensions, 'txt');
                                 IF NOT CommonDialog.SaveFile(FileName, ServerFileName, ctDefaultFileName) THEN
                                   EXIT;

                                 RenumberObjectMgt.ExportDefinition(ServerFileName);

                                 FileHandler.DownloadToClient(ServerFileName, FileName, ctDefaultFileName);
                               END;
                                }
      { 11      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Import Definition from File;
                                 ENU=Import Definition from File;
                                 ESP=Import Definition from File;
                                 FRA=Import Definition from File;
                                 NLD=Definitie &importeren;
                                 DEA=Import Definition from File];
                      Image=Import;
                      OnAction=VAR
                                 RenumberObjectMgt@1000 : Codeunit 11102048;
                                 CommonDialog@1001 : Codeunit 11102059;
                                 FileName@1002 : Text;
                                 ServerFileName@1003 : Text;
                                 ctExtensions@1004 : TextConst 'DEU=Renumber Objects Definition File (*.txt)|*.txt;ENU=Renumber Objects Definition File (*.txt)|*.txt;ESP=Renumber Objects Definition File (*.txt)|*.txt;FRA=Renumber Objects Definition File (*.txt)|*.txt;NLD=Renumber Objects Definition File (*.txt)|*.txt;DEA=Renumber Objects Definition File (*.txt)|*.txt';
                                 ctFileImported@1005 : TextConst 'DEU=Definition file imported.;ENU=Definition file imported.;ESP=Definition file imported.;FRA=Definition file imported.;NLD=Definitiebestand ge‹mporteerd.;DEA=Definition file imported.';
                               BEGIN
                                 CommonDialog.AddExtension(ctExtensions, 'txt');
                                 IF NOT CommonDialog.OpenFile(FileName, ServerFileName) THEN
                                   EXIT;

                                 RenumberObjectMgt.ImportDefinition(ServerFileName);

                                 IF FINDFIRST THEN
                                   ;

                                 MESSAGE(ctFileImported);
                               END;
                                }
      { 12      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unctions;
                                 ENU=F&unctions;
                                 ESP=F&unctions;
                                 FRA=F&unctions;
                                 NLD=F&uncties;
                                 DEA=F&unctions] }
      { 13      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F5;
                      CaptionML=[DEU=Update Where Used;
                                 ENU=Update Where Used;
                                 ESP=Update Where Used;
                                 FRA=Update Where Used;
                                 NLD=Waar-gebruikt bijwerken;
                                 DEA=Update Where Used];
                      Image=Refresh;
                      OnAction=VAR
                                 WhereUsedMgt@1000 : Codeunit 11102045;
                               BEGIN
                                 WhereUsedMgt.UpdateObjects;
                               END;
                                }
      { 14      ;2   ;Separator  }
      { 15      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Copy to Project as Actions &Before;
                                 ENU=Copy to Project as Actions &Before;
                                 ESP=Copy to Project as Actions &Before;
                                 FRA=Copy to Project as Actions &Before;
                                 NLD=Kopi‰ren naar project als acties &vooraf;
                                 DEA=Copy to Project as Actions &Before];
                      Visible=FALSE;
                      Image=Copy;
                      OnAction=BEGIN
                                 CopyToProject;
                               END;
                                }
      { 16      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Rename Objects;
                                 ENU=Rename Objects;
                                 ESP=Rename Objects;
                                 FRA=Rename Objects;
                                 NLD=Objecten &hernoemen;
                                 DEA=Rename Objects];
                      Visible=FALSE;
                      Image=StepInto;
                      OnAction=VAR
                                 RenameObjects@1000 : Report 11102042;
                                 ctRenameObjects@1001 : TextConst 'DEU=Object will not be renumbered but renamed.\Are you sure?;ENU=Object will not be renumbered but renamed.\Are you sure?;ESP=Object will not be renumbered but renamed.\Are you sure?;FRA=Object will not be renumbered but renamed.\Are you sure?;NLD=Objecten worden niet omgenummerd maar hernoemd.\Weet u het zeker?;DEA=Object will not be renumbered but renamed.\Are you sure?';
                               BEGIN
                                 IF CONFIRM(ctRenameObjects, TRUE) THEN
                                   RenameObjects.RUN;
                               END;
                                }
      { 17      ;2   ;Action    ;
                      CaptionML=[DEU=Export to Repository;
                                 ENU=Export to Repository;
                                 ESP=Export to Repository;
                                 FRA=Export to Repository;
                                 NLD=Naar repository exporteren;
                                 DEA=Export to Repository];
                      Visible=FALSE;
                      Image=Database;
                      OnAction=VAR
                                 TmpAction@1000 : TEMPORARY Record 11102048;
                                 TmpActionField@1001 : TEMPORARY Record 11102049;
                                 ObjectToRenumber@1002 : Record 11102063;
                                 RepositorySetup@1003 : Record 11102085;
                                 RepositoryMgt@1004 : Codeunit 11102067;
                                 NoOfActionsAdded@1005 : Integer;
                                 ctActionsExported@1006 : TextConst 'DEU=%1 actions exported to repository.;ENU=%1 actions exported to repository.;ESP=%1 actions exported to repository.;FRA=%1 actions exported to repository.;NLD=%1 acties naar repository ge‰xporteerd.;DEA=%1 actions exported to repository.';
                               BEGIN
                                 RepositorySetup.GET;
                                 RepositorySetup.TESTFIELD("Use Repository", TRUE);

                                 ObjectToRenumber := Rec;

                                 IF FINDSET THEN
                                   REPEAT
                                     IF "Object No." <> "New Number" THEN BEGIN

                                       FillActions(TmpAction, TmpActionField);
                                       NoOfActionsAdded += 1;
                                     END;

                                   UNTIL NEXT = 0;

                                 Rec := ObjectToRenumber;

                                 IF NoOfActionsAdded > 0 THEN
                                   RepositoryMgt.AddAction(TmpAction, TmpActionField);

                                 MESSAGE(ctActionsExported, NoOfActionsAdded);
                               END;
                                }
      { 40      ;2   ;ActionGroup;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten &markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter }
      { 38      ;3   ;Action    ;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=Mark Objects;
                                 ENU=Mark Objects;
                                 ESP=Mark Objects;
                                 FRA=Mark Objects;
                                 NLD=Objecten markeren;
                                 DEA=Mark Objects];
                      Image=EditFilter;
                      OnAction=VAR
                                 ObjectToRenumber@1000 : Record 11102063;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.CheckMarkLock;

                                 CurrPage.SETSELECTIONFILTER(ObjectToRenumber);
                                 IF ObjectToRenumber.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(ObjectToRenumber."Object Type", '', ObjectToRenumber."Object No.") THEN
                                       VersionControl.MarkObject(Object, TRUE);
                                   UNTIL ObjectToRenumber.NEXT = 0;
                               END;
                                }
      { 35      ;3   ;Action    ;
                      CaptionML=[DEU=Remove Marks;
                                 ENU=Remove Marks;
                                 ESP=Remove Marks;
                                 FRA=Remove Marks;
                                 NLD=Verwijder markeringen;
                                 DEA=Remove Marks];
                      Image=RemoveFilterLines;
                      OnAction=VAR
                                 ObjectToRenumber@1000 : Record 11102063;
                                 Object@1001 : Record 2000000001;
                                 VersionControl@1002 : Codeunit 11102040;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ObjectToRenumber);
                                 IF ObjectToRenumber.FINDSET THEN
                                   REPEAT
                                     IF Object.GET(ObjectToRenumber."Object Type", '', ObjectToRenumber."Object No.") THEN
                                       VersionControl.RemoveObjectMark(Object);
                                   UNTIL ObjectToRenumber.NEXT = 0;

                                 VersionControl.RemoveMarkLock(TRUE);
                               END;
                                }
      { 36      ;3   ;Action    ;
                      ShortCutKey=Shift+Ctrl+M;
                      CaptionML=[DEU=Remove &all Marks;
                                 ENU=Remove &all Marks;
                                 ESP=Remove &all Marks;
                                 FRA=Remove &all Marks;
                                 NLD=Verwijder &alle markeringen;
                                 DEA=Remove &all Marks];
                      Image=ClearFilter;
                      OnAction=VAR
                                 VersionControl@1000 : Codeunit 11102040;
                               BEGIN
                                 VersionControl.RemoveObjectMarks(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 18  ;0   ;Container ;
                ContainerType=ContentArea }

    { 19  ;1   ;Group      }

    { 20  ;2   ;Field     ;
                CaptionML=[DEU=Export / Update in DB;
                           ENU=Export / Update in DB;
                           ESP=Export / Update in DB;
                           FRA=Export / Update in DB;
                           NLD=Exporteren / Bijwerken in DB;
                           DEA=Export / Update in DB];
                OptionCaptionML=[DEU=Update in DB,Export,Update Text File;
                                 ENU=Update in DB,Export,Update Text File;
                                 ESP=Update in DB,Export,Update Text File;
                                 FRA=Update in DB,Export,Update Text File;
                                 NLD=Bijwerken in DB,Exporteren,Tekstbestand bijwerken;
                                 DEA=Update in DB,Export,Update Text File];
                SourceExpr=RunType;
                OnValidate=BEGIN
                             SetEnabled;
                           END;
                            }

    { 21  ;2   ;Field     ;
                CaptionML=[DEU=Source File Name;
                           ENU=Source File Name;
                           ESP=Source File Name;
                           FRA=Source File Name;
                           NLD=Bronbestandsnaam;
                           DEA=Source File Name];
                SourceExpr=SourceFileName;
                Enabled=SourceFileNameEnabled;
                Editable=NOT WebClient;
                OnLookup=VAR
                           CommonDialog@1000 : Codeunit 11102059;
                         BEGIN
                           CommonDialog.AddKnownExtension('txt-o');
                           CommonDialog.GetOpenFileName(SourceFileName, WebClient);
                         END;
                          }

    { 22  ;2   ;Field     ;
                CaptionML=[DEU=File Name;
                           ENU=File Name;
                           ESP=File Name;
                           FRA=File Name;
                           NLD=Bestandsnaam;
                           DEA=File Name];
                SourceExpr=ExportFileName;
                Visible=NOT WebClient;
                Enabled=TargetFileNameEnabled;
                OnLookup=VAR
                           CommonDialog@1000 : Codeunit 11102059;
                         BEGIN
                           CommonDialog.AddKnownExtension('txt-o');
                           CommonDialog.GetSaveFileName(ExportFileName);
                         END;
                          }

    { 23  ;2   ;Field     ;
                CaptionML=[DEU=No. of Involved Objects;
                           ENU=No. of Involved Objects;
                           ESP=No. of Involved Objects;
                           FRA=No. of Involved Objects;
                           NLD=Aantal betrokken objecten;
                           DEA=No. of Involved Objects];
                SourceExpr=NoOfInvolvedObjects;
                Editable=FALSE;
                OnDrillDown=VAR
                              RenumberObjectMgt@1000 : Codeunit 11102048;
                            BEGIN
                              RenumberObjectMgt.DrillDownNoOfInvolvedObjects(UpdateReferencingObjects);
                            END;
                             }

    { 24  ;2   ;Field     ;
                CaptionML=[DEU=Keep Delta Equal;
                           ENU=Keep Delta Equal;
                           ESP=Keep Delta Equal;
                           FRA=Keep Delta Equal;
                           NLD=Afwijking gelijk houden;
                           DEA=Keep Delta Equal];
                SourceExpr=KeepDeltaEqual;
                OnValidate=VAR
                             ObjectToRenumber@1000 : Record 11102063;
                           BEGIN
                             IF KeepDeltaEqual THEN BEGIN
                               Delta := "New Number" - "Object No.";
                               IF ObjectToRenumber.FINDSET THEN
                                 REPEAT
                                   IF (ObjectToRenumber."Object Type" <> "Object Type") OR (ObjectToRenumber."Object No." <> "Object No.") THEN BEGIN
                                     ObjectToRenumber."New Number" := ObjectToRenumber."Object No." + Delta;
                                     ObjectToRenumber.MODIFY;
                                   END;
                                 UNTIL ObjectToRenumber.NEXT = 0;
                             END;
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 25  ;2   ;Field     ;
                CaptionML=[DEU=Delta;
                           ENU=Delta;
                           ESP=Delta;
                           FRA=Delta;
                           NLD=Afwijking;
                           DEA=Delta];
                SourceExpr=Delta;
                OnValidate=VAR
                             ObjectToRenumber@1000 : Record 11102063;
                             ObjectToRenumber2@1001 : Record 11102063;
                           BEGIN
                             IF KeepDeltaEqual THEN BEGIN

                               CurrPage.SETSELECTIONFILTER(ObjectToRenumber);
                               IF ObjectToRenumber.COUNT <= 1 THEN
                                 ObjectToRenumber.RESET;

                               IF ObjectToRenumber.FINDSET THEN
                                 REPEAT
                                   ObjectToRenumber2 := ObjectToRenumber;
                                   ObjectToRenumber2."New Number" := ObjectToRenumber2."Object No." + Delta;
                                   ObjectToRenumber2.MODIFY;
                                 UNTIL ObjectToRenumber.NEXT = 0;

                             END ELSE BEGIN
                               "New Number" := "Object No." + Delta;
                               MODIFY;
                             END;
                             CurrPage.UPDATE(FALSE);

                             RefreshNoOfInvolvedObjects := TRUE;
                           END;
                            }

    { 26  ;1   ;Group     ;
                GroupType=Repeater }

    { 27  ;2   ;Field     ;
                SourceExpr="Object Type";
                StyleExpr=LineStyleExpr }

    { 28  ;2   ;Field     ;
                Width=13;
                BlankZero=Yes;
                SourceExpr=ObjectNo;
                CaptionClass=FIELDCAPTION("Object No.");
                StyleExpr=LineStyleExpr;
                OnValidate=VAR
                             Mgt@1000 : Codeunit 11102035;
                           BEGIN
                             Mgt.AfterInputObjectNo("Object Type", ObjectNo);
                             EVALUATE("Object No.", ObjectNo);
                             VALIDATE("Object No.");

                             IF (Delta <> 0) AND KeepDeltaEqual THEN
                               "New Number" := "Object No." + Delta;
                           END;

                OnLookup=VAR
                           Mgt@1000 : Codeunit 11102035;
                         BEGIN
                           EXIT(Mgt.LookupObjectNo("Object Type", Text));
                         END;
                          }

    { 29  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Object Name";
                StyleExpr=LineStyleExpr }

    { 30  ;2   ;Field     ;
                SourceExpr="New Number";
                StyleExpr=LineStyleExpr;
                OnValidate=VAR
                             OriginalObjectToRenumber@1000 : Record 11102063;
                           BEGIN
                             IF KeepDeltaEqual THEN BEGIN

                               OriginalObjectToRenumber := Rec;

                               Delta := "New Number" - "Object No.";
                               IF FINDSET THEN
                                 REPEAT
                                   IF ("Object Type" <> OriginalObjectToRenumber."Object Type") OR
                                      ("Object No." <> OriginalObjectToRenumber."Object No.")
                                   THEN BEGIN
                                     "New Number" := "Object No." + Delta;
                                     MODIFY;
                                   END;
                                 UNTIL NEXT = 0;

                               Rec := OriginalObjectToRenumber;
                               CurrPage.UPDATE(TRUE);
                             END;
                           END;
                            }

    { 31  ;1   ;Field     ;
                SourceExpr=FormInitialized;
                Visible=FALSE }

    { 32  ;1   ;Group     ;
                CaptionML=[DEU=Options;
                           ENU=Options;
                           ESP=Options;
                           FRA=Options;
                           NLD=Opties;
                           DEA=Options];
                GroupType=Group }

    { 34  ;2   ;Field     ;
                CaptionML=[DEU=Update Referencing Objects;
                           ENU=Update Referencing Objects;
                           ESP=Update Referencing Objects;
                           FRA=Update Referencing Objects;
                           NLD=Refererende objecten bijwerken;
                           DEA=Update Referencing Objects];
                SourceExpr=UpdateReferencingObjects;
                OnValidate=BEGIN
                             NoOfInvolvedObjects := CalcNoOfInvolvedObjects;
                           END;
                            }

    { 33  ;2   ;Field     ;
                CaptionML=[DEU=Set Modify Flag;
                           ENU=Set Modify Flag;
                           ESP=Set Modify Flag;
                           FRA=Set Modify Flag;
                           NLD=Wijzigvlag zetten;
                           DEA=Set Modify Flag];
                SourceExpr=SetModifyFlag }

  }
  CODE
  {
    VAR
      ExportFileName@1000 : Text;
      LineStyleExpr@1001 : Text INDATASET;
      ObjectNo@1002 : Text INDATASET;
      SourceFileName@1003 : Text;
      Delta@1004 : Integer;
      NoOfInvolvedObjects@1005 : Integer;
      RunType@1006 : 'Update in DB,Export,Update Text File';
      FormInitialized@1007 : Boolean;
      KeepDeltaEqual@1008 : Boolean;
      RefreshNoOfInvolvedObjects@1009 : Boolean;
      SetModifyFlag@1010 : Boolean;
      SourceFileNameEnabled@1011 : Boolean INDATASET;
      TargetFileNameEnabled@1012 : Boolean INDATASET;
      UpdateReferencingObjects@1013 : Boolean;
      WebClient@1014 : Boolean INDATASET;

    LOCAL PROCEDURE OnAfterGetCurrRecord@1();
    BEGIN
      IF "Object No." <> 0 THEN
        Delta := "New Number" - "Object No.";

      IF RefreshNoOfInvolvedObjects THEN BEGIN
        NoOfInvolvedObjects := CalcNoOfInvolvedObjects;
        RefreshNoOfInvolvedObjects := FALSE;
      END;
    END;

    LOCAL PROCEDURE CalcNoOfInvolvedObjects@2() : Integer;
    VAR
      RenumberObjectMgt@1000 : Codeunit 11102048;
    BEGIN
      EXIT(RenumberObjectMgt.NoOfInvolvedObjects(UpdateReferencingObjects));
    END;

    LOCAL PROCEDURE SetEnabled@3();
    BEGIN
      SourceFileNameEnabled := RunType = RunType::"Update Text File";
      TargetFileNameEnabled := RunType IN [RunType::Export, RunType::"Update Text File"];
    END;

    LOCAL PROCEDURE CopyToProject@4();
    VAR
      Project@1000 : Record 11102036;
      Actions@1001 : Record 11102048;
      TmpAction@1002 : TEMPORARY Record 11102048;
      ActionField@1003 : Record 11102049;
      TmpActionField@1004 : TEMPORARY Record 11102049;
      ObjectToRenumber@1005 : Record 11102063;
      LastLineNo@1006 : Integer;
      NoOfActionsAdded@1007 : Integer;
      ctActionsInsert@1008 : TextConst 'DEU=%1 actions added to project.;ENU=%1 actions added to project.;ESP=%1 actions added to project.;FRA=%1 actions added to project.;NLD=%1 acties aan project toegevoegd.;DEA=%1 actions added to project.';
    BEGIN
      IF PAGE.RUNMODAL(PAGE::"OM - Projects", Project) = ACTION::LookupOK THEN BEGIN
        Actions.SETRANGE(Type, Actions.Type::Project);
        Actions.SETRANGE("No.", Project."No.");
        Actions.SETRANGE("Sub Type", Actions."Sub Type"::Before);
        IF Actions.FINDLAST THEN
          LastLineNo := Actions."Line No.";

        ObjectToRenumber := Rec;

        IF FINDSET THEN
          REPEAT
            IF "Object No." <> "New Number" THEN BEGIN

              TmpAction.DELETEALL;
              TmpActionField.DELETEALL;

              FillActions(TmpAction, TmpActionField);

              Actions := TmpAction;
              Actions.Type := Actions.Type::Project;
              Actions."No." := Project."No.";
              Actions."Sub Type" := Actions."Sub Type"::Before;
              Actions."Line No." := LastLineNo + 10000;
              Actions.INSERT;

              LastLineNo += 10000;

              IF TmpActionField.FINDSET THEN
                REPEAT
                  ActionField := TmpActionField;

                  ActionField.Type := Actions.Type;
                  ActionField."No." := Actions."No.";
                  ActionField."Sub Type" := Actions."Sub Type";
                  ActionField."Line No." := Actions."Line No.";
                  ActionField.INSERT;

                UNTIL TmpActionField.NEXT = 0;

              NoOfActionsAdded += 1;
            END;

          UNTIL NEXT = 0;

        Rec := ObjectToRenumber;
      END;

      MESSAGE(ctActionsInsert, NoOfActionsAdded);
    END;

    LOCAL PROCEDURE AddProjects@5();
    VAR
      Project@1000 : Record 11102036;
      ProjectObject@1001 : Record 11102037;
      ObjectToRenumber@1002 : Record 11102063;
      Projects@1003 : Page 11102040;
    BEGIN
      Projects.EDITABLE := FALSE;
      Projects.LOOKUPMODE := TRUE;

      IF Projects.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Projects.GetSelectionFilter(Project);
        IF Project.FINDSET THEN BEGIN
          REPEAT
            ProjectObject.SETFILTER("Object Type", '<>%1', ProjectObject."Object Type"::MenuSuite);
            ProjectObject.SETRANGE("Project No.", Project."No.");
            IF ProjectObject.FINDSET THEN
              REPEAT
                IF NOT ObjectToRenumber.GET(ProjectObject."Object Type", ProjectObject."Object No.") THEN BEGIN
                  ObjectToRenumber."Object Type" := ProjectObject."Object Type";
                  ObjectToRenumber."Object No." := ProjectObject."Object No.";
                  IF KeepDeltaEqual THEN
                    ObjectToRenumber.VALIDATE("New Number", ProjectObject."Object No." + Delta)
                  ELSE
                    ObjectToRenumber.VALIDATE("New Number", ProjectObject."Object No.");
                  ObjectToRenumber.INSERT;
                END;
              UNTIL ProjectObject.NEXT = 0;
          UNTIL Project.NEXT = 0;

          IF FINDFIRST THEN
            ;

        END;
      END;
    END;

    LOCAL PROCEDURE AddTransports@6();
    VAR
      Project@1000 : Record 11102036;
      ProjectObject@1001 : Record 11102037;
      Transport@1002 : Record 11102043;
      ObjectToRenumber@1003 : Record 11102063;
      Transports@1004 : Page 11102055;
    BEGIN
      Transports.EDITABLE := FALSE;
      Transports.LOOKUPMODE := TRUE;

      IF Transports.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Transports.GetSelectionFilter(Transport);
        IF Transport.FINDSET THEN BEGIN
          REPEAT
            Project.SETCURRENTKEY("Transport No.");
            Project.SETRANGE("Transport No.", Transport."No.");
            IF Project.FINDSET THEN
              REPEAT
                ProjectObject.SETFILTER("Object Type", '<>%1', ProjectObject."Object Type"::MenuSuite);
                ProjectObject.SETRANGE("Project No.", Project."No.");
                IF ProjectObject.FINDSET THEN
                  REPEAT
                    IF NOT ObjectToRenumber.GET(ProjectObject."Object Type", ProjectObject."Object No.") THEN BEGIN
                      ObjectToRenumber."Object Type" := ProjectObject."Object Type";
                      ObjectToRenumber."Object No." := ProjectObject."Object No.";
                      IF KeepDeltaEqual THEN
                        ObjectToRenumber.VALIDATE("New Number", ProjectObject."Object No." + Delta)
                      ELSE
                        ObjectToRenumber.VALIDATE("New Number", ProjectObject."Object No.");
                      ObjectToRenumber.INSERT;
                    END;
                  UNTIL ProjectObject.NEXT = 0;
              UNTIL Project.NEXT = 0;

          UNTIL Transport.NEXT = 0;
          IF FINDFIRST THEN
            ;

        END;
      END;
    END;

    LOCAL PROCEDURE SetLineStyle@7();
    BEGIN
      CASE TRUE OF

        NewNumberDouble :
          LineStyleExpr := 'Attention';

        NewNumberExist :
          LineStyleExpr := 'Attention';

        NOT ("New Number" IN [0, "Object No."]) :
          LineStyleExpr := 'Favorable';

        ELSE
          LineStyleExpr := '';

      END;
    END;

    BEGIN
    END.
  }
}

