OBJECT Codeunit 9801 Identity Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      UserAccountHelper@1003 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=9.0.*.*, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavUserAccount.NavUserAccountHelper";

    PROCEDURE SetAuthenticationKey@1(UserSecurityID@1000 : GUID;Key@1001 : Text[80]);
    BEGIN
      IF NOT UserAccountHelper.TrySetAuthenticationKey(UserSecurityID,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetAuthenticationKey@14(UserSecurityID@1000 : GUID) Key : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryGetAuthenticationKey(UserSecurityID,Key) THEN
        Key := FORMAT(GETLASTERRORTEXT,80);
    END;

    PROCEDURE GetNameIdentifier@2(UserSecurityID@1000 : GUID) NameID : Text[250];
    BEGIN
      IF NOT UserAccountHelper.TryGetNameIdentifier(UserSecurityID,NameID) THEN
        NameID := FORMAT(GETLASTERRORTEXT,250);
    END;

    PROCEDURE CreateWebServicesKey@8(UserSecurityID@1000 : GUID;ExpiryDate@1001 : DateTime) Key : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryCreateWebServicesKey(UserSecurityID,ExpiryDate,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE CreateWebServicesKeyNoExpiry@13(UserSecurityID@1000 : GUID) Key : Text[80];
    VAR
      ExpiryDate@1001 : DateTime;
    BEGIN
      IF NOT UserAccountHelper.TryCreateWebServicesKey(UserSecurityID,ExpiryDate,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetWebServicesKey@4(UserSecurityID@1000 : GUID) Key : Text[80];
    VAR
      ExpiryDate@1001 : DateTime;
    BEGIN
      IF NOT UserAccountHelper.TryGetWebServicesKey(UserSecurityID,Key,ExpiryDate) THEN
        Key := FORMAT(GETLASTERRORTEXT,80);
    END;

    PROCEDURE IsAzure@5() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsAzure;
    END;

    LOCAL PROCEDURE CreateAuthenticationKey@3(UserSecurityID@1000 : GUID) Key : Text[80];
    BEGIN
      // Generates a crypt random code, with high enough entropy
      IF NOT UserAccountHelper.TrySetAuthenticationKey(UserSecurityID,Key) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetWebServiceExpiryDate@6(UserSecurityID@1000 : GUID) ExpiryDate : DateTime;
    VAR
      Key@1001 : Text[80];
    BEGIN
      IF NOT UserAccountHelper.TryGetWebServicesKey(UserSecurityID,Key,ExpiryDate) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetACSStatus@7(UserSecurityID@1001 : GUID) ACSStatus : Integer;
    VAR
      ACSStatusOption@1000 : 'Disabled,Pending,Registered,Unknown';
      Key@1002 : Text[80];
      NameID@1003 : Text[250];
    BEGIN
      // Determines the status as follows:
      // If neither Nameidentifier, nor authentication key then Disabled
      // If authentiation key then Pending
      // If NameIdentifier then Registered
      // If no permission: Unknown

      IF NOT UserAccountHelper.TryGetAuthenticationKey(UserSecurityID,Key) THEN BEGIN
        ACSStatusOption := ACSStatusOption::Unknown;
        ACSStatus := ACSStatusOption;
        EXIT;
      END;

      IF NOT UserAccountHelper.TryGetNameIdentifier(UserSecurityID,NameID) THEN BEGIN
        ACSStatusOption := ACSStatusOption::Unknown;
        ACSStatus := ACSStatusOption;
        EXIT;
      END;

      IF NameID = '' THEN BEGIN
        IF Key = '' THEN
          ACSStatusOption := ACSStatusOption::Disabled
        ELSE
          ACSStatusOption := ACSStatusOption::Pending;
      END ELSE
        ACSStatusOption := ACSStatusOption::Registered;

      ACSStatus := ACSStatusOption;
    END;

    LOCAL PROCEDURE ValidateKeyStrength@9(Key@1001 : Text[250]) : Boolean;
    VAR
      i@1000 : Integer;
      KeyLen@1002 : Integer;
      HasUpper@1003 : Boolean;
      HasLower@1004 : Boolean;
      HasNumeric@1005 : Boolean;
    BEGIN
      KeyLen := STRLEN(Key);

      IF KeyLen < 8 THEN
        EXIT(FALSE);

      FOR i := 1 TO STRLEN(Key) DO BEGIN
        CASE Key[i] OF
          'A'..'Z':
            HasUpper := TRUE;
          'a'..'z':
            HasLower := TRUE;
          '0'..'9':
            HasNumeric := TRUE;
        END;

        IF HasUpper AND HasLower AND HasNumeric THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE ValidatePasswordStrength@10(Password@1000 : Text[250]) IsValid : Boolean;
    BEGIN
      IsValid := ValidateKeyStrength(Password);
    END;

    PROCEDURE ValidateAuthKeyStrength@11(AuthKey@1000 : Text[250]) IsValid : Boolean;
    BEGIN
      IsValid := ValidateKeyStrength(AuthKey);
    END;

    PROCEDURE GetMaskedNavPassword@15(UserSecurityID@1000 : GUID) MaskedPassword : Text[80];
    BEGIN
      IF UserAccountHelper.IsPasswordSet(UserSecurityID) THEN
        MaskedPassword := '********'
      ELSE
        MaskedPassword := '';
    END;

    PROCEDURE IsWindowsAuthentication@16() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsWindowsAuthentication();
    END;

    PROCEDURE IsUserNamePasswordAuthentication@17() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsUserNamePasswordAuthentication();
    END;

    PROCEDURE IsAccessControlServiceAuthentication@18() Ok : Boolean;
    BEGIN
      Ok := UserAccountHelper.IsAccessControlServiceAuthentication();
    END;

    PROCEDURE UserName@19(Sid@1000 : Text) : Text[208];
    BEGIN
      IF Sid = '' THEN
        EXIT('');

      EXIT(UserAccountHelper.UserName(Sid));
    END;

    LOCAL PROCEDURE GetAuthenticationEmail@20(UserSecurityID@1000 : GUID) authenticationEmail : Text[250];
    BEGIN
      IF NOT UserAccountHelper.TryGetAuthenticationEmail(UserSecurityID,authenticationEmail) THEN
        authenticationEmail := FORMAT(GETLASTERRORTEXT,250);
    END;

    PROCEDURE SetAuthenticationEmail@12(UserSecurityId@1000 : GUID;AuthenticationEmail@1001 : Text[250]);
    BEGIN
      CLEARLASTERROR;
      IF NOT UserAccountHelper.TrySetAuthenticationEmail(UserSecurityId,AuthenticationEmail) THEN
        ERROR(GETLASTERRORTEXT);
    END;

    PROCEDURE GetAuthenticationStatus@21(UserSecurityId@1001 : GUID) O365AuthStatus : Integer;
    BEGIN
      O365AuthStatus := UserAccountHelper.GetAuthenticationStatus(UserSecurityId);
    END;

    BEGIN
    END.
  }
}

