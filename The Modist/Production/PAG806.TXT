OBJECT Page 806 Online Map Location
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Online Map Location;
               ENG=Online Map Location];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    PageType=Card;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 IF NOT LocationProvider.IsAvailable THEN BEGIN
                   MESSAGE(LocationNotAvailableMsg);
                   CurrPage.CLOSE;
                   EXIT;
                 END;
                 LocationProvider := LocationProvider.Create;
                 LocationProvider.RequestLocationAsync;
               END;

  }
  CONTROLS
  {
    { 1900000001;;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                CaptionML=[ENU=Status;
                           ENG=Status];
                SourceExpr=GeolocationLbl;
                Importance=Promoted }

  }
  CODE
  {
    VAR
      LocationProvider@1013 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.LocationProvider" WITHEVENTS RUNONCLIENT;
      ToTableNo@1001 : Integer;
      ToRecordPosition@1002 : Text[1000];
      GeolocationLbl@1003 : TextConst 'ENU=Searching for your location.;ENG=Searching for your location.';
      LocationNotAvailableMsg@1005 : TextConst 'ENU=Your location cannot be determined.;ENG=Your location cannot be determined.';

    PROCEDURE SetRecordTo@2(NewToTableNo@1000 : Integer;NewToRecordPosition@1001 : Text[1000]);
    BEGIN
      ToTableNo := NewToTableNo;
      ToRecordPosition := NewToRecordPosition;
    END;

    EVENT LocationProvider@1013::LocationChanged@10(location@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.Location");
    VAR
      OnlineMapSetup@1002 : Record 800;
      Geolocation@1004 : Record 806;
      OnlineMapManagement@1001 : Codeunit 802;
    BEGIN
      IF location.Status <> 0 THEN BEGIN
        MESSAGE(LocationNotAvailableMsg);
        CurrPage.CLOSE;
        EXIT;
      END;

      Geolocation.INIT;
      Geolocation.ID := CREATEGUID;
      Geolocation.Latitude := location.Coordinate.Latitude;
      Geolocation.Longitude := location.Coordinate.Longitude;
      Geolocation.INSERT;

      IF NOT OnlineMapSetup.GET THEN BEGIN
        OnlineMapManagement.SetupDefault;
        OnlineMapSetup.GET;
      END;

      OnlineMapManagement.ProcessDirections(
        DATABASE::Geolocation,Geolocation.GETPOSITION,
        ToTableNo,ToRecordPosition,
        OnlineMapSetup."Distance In",OnlineMapSetup.Route);

      Geolocation.DELETE;
      CurrPage.CLOSE;
    END;

    BEGIN
    END.
  }
}

