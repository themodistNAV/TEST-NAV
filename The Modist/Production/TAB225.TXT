OBJECT Table 225 Post Code
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:39:59 PM];
    Version List=NAVW19.00,EC2016.6;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Post Code;
               ENG=Post Code];
    LookupPageID=Page367;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                PostCode.SETCURRENTKEY("Search City");
                                                                PostCode.SETRANGE("Search City","Search City");
                                                                PostCode.SETRANGE(Code,Code);
                                                                IF PostCode.FINDFIRST THEN
                                                                  ERROR(Text000,FIELDCAPTION(Code),Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;City                ;Text30        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                TESTFIELD(Code);
                                                                "Search City" := City;
                                                                IF xRec."Search City" <> "Search City" THEN BEGIN
                                                                  PostCode.SETCURRENTKEY("Search City");
                                                                  PostCode.SETRANGE("Search City","Search City");
                                                                  PostCode.SETRANGE(Code,Code);
                                                                  IF PostCode.FINDFIRST THEN
                                                                    ERROR(Text000,FIELDCAPTION(City),City);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=City;
                                                              ENG=City];
                                                   NotBlank=Yes }
    { 3   ;   ;Search City         ;Code30        ;CaptionML=[ENU=Search City;
                                                              ENG=Search City] }
    { 4   ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ENG=Country/Region Code] }
    { 5   ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              ENG=County] }
    { 9021240;;Web Location        ;Code10        ;TableRelation="Web Location".Code;
                                                   OnValidate=VAR
                                                                StdTableTriggerCode@1000000001 : Codeunit 9021668;
                                                              BEGIN
                                                                StdTableTriggerCode.T225WebLocationOnVal(Rec);
                                                              END;

                                                   CaptionML=[ENU=Web Location;
                                                              ENG=Web Location];
                                                   Description=MQ }
    { 9021260;;Default Tax Area Code;Boolean      ;CaptionML=[ENU=Default Tax Area Code;
                                                              ENG=Default Tax Area Code];
                                                   Description=MQ }
    { 9021261;;Tax Area Code       ;Code10        ;TableRelation="Tax Area";
                                                   CaptionML=[ENU=Tax Area Code;
                                                              ENG=Tax Area Code];
                                                   Description=MQ }
  }
  KEYS
  {
    {    ;Code,City                               ;Clustered=Yes }
    {    ;City,Code                                }
    {    ;Search City                              }
    {    ;Country/Region Code                      }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,City,Search City,Country/Region Code,County }
    { 2   ;Brick               ;Code,City,County,Country/Region Code     }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already exists.;ENG=%1 %2 already exists.';

    PROCEDURE ValidateCity@5(VAR City@1000 : Text[30];VAR PostCode@1001 : Code[20];VAR County@1006 : Text[30];VAR CountryCode@1005 : Code[10];UseDialog@1007 : Boolean);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1004 : Record 225;
      SearchCity@1003 : Code[30];
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      IF City <> '' THEN BEGIN
        SearchCity := City;
        PostCodeRec.SETCURRENTKEY("Search City");
        IF STRPOS(SearchCity,'*') = STRLEN(SearchCity) THEN
          PostCodeRec.SETFILTER("Search City",SearchCity)
        ELSE
          PostCodeRec.SETRANGE("Search City",SearchCity);
        IF NOT PostCodeRec.FINDFIRST THEN
          EXIT;
        PostCodeRec2.COPY(PostCodeRec);
        IF UseDialog AND (PostCodeRec2.NEXT = 1) THEN
          IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
            EXIT;
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        CountryCode := PostCodeRec."Country/Region Code";
        County := PostCodeRec.County;
      END;
    END;

    PROCEDURE ValidatePostCode@6(VAR City@1001 : Text[30];VAR PostCode@1000 : Code[20];VAR County@1005 : Text[30];VAR CountryCode@1006 : Code[10];UseDialog@1004 : Boolean);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1003 : Record 225;
    BEGIN
      IF PostCode <> '' THEN BEGIN
        IF STRPOS(PostCode,'*') = STRLEN(PostCode) THEN
          PostCodeRec.SETFILTER(Code,PostCode)
        ELSE
          PostCodeRec.SETRANGE(Code,PostCode);
        IF NOT PostCodeRec.FINDFIRST THEN
          EXIT;
        PostCodeRec2.COPY(PostCodeRec);
        IF UseDialog AND (PostCodeRec2.NEXT = 1) AND GUIALLOWED THEN
          IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
            EXIT;
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        CountryCode := PostCodeRec."Country/Region Code";
        County := PostCodeRec.County;
      END;
    END;

    BEGIN
    {
      //DOC EC2009.2.64 MQ 01/01/2012 - Add fields; Add code in OnInsert, OnModify and OnRename
      //DOC EC2015.5.01 MQ 13/01/2015 - GW150113 Removed MQ field 9021250 Country/Region as can now use standard field 4, Country/Region Code
      //DOC EC2015.5.03 MQ 05/05/2015 - GW150505 Removed field DateTimeInsertedModifed and code in OnInsert, OnModify and OnRename
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

