OBJECT Codeunit 9087025 Update Buying Sheet Items
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=12:03:01 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    TableNo=9087020;
    OnRun=BEGIN
            UpdateItems(Rec);
          END;

  }
  CODE
  {
    VAR
      nSembleSetup@1000000006 : Record 9087000;
      BuyingSheetHeader@1000000000 : Record 9087020;
      BuyingSheetSummaryLine@1000000001 : Record 9087021;
      Item@1000000002 : Record 27;
      nSembleToolkit@1000000004 : Codeunit 9087000;
      TnpToolkit@1000000003 : Codeunit 9062230;

    PROCEDURE UpdateItems@1000000008(VAR pBuyingSheetHeader@1000000000 : Record 9087020);
    BEGIN
      //DOC NS2016.16 - Updating items
      //  <> pBuyingSheetHeader: Buying sheet header to update items for

      BuyingSheetHeader := pBuyingSheetHeader;

      ReleaseBuyingSheet();

      BuyingSheetSummaryLine.RESET();
      BuyingSheetSummaryLine.SETRANGE("Buying Sheet No.", BuyingSheetHeader."No.");
      IF (BuyingSheetSummaryLine.FINDSET()) THEN
      REPEAT
        UpdateSummaryLineItemInternal(FALSE);
      UNTIL (BuyingSheetSummaryLine.NEXT() = 0);

      pBuyingSheetHeader := BuyingSheetHeader;
    END;

    PROCEDURE UpdateSummaryLineItem@1000000012(VAR pBuyingSheetSummaryLine@1000000002 : Record 9087021);
    VAR
      lItem@1000000000 : Record 27;
      lModify@1000000001 : Boolean;
    BEGIN
      //DOC NS2016.16 - Updating (buying sheet) summary line item
      //  <> pBuyingSheetSummaryLine: The line

      BuyingSheetSummaryLine := pBuyingSheetSummaryLine;
      UpdateSummaryLineItemInternal(TRUE);
      pBuyingSheetSummaryLine := BuyingSheetSummaryLine;
    END;

    LOCAL PROCEDURE UpdateSummaryLineItemInternal@1000000001(pRelease@1000000002 : Boolean);
    VAR
      lItemModified@1000000000 : Boolean;
      lItemVariant@1000000001 : Variant;
    BEGIN
      //DOC NS2016.16 - Updating (buying sheet) summary line item
      //  -> pRelease: Flag to release buying sheet

      IF (pRelease) THEN
        ReleaseBuyingSheet();

      IF (BuyingSheetSummaryLine."Item No." <> '') THEN
        IF ( NOT (Item.GET(BuyingSheetSummaryLine."Item No."))) THEN
          BuyingSheetSummaryLine."Item No." := '';
      IF (BuyingSheetSummaryLine."Item No." = '') THEN
        EXIT;

      BuyingSheetSummaryLine.CALCFIELDS(Picture);

      CLEAR(nSembleToolkit);
      nSembleToolkit.SetRecord(Item);
      nSembleToolkit.ValidateRecordField(Item.FIELDNO(Description), BuyingSheetSummaryLine.Description);
      nSembleToolkit.ValidateRecordField(Item.FIELDNO("Unit Price"), BuyingSheetSummaryLine."Unit Price (LCY)");
      nSembleToolkit.ValidateRecordField(Item.FIELDNO("Colour Code"), BuyingSheetSummaryLine."Colour Code");
      nSembleToolkit.ValidateRecordField(Item.FIELDNO("Vendor Colour Code"), BuyingSheetSummaryLine."Vendor Colour Code");
      nSembleToolkit.ValidateRecordField(Item.FIELDNO("Size Group Code"), BuyingSheetSummaryLine."Size Group Code");
      nSembleToolkit.ForceModifyRecordField(Item.FIELDNO(Picture), BuyingSheetSummaryLine.Picture);
      IF (ItemHasNoEntries()) THEN
        nSembleToolkit.ValidateRecordField(Item.FIELDNO("Unit Cost"), BuyingSheetSummaryLine."Direct Unit Cost");
      lItemModified := nSembleToolkit.GetRecord(lItemVariant);
      IF (lItemModified) THEN
      BEGIN
        Item := lItemVariant;
        Item.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE ReleaseBuyingSheet@1000000004();
    BEGIN
      //DOC NS2016.16 - Releasing buying sheet

      CODEUNIT.RUN(CODEUNIT::"Buying Sheet Status Management", BuyingSheetHeader);
      BuyingSheetHeader.CheckStatusReleased();
    END;

    LOCAL PROCEDURE ItemHasNoEntries@1000000000() : Boolean;
    VAR
      lItemLedgerEntry@1000000000 : Record 32;
    BEGIN
      //DOC NS2016.16 - Returning if item has no entries
      //  <- Response

      lItemLedgerEntry.RESET();
      lItemLedgerEntry.SETCURRENTKEY("Item No.");
      lItemLedgerEntry.SETRANGE("Item No.", Item."No.");
      EXIT(lItemLedgerEntry.ISEMPTY());
    END;

    BEGIN
    {
      //DOC NS2016.16 JH 06/09/2016 - Created
    }
    END.
  }
}

