OBJECT Codeunit 9087026 nSemble Item Events
{
  OBJECT-PROPERTIES
  {
    Date=21/02/17;
    Time=[ 2:08:33 PM];
    Modified=Yes;
    Version List=NS2016.16.2;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    LOCAL PROCEDURE "--- Item ---"@1000000006();
    BEGIN
    END;

    [EventSubscriber(Table,27,OnAfterDeleteEvent)]
    LOCAL PROCEDURE Item_AfterDelete@1000000001(VAR Rec@1000000000 : Record 27;RunTrigger@1000000001 : Boolean);
    VAR
      lStyleColourSize@1000000002 : Record 9087014;
      lStyleColour@1000000003 : Record 9087013;
      lBuyingStagingLine@1000000004 : Record 9087017;
    BEGIN
      IF Rec.ISTEMPORARY THEN  //DOC NS2016.16.1
        EXIT;                  //DOC NS2016.16.1

      lStyleColourSize.RESET();
      lStyleColourSize.SETCURRENTKEY("Item No.", "Variant Code");
      lStyleColourSize.SETRANGE("Item No.", Rec."No.");
      lStyleColourSize.MODIFYALL("Variant Code", '');
      lStyleColourSize.MODIFYALL("Item No.", '');

      lStyleColour.RESET();
      lStyleColourSize.SETCURRENTKEY("Item No.");
      lStyleColour.SETRANGE("Item No.", Rec."No.");
      lStyleColour.MODIFYALL("Item No.", '');

      lBuyingStagingLine.RESET();
      lBuyingStagingLine.SETCURRENTKEY("Item No.");
      lBuyingStagingLine.SETRANGE("Item No.", Rec."No.");
      lBuyingStagingLine.MODIFYALL("Item No.", '');
    END;

    [EventSubscriber(Table,27,OnAfterValidateEvent,"Vendor No.")]
    LOCAL PROCEDURE Item_AfterValidateVendorNo@1000000004(VAR Rec@1000000000 : Record 27;VAR xRec@1000000001 : Record 27;CurrFieldNo@1000000002 : Integer);
    BEGIN
      IF (Rec."Vendor No." <> xRec."Vendor No.") THEN
        Rec.VALIDATE("Vendor Colour Code", '');
    END;

    LOCAL PROCEDURE "--- Item Variant ---"@1000000002();
    BEGIN
    END;

    [EventSubscriber(Table,5401,OnAfterInsertEvent)]
    LOCAL PROCEDURE ItemVariant_AfterInsert@1000000005(VAR Rec@1000000000 : Record 5401;RunTrigger@1000000001 : Boolean);
    BEGIN
      //DOC NS2016.16.2 >>
      IF (Rec.ISTEMPORARY()) THEN
        EXIT;
      //DOC NS2016.16.2 <<
      ItemVariant_UpdateRecord(Rec);
    END;

    [EventSubscriber(Table,5401,OnAfterDeleteEvent)]
    LOCAL PROCEDURE ItemVariant_AfterDelete@1000000013(VAR Rec@1000000000 : Record 5401;RunTrigger@1000000001 : Boolean);
    VAR
      lStyleColourSize@1000000002 : Record 9087014;
    BEGIN
      IF Rec.ISTEMPORARY THEN  //DOC NS2016.16.1
        EXIT;                  //DOC NS2016.16.1

      lStyleColourSize.RESET();
      lStyleColourSize.SETCURRENTKEY("Item No.", "Variant Code");
      lStyleColourSize.SETRANGE("Item No.", Rec."Item No.");
      lStyleColourSize.SETRANGE("Variant Code", Rec.Code);
      lStyleColourSize.MODIFYALL("Variant Code", '', TRUE);
    END;

    [EventSubscriber(Table,5401,OnAfterValidateEvent,Code)]
    LOCAL PROCEDURE ItemVariant_AfterValidateCode@1000000020(VAR Rec@1000000000 : Record 5401;VAR xRec@1000000001 : Record 5401;CurrFieldNo@1000000002 : Integer);
    BEGIN
      //DOC NS2016.16.2 >>
      IF (Rec.ISTEMPORARY()) THEN
        EXIT;
      //DOC NS2016.16.2 <<
      ItemVariant_CreateUniqueId(Rec);
      ItemVariant_UpdateRecord(Rec);
    END;

    [EventSubscriber(Table,5401,OnAfterValidateEvent,Description)]
    LOCAL PROCEDURE ItemVariant_AfterValidateDescription@1000000023(VAR Rec@1000000000 : Record 5401;VAR xRec@1000000001 : Record 5401;CurrFieldNo@1000000002 : Integer);
    BEGIN
      //DOC NS2016.16.2 >>
      IF (Rec.ISTEMPORARY()) THEN
        EXIT;
      //DOC NS2016.16.2 <<
      ItemVariant_CreateUniqueId(Rec);
    END;

    LOCAL PROCEDURE ItemVariant_UpdateRecord@1000000009(VAR pItemVariant@1000000000 : Record 5401);
    VAR
      lSize@1000000001 : Record 9087002;
    BEGIN
      IF (lSize.GET(pItemVariant."Size Group Code", pItemVariant."Size Code")) THEN
        pItemVariant.Sequence := lSize.Sequence;
      IF (pItemVariant.Description = '') THEN
      BEGIN
        pItemVariant.CALCFIELDS("Item Description");
        pItemVariant.Description := COPYSTR(STRSUBSTNO('%1 - %2', pItemVariant."Item Description", pItemVariant.Code), 1,
          MAXSTRLEN(pItemVariant.Description));
      END;
    END;

    LOCAL PROCEDURE ItemVariant_CreateUniqueId@1000000018(pItemVariant@1000000000 : Record 5401);
    BEGIN
      pItemVariant.VALIDATE("Unique ID", pItemVariant."Item No." + '$' + pItemVariant.Code);
    END;

    LOCAL PROCEDURE "--- Item Jnl.-Post Line ---"@1000000007();
    BEGIN
    END;

    [EventSubscriber(Codeunit,22,OnAfterInitItemLedgEntry)]
    LOCAL PROCEDURE ItemJnlPostLine_AfterInitItemLedgEntry@1000000000(VAR NewItemLedgEntry@1000000000 : Record 32;VAR ItemJournalLine@1000000001 : Record 83);
    BEGIN
      NewItemLedgEntry."Season Code" := ItemJournalLine."Season Code";
      NewItemLedgEntry."Sub-Season Code" := ItemJournalLine."Sub-Season Code";
      NewItemLedgEntry."Brand Code" := ItemJournalLine."Brand Code";
      NewItemLedgEntry."Division Code" := ItemJournalLine."Division Code";
      NewItemLedgEntry."Department Code" := ItemJournalLine."Department Code";
      NewItemLedgEntry."Group Code" := ItemJournalLine."Group Code";
      NewItemLedgEntry."Category Code" := ItemJournalLine."Category Code";
      NewItemLedgEntry."Sub-Category Code" := ItemJournalLine."Sub-Category Code";
      NewItemLedgEntry."Size Group Code" := ItemJournalLine."Size Group Code";
      NewItemLedgEntry."Size Code" := ItemJournalLine."Size Code";
      NewItemLedgEntry."Standard Size Group Code" := ItemJournalLine."Standard Size Group Code";
      NewItemLedgEntry."Standard Size Code" := ItemJournalLine."Standard Size Code";
      NewItemLedgEntry."Vendor No." := ItemJournalLine."Vendor No.";
      NewItemLedgEntry."Style No." := ItemJournalLine."Style No.";
    END;

    [EventSubscriber(Codeunit,22,OnBeforeInsertValueEntry)]
    LOCAL PROCEDURE ItemJnlPostLine_BeforeInsertValueEntry@1000000003(VAR ValueEntry@1000000000 : Record 5802;VAR ItemJournalLine@1000000001 : Record 83);
    BEGIN
      ValueEntry."Season Code" := ItemJournalLine."Season Code";
      ValueEntry."Brand Code" := ItemJournalLine."Brand Code";
      ValueEntry."Division Code" := ItemJournalLine."Division Code";
      ValueEntry."Department Code" := ItemJournalLine."Department Code";
      ValueEntry."Group Code" := ItemJournalLine."Group Code";
      ValueEntry."Category Code" := ItemJournalLine."Category Code";
      ValueEntry."Sub-Category Code" := ItemJournalLine."Sub-Category Code";
      ValueEntry."Size Group Code" := ItemJournalLine."Size Group Code";
      ValueEntry."Size Code" := ItemJournalLine."Size Code";
      ValueEntry."Standard Size Group Code" := ItemJournalLine."Standard Size Group Code";
      ValueEntry."Standard Size Code" := ItemJournalLine."Standard Size Code";
      ValueEntry."Vendor No." := ItemJournalLine."Vendor No.";
      ValueEntry."Style No." := ItemJournalLine."Style No.";
    END;

    BEGIN
    {
      //DOC NS2016.16   JH  07/09/2016 - Created
      //DOC NS2016.16.1 PCM 09/02/2017 - Avoid clearing "Item No." fields when deletion from temporary table from C5895.
      //DOC NS2016.16.2 JH  21/02/2017 - Making sure events are not called for temporary records
    }
    END.
  }
}

