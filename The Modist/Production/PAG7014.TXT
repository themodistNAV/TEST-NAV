OBJECT Page 7014 Purchase Line Discounts
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Purchase Line Discounts;
               ENG=Purchase Line Discounts];
    SourceTable=Table7014;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=Worksheet;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 25  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENG=General] }

    { 26  ;2   ;Field     ;
                Name=VendNoFilterCtrl;
                CaptionML=[ENU=Vendor No. Filter;
                           ENG=Vendor No. Filter];
                SourceExpr=VendNoFilter;
                OnValidate=BEGIN
                             VendNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           VendList@1005 : Page 27;
                         BEGIN
                           VendList.LOOKUPMODE := TRUE;
                           IF VendList.RUNMODAL = ACTION::LookupOK THEN
                             Text := VendList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 32  ;2   ;Field     ;
                Name=ItemNoFilterCtrl;
                CaptionML=[ENU=Item No. Filter;
                           ENG=Item No. Filter];
                SourceExpr=ItemNoFilter;
                OnValidate=BEGIN
                             ItemNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE := TRUE;
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN
                             Text := ItemList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Starting Date Filter;
                           ENG=Starting Date Filter];
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             ApplicationMgt@1030 : Codeunit 1;
                           BEGIN
                             IF ApplicationMgt.MakeDateFilter(StartingDateFilter) = 0 THEN;
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                SourceExpr="Vendor No." }

    { 6   ;2   ;Field     ;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 2   ;2   ;Field     ;
                SourceExpr="Item No." }

    { 23  ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code" }

    { 12  ;2   ;Field     ;
                SourceExpr="Minimum Quantity" }

    { 10  ;2   ;Field     ;
                SourceExpr="Line Discount %" }

    { 8   ;2   ;Field     ;
                SourceExpr="Starting Date" }

    { 14  ;2   ;Field     ;
                SourceExpr="Ending Date" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      VendNoFilter@1000 : Text;
      ItemNoFilter@1001 : Text;
      StartingDateFilter@1002 : Text[30];

    LOCAL PROCEDURE GetRecFilters@2();
    BEGIN
      IF GETFILTERS <> '' THEN BEGIN
        VendNoFilter := GETFILTER("Vendor No.");
        ItemNoFilter := GETFILTER("Item No.");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    PROCEDURE SetRecFilters@1();
    BEGIN
      IF VendNoFilter <> '' THEN
        SETFILTER("Vendor No.",VendNoFilter)
      ELSE
        SETRANGE("Vendor No.");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      IF ItemNoFilter <> '' THEN BEGIN
        SETFILTER("Item No.",ItemNoFilter);
      END ELSE
        SETRANGE("Item No.");

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE GetCaption@3() : Text[250];
    VAR
      Vendor@1003 : Record 23;
      ObjTransl@1000 : Record 377;
      SourceTableName@1002 : Text[100];
      Description@1001 : Text[250];
    BEGIN
      GetRecFilters;

      IF ItemNoFilter <> '' THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,DATABASE::Item)
      ELSE
        SourceTableName := '';

      IF VendNoFilter = '' THEN
        Description := ''
      ELSE BEGIN
        Vendor.SETFILTER("No.",VendNoFilter);
        IF Vendor.FINDFIRST THEN
          Description := Vendor.Name;
      END;

      EXIT(STRSUBSTNO('%1 %2 %3 %4 ',VendNoFilter,Description,SourceTableName,ItemNoFilter));
    END;

    LOCAL PROCEDURE VendNoFilterOnAfterValidate@19075608();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemNoFilterOnAfterValidate@19009808();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    BEGIN
    END.
  }
}

