OBJECT Table 9062816 nHanced Search Tables
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Table No");
             END;

    OnDelete=BEGIN
               nHancedSearchFields.SETRANGE("Parent Id","Parent Id");
               nHancedSearchFields.SETRANGE("Table No","Table No");
               IF nHancedSearchFields.FINDSET THEN
                 nHancedSearchFields.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=nHanced Search Tables;
               ENG=nHanced Search Tables];
  }
  FIELDS
  {
    { 1   ;   ;Parent Id           ;Integer       ;TableRelation="nHanced Search Header";
                                                   CaptionML=[ENU=Parent Id;
                                                              ENG=Parent Id] }
    { 2   ;   ;Table No            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   CaptionML=[ENU=Table No;
                                                              ENG=Table No] }
    { 3   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         ID=FIELD(Table No)));
                                                   CaptionML=[ENU=Table Name;
                                                              ENG=Table Name] }
  }
  KEYS
  {
    {    ;Parent Id,Table No                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Lines@1000000000 : Record 9062816;
      FieldsLookup@1000000001 : Page 9806;
      FieldsRec@1000000002 : Record 2000000041;
      nHancedSearchFields@1000000003 : Record 9062820;
      EnhancedFieldDefinition@1000000004 : Record 9062327;

    PROCEDURE SuggestFields@1000000000();
    VAR
      Field@1000000000 : Record 2000000041;
      nHancedSearchFields@1000000001 : Record 9062820;
      nHancedSearchFields2@1000000002 : Record 9062820;
    BEGIN
      //populate a list of fields related to this table that are of type Text / Code
      Field.SETRANGE(TableNo,"Table No");
      Field.SETRANGE(Class,Field.Class::Normal);
      Field.SETRANGE(Enabled,TRUE);
      Field.SETFILTER(Type,'%1|%2',Field.Type::Text,Field.Type::Code);
      IF Field.FINDSET THEN
        REPEAT
          IF NOT nHancedSearchFields.GET("Parent Id","Table No",nHancedSearchFields."Field Type"::Field,Field."No.") THEN BEGIN
            nHancedSearchFields.INIT;
            nHancedSearchFields.VALIDATE("Parent Id","Parent Id");
            nHancedSearchFields.VALIDATE("Table No","Table No");
            nHancedSearchFields.VALIDATE("Field Type",nHancedSearchFields."Field Type"::Field);
            nHancedSearchFields.VALIDATE("Field No",Field."No.");
            nHancedSearchFields.INSERT(TRUE);
          END;
        UNTIL Field.NEXT = 0;



      //Enhanced Fields
      EnhancedFieldDefinition.SETRANGE("Table No.","Table No");
      EnhancedFieldDefinition.SETFILTER(Type,'%1|%2',EnhancedFieldDefinition.Type::Text,EnhancedFieldDefinition.Type::Code);
      EnhancedFieldDefinition.SETFILTER("NAV Data Type",'%1|%2',EnhancedFieldDefinition."NAV Data Type"::Text,EnhancedFieldDefinition."NAV Data Type"::Code);

      IF EnhancedFieldDefinition.FINDSET THEN
        REPEAT
          IF NOT nHancedSearchFields.GET("Parent Id","Table No",nHancedSearchFields."Field Type"::"Enhanced Field",EnhancedFieldDefinition."Enhanced Field No.") THEN BEGIN
            nHancedSearchFields.INIT;
            nHancedSearchFields.VALIDATE("Parent Id","Parent Id");
            nHancedSearchFields.VALIDATE("Table No","Table No");
            nHancedSearchFields.VALIDATE("Field Type",nHancedSearchFields."Field Type"::"Enhanced Field");
            nHancedSearchFields.VALIDATE("Field No",EnhancedFieldDefinition."Enhanced Field No.");
            nHancedSearchFields.INSERT(TRUE);
          END;
        UNTIL EnhancedFieldDefinition.NEXT = 0;
    END;

    BEGIN
    {
      //DOC NA2016.12 BM 02/02/2016 - Created
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

