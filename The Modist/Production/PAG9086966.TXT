OBJECT Page 9086966 nF. Permission Groups Perm.
{
  OBJECT-PROPERTIES
  {
    Date=30/10/16;
    Time=12:11:12 PM;
    Version List=NF2016.17;
  }
  PROPERTIES
  {
    CaptionML=[ENU=nForcement Permission Groups Permissions;
               ENG=nForcement Permission Groups Permissions];
    SourceTable=Table9086958;
    PopulateAllFields=Yes;
    SourceTableView=SORTING(nF. Permission Group ID,Object Type,Object ID);
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       UpdateGlobals();
                     END;

    OnNewRecord=BEGIN
                  Rec.InitRecord(xRec);
                END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateGlobals();
                         END;

    ActionList=ACTIONS
    {
      { 1000000002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000003;1 ;ActionGroup;
                      Name=AddGroup;
                      CaptionML=[ENU=Add;
                                 ENG=Add];
                      Image=Add }
      { 1000000016;2 ;Action    ;
                      Name=AddTablesAction;
                      CaptionML=[ENU=Add Tables;
                                 ENG=Add Tables];
                      Image=Table;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::Table);
                               END;
                                }
      { 1000000018;2 ;Action    ;
                      Name=AddPagesAction;
                      CaptionML=[ENU=Add Pages;
                                 ENG=Add Pages];
                      Image=TaskPage;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::Page);
                               END;
                                }
      { 1000000017;2 ;Action    ;
                      Name=AddReportsAction;
                      CaptionML=[ENU=Add Reports;
                                 ENG=Add Reports];
                      Image=Report;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::Report);
                               END;
                                }
      { 1000000019;2 ;Action    ;
                      Name=AddCodeunitsAction;
                      CaptionML=[ENU=Add Codeunits;
                                 ENG=Add Codeunits];
                      Image=NonStockItemSetup;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::Codeunit);
                               END;
                                }
      { 1000000021;2 ;Action    ;
                      Name=AddQueriesAction;
                      CaptionML=[ENU=Add Queries;
                                 ENG=Add Queries];
                      Image=ImportDatabase;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::Query);
                               END;
                                }
      { 1000000020;2 ;Action    ;
                      Name=AddXmlPortsAction;
                      CaptionML=[ENU=Add XMLports;
                                 ENG=Add XMLports];
                      Image=XMLFile;
                      OnAction=BEGIN
                                 AddObjects(Object."Object Type"::XMLport);
                               END;
                                }
      { 1000000023;1 ;Action    ;
                      Name=EditRecordFilterAction;
                      CaptionML=[ENU=Edit Record Filter;
                                 ENG=Edit Record Filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Filter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 EditRecordFilter();
                               END;
                                }
      { 1000000029;1 ;Action    ;
                      Name=ShowHideQuickFilterAction;
                      CaptionML=[ENU=Show/Hide Quick Filter;
                                 ENG=Show/Hide Quick Filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditFilter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 QuickFilterEnabled := ( NOT (QuickFilterEnabled));
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1000000028;1 ;Action    ;
                      Name=ClearQuickFilterAction;
                      CaptionML=[ENU=Clear Quick Filter;
                                 ENG=Clear Quick Filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ClearFilter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ClearQuickFilter();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                CaptionML=[ENU=Group;
                           ENG=Group];
                GroupType=Repeater }

    { 1000000004;2;Field  ;
                SourceExpr="Object Type";
                StyleExpr=Style }

    { 1000000005;2;Field  ;
                SourceExpr="Object ID";
                Visible=FALSE;
                StyleExpr=Style }

    { 1000000006;2;Field  ;
                Name=ObjectField;
                CaptionML=[ENU=Object;
                           ENG=Object];
                SourceExpr=ObjectText;
                StyleExpr=Style;
                OnValidate=BEGIN
                             NfObjectToolkit.ValidateObjectName(Rec."Object Type", Rec."Object ID", ObjectText, FALSE);
                           END;

                OnLookup=BEGIN
                           IF (NfObjectToolkit.ObjectLookup(Rec."Object Type", Rec."Object ID")) THEN
                             Rec.VALIDATE("Object ID");
                         END;
                          }

    { 1000000007;2;Field  ;
                SourceExpr="Read Permission";
                StyleExpr=Style }

    { 1000000008;2;Field  ;
                SourceExpr="Insert Permission";
                StyleExpr=Style }

    { 1000000009;2;Field  ;
                SourceExpr="Modify Permission";
                StyleExpr=Style }

    { 1000000010;2;Field  ;
                SourceExpr="Delete Permission";
                StyleExpr=Style }

    { 1000000011;2;Field  ;
                SourceExpr="Execute Permission";
                StyleExpr=Style }

    { 1000000022;2;Field  ;
                Name=RecordFilterField;
                CaptionML=[ENU=Record Filter;
                           ENG=Record Filter];
                SourceExpr=RecordFilter;
                Editable=FALSE;
                StyleExpr=Style;
                OnDrillDown=BEGIN
                              EditRecordFilter();
                            END;
                             }

    { 1000000030;2;Field  ;
                SourceExpr=Type;
                StyleExpr=Style }

    { 1000000024;2;Field  ;
                SourceExpr=Force;
                StyleExpr=Style }

    { 1000000012;2;Field  ;
                SourceExpr="Created By";
                Visible=FALSE;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000013;2;Field  ;
                SourceExpr="Created Date Time";
                Visible=FALSE;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000014;2;Field  ;
                SourceExpr="Modified By";
                Visible=FALSE;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000015;2;Field  ;
                SourceExpr="Modified Date Time";
                Visible=FALSE;
                Editable=FALSE;
                StyleExpr=Style }

    { 1000000027;1;Group  ;
                Name=QuickFilterGroup;
                CaptionML=[ENU=Quick Filter;
                           ENG=Quick Filter];
                Visible=QuickFilterEnabled;
                Enabled=QuickFilterEnabled;
                GroupType=Group }

    { 1000000026;2;Field  ;
                Name=QuickFilterObjectTypeField;
                CaptionML=[ENU=Object Type;
                           ENG=Object Type];
                SourceExpr=QuickFilterObjectType;
                OnValidate=BEGIN
                             QuickFilterObjectId := 0;
                             QuickFilterObjectName := '';
                             ApplyQuickFilter();
                           END;
                            }

    { 1000000025;2;Field  ;
                Name=QuickFilterObjectField;
                CaptionML=[ENU=Object;
                           ENG=Object];
                SourceExpr=QuickFilterObjectName;
                OnValidate=BEGIN
                             NfObjectToolkit.ValidateObjectName(QuickFilterObjectType, QuickFilterObjectId, QuickFilterObjectName, FALSE);
                             ApplyQuickFilter();
                           END;

                OnLookup=BEGIN
                           IF (NfObjectToolkit.ObjectLookup(QuickFilterObjectType, QuickFilterObjectId)) THEN
                           BEGIN
                             QuickFilterObjectName := NfObjectToolkit.GetObjectName(QuickFilterObjectType, QuickFilterObjectId);
                             ApplyQuickFilter();
                           END;
                         END;
                          }

  }
  CODE
  {
    VAR
      Object@1000000003 : Record 2000000038;
      NfObjectToolkit@1000000004 : Codeunit 9086974;
      NfPermissionManagement@1000000001 : Codeunit 9086960;
      NfPermRecordFilterMgt@1000000006 : Codeunit 9086961;
      ObjectText@1000000000 : Text;
      Style@1000000002 : Text;
      RecordFilter@1000000005 : Text;
      QuickFilterEnabled@1000000010 : Boolean INDATASET;
      QuickFilterObjectType@1000000009 : ' ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System';
      QuickFilterObjectId@1000000008 : Integer;
      QuickFilterObjectName@1000000007 : Text;

    LOCAL PROCEDURE UpdateGlobals@1000000000();
    BEGIN
      Rec.CALCFIELDS("Object Name");
      ObjectText := Rec."Object Name";
      Style := Rec.GetStyle();
      RecordFilter := Rec.GetRecordFilter();
    END;

    LOCAL PROCEDURE AddObjects@1000000002(pObjectType@1000000000 : Option);
    BEGIN
      NfPermissionManagement.AddObjects(Rec, pObjectType);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE EditRecordFilter@1000000005();
    BEGIN
      CurrPage.SAVERECORD();
      IF (NfPermRecordFilterMgt.EditRecordFilter(Rec, CurrPage.EDITABLE)) THEN
      BEGIN
        CurrPage.UPDATE(TRUE);
        UpdateGlobals();
      END;
    END;

    LOCAL PROCEDURE ApplyQuickFilter@1000000001();
    BEGIN
      IF ((QuickFilterObjectType <> QuickFilterObjectType::" ") AND (QuickFilterObjectId <> 0)) THEN
      BEGIN
        Rec.SETRANGE("Object Type", QuickFilterObjectType);
        Rec.SETRANGE("Object ID", QuickFilterObjectId);
      END
      ELSE
      BEGIN
        Rec.SETRANGE("Object Type");
        Rec.SETRANGE("Object ID");
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ClearQuickFilter@1000000003();
    BEGIN
      CLEAR(QuickFilterObjectType);
      CLEAR(QuickFilterObjectId);
      CLEAR(QuickFilterObjectName);
      ApplyQuickFilter();
    END;

    BEGIN
    {
      ///<summary>Permission groups page permissions part</summary>
      ///<change version="NF2016.17" initials="JH" date="2016-06-20">Created</change>
    }
    END.
  }
}

