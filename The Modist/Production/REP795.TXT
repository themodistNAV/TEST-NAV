OBJECT Report 795 Adjust Cost - Item Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    Permissions=TableData 32=rimd,
                TableData 339=r,
                TableData 5802=rimd,
                TableData 5804=rimd;
    CaptionML=[ENU=Adjust Cost - Item Entries;
               ENG=Adjust Cost - Item Entries];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  ItemLedgEntry@1000 : Record 32;
                  ValueEntry@1001 : Record 5802;
                  ItemApplnEntry@1002 : Record 339;
                  AvgCostAdjmtEntryPoint@1003 : Record 5804;
                  Item@1006 : Record 27;
                  UpdateItemAnalysisView@1004 : Codeunit 7150;
                BEGIN
                  ItemApplnEntry.LOCKTABLE;
                  IF NOT ItemApplnEntry.FINDLAST THEN
                    EXIT;
                  ItemLedgEntry.LOCKTABLE;
                  IF NOT ItemLedgEntry.FINDLAST THEN
                    EXIT;
                  AvgCostAdjmtEntryPoint.LOCKTABLE;
                  IF AvgCostAdjmtEntryPoint.FINDLAST THEN;
                  ValueEntry.LOCKTABLE;
                  IF NOT ValueEntry.FINDLAST THEN
                    EXIT;

                  IF (ItemNoFilter <> '') AND (ItemCategoryFilter <> '') THEN
                    ERROR(Text005);

                  IF ItemNoFilter <> '' THEN
                    Item.SETFILTER("No.",ItemNoFilter);
                  IF ItemCategoryFilter <> '' THEN
                    Item.SETFILTER("Item Category Code",ItemCategoryFilter);

                  InvtAdjmt.SetProperties(FALSE,PostToGL);
                  InvtAdjmt.SetFilterItem(Item);
                  InvtAdjmt.MakeMultiLevelAdjmt;

                  UpdateItemAnalysisView.UpdateAll(0,TRUE);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FilterItemCategoryEditable := TRUE;
               FilterItemNoEditable := TRUE;
               PostEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InvtSetup.GET;
                   PostToGL := InvtSetup."Automatic Cost Posting";
                   PostEnable := PostToGL;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 3   ;2   ;Field     ;
                  Name=FilterItemNo;
                  CaptionML=[ENU=Item No. Filter;
                             ENG=Item No. Filter];
                  SourceExpr=ItemNoFilter;
                  TableRelation=Item;
                  Editable=FilterItemNoEditable }

      { 5   ;2   ;Field     ;
                  Name=FilterItemCategory;
                  CaptionML=[ENU=Item Category Filter;
                             ENG=Item Category Filter];
                  SourceExpr=ItemCategoryFilter;
                  TableRelation="Item Category";
                  Editable=FilterItemCategoryEditable }

      { 7   ;2   ;Field     ;
                  Name=Post;
                  CaptionML=[ENU=Post to G/L;
                             ENG=Post to G/L];
                  SourceExpr=PostToGL;
                  Enabled=PostEnable;
                  OnValidate=VAR
                               ObjTransl@1002 : Record 377;
                             BEGIN
                               IF NOT PostToGL THEN
                                 MESSAGE(
                                   ResynchronizeInfoMsg,
                                   ObjTransl.TranslateObject(ObjTransl."Object Type"::Report,REPORT::"Post Inventory Cost to G/L"));
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ResynchronizeInfoMsg@1008 : TextConst 'ENU=Your general and item ledgers will no longer be synchronized after running the cost adjustment. You must run the %1 report to synchronize them again.;ENG=Your general and item ledgers will no longer be synchronised after running the cost adjustment. You must run the %1 report to synchronise them again.';
      InvtSetup@1006 : Record 313;
      InvtAdjmt@1001 : Codeunit 5895;
      ItemNoFilter@1004 : Text[250];
      ItemCategoryFilter@1005 : Text[250];
      Text005@1003 : TextConst 'ENU=You must not use Item No. Filter and Item Category Filter at the same time.;ENG=You must not use Item No. Filter and Item Category Filter at the same time.';
      PostToGL@1007 : Boolean;
      PostEnable@19010160 : Boolean INDATASET;
      FilterItemNoEditable@19034962 : Boolean INDATASET;
      FilterItemCategoryEditable@19002320 : Boolean INDATASET;

    PROCEDURE InitializeRequest@2(NewItemNoFilter@1000 : Text[250];NewItemCategoryFilter@1001 : Text[250]);
    BEGIN
      ItemNoFilter := NewItemNoFilter;
      ItemCategoryFilter := NewItemCategoryFilter;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

