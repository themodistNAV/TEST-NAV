OBJECT Codeunit 9062424 PDF Viewer Management
{
  OBJECT-PROPERTIES
  {
    Date=25/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      mData@1000000002 : DotNet "'TheNavPeople.PdfViewer.9.0.1.2, Version=9.0.1.2, Culture=neutral, PublicKeyToken=7ddee031ca97e96c'.TheNavPeople.PdfViewer.PdfViewerData";
      mDataSerialiser@1000000001 : DotNet "'TheNavPeople.PdfViewer.9.0.1.2, Version=9.0.1.2, Culture=neutral, PublicKeyToken=7ddee031ca97e96c'.TheNavPeople.PdfViewer.PdfViewerDataSerialiser";
      mFileSystem@1000000000 : Codeunit 9062264;
      mFileManagement@1000000003 : Codeunit 419;

    PROCEDURE ShowFile@1000000005(pFilePath@1000000001 : Text[250];pDisableButtons@1000000003 : Boolean;pHideFilePath@1000000004 : Boolean);
    VAR
      lPdfViewer@1000000000 : Page 9062346;
    BEGIN
      //DOC NA2015.4 - Showing a file
      //  -> pFilePath: File path
      //  -> pDisableButtons: Flag to disable buttons
      //  -> pHideFilePath: Flag to hide file path

      lPdfViewer.Initialise(pFilePath, pDisableButtons, pHideFilePath);
      lPdfViewer.RUN();
    END;

    PROCEDURE OpenFileSelected@1000000003(VAR pFilePath@1000000001 : Text;VAR pBlob@1000000000 : Record 99008535) : Boolean;
    VAR
      lFilePath@1000000002 : Text;
      lResult@1000000003 : Boolean;
      eOpenFileDialogFilter@1000000004 : TextConst 'ENU=PDF Files (*.pdf)|*.pdf|All Files (*.*)|*.*;ENG=PDF Files (*.pdf)|*.pdf|All Files (*.*)|*.*';
    BEGIN
      //DOC NA2015.4 - Opening file selected by the user
      //  <> pFilePath: Target file path
      //  <> pBlob: Target BLOB
      //  <- TRUE = Opened, FALSE = Cancelled

      lResult := FALSE;
      lFilePath := mFileManagement.OpenFileDialog('', '', eOpenFileDialogFilter);
      IF (lFilePath <> '') THEN
      BEGIN
        OpenFile(lFilePath, pBlob);
        pFilePath := lFilePath;
        lResult := TRUE;
      END;

      EXIT(lResult);
    END;

    PROCEDURE OpenFile@1000000001(pFilePath@1000000001 : Text;VAR pBlob@1000000000 : Record 99008535);
    BEGIN
      //DOC NA2015.4 - Opening file
      //  -> pFilePath: The file path
      //  <> pBlob: Target BLOB

      mData := mData.PdfViewerData();
      mData.FilePath := pFilePath;
      UpdateAddIn(pBlob);
    END;

    PROCEDURE OpenBlob@1000000002(VAR pFromBlob@1000000004 : Record 99008535;VAR pToBlob@1000000003 : Record 99008535);
    VAR
      lBlob@1000000000 : Record 99008535;
      lInStream@1000000001 : InStream;
      lMemoryStream@1000000002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
    BEGIN
      //DOC NA2015.4 - Opening BLOB
      //  -> pFromBlob: Source BLOB
      //  <> pBlob: Target BLOB

      pFromBlob.Blob.CREATEINSTREAM(lInStream);
      lMemoryStream := lMemoryStream.MemoryStream();
      COPYSTREAM(lMemoryStream, lInStream);

      mData := mData.PdfViewerData();
      mData.Stream := lMemoryStream;

      UpdateAddIn(pToBlob);
    END;

    PROCEDURE CloseFile@1000000000(VAR pBlob@1000000000 : Record 99008535);
    BEGIN
      //DOC NA2015.4 - Closing file
      //  <> pBlob: Target BLOB

      CLEAR(mData);
      UpdateAddIn(pBlob);
    END;

    LOCAL PROCEDURE UpdateAddIn@1000000004(VAR pBlob@1000000002 : Record 99008535);
    VAR
      lMemoryStream@1000000001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      lOutStream@1000000000 : OutStream;
    BEGIN
      //DOC NA2015.4 - Updating PDF viewer add-in
      //  <> pBlob: Target BLOB

      lMemoryStream := lMemoryStream.MemoryStream();
      IF ( NOT (ISNULL(mData))) THEN
        mDataSerialiser.Serialise(mData, lMemoryStream);

      CLEAR(pBlob.Blob);
      pBlob.Blob.CREATEOUTSTREAM(lOutStream);
      COPYSTREAM(lOutStream, lMemoryStream);
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 08/04/2014 - Created
      //DOC NA2015.4  JH 28/01/2015 - Click-once supported
      //DOC NA2016.10 JH 29/10/2015 - Upgrade to 2016
      //DOC NA2016.16 JH 25/05/2016 - CfMD amendments (new .NET library)
    }
    END.
  }
}

