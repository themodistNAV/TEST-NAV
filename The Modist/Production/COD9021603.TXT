OBJECT Codeunit 9021603 Multi Language Functions
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:16 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MultiLangRec@1190000006 : Record 9021603;
      LabelRec@1190000005 : Record 9021618;
      InternetSetup@1190000007 : Record 9021609;
      ResourceURL@1190000008 : Text[1000];
      Text000@1190000009 : TextConst 'ENU=This only works if the key is not blank.;ENG=This only works if the key is not blank.';
      Text001@1190000010 : TextConst 'ENU=The key contains an invalid character.  It can only contain numbers, letters or a under score.;ENG=The key contains an invalid character.  It can only contain numbers, letters or a under score.';
      i@1190000011 : Integer;
      j@1190000012 : Integer;
      TxtFileName@1000000000 : TextConst 'ENU=LANGUAGE;ENG=LANGUAGE';

    PROCEDURE ReturnResxName@1190000005(ResxName@1190000005 : Code[100];LabelC@1190000008 : Code[10]) : Code[100];
    VAR
      VersionCalls@1000000000 : Codeunit 9021662;
    BEGIN
      VersionCalls.ReturnResxName(ResxName,LabelC);  //DOC EC2013.4.00
    END;

    PROCEDURE UpdateInMultiLangMode@1190000006(ModeTrue@1190000005 : Boolean);
    VAR
      Promo@1190000006 : Record 9021656;
      TxtConfirmation@1000000000 : TextConst 'ENU=There are %1 Promotions.\\Do you wish to update each one to MultiLanguage is TRUE?;ENG=There are %1 Promotions.\\Do you wish to update each one to MultiLanguage is TRUE?';
    BEGIN
      IF NOT ModeTrue THEN EXIT;
      Promo.RESET;
      //DOC EC2013.3.40 -
      IF Promo.COUNT > 0 THEN BEGIN
        IF CONFIRM(TxtConfirmation, TRUE, Promo.COUNT) THEN BEGIN
      //DOC EC2013.3.40 +
          IF Promo.FIND('-') THEN
          REPEAT
            Promo.VALIDATE("In Multi Language Mode",ModeTrue);
            Promo.MODIFY;
          UNTIL Promo.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CreateKey@1190000007(TableID@1190000005 : Integer;InLabelCode@1190000006 : Code[10];KeyFieldString@1190000007 : Code[100];SubType@1190000010 : Integer) : Code[100];
    VAR
      TextL000@1190000008 : TextConst 'ENU=CreateKey in Multi Language Functions not set to handle table %1.;ENG=CreateKey in Multi Language Functions not set to handle table %1.';
      NewKey@1190000009 : Code[100];
      Comment@1190000011 : Text[100];
      TxtShippingAgent@1000000000 : TextConst 'ENU=Shipping Agent %1 (Sys Gen %2);ENG=Shipping Agent %1 (Sys Gen %2)';
      TxtShippingAgentService@1000000001 : TextConst 'ENU=Shipping Agent Service %1 (Sys Gen %2);ENG=Shipping Agent Service %1 (Sys Gen %2)';
      TxtContentTypeAttribute@1000000002 : TextConst 'ENU=Content Type Attribute %1 (Sys Gen %2);ENG=Content Type Attribute %1 (Sys Gen %2)';
      TxtPageContentValue@1000000003 : TextConst 'ENU=Page Content Value %1 (Sys Gen %2);ENG=Page Content Value %1 (Sys Gen %2)';
      TxtPageItemContentTitle@1000000004 : TextConst 'ENU=Page Item Content Title %1 (Sys Gen %2);ENG=Page Item Content Title %1 (Sys Gen %2)';
      TxtPageItemContentWebDescription@1000000005 : TextConst 'ENU=Page Item Content Web Description %1 (Sys Gen %2);ENG=Page Item Content Web Description %1 (Sys Gen %2)';
      TxtPageItemContentImageAlternativeText@1000000006 : TextConst 'ENU=Page Item Content Image Alternative Text %1 (Sys Gen %2);ENG=Page Item Content Image Alternative Text %1 (Sys Gen %2)';
      TxtPageItemContentImageTitle@1000000007 : TextConst 'ENU=Page Item Content Image Title %1 (Sys Gen %2);ENG=Page Item Content Image Title %1 (Sys Gen %2)';
      TxtSubscriptionName@1000000008 : TextConst 'ENU=Subscription Name %1 (Sys Gen %2);ENG=Subscription Name %1 (Sys Gen %2)';
      TxtSubscriptionDescription@1000000009 : TextConst 'ENU=Subscription Description %1 (Sys Gen %2);ENG=Subscription Description %1 (Sys Gen %2)';
      TxtPromotionProductText@1000000010 : TextConst 'ENU=Promotion Product Text %1 (Sys Gen %2);ENG=Promotion Product Text %1 (Sys Gen %2)';
      TxtPromotionBasketText@1000000011 : TextConst 'ENU=Promotion Basket Text %1 (Sys Gen %2);ENG=Promotion Basket Text %1 (Sys Gen %2)';
    BEGIN
      KeyFieldString := DELCHR(KeyFieldString, '=', DELCHR(KeyFieldString, '=', 'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789_'));

      InternetSetup.GET;
      CASE TableID OF
        291 :  //Shipping Agent
          BEGIN
            NewKey := 'SHIPAGENT_' + KeyFieldString;
            Comment := TxtShippingAgent;
          END;
        //DOC EC2013.3.40 -
        5790 :  //Shipping Agent Services
          BEGIN
            NewKey := 'SHIPAGENTSERV_' + KeyFieldString;
            Comment := TxtShippingAgentService;
          END;
        //DOC EC2013.3.40 +
        9021634 :  //Content Type Attribute
          BEGIN
            NewKey := 'CONTENTTYPEATT_' + KeyFieldString;
            Comment := TxtContentTypeAttribute;
          END;
        9021635 :  //Page Content Value
          BEGIN
            NewKey := 'PAGE_CONT_V_' + KeyFieldString;
            Comment := TxtPageContentValue;
          END;
        9021636 :  //Page Item Content
          BEGIN
            CASE SubType OF
            0 :
              BEGIN
                //Title
                NewKey := 'PAGEITEMCON_TITLE_' + KeyFieldString;
                Comment := TxtPageItemContentTitle;
              END;
            1 :
              BEGIN
                //Web Description
                NewKey := 'PAGEITEMCON_WEBDESC_' + KeyFieldString;
                Comment := TxtPageItemContentWebDescription;
              END;
            2 :
              BEGIN
                //Image Alternative Text
                NewKey := 'PAGEITEMCON_IMAGE_ALTTEXT_' + KeyFieldString;
                Comment := TxtPageItemContentImageAlternativeText;
              END;
            3 :
              BEGIN
                //Image Title
                NewKey := 'PAGEITEMCON_IMAGE_TITLE_' + KeyFieldString;
                Comment := TxtPageItemContentImageTitle;
              END;
            END;
          END;
        9021653 :  //Subscription
          BEGIN
            IF SubType = 0 THEN BEGIN
              //Name
              NewKey := 'SUBSCRIPT_NAME_' + KeyFieldString;
              Comment := TxtSubscriptionName;
            END ELSE BEGIN
              //Description
              NewKey := 'SUBSCRIPT_DESC_' + KeyFieldString;
              Comment := TxtSubscriptionDescription;
            END;
          END;
        9021656 :  //Promotion Header
          BEGIN
            IF SubType = 0 THEN BEGIN
              //Product
              NewKey := 'PROMO_PROD_' + KeyFieldString;
              Comment := TxtPromotionProductText;
            END ELSE BEGIN
              //Basket
              NewKey := 'PROMO_BASK_' + KeyFieldString;
              Comment := TxtPromotionBasketText;
            END;
          END;
        ELSE
          ERROR(TextL000,TableID);
      END;
      Comment := STRSUBSTNO(Comment,KeyFieldString,FORMAT(TODAY));

      MultiLangRec.RESET;
      MultiLangRec.SETCURRENTKEY("Label Code","Base File Name","Language ID","Resx Name");
      MultiLangRec.SETFILTER("Label Code",'%1|%2',InLabelCode,'');
      MultiLangRec.SETRANGE("Base File Name",TxtFileName);
      MultiLangRec.SETRANGE("Language ID",InternetSetup."Multi-Language Lookup Language");
      MultiLangRec.SETRANGE("Resx Name",NewKey);
      IF NOT MultiLangRec.FINDFIRST THEN BEGIN
        CLEAR(MultiLangRec);
        MultiLangRec.INIT;
        MultiLangRec."Label Code" := InLabelCode;
        MultiLangRec."Base File Name" := TxtFileName;
        MultiLangRec.VALIDATE("Language ID",InternetSetup."Multi-Language Lookup Language");
        MultiLangRec."Resx Name" := NewKey;
        MultiLangRec."Resx Comment Short" := Comment;
        MultiLangRec."Customer Entry" := TRUE;
        MultiLangRec.INSERT(TRUE);
      END;

      EXIT(NewKey);
    END;

    PROCEDURE UpdateMultiLang@1190000010(InKey@1190000005 : Code[100];InLabelCode@1190000006 : Code[10]);
    VAR
      TxtManuallyCreated@1000000000 : TextConst 'ENU=Manually Created;ENG=Manually Created';
    BEGIN
      FOR i := 1 TO STRLEN(InKey) DO
        IF COPYSTR(InKey,i,1) IN ['0'..'9','A'..'Z','_'] THEN
          j := 1
        ELSE
          ERROR(Text001);

      InternetSetup.GET;

      MultiLangRec.RESET;
      MultiLangRec.SETCURRENTKEY("Label Code","Base File Name","Language ID","Resx Name");
      //MultiLangRec.SETRANGE("Label Code",InLabelCode);
      MultiLangRec.SETFILTER("Label Code",'%1|%2',InLabelCode,'');
      MultiLangRec.SETRANGE("Base File Name",TxtFileName);
      MultiLangRec.SETRANGE("Language ID",InternetSetup."Multi-Language Lookup Language");
      MultiLangRec.SETRANGE("Resx Name",InKey);
      IF NOT MultiLangRec.FINDFIRST THEN BEGIN
        CLEAR(MultiLangRec);
        MultiLangRec.INIT;
        MultiLangRec."Label Code" := InLabelCode;
        MultiLangRec."Base File Name" := TxtFileName;
        MultiLangRec."Language ID" := InternetSetup."Multi-Language Lookup Language";
        MultiLangRec."Resx Name" := InKey;
        MultiLangRec."Resx Comment Short" := TxtManuallyCreated;
        MultiLangRec."Customer Entry" := TRUE;
        MultiLangRec.INSERT(TRUE);
      END;
    END;

    PROCEDURE PrepareLaunchHyperlink@1190000008(InLabelCode@1190000005 : Code[10];InKey@1190000006 : Code[100]);
    BEGIN
      IF InKey = '' THEN
        ERROR(Text000);
      MultiLangRec.RESET;
      MultiLangRec.SETCURRENTKEY("Label Code","Base File Name","Language ID","Resx Name");
      MultiLangRec.SETRANGE("Label Code",InLabelCode);
      MultiLangRec.SETRANGE("Base File Name",TxtFileName);
      MultiLangRec.SETRANGE("Resx Name",InKey);
      MultiLangRec.SETRANGE("Customer Entry",TRUE);
      IF MultiLangRec.FINDFIRST THEN BEGIN
        MultiLangRec.SETRANGE("Customer Entry",FALSE);
        IF MultiLangRec.FINDFIRST THEN BEGIN
          MultiLangRec.SETRANGE("Customer Entry",TRUE);
          MultiLangRec.DELETEALL;
          LaunchHyperlink(InLabelCode,InKey,FALSE,'');
        END ELSE
          LaunchHyperlink(InLabelCode,InKey,TRUE,
            MultiLangRec."Resx Comment Short");
      END ELSE
        LaunchHyperlink(InLabelCode,InKey,FALSE,'');
    END;

    PROCEDURE LaunchHyperlink@1190000009(InLabelCode@1190000005 : Code[10];InKey@1190000006 : Code[100];NewEntry@1190000007 : Boolean;InComment@1190000008 : Text[250]);
    BEGIN
      LabelRec.GET(InLabelCode);
      ResourceURL := LabelRec."Staging Site URL" + '/admin/resxmgmt.aspx?LabelCode=%1&BaseFileName=%2&KeyName=%3';
      ResourceURL := STRSUBSTNO(ResourceURL,InLabelCode,TxtFileName,InKey);
      IF NewEntry THEN BEGIN
        ResourceURL := ResourceURL + '&InsertAllowed=true';
        IF InComment <> '' THEN BEGIN
          InComment := CONVERTSTR(InComment,' ','_');
          ResourceURL := ResourceURL + '&CommentToSave=' + InComment;
        END;

      END;
      HYPERLINK(ResourceURL);
    END;

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - New Codeunit
      //DOC EC2013.3.40 MQ 03/01/2013 - GW130103 Enhancement in UpdateInMultiLangMode and CreateKey
      //DOC EC2013.4.00 MQ 06/11/2013 - GW131106 Changed function ReturnResxName to call Version Calls CU
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

