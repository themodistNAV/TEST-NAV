OBJECT Codeunit 9062245 Web View Links Management
{
  OBJECT-PROPERTIES
  {
    Date=14/07/16;
    Time=12:00:00 PM;
    Version List=NA2016.16;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WebViewLink@1000000000 : Record 9062234;
      FileSystem@1000000001 : Codeunit 9062264;

    PROCEDURE ShowDesktopControl@1000000003() : Boolean;
    BEGIN
      //DOC NA2016.16 - Returning whether to show desktop control
      //  <- Answer

      EXIT(CURRENTCLIENTTYPE = CLIENTTYPE::Windows);
    END;

    PROCEDURE ShowWebControl@1000000005() : Boolean;
    BEGIN
      //DOC NA2016.16 - Returning whether to show desktop control
      //  <- Answer

      EXIT(CURRENTCLIENTTYPE IN [CLIENTTYPE::Web, CLIENTTYPE::Tablet, CLIENTTYPE::Phone]);
    END;

    PROCEDURE GetUrlWithCheck@1000000000(VAR pWebViewLink@1000000000 : Record 9062234) : Text;
    VAR
      lUrl@1000000001 : Text;
    BEGIN
      //DOC NA2015.4 - Getting web view link URL with a check
      //  -> pWebViewLink: The web view link
      //  <- The URL

      lUrl := pWebViewLink.GetUrl();

      IF (pWebViewLink."Local File") THEN
      BEGIN
        IF ( NOT (FileSystem.FileExists(FileSystem.Client, lUrl))) THEN
          lUrl := '';
      END;

      EXIT(lUrl);
    END;

    PROCEDURE OpenInBrowser@1000000002(VAR pWebViewLink@1000000000 : Record 9062234) : Boolean;
    VAR
      lUrl@1000000003 : Text;
    BEGIN
      //DOC NA2015.4 - Opening a web view link in a browser
      //  <> pWebViewLink: The web view link to open
      //  <- TRUE = Opened, FALSE = Cancelled

      lUrl := pWebViewLink.GetUrl();
      HYPERLINK(lUrl);

      EXIT(TRUE);
    END;

    PROCEDURE OpenInBrowserWithInteraction@1000000004(VAR pWebViewLink@1000000000 : Record 9062234) : Boolean;
    VAR
      lWebViewLinkParameters@1000000002 : Report 9062304;
      lParameter@1000000001 : ARRAY [4] OF Text;
      lUrl@1000000003 : Text;
    BEGIN
      //DOC NA2016.16 - Opening a web view link in a browser (with interaction)
      //  <> pWebViewLink: The web view link to open
      //  <- TRUE = Opened, FALSE = Cancelled

      WebViewLink := pWebViewLink;
      WebViewLink.ClearParameters();

      IF (WebViewLink.AreParametersUsedInUrl()) THEN
      BEGIN
        CLEAR(lWebViewLinkParameters);
        lWebViewLinkParameters.Initialise(WebViewLink);
        lWebViewLinkParameters.RUNMODAL();
        IF ( NOT (lWebViewLinkParameters.OK)) THEN
          EXIT(FALSE);
        lWebViewLinkParameters.GetParameters(lParameter);
        WebViewLink.SetParameters(lParameter);
      END;

      lUrl := WebViewLink.GetUrl();
      HYPERLINK(lUrl);

      WebViewLink.ClearParameters();

      EXIT(TRUE);
    END;

    PROCEDURE RefreshJetReport@1000000001(VAR pWebViewLink@1000000008 : Record 9062234);
    VAR
      lJetReportHeader@1000000000 : Record 9062296;
      lJetReportHeaderCopy@1000000004 : Record 9062296;
      lJetReportParameter@1000000002 : Record 9062297;
      lFileSystem@1000000001 : Codeunit 9062264;
      lPath@1000000003 : Text;
      lDirectoryName@1000000007 : Text;
      lFileName@1000000005 : Text;
      lParameter@1000000006 : ARRAY [4] OF Text[250];
    BEGIN
      //DOC NA2015.4 - Refreshing a jet report
      //  <> pWebViewLink: The web view link to refresh jet report for

      pWebViewLink.TESTFIELD("Jet Report ID");
      lJetReportHeader.GET(pWebViewLink."Jet Report ID");

      lPath := pWebViewLink.URL;
      IF ( NOT (lFileSystem.FileExists(lFileSystem.Client, lPath))) THEN
        EXIT;

      pWebViewLink.GetParameters(lParameter);

      lDirectoryName := lFileSystem.GetFolderName(lFileSystem.Client, lPath);
      lFileName := lFileSystem.GetFileNameWithoutExtension(lFileSystem.Client, lPath);

      lJetReportHeader.CreateCopy(lJetReportHeaderCopy, lJetReportParameter);
      lJetReportHeaderCopy."Short Code" := FORMAT(lJetReportHeaderCopy.ID);
      lJetReportHeaderCopy."Output Folder Path" := lDirectoryName;
      lJetReportHeaderCopy."Output File Name" := lFileName;
      lJetReportHeaderCopy."Output Format" := lJetReportHeaderCopy."Output Format"::HTML;
      lJetReportHeaderCopy.MODIFY(TRUE);

      lJetReportHeaderCopy.QuickRun(lJetReportHeaderCopy."Short Code", '', '', '', pWebViewLink."Jet Report Parameter 1", lParameter[1],
        pWebViewLink."Jet Report Parameter 2", lParameter[2], pWebViewLink."Jet Report Parameter 3", lParameter[3], FALSE);

      lJetReportHeaderCopy.DELETE(TRUE);
    END;

    BEGIN
    {
      //DOC NA2015.4  JH 06/02/2015 - Created
      //DOC NA2016.10 JH 26/08/2015 - Upgraded to 2016
      //DOC NA2016.16 PC 20/05/2016 - Change to "OpenInBrowser" to not ask for Params if they've been set already
      //DOC NA2016.16 JH 07/07/2016 - Web view
      //DOC NA2016.16 JH 13/07/2016 - Bugfixes
    }
    END.
  }
}

