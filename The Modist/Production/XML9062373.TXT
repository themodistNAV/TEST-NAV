OBJECT XMLport 9062373 Record Browser List Data
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Record Browser List Data;
               ENG=Record Browser List Data];
    Encoding=ISO-8859-2;
    XMLVersionNo=1.1;
    Format/Evaluate=XML Format/Evaluate;
    OnPreXMLport=BEGIN
                   AsArray1 := 'true';
                   AsArray2 := AsArray1;
                   AsArray3 := AsArray1;
                   AsArray4 := AsArray1;
                   AsArray5 := AsArray1;
                 END;

    OnPostXMLport=BEGIN
                    CloseRecord();
                  END;

  }
  ELEMENTS
  {
    { [{4DDA94A0-B589-43B1-A1C4-74B3EBF9C25E}];  ;Browser             ;Element ;Table   ;
                                                  VariableName=Browser;
                                                  SourceTable=Table9062795;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             Browser_CurrentLevel := FORMAT(BrowserLevel, 0, 9);
                                                                             Browser_CurrentLineNo := FORMAT(BrowserParentLineNo, 0, 9);
                                                                             Browser_ShowDetails := FORMAT((ItemDetails_Position <> ''), 0, 9);
                                                                           END;
                                                                            }

    { [{B999F588-F695-4061-8544-EC2C5CF44B75}];1 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Browser::Description }

    { [{E3565ADC-99E7-46F9-A970-FD83F9B2F852}];1 ;TableNo             ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=Browser::Table No. }

    { [{4DA18308-7448-4BB5-96B2-772059380DC9}];1 ;CurrentLevel        ;Element ;Text    ;
                                                  VariableName=Browser_CurrentLevel }

    { [{BAD5ACAF-97DE-4192-83B4-90A8422FEDFE}];1 ;CurrentLineNo       ;Element ;Text    ;
                                                  VariableName=Browser_CurrentLineNo }

    { [{2336C322-75F6-47BF-8AB7-0450376C1024}];1 ;FilterText          ;Element ;Text     }

    { [{7F514BAA-0884-4114-8A79-23357CBB4ABD}];1 ;ShowDetails         ;Element ;Text    ;
                                                  VariableName=Browser_ShowDetails }

    { [{23B2A1C6-D622-4BE5-9F9D-592EDA846948}];1 ;ParentBrowserLine   ;Element ;Table   ;
                                                  VariableName=BrowserStack;
                                                  SourceTable=Table9062797;
                                                  Temporary=Yes;
                                                  MinOccurs=Zero }

    { [{AD5ED19C-7D94-428E-9379-7BD03B1ECF07}];2 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray1 }

    { [{F44C235D-A494-4A4A-BA26-31EBEC657D8B}];2 ;Level               ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=BrowserStack::Level }

    { [{04F391E5-495B-43A4-B376-BC1C4064C891}];2 ;LineNo              ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=BrowserStack::Record Browser Line No. }

    { [{5E756990-E687-47C1-A839-07C380EC6796}];2 ;LineLevel           ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=BrowserStack::Record Browser Line Level }

    { [{29C3C13C-DBA3-4FF3-9252-C9F9E4E11061}];2 ;EntryType           ;Element ;Field   ;
                                                  DataType=Option;
                                                  SourceField=BrowserStack::Stack Entry Type }

    { [{CEC2FA4A-BE14-4DDE-A34B-8196CA7251FC}];2 ;Name                ;Element ;Text    ;
                                                  VariableName=BrowserStack_Name }

    { [{DDDDF15C-010E-4097-92B5-01209933E6FA}];2 ;Record              ;Element ;Text     }

    { [{FF19E4DF-489C-491E-8C95-AED1BEF63C65}];3 ;Field               ;Element ;Table   ;
                                                  VariableName=ParentRecordField;
                                                  SourceTable=Table9062798;
                                                  SourceTableView=WHERE(Field11=CONST(0));
                                                  LinkFields=Field1=FIELD(Field2);
                                                  LinkTable=BrowserStack;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         OpenRecord(BrowserStack."Source Table No.");
                                                                         IF (BrowserStack."Source Position" <> '') THEN BEGIN
                                                                           RecRef.SETPOSITION(BrowserStack."Source Position");
                                                                           RecRef.FIND('=');
                                                                         END;

                                                                         IF (BrowserStack."Stack Entry Type" = BrowserStack."Stack Entry Type"::"Search Results") THEN
                                                                           ParentRecordField.SETRANGE("Record Browser Line No.", 0)
                                                                         ELSE
                                                                           ParentRecordField.SETRANGE("Record Browser Line No.", BrowserStack."Record Browser Line No.");
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             //IF (BrowserStack."Stack Entry Type" = BrowserStack."Stack Entry Type"::"Search Results") THEN
                                                                             //  ParentRecordField_Value := txtSearchResultsFieldValue
                                                                             //ELSE
                                                                             ParentRecordField_Value := GetFieldValue(ParentRecordField);
                                                                           END;
                                                                            }

    { [{064607C1-93F6-425E-BCEB-663CD4F8E67D}];4 ;name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ParentRecordField::Name }

    { [{CFDB2B2B-38C0-4426-9FA5-F5222939F7FD}];4 ;value               ;Attribute;Text   ;
                                                  VariableName=ParentRecordField_Value }

    { [{A30A04C3-7428-486F-A631-8E540B06E0E9}];1 ;BrowserLine         ;Element ;Table   ;
                                                  VariableName=BrowserLine;
                                                  SourceTable=Table9062796;
                                                  Temporary=Yes;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         //BrowserLine.SETRANGE(Level, BrowserLevel);
                                                                         //BrowserLine.SETRANGE("Parent Line No.", BrowserParentLineNo);
                                                                       END;
                                                                        }

    { [{DDEFF8D2-BE99-49F0-910C-EFF822E8391B}];2 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray2 }

    { [{6C400CAA-00F9-4B65-81B6-572698A8EB16}];2 ;Type                ;Element ;Field   ;
                                                  DataType=Option;
                                                  SourceField=BrowserLine::Type }

    { [{2C26DA25-E23C-4816-9EAC-0FEB6655F2F0}];2 ;Name                ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=BrowserLine::Description }

    { [{2DB286B0-20C3-4828-9DA3-589C9245914B}];2 ;LineNo              ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=BrowserLine::Line No. }

    { [{21AC2DA0-91C6-4CE5-9543-0F4C5E8B36B5}];2 ;Record              ;Element ;Table   ;
                                                  VariableName=RecordList;
                                                  SourceTable=Table2000000026;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=VAR
                                                                         CriteriaField@1000000000 : Record 9062627;
                                                                         CriteriaSource@1000000001 : Record 9062796;
                                                                         CriteriaToUse@1000000002 : Integer;
                                                                         CriteriaPositionText@1000000003 : Text;
                                                                       BEGIN
                                                                         OpenRecord(BrowserLine."Source Table No.");

                                                                         CriteriaToUse := 0;
                                                                         CriteriaPositionText := '';

                                                                         IF (ApplyFilterCriteria) AND (BrowserLine.Type = BrowserLine.Type::Record) THEN BEGIN
                                                                           IF (BrowserStack.FINDLAST()) THEN BEGIN
                                                                             IF (BrowserStack."Stack Entry Type" = BrowserStack."Stack Entry Type"::"Search Results") AND (BrowserStack."Record Browser Line No." <> 0) THEN BEGIN
                                                                               CriteriaSource.GET(Browser.Code, BrowserStack."Record Browser Line No.");
                                                                               CriteriaToUse := CriteriaSource."Filter Criteria ID";
                                                                               CriteriaPositionText := BrowserStack."Source Position";
                                                                             END;
                                                                           END;
                                                                         END;

                                                                         IF (CriteriaToUse = 0) THEN BEGIN
                                                                           IF (BrowserLine."Link Criteria ID" <> 0) THEN BEGIN
                                                                             CriteriaToUse := BrowserLine."Link Criteria ID";
                                                                             IF (BrowserLine."Parent Line No." <> 0) THEN BEGIN
                                                                               BrowserStack.RESET();
                                                                               BrowserStack.SETRANGE("Record Browser Line No.", BrowserLine."Parent Line No.");
                                                                               IF (BrowserStack.FINDFIRST()) THEN
                                                                                 CriteriaPositionText := BrowserStack."Source Position";
                                                                             END;
                                                                           END;
                                                                         END;

                                                                         IF (CriteriaToUse <> 0) AND (CriteriaPositionText <> '') THEN BEGIN
                                                                           Criteria.GET(CriteriaToUse);
                                                                           Criteria."Filter Source Record Position" := CriteriaPositionText;
                                                                           Criteria.MODIFY();
                                                                           CriteriaMgt.ApplyCriteriaToRecRef(Criteria.ID, RecRef);
                                                                         END;

                                                                         RecordCount := RecRef.COUNT;
                                                                         IF (RecordCount = 0) THEN
                                                                           currXMLport.BREAK()
                                                                         ELSE
                                                                           RecordList.SETRANGE(Number, 1, RecordCount);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             ProcessRecord@1000000000 : Boolean;
                                                                           BEGIN
                                                                             IF (RecordList.Number = 1) THEN
                                                                               RecRef.FINDFIRST()
                                                                             ELSE
                                                                               RecRef.NEXT();
                                                                             Record_Position := RecRef.GETPOSITION(FALSE);

                                                                             ProcessRecord := TRUE;
                                                                             IF (ApplyFilterCriteria) AND (BrowserLine.Type = BrowserLine.Type::Record) THEN
                                                                               ProcessRecord := CriteriaMgt.RecordInFilter(FilterCriteriaId, RecRef.NUMBER, Record_Position);

                                                                             IF (ProcessRecord) THEN BEGIN
                                                                               Record_Picture := GetPictureUrl();
                                                                             END ELSE
                                                                               currXMLport.SKIP();
                                                                           END;
                                                                            }

    { [{3E77639A-E98B-4C55-8A66-85120551C327}];3 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray3 }

    { [{6E128C81-F783-47DA-BEBC-CD41F3E699F1}];3 ;Position            ;Element ;Text    ;
                                                  VariableName=Record_Position }

    { [{804B95CD-F3A8-4999-A469-C8A82DC7AB3B}];3 ;Picture             ;Element ;Text    ;
                                                  VariableName=Record_Picture }

    { [{9B66EEB6-39E0-426A-93FC-ABF485140FFD}];3 ;Field               ;Element ;Table   ;
                                                  VariableName=RecordField;
                                                  SourceTable=Table9062798;
                                                  SourceTableView=WHERE(Field11=CONST(0));
                                                  LinkFields=Field1=FIELD(Field1),
                                                             Field2=FIELD(Field2);
                                                  LinkTable=BrowserLine;
                                                  MinOccurs=Zero;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             RecordField_Value := GetFieldValue(RecordField);
                                                                           END;
                                                                            }

    { [{EC975FCD-8F07-44A7-A669-D9BA56CB3AF7}];4 ;name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=RecordField::Name }

    { [{B0DB92FF-A124-4BB2-9D3D-0F85F13C1114}];4 ;value               ;Attribute;Text   ;
                                                  VariableName=RecordField_Value }

    { [{6830D38A-0490-4CD5-971B-BED971113D4B}];1 ;ItemDetails         ;Element ;Text    ;
                                                  VariableName=ItemDetails;
                                                  MinOccurs=Zero;
                                                  MaxOccurs=Once;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 IF (ItemDetails_Position = '') THEN
                                                                                   currXMLport.SKIP()
                                                                                 ELSE BEGIN
                                                                                   OpenRecord(Browser."Table No.");
                                                                                   RecRef.SETPOSITION(ItemDetails_Position);
                                                                                   IF (NOT(RecRef.FIND('='))) THEN
                                                                                     currXMLport.SKIP();
                                                                                 END;
                                                                                 ItemDetails_Picture := GetPictureUrl();
                                                                               END;
                                                                                }

    { [{4B5CBCF7-2B37-4750-8830-D27F856D2F0B}];2 ;Position            ;Element ;Text    ;
                                                  VariableName=ItemDetails_Position }

    { [{42C39681-EC0D-4233-91ED-DFDB239E9AE6}];2 ;Picture             ;Element ;Text    ;
                                                  VariableName=ItemDetails_Picture }

    { [{F040969B-DF81-4F8E-827C-15C9562162A9}];2 ;Field               ;Element ;Table   ;
                                                  VariableName=ItemDetailsField;
                                                  SourceTable=Table9062798;
                                                  SourceTableView=WHERE(Field2=CONST(0),
                                                                        Field11=CONST(2));
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=Browser;
                                                  MinOccurs=Zero;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             ItemDetailsField_Value := GetFieldValue(ItemDetailsField);
                                                                           END;
                                                                            }

    { [{B61D33BA-E171-4DE9-A4FE-C9B73ED49B2C}];3 ;name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ItemDetailsField::Name }

    { [{EDE6243F-A317-44FE-80BB-CDD068D5B7F6}];3 ;value               ;Attribute;Text   ;
                                                  VariableName=ItemDetailsField_Value }

    { [{753F61A5-C1C8-4696-BFC1-AFB4C9083B80}];2 ;Related             ;Element ;Table   ;
                                                  VariableName=RelatedLine;
                                                  SourceTable=Table9062796;
                                                  SourceTableView=WHERE(Field3=CONST(2));
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=BrowserLine;
                                                  MinOccurs=Zero }

    { [{0043E730-A402-43F0-88C5-CAF853626D4E}];3 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray4 }

    { [{569B8D73-CF1B-4BF2-A8F2-3FB7F73566F2}];3 ;Type                ;Element ;Field   ;
                                                  DataType=Option;
                                                  SourceField=RelatedLine::Type }

    { [{47F40C85-F14B-45DE-ACAB-3DB7A10964EB}];3 ;LineNo              ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=RelatedLine::Line No. }

    { [{262B4A2D-5A8C-4016-8DC0-1F61D3832765}];3 ;Caption             ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=RelatedLine::Description }

    { [{7085832C-4CC8-480D-A7CA-F1C422642F9A}];3 ;Record              ;Element ;Table   ;
                                                  VariableName=RelatedRecord;
                                                  SourceTable=Table2000000026;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         OpenRecord(RelatedLine."Source Table No.");

                                                                         IF (RelatedLine."Link Criteria ID" <> 0) THEN BEGIN
                                                                           Criteria.GET(RelatedLine."Link Criteria ID");
                                                                           IF (RelatedLine."Parent Line No." <> 0) THEN BEGIN
                                                                             BrowserStack.RESET();
                                                                             BrowserStack.SETRANGE("Record Browser Line No.", RelatedLine."Parent Line No.");
                                                                             IF (BrowserStack.FINDFIRST()) THEN
                                                                               Criteria."Filter Source Record Position" := BrowserStack."Source Position";
                                                                           END;
                                                                           Criteria.MODIFY();
                                                                           CriteriaMgt.ApplyCriteriaToRecRef(Criteria.ID, RecRef);
                                                                         END;

                                                                         RecordCount := RecRef.COUNT;
                                                                         IF (RecordCount = 0) THEN
                                                                           currXMLport.BREAK()
                                                                         ELSE
                                                                           RelatedRecord.SETRANGE(Number, 1, RecordCount);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             IF (RelatedRecord.Number = 1) THEN
                                                                               RecRef.FINDFIRST()
                                                                             ELSE
                                                                               RecRef.NEXT();
                                                                             RelatedRecord_Position := RecRef.GETPOSITION(FALSE);
                                                                             RelatedRecord_Picture := GetPictureUrl();
                                                                           END;
                                                                            }

    { [{240ED78A-AF20-4BFD-8B0D-A373B42BE099}];4 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray5 }

    { [{D7672FD0-B8EF-40D0-9CF5-5A519B91E521}];4 ;Position            ;Element ;Text    ;
                                                  VariableName=RelatedRecord_Position }

    { [{493FECD8-F307-427D-A79F-A98F27B9066D}];4 ;Picture             ;Element ;Text    ;
                                                  VariableName=RelatedRecord_Picture }

    { [{CF47D333-3DB7-41D6-81FC-7690918ACD39}];4 ;Field               ;Element ;Table   ;
                                                  VariableName=RelatedRecordField;
                                                  SourceTable=Table9062798;
                                                  SourceTableView=WHERE(Field11=CONST(0));
                                                  LinkFields=Field1=FIELD(Field1),
                                                             Field2=FIELD(Field2);
                                                  LinkTable=RelatedLine;
                                                  MinOccurs=Zero;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             RelatedRecordField_Value := GetFieldValue(RelatedRecordField);
                                                                           END;
                                                                            }

    { [{ABDA29AA-849E-4FD2-A9A0-F760DF926C6F}];5 ;name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=RelatedRecordField::Name }

    { [{82DBF03E-C77F-4D06-8000-E40715D80B4E}];5 ;value               ;Attribute;Text   ;
                                                  VariableName=RelatedRecordField_Value }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Criteria@1000000006 : Record 9062626;
      CriteriaMgt@1000000007 : Codeunit 9062462;
      RecRef@1000000000 : RecordRef;
      FldRef@1000000001 : FieldRef;
      RecordCount@1000000002 : Integer;
      RecordOpen@1000000003 : Boolean;
      BrowserLevel@1000000004 : Integer;
      BrowserParentLineNo@1000000005 : Integer;
      ApplyFilterCriteria@1000000008 : Boolean;
      UseFieldsFilter@1000000010 : Boolean;
      UseEnhancedFieldsFilter@1000000011 : Boolean;
      FilterCriteriaId@1000000009 : Integer;
      txtSearchResultsFieldValue@1000000012 : TextConst 'ENU=Search Results;ENG=Search Results';

    PROCEDURE SetData@1000000000(VAR NewBrowserStack@1000000001 : Record 9062797;VAR NewBrowserHeader@1000000003 : Record 9062795);
    VAR
      SrcBrowserLine@1000000002 : Record 9062796;
      DetailsBrowserLine@1000000000 : Record 9062796;
      CriteriaField@1000000004 : Record 9062627;
    BEGIN
      BrowserLevel := 0;
      BrowserParentLineNo := 0;
      ItemDetails_Position := '';
      BrowserStack.RESET();
      BrowserStack.DELETEALL();

      IF (NewBrowserStack.FINDSET()) THEN BEGIN
        REPEAT
          BrowserStack.TRANSFERFIELDS(NewBrowserStack);
          BrowserStack.INSERT();

          BrowserLevel := NewBrowserStack."Record Browser Line Level" + 1;
          BrowserParentLineNo := NewBrowserStack."Record Browser Line No.";
        UNTIL (NewBrowserStack.NEXT() = 0);
      END;

      //filter processing
      ApplyFilterCriteria := FALSE;
      FilterCriteriaId := 0;
      IF (BrowserStack."Stack Entry Type" = BrowserStack."Stack Entry Type"::"Search Results") THEN BEGIN
        ApplyFilterCriteria := TRUE;
        IF (NewBrowserHeader.GETFILTER("Criteria Id Filter") <> '') THEN
          FilterCriteriaId := Browser.GETRANGEMIN("Criteria Id Filter");

        {
        IF (NewBrowserHeader.GETFILTER("Criteria Id Filter") <> '') THEN
          FilterCriteriaId := Browser.GETRANGEMIN("Criteria Id Filter");

        IF (FilterCriteriaId <> 0) THEN BEGIN
          Criteria.GET(FilterCriteriaId);
          ApplyFilterCriteria := (Criteria.View <> '');

          IF (NOT(ApplyFilterCriteria)) THEN BEGIN
            CriteriaField.RESET();
            CriteriaField.SETRANGE("Criteria ID", FilterCriteriaId);
            CriteriaField.SETFILTER(Value, '<>%1', '');
            IF (NOT(CriteriaField.ISEMPTY)) THEN BEGIN
              ApplyFilterCriteria := TRUE;
              CriteriaField.SETRANGE(CriteriaField."Field Type", CriteriaField."Field Type"::Field);
              UseFieldsFilter := NOT(CriteriaField.ISEMPTY);
              CriteriaField.SETRANGE(CriteriaField."Field Type", CriteriaField."Field Type"::"Enhanced Field");
              UseEnhancedFieldsFilter := NOT(CriteriaField.ISEMPTY);
            END;
          END;
        END;
        }

      END;

      //browser list lines
      SrcBrowserLine.RESET();
      SrcBrowserLine.SETRANGE("Record Browser Code", NewBrowserHeader.Code);
      IF (ApplyFilterCriteria) THEN BEGIN
        SrcBrowserLine.SETRANGE(Type, SrcBrowserLine.Type::Record);
      END ELSE BEGIN
        SrcBrowserLine.SETRANGE(Level, BrowserLevel);
        SrcBrowserLine.SETRANGE("Parent Line No.", BrowserParentLineNo);
      END;
      IF (SrcBrowserLine.FINDSET()) THEN BEGIN
        REPEAT
          BrowserLine.TRANSFERFIELDS(SrcBrowserLine);
          IF (ApplyFilterCriteria) THEN
            BrowserLine."Link Criteria ID" := 0;
          BrowserLine.INSERT();
        UNTIL (SrcBrowserLine.NEXT() = 0);
      END;

      //details processing
      IF (NewBrowserStack.FINDLAST()) THEN BEGIN
        IF (DetailsBrowserLine.GET(NewBrowserStack."Record Browser Code", NewBrowserStack."Record Browser Line No.")) THEN BEGIN
          IF (DetailsBrowserLine.Type = DetailsBrowserLine.Type::Record) THEN BEGIN
            ItemDetails_Position := NewBrowserStack."Source Position";
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE OpenRecord@1000000003(TableNo@1000000000 : Integer);
    BEGIN
      IF (RecordOpen) THEN
        RecRef.CLOSE();
      RecRef.OPEN(TableNo);
      RecordOpen := TRUE;
    END;

    LOCAL PROCEDURE CloseRecord@1000000009();
    BEGIN
      IF (RecordOpen) THEN BEGIN
        RecRef.CLOSE();
        RecordOpen := FALSE;
      END;
    END;

    LOCAL PROCEDURE GetFieldValue@1000000006(VAR BrowserLineField@1000000000 : Record 9062798) : Text;
    VAR
      TextValue@1000000001 : Text;
      CrLf@1000000003 : Text;
      TnpToolkit@1000000002 : Codeunit 9062230;
    BEGIN
      TextValue := BrowserLineField.GetTextValue(RecRef);
      EXIT(TextValue);
      {
      CASE (BrowserLineField.Type) OF
        BrowserLineField.Type::Text:
          EXIT(BrowserLineField."Text Value");
        BrowserLineField.Type::Field:
          IF (BrowserLineField."Source Field ID" <> 0) THEN BEGIN
            FldRef := RecRef.FIELD(BrowserLineField."Source Field ID");
            EXIT(FORMAT(FldRef.VALUE, 0, 9));
          END;
      END;
      EXIT('');
      }
    END;

    LOCAL PROCEDURE GetPictureUrl@1000000001() : Text;
    VAR
      Item@1000000000 : Record 27;
      ItemCategory@1000000002 : Record 5722;
      ProductGroup@1000000003 : Record 5723;
      BaseUrl@1000000001 : Text;
    BEGIN
      //!!! TO-DO !!!!!
      //!!! REMOVE THIS FUNCTION !!!!
      //!!! DIRTY DEMO HACK !!!!
      BaseUrl := 'http://content.navondemand.co.uk/demos/carpetright/';
      CASE (RecRef.NUMBER) OF
        DATABASE::"Item Category":
          EXIT(BaseUrl + 'ic_' + LOWERCASE(FORMAT(RecRef.FIELD(ItemCategory.FIELDNO(Code)).VALUE)) + '.jpg');
        DATABASE::"Product Group":
          EXIT(BaseUrl + 'pg_' +
            LOWERCASE(FORMAT(RecRef.FIELD(ProductGroup.FIELDNO("Item Category Code")).VALUE)) +
            '_' +
            LOWERCASE(FORMAT(RecRef.FIELD(ProductGroup.FIELDNO(Code)).VALUE)) + '.jpg');
        DATABASE::Item:
          EXIT(BaseUrl + 'i_' + LOWERCASE(FORMAT(RecRef.FIELD(Item.FIELDNO("No.")).VALUE)) + '.jpg');
      END;
      EXIT(BaseUrl + 'default_picture.jpg');
    END;

    BEGIN
    {
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

