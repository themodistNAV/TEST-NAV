OBJECT Codeunit 1306 Mini Company Information Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CompanyBankAccountTxt@1002 : TextConst 'ENU=CompanyBankAccount;ENG=CompanyBankAccount';
      XPAYMENTTxt@1001 : TextConst '@@@=Payment;ENU=PAYMENT;ENG=PAYMENT';
      XPmtRegTxt@1000 : TextConst '@@@=Payment Registration;ENU=PMT REG;ENG=PMT REG';

    LOCAL PROCEDURE UpdateGeneralJournalBatch@5(BankAccount@1000 : Record 270;JournalTemplateName@1001 : Code[10];JournalBatchName@1002 : Code[10]);
    VAR
      GenJournalBatch@1003 : Record 232;
    BEGIN
      WITH GenJournalBatch DO
        IF GET(JournalTemplateName,JournalBatchName) THEN
          IF ("Bal. Account Type" = "Bal. Account Type"::"Bank Account") AND ("Bal. Account No." = '') THEN BEGIN
            VALIDATE("Bal. Account No.",BankAccount."No.");
            MODIFY;
          END;
    END;

    PROCEDURE UpdateCompanyBankAccount@1(VAR CompanyInformation@1000 : Record 79;BankAccountPostingGroup@1002 : Code[10]);
    VAR
      BankAccount@1001 : Record 270;
    BEGIN
      // create or update existing company bank account with the information entered by the user
      // update general journal payment batches to point to the company bank account (unless a bank account is already specified in them)
      WITH CompanyInformation DO BEGIN
        IF (("Bank Branch No." = '') OR ("Bank Account No." = '')) AND (("SWIFT Code" = '') OR (IBAN = '')) THEN
          EXIT;
        UpdateBankAccount(BankAccount,CompanyInformation,BankAccountPostingGroup);
        UpdateGeneralJournalBatch(BankAccount,XPAYMENTTxt,XPmtRegTxt);
        UpdatePaymentRegistrationSetup(BankAccount,XPAYMENTTxt,XPmtRegTxt);
      END;
    END;

    LOCAL PROCEDURE UpdatePaymentRegistrationSetup@2(BankAccount@1000 : Record 270;JournalTemplateName@1001 : Code[10];JournalBatchName@1002 : Code[10]);
    VAR
      PaymentRegistrationSetup@1003 : Record 980;
    BEGIN
      IF NOT PaymentRegistrationSetup.GET(USERID) THEN BEGIN
        PaymentRegistrationSetup."User ID" := USERID;
        PaymentRegistrationSetup.INSERT;
      END;
      PaymentRegistrationSetup."Journal Template Name" := JournalTemplateName;
      PaymentRegistrationSetup."Journal Batch Name" := JournalBatchName;
      PaymentRegistrationSetup."Bal. Account Type" := PaymentRegistrationSetup."Bal. Account Type"::"Bank Account";
      PaymentRegistrationSetup."Bal. Account No." := BankAccount."No.";
      PaymentRegistrationSetup."Use this Account as Def." := TRUE;
      PaymentRegistrationSetup."Auto Fill Date Received" := TRUE;
      PaymentRegistrationSetup.MODIFY;
    END;

    LOCAL PROCEDURE UpdateBankAccount@3(VAR BankAccount@1001 : Record 270;CompanyInformation@1000 : Record 79;BankAccountPostingGroup@1002 : Code[10]);
    BEGIN
      WITH CompanyInformation DO BEGIN
        IF NOT BankAccount.GET(CompanyBankAccountTxt) THEN BEGIN
          BankAccount.INIT;
          BankAccount.VALIDATE("No.",CompanyBankAccountTxt);
          BankAccount.INSERT;
        END;
        BankAccount.VALIDATE(Name,"Bank Name");
        BankAccount.VALIDATE("Bank Branch No.","Bank Branch No.");
        BankAccount.VALIDATE("Bank Account No.","Bank Account No.");
        BankAccount.VALIDATE("SWIFT Code","SWIFT Code");
        BankAccount.VALIDATE(IBAN,IBAN);
        BankAccount.VALIDATE("Bank Acc. Posting Group",BankAccountPostingGroup);
        BankAccount.MODIFY;
      END;
    END;

    PROCEDURE GetCompanyBankAccountPostingGroup@4() : Code[10];
    VAR
      BankAccount@1000 : Record 270;
    BEGIN
      IF BankAccount.GET(CompanyBankAccountTxt) THEN
        EXIT(BankAccount."Bank Acc. Posting Group");
      EXIT('');
    END;

    BEGIN
    END.
  }
}

