OBJECT Codeunit 1300 Mini Sales-Calc Disc. By Type
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InvDiscBaseAmountIsZeroErr@1000 : TextConst 'ENU=There is no amount that you can apply an invoice discount to.;ENG=There is no amount that you can apply an invoice discount to.';

    PROCEDURE ApplyDefaultInvoiceDiscount@70(InvoiceDiscountAmount@1000 : Decimal;VAR SalesHeader@1001 : Record 36);
    BEGIN
      IF SalesHeader."Invoice Discount Calculation" = SalesHeader."Invoice Discount Calculation"::Amount THEN
        ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,SalesHeader)
      ELSE
        ApplyInvDiscBasedOnPct(SalesHeader);
    END;

    PROCEDURE ApplyInvDiscBasedOnAmt@60(InvoiceDiscountAmount@1000 : Decimal;VAR SalesHeader@1004 : Record 36);
    VAR
      TempVATAmountLine@1001 : TEMPORARY Record 290;
      SalesLine@1002 : Record 37;
      InvDiscBaseAmount@1003 : Decimal;
    BEGIN
      WITH SalesHeader DO BEGIN
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETRANGE("Document Type","Document Type");

        SalesLine.CalcVATAmountLines(0,SalesHeader,SalesLine,TempVATAmountLine);

        InvDiscBaseAmount := TempVATAmountLine.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");

        IF (InvDiscBaseAmount = 0) AND (InvoiceDiscountAmount > 0) THEN
          ERROR(InvDiscBaseAmountIsZeroErr);

        TempVATAmountLine.SetInvoiceDiscountAmount(InvoiceDiscountAmount,"Currency Code",
          "Prices Including VAT","VAT Base Discount %");

        SalesLine.UpdateVATOnLines(0,SalesHeader,SalesLine,TempVATAmountLine);

        "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
        "Invoice Discount Value" := InvoiceDiscountAmount;

        MODIFY;
      END;
    END;

    LOCAL PROCEDURE ApplyInvDiscBasedOnPct@73(VAR SalesHeader@1002 : Record 36);
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      WITH SalesHeader DO BEGIN
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETRANGE("Document Type","Document Type");
        IF SalesLine.FINDFIRST THEN BEGIN
          CODEUNIT.RUN(CODEUNIT::"Sales-Calc. Discount",SalesLine);
          GET("Document Type","No.");
        END;
      END;
    END;

    PROCEDURE GetCustInvoiceDiscountPct@64(SalesLine@1001 : Record 37) : Decimal;
    VAR
      SalesHeader@1000 : Record 36;
      CustInvDisc@1002 : Record 19;
    BEGIN
      WITH SalesHeader DO
        IF GET(SalesLine."Document Type",SalesLine."Document No.") THEN
          IF "Invoice Discount Calculation" = "Invoice Discount Calculation"::"%" THEN BEGIN
            // Only if CustInvDisc table is empty header is not updated
            CustInvDisc.SETRANGE(Code,"Invoice Disc. Code");
            IF NOT CustInvDisc.FINDFIRST THEN
              EXIT(0);

            EXIT("Invoice Discount Value");
          END;

      // We are returning zero because Discount percentage from customer is not used
      EXIT(0);
    END;

    BEGIN
    END.
  }
}

