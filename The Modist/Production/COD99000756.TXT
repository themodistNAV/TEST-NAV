OBJECT Codeunit 99000756 VersionManagement
{
  OBJECT-PROPERTIES
  {
    Date=22/12/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44365;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetBOMVersion@1(BOMHeaderNo@1000 : Code[20];Date@1001 : Date;OnlyCertified@1002 : Boolean) : Code[20];
    VAR
      ProdBOMVersion@1003 : Record 99000779;
    BEGIN
      ProdBOMVersion.SETCURRENTKEY("Production BOM No.","Starting Date");
      ProdBOMVersion.SETRANGE("Production BOM No.",BOMHeaderNo);
      ProdBOMVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF OnlyCertified THEN
        ProdBOMVersion.SETRANGE(Status,ProdBOMVersion.Status::Certified)
      ELSE
        ProdBOMVersion.SETFILTER(Status,'<>%1',ProdBOMVersion.Status::Closed);
      IF NOT ProdBOMVersion.FINDLAST THEN
        CLEAR(ProdBOMVersion);

      EXIT(ProdBOMVersion."Version Code");
    END;

    PROCEDURE GetBOMUnitOfMeasure@3(BOMHeaderNo@1000 : Code[20];VersionCode@1001 : Code[20]) : Code[10];
    VAR
      ProdBOMHeader@1002 : Record 99000771;
      ProdBOMVersion@1003 : Record 99000779;
    BEGIN
      IF BOMHeaderNo = ''THEN
        EXIT('');

      IF VersionCode = '' THEN BEGIN
        ProdBOMHeader.GET(BOMHeaderNo);
        EXIT(ProdBOMHeader."Unit of Measure Code");
      END;

      ProdBOMVersion.GET(BOMHeaderNo,VersionCode);
      EXIT(ProdBOMVersion."Unit of Measure Code");
    END;

    PROCEDURE GetRtngVersion@2(RoutingNo@1000 : Code[20];Date@1001 : Date;OnlyCertified@1002 : Boolean) : Code[20];
    VAR
      RtngVersion@1003 : Record 99000786;
    BEGIN
      RtngVersion.SETCURRENTKEY("Routing No.","Starting Date");
      RtngVersion.SETRANGE("Routing No.",RoutingNo);
      RtngVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF OnlyCertified THEN
        RtngVersion.SETRANGE(Status,RtngVersion.Status::Certified)
      ELSE
        RtngVersion.SETFILTER(Status,'<>%1',RtngVersion.Status::Closed);

      IF NOT RtngVersion.FINDLAST THEN
        CLEAR(RtngVersion);

      EXIT(RtngVersion."Version Code");
    END;

    BEGIN
    END.
  }
}

