OBJECT Codeunit 903 Release Assembly Document
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=900;
    Permissions=TableData 900=m,
                TableData 901=r;
    OnRun=VAR
            AssemblyLine@1001 : Record 901;
            InvtSetup@1000 : Record 313;
            WhseAssemblyRelease@1002 : Codeunit 904;
          BEGIN
            IF Status = Status::Released THEN
              EXIT;

            AssemblyLine.SETRANGE("Document Type","Document Type");
            AssemblyLine.SETRANGE("Document No.","No.");
            AssemblyLine.SETFILTER(Type,'<>%1',AssemblyLine.Type::" ");
            AssemblyLine.SETFILTER(Quantity,'<>0');
            IF NOT AssemblyLine.FIND('-') THEN
              ERROR(Text001,"Document Type","No.");
            InvtSetup.GET;
            IF InvtSetup."Location Mandatory" THEN BEGIN
              AssemblyLine.SETRANGE(Type,AssemblyLine.Type::Item);
              IF AssemblyLine.FINDSET THEN
                REPEAT
                  AssemblyLine.TESTFIELD("Location Code");
                UNTIL AssemblyLine.NEXT = 0;
            END;

            Status := Status::Released;
            MODIFY;

            IF "Document Type" = "Document Type"::Order THEN
              WhseAssemblyRelease.Release(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@="%1 = Document Type, %2 = No.";ENU=There is nothing to release for %1 %2.;ENG=There is nothing to release for %1 %2.';

    PROCEDURE Reopen@1(VAR AssemblyHeader@1000 : Record 900);
    VAR
      WhseAssemblyRelease@1001 : Codeunit 904;
    BEGIN
      WITH AssemblyHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        Status := Status::Open;
        MODIFY(TRUE);

        IF "Document Type" = "Document Type"::Order THEN
          WhseAssemblyRelease.Reopen(AssemblyHeader);
      END;
    END;

    BEGIN
    END.
  }
}

