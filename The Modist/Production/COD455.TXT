OBJECT Codeunit 455 Job Queue Restart User Session
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            JobQueueEntry@1000 : Record 472;
          BEGIN
            SetUserSessionFilter(JobQueueEntry);
            WITH JobQueueEntry DO BEGIN
              SETCURRENTKEY(Priority); // To avoid deadlock with the Job Queue Scheduler
              LOCKTABLE;
              IF FINDSET THEN
                REPEAT
                  IF NOT SessionExists THEN BEGIN
                    STARTSESSION("User Session ID",CODEUNIT::"Job Queue User Session",COMPANYNAME,JobQueueEntry);
                    "User Service Instance ID" := SERVICEINSTANCEID;
                    Status := Status::Ready;
                    MODIFY;
                    COMMIT;
                  END;
                UNTIL NEXT = 0;
            END;
          END;

  }
  CODE
  {

    PROCEDURE HasAnyJobQueueEntries@1() : Boolean;
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      SetUserSessionFilter(JobQueueEntry);
      EXIT(NOT JobQueueEntry.ISEMPTY);
    END;

    LOCAL PROCEDURE SetUserSessionFilter@2(VAR JobQueueEntry@1000 : Record 472);
    BEGIN
      JobQueueEntry.SETRANGE("User ID",USERID);
      JobQueueEntry.SETRANGE("Run in User Session",TRUE);
      JobQueueEntry.SETFILTER("Expiration Date/Time",'%1|>%2',0DT,CURRENTDATETIME);
    END;

    PROCEDURE CheckAndRestartUserSessions@3();
    VAR
      NewSessionID@1000 : Integer;
    BEGIN
      IF NOT HasAnyJobQueueEntries THEN
        EXIT;

      NewSessionID := 0;
      IF STARTSESSION(NewSessionID,CODEUNIT::"Job Queue Restart User Session") THEN;
    END;

    BEGIN
    END.
  }
}

