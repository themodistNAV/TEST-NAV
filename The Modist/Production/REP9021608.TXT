OBJECT Report 9021608 Build Also Bought
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:03 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Build Also Bought;
               ENG=Build Also Bought];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF DateStart = 0D THEN
                    ERROR(Text000);
                  IF DateEnd = 0D THEN
                    EndDate := TODAY
                  ELSE
                    EndDate := DateEnd;

                  IF EndDate < DateStart THEN
                    ERROR(Text003,DateStart,EndDate);

                  IF NOT CONFIRM(Text001,TRUE) THEN
                    ERROR(Text002);

                  AlsoBought.DELETEALL;
                END;

    OnPostReport=VAR
                   TxtL001@1000000000 : TextConst 'ENU=Reducing to No. of Also Bought;ENG=Reducing to No. of Also Bought';
                 BEGIN
                   IF QtyOfAlsoBoughts <> 0 THEN BEGIN
                     Window.OPEN(TxtL001);
                     AlsoBought.RESET;
                     AlsoBought.SETCURRENTKEY("Item No.","Quantity Bought");
                     ItemNo := '';
                     IF AlsoBought.FIND('+') THEN
                     REPEAT
                       IF AlsoBought."Item No." <> ItemNo THEN BEGIN
                         ItemNo := AlsoBought."Item No.";
                         Counted := 1;
                       END ELSE BEGIN
                         Counted := Counted + 1;
                       END;
                       IF Counted > QtyOfAlsoBoughts THEN BEGIN
                         AlsoBought1 := AlsoBought;
                         AlsoBought1.DELETE;
                       END;
                     UNTIL AlsoBought.NEXT(-1) = 0;
                     Window.CLOSE;
                   END;
                 END;

  }
  DATASET
  {
    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112;
               ReqFilterHeadingML=[ENU=5581;
                                   ENG=5581];
               OnPreDataItem=VAR
                               TxtL001@1000000000 : TextConst 'ENU=Analysing Sales Invoices;ENG=Analysing Sales Invoices';
                             BEGIN
                               "Sales Invoice Header".SETFILTER("Posting Date",'%1..%2',DateStart,EndDate);
                               SalesInvLine.RESET;
                               SalesInvLine1.RESET;
                               Window.OPEN(STRSUBSTNO(TxtL001, '#1', 20));
                             END;

               OnAfterGetRecord=BEGIN
                                  SalesInvLine.SETRANGE("Document No.","No.");
                                  SalesInvLine.SETRANGE(Type,SalesInvLine.Type::Item);
                                  SalesInvLine1.SETRANGE("Document No.","No.");
                                  SalesInvLine1.SETRANGE(Type,SalesInvLine.Type::Item);

                                  Window.UPDATE(1,"No.");
                                  IF SalesInvLine.FIND('-') THEN
                                  REPEAT
                                    SalesInvLine1.SETFILTER("No.",'<>%1',SalesInvLine."No.");
                                    IF SalesInvLine1.FIND('-') THEN
                                    REPEAT
                                      IF AlsoBought.GET(SalesInvLine."No.",SalesInvLine1."No.") THEN BEGIN
                                        AlsoBought."Quantity Bought" := AlsoBought."Quantity Bought" + SalesInvLine1."Quantity (Base)";
                                        AlsoBought.MODIFY;
                                      END ELSE BEGIN
                                        AlsoBought.INIT;
                                        AlsoBought."Item No." := SalesInvLine."No.";
                                        AlsoBought."Also Bought Item No." := SalesInvLine1."No.";
                                        AlsoBought."Quantity Bought" := SalesInvLine1."Quantity (Base)";
                                        AlsoBought.INSERT;
                                      END;
                                    UNTIL SalesInvLine1.NEXT = 0;
                                  UNTIL SalesInvLine.NEXT = 0;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  CaptionML=ENG=<Control1000000000>;
                  ContainerType=ContentArea }

      { 1000000001;1;Group  ;
                  GroupType=Group }

      { 1000000002;2;Field  ;
                  CaptionML=ENG=Start Date;
                  SourceExpr=DateStart }

      { 1000000003;2;Field  ;
                  CaptionML=ENG=End Date;
                  SourceExpr=DateEnd }

      { 1000000004;2;Field  ;
                  CaptionML=ENG=Quantity of Also Bought;
                  SourceExpr=QtyOfAlsoBoughts }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesInvLine@1190000005 : Record 113;
      SalesInvLine1@1190000011 : Record 113;
      AlsoBought@1190000006 : Record 9021721;
      AlsoBought1@1190000017 : Record 9021721;
      DateStart@1190000007 : Date;
      DateEnd@1190000008 : Date;
      EndDate@1190000010 : Date;
      QtyOfAlsoBoughts@1190000009 : Integer;
      Text000@1190000012 : TextConst 'ENU=You must enter a start date on the Options tab.;ENG=You must enter a start date on the Options tab.';
      Text001@1190000013 : TextConst 'ENU=This will delete and rebuild the also bought table and may take a long time.\\Are you sure you wish to continue?;ENG=This will delete and rebuild the also bought table and may take a long time.\\Are you sure you wish to continue?';
      Text002@1190000014 : TextConst 'ENU=Abandonned.;ENG=Abandonned.';
      Counted@1190000016 : Integer;
      ItemNo@1190000015 : Code[20];
      Window@1190000018 : Dialog;
      Text003@1190000019 : TextConst 'ENU=The Start Date %1 must be before the End Date %2.;ENG=The Start Date %1 must be before the End Date %2.';

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - Updated for Pages
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

