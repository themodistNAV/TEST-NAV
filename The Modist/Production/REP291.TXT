OBJECT Report 291 Delete Invd Blnkt Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=29/01/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44974;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invd Blnkt Sales Orders;
               ENG=Delete Invd Blnkt Sales Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Blanket Order));
               ReqFilterHeadingML=[ENU=Blanket Sales Order;
                                   ENG=Blanket Sales Order];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ATOLink@1002 : Record 904;
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  SalesLine.RESET;
                                  SalesLine.SETRANGE("Document Type","Document Type");
                                  SalesLine.SETRANGE("Document No.","No.");
                                  SalesLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF SalesLine.FINDFIRST THEN BEGIN
                                    SalesLine.SETRANGE("Quantity Invoiced");
                                    SalesLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT SalesLine.FINDFIRST THEN BEGIN
                                      SalesLine.SETRANGE("Outstanding Quantity");
                                      SalesLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                                      IF NOT SalesLine.FINDFIRST THEN BEGIN
                                        SalesLine.LOCKTABLE;
                                        IF NOT SalesLine.FINDFIRST THEN BEGIN
                                          SalesLine.SETRANGE("Qty. Shipped Not Invoiced");
                                          SalesLine2.SETRANGE("Blanket Order No.","No.");
                                          IF NOT SalesLine2.FINDFIRST THEN BEGIN
                                            SalesLine.SETFILTER("Qty. to Assemble to Order",'<>0');
                                            IF SalesLine.FINDSET THEN
                                              REPEAT
                                                ATOLink.DeleteAsmFromSalesLine(SalesLine);
                                              UNTIL SalesLine.NEXT = 0;
                                            SalesLine.SETRANGE("Qty. to Assemble to Order");

                                            SalesLine.DELETEALL;

                                            SalesCommentLine.SETRANGE("Document Type","Document Type");
                                            SalesCommentLine.SETRANGE("No.","No.");
                                            SalesCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntry(DATABASE::"Sales Header","Document Type","No.");

                                            DELETE;

                                            COMMIT;
                                          END;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing sales orders #1##########;ENG=Processing sales orders #1##########';
      SalesLine@1001 : Record 37;
      SalesLine2@1002 : Record 37;
      SalesCommentLine@1003 : Record 44;
      Window@1005 : Dialog;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

