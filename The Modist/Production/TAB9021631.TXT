OBJECT Table 9021631 Layout Component
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:02 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckUniqueRowCol;
             END;

    OnModify=BEGIN
               CheckUniqueRowCol;
             END;

    CaptionML=[ENU=Layout Component;
               ENG=Layout Component];
    LookupPageID=Page9021630;
    DrillDownPageID=Page9021630;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   Editable=No }
    { 3   ;   ;Template Page GroupGuID;Integer    ;TableRelation=ePage."Page GUID" WHERE (Label Code=FIELD(Label Code),
                                                                                          System Entry=CONST(Yes),
                                                                                          Template Page Entry=CONST(Yes));
                                                   OnLookup=VAR
                                                              PageGuID@1000000001 : Integer;
                                                              VersionCalls@1000000000 : Codeunit 9021662;
                                                            BEGIN
                                                              //CLEAR(TemplatePageList); //DOC EC2013.4.00
                                                              PageRec.RESET;
                                                              PageRec.SETRANGE("Label Code","Label Code");
                                                              IF "Template Page GroupGuID" <> 0 THEN
                                                                PageRec.SETRANGE("Page GUID","Template Page GroupGuID");
                                                              PageRec.SETRANGE("System Entry",TRUE);
                                                              PageRec.SETRANGE("Template Page Entry");

                                                              //DOC EC2013.4.00 -
                                                              //TemplatePageList.SETTABLEVIEW(PageRec);
                                                              //TemplatePageList.RUN;
                                                              IF PageRec.FINDSET THEN;
                                                              VersionCalls.ShowTemplatePage(PageRec);
                                                              //DOC EC2013.4.00 +
                                                            END;

                                                   CaptionML=[ENU=Template Page GroupGuID;
                                                              ENG=Template Page GroupGuID];
                                                   Editable=No }
    { 5   ;   ;PlaceHolderID       ;Text20        ;CaptionML=[ENU=PlaceHolderID;
                                                              ENG=PlaceHolderID];
                                                   NotBlank=Yes }
    { 8   ;   ;Row Index           ;Integer       ;OnValidate=BEGIN
                                                                ChangeRoworColumnIndex(0,"Row Index");
                                                              END;

                                                   CaptionML=[ENU=Row Index;
                                                              ENG=Row Index] }
    { 9   ;   ;Column Index        ;Integer       ;OnValidate=BEGIN
                                                                ChangeRoworColumnIndex(1,"Column Index");
                                                              END;

                                                   CaptionML=[ENU=Column Index;
                                                              ENG=Column Index] }
    { 10  ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                ERROR(Text001);  //DOC EC2013.3.00
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 11  ;   ;Template Page Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(ePage.Description WHERE (Label Code=FIELD(Label Code),
                                                                                               Page GUID=FIELD(Template Page GroupGuID)));
                                                   CaptionML=[ENU=Template Page Description;
                                                              ENG=Template Page Description];
                                                   Editable=No }
    { 15  ;   ;Usage Count         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Page Content" WHERE (Label Code=FIELD(Label Code),
                                                                                           Page GroupGuid=FIELD(Template Page GroupGuID),
                                                                                           PlaceholderID=FIELD(PlaceHolderID)));
                                                   CaptionML=[ENU=Usage Count;
                                                              ENG=Usage Count];
                                                   Editable=No }
    { 20  ;   ;% of Layout Height  ;Integer       ;CaptionML=[ENU=% of Layout Height;
                                                              ENG=% of Layout Height] }
    { 21  ;   ;% of Layout Width   ;Integer       ;CaptionML=[ENU=% of Layout Width;
                                                              ENG=% of Layout Width] }
    { 40  ;   ;Not Template        ;Boolean       ;CaptionML=[ENU=Not Template;
                                                              ENG=Not Template] }
  }
  KEYS
  {
    {    ;Label Code,Template Page GroupGuID,PlaceHolderID;
                                                   Clustered=Yes }
    {    ;Description                              }
    {    ;Label Code,Template Page GroupGuID,Row Index,Column Index }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LayoutCptRec@1000000000 : Record 9021631;
      Text000@1000000001 : TextConst 'ENU=The combined Label Code %1, Template Page GroupGuID %2, Row Index %3 and Column Index %4 must be unique and this combination already exists. (0);ENG=The combined Label Code %1, Template Page GroupGuID %2, Row Index %3 and Column Index %4 must be unique and this combination already exists. (0)';
      LayoutCptRec1@1000000006 : Record 9021631;
      LayoutCptRec2@1000000015 : Record 9021631;
      PageContentRec@1000000002 : Record 9021630;
      PageRec@1000000003 : Record 9021628;
      NoOfRows@1000000007 : Integer;
      NoOfColumns@1000000008 : Integer;
      RowIndex@1000000009 : Integer;
      ColumnIndex@1000000010 : Integer;
      TotalNoOfRows@1000000013 : Integer;
      TotalNoOfColumns@1000000014 : Integer;
      RowName@1000000011 : Text[20];
      ColumnName@1000000012 : Text[20];
      Text001@1000000016 : TextConst 'ENU=The Description is generated by code.;ENG=The Description is generated by code.';

    PROCEDURE CheckUniqueRowCol@1000000000();
    BEGIN
      LayoutCptRec.RESET;
      LayoutCptRec.SETRANGE("Label Code","Label Code");
      LayoutCptRec.SETRANGE("Template Page GroupGuID","Template Page GroupGuID");
      LayoutCptRec.SETRANGE("Row Index","Row Index");
      LayoutCptRec.SETRANGE("Column Index","Column Index");
      LayoutCptRec.SETFILTER(PlaceHolderID,'<>%1',PlaceHolderID);
      IF LayoutCptRec.FINDFIRST THEN
        ERROR(Text000,"Label Code","Template Page GroupGuID","Row Index","Column Index");
    END;

    PROCEDURE ChangeRoworColumnIndex@1000000001(RoworColumn@1000000000 : 'Row,Column';NewValue@1000000001 : Integer);
    BEGIN
      PageContentRec.RESET;
      PageContentRec.SETRANGE("Label Code","Label Code");
      PageContentRec.SETRANGE("Template Page GroupGuID","Template Page GroupGuID");
      PageContentRec.SETRANGE(PlaceholderID,PlaceHolderID);
      IF PageContentRec.FIND('-') THEN REPEAT
        IF RoworColumn = 0 THEN
          PageContentRec."Component Row Index" := NewValue
        ELSE
          PageContentRec."Component Column Index" := NewValue;
        PageContentRec.MODIFY;
      UNTIL PageContentRec.NEXT = 0;
    END;

    PROCEDURE GenerateDescription@1000000002();
    VAR
      TxtTopRow@1000000000 : TextConst 'ENU=Top Row;ENG=Top Row';
      TxtBottomRow@1000000001 : TextConst 'ENU=Bottom Row;ENG=Bottom Row';
      TxtMiddleRow@1000000002 : TextConst 'ENU=Middle Row;ENG=Middle Row';
      TxtLeftColumn@1000000003 : TextConst 'ENU=Left Column;ENG=Left Column';
      TxtRightColumn@1000000004 : TextConst 'ENU=Right Column;ENG=Right Column';
      TxtMiddleColumn@1000000005 : TextConst 'ENU=Middle Column;ENG=Middle Column';
    BEGIN
      LayoutCptRec.RESET;
      LayoutCptRec.SETRANGE("Label Code","Label Code");
      LayoutCptRec.SETRANGE("Template Page GroupGuID","Template Page GroupGuID");
      LayoutCptRec.SETCURRENTKEY("Label Code","Template Page GroupGuID","Row Index",
                                 "Column Index");

      LayoutCptRec1.RESET;
      LayoutCptRec1.SETRANGE("Label Code","Label Code");
      LayoutCptRec1.SETRANGE("Template Page GroupGuID","Template Page GroupGuID");
      LayoutCptRec1.SETCURRENTKEY("Label Code","Template Page GroupGuID","Row Index",
                                 "Column Index");
      RowIndex := 0;
      NoOfRows := 0;
      IF LayoutCptRec.FIND('-') THEN  //DOC EC2013.3.20 change to IF
      REPEAT
        IF LayoutCptRec."Row Index" <> RowIndex THEN BEGIN
          RowIndex := LayoutCptRec."Row Index";
          NoOfRows := NoOfRows + 1;
        END;
      UNTIL LayoutCptRec.NEXT = 0;
      TotalNoOfRows := NoOfRows;

      NoOfRows := 0;
      RowIndex := 0;
      IF LayoutCptRec.FIND('-') THEN  //DOC EC2013.3.20 change to IF
      REPEAT
        IF LayoutCptRec."Row Index" <> RowIndex THEN BEGIN
          RowIndex := LayoutCptRec."Row Index";
          NoOfRows := NoOfRows + 1;
          IF NoOfRows = 1 THEN BEGIN
            RowName := TxtTopRow;
          END ELSE BEGIN
            IF NoOfRows = TotalNoOfRows THEN BEGIN
              RowName := TxtBottomRow;
            END ELSE BEGIN
              IF TotalNoOfRows = 3 THEN
                RowName := TxtMiddleRow
              ELSE
                RowName := TxtMiddleRow + ' ' + FORMAT(NoOfRows - 1);
            END;
          END;
          LayoutCptRec1.SETRANGE("Row Index",LayoutCptRec."Row Index");
          TotalNoOfColumns := LayoutCptRec1.COUNT;
          ColumnIndex := 0;
          NoOfColumns := 0;
          LayoutCptRec1.FIND('-');
          REPEAT
            IF LayoutCptRec1."Column Index" <> ColumnIndex THEN BEGIN
              ColumnIndex := LayoutCptRec1."Column Index";
              NoOfColumns := NoOfColumns + 1;
              IF NoOfColumns = 1 THEN BEGIN
                IF TotalNoOfColumns = 1 THEN
                  ColumnName := ''
                ELSE
                  ColumnName := ', ' + TxtLeftColumn;
              END ELSE BEGIN
                IF NoOfColumns = TotalNoOfColumns THEN BEGIN
                  ColumnName := ', ' + TxtRightColumn;
                END ELSE BEGIN
                  IF TotalNoOfColumns = 3 THEN
                    ColumnName := ', ' + TxtMiddleColumn
                  ELSE
                    ColumnName := ', ' + TxtMiddleColumn + FORMAT(NoOfColumns - 1);
                END;
              END;
            END;
            LayoutCptRec2 := LayoutCptRec1;
            LayoutCptRec2.Description := RowName + ColumnName;
            LayoutCptRec2.MODIFY;
          UNTIL LayoutCptRec1.NEXT = 0;
        END;
      UNTIL LayoutCptRec.NEXT = 0;
    END;

    BEGIN
    {
      //DOC EC2013.3.20 MQ 02/11/2012 - GW121102 Add function to generate Description and new field Not Template
      //DOC EC2013.4.00 MQ 04/11/2013 - GW131104 RTC
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

