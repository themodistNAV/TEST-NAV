OBJECT Codeunit 9062634 PS Variable Store
{
  OBJECT-PROPERTIES
  {
    Date=02/11/15;
    Time=11:27:37 AM;
    Version List=NA2016.11;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempFileList@9062233 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.List`1";
      TempDictionary@9062232 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.Dictionary`2";
      GlobalDevModulePath@9062231 : Text;
      GlobalServerModulePath@9062230 : Text;

    PROCEDURE SetVariable@5(VariableKey@1000 : Text;VariableValue@1001 : Variant);
    BEGIN
      Initialize;

      ClearVariable(VariableKey);
      TempDictionary.Add(VariableKey,VariableValue);
    END;

    PROCEDURE GetVariable@6(VariableKey@1001 : Text;VAR VariableValue@1000 : Variant);
    VAR
      DictionaryValue@1002 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,DictionaryValue) THEN
        VariableValue := DictionaryValue;
    END;

    PROCEDURE GetTextVariable@1007(VariableKey@1001 : Text) TextValue : Text;
    VAR
      VariableValue@1000 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,VariableValue) THEN
        TextValue := VariableValue;
    END;

    PROCEDURE GetIntegerVariable@1006(VariableKey@1001 : Text) IntegerValue : Integer;
    VAR
      VariableValue@1000 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,VariableValue) THEN
        IntegerValue := VariableValue;
    END;

    PROCEDURE GetDecimalVariable@1005(VariableKey@1001 : Text) DecimalValue : Decimal;
    VAR
      VariableValue@1000 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,VariableValue) THEN
        DecimalValue := VariableValue;
    END;

    PROCEDURE GetDateVariable@1004(VariableKey@1001 : Text) DateValue : Date;
    VAR
      VariableValue@1000 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,VariableValue) THEN
        DateValue := VariableValue;
    END;

    PROCEDURE GetBooleanVariable@8(VariableKey@1001 : Text) BoolValue : Boolean;
    VAR
      VariableValue@1000 : Variant;
    BEGIN
      Initialize;

      IF TempDictionary.TryGetValue(VariableKey,VariableValue) THEN
        BoolValue := VariableValue;
    END;

    LOCAL PROCEDURE ClearVariable@7(VariableKey@1000 : Text);
    BEGIN
      TempDictionary.Remove(VariableKey);
    END;

    PROCEDURE AddTempFileName@2(TempFileName@1000 : Text) : Text;
    BEGIN
      Initialize;
      TempFileList.Add(TempFileName);
    END;

    PROCEDURE GetTempFileName@1() : Text;
    BEGIN
      Initialize;
      IF TempFileList.Count > 0 THEN
        EXIT(TempFileList.Item(0));
    END;

    PROCEDURE DeleteTempFiles@3();
    VAR
      TempFileName@1000 : Text;
      Loop@1001 : Integer;
    BEGIN
      Initialize;

      FOR Loop := 1 TO TempFileList.Count DO BEGIN
        TempFileName := TempFileList.Item(Loop - 1);
        IF EXISTS(TempFileName) THEN
          ERASE(TempFileName);
      END;
      CLEAR(TempFileList);
    END;

    LOCAL PROCEDURE Initialize@4();
    BEGIN
      IF ISNULL(TempFileList) THEN
        TempFileList := TempFileList.List;

      IF ISNULL(TempDictionary) THEN
        TempDictionary := TempDictionary.Dictionary;
    END;

    LOCAL PROCEDURE ClientModulePath@1000() : Text;
    VAR
      ClientAssembly@1100408001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Reflection.Assembly" RUNONCLIENT;
      ClientPath@1100408000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Path" RUNONCLIENT;
    BEGIN
      ClientAssembly := ClientAssembly.GetExecutingAssembly;
      EXIT(ClientPath.Combine(ClientPath.GetDirectoryName(ClientAssembly.Location),'NavModelTools.ps1'));
    END;

    LOCAL PROCEDURE ServerModulePath@1001() : Text;
    VAR
      PathHelper@1000 : DotNet "'mscorlib'.System.IO.Path";
    BEGIN
      EXIT(PathHelper.Combine(APPLICATIONPATH,'NavAdminTool.ps1'));
    END;

    PROCEDURE GetServerModulePath@1002() : Text;
    BEGIN
      IF GlobalServerModulePath <> '' THEN
        EXIT(GlobalServerModulePath);
      GlobalServerModulePath := ServerModulePath;
      EXIT(GlobalServerModulePath);
    END;

    PROCEDURE GetDevModulePath@1003() : Text;
    BEGIN
      IF GlobalDevModulePath <> '' THEN
        EXIT(GlobalDevModulePath);
      GlobalDevModulePath := ClientModulePath;
      EXIT(GlobalDevModulePath);
    END;

    BEGIN
    {
      //DOC NH2016.10 AP 29/10/2015 - Created
                        //DOC NH2016.11 JH 17/11/2015 - New version
    }
    END.
  }
}

