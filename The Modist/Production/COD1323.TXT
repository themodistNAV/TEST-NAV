OBJECT Codeunit 1323 Cancel PstdSalesInv (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    TableNo=112;
    Permissions=TableData 112=rm,
                TableData 114=rm;
    OnRun=BEGIN
            CancelInvoice(Rec);
          END;

  }
  CODE
  {
    VAR
      CancelPostedInvoiceQst@1000 : TextConst 'ENU=The posted sales invoice will be canceled, and a sales credit memo will be created and posted, which reverses the posted sales invoice.\ \Do you want to continue?;ENG=The posted sales invoice will be cancelled, and a sales credit memo will be created and posted, which reverses the posted sales invoice.\ \Do you want to continue?';
      OpenPostedCreditMemoQst@1001 : TextConst 'ENU=A credit memo was successfully created. Do you want to open the posted credit memo?;ENG=A credit memo was successfully created. Do you want to open the posted credit memo?';
      CancelIfContainingJobQst@1004 : TextConst 'ENU=The sales invoice contains lines that are related to a job. The job information on these lines will not be transferred to the sales credit memo that is created when you choose Cancel.\\To ensure that job information is available on a sales credit memo, use the Create Sales Credit Memo action on the job planning line.\\;ENG=The sales invoice contains lines that are related to a job. The job information on these lines will not be transferred to the sales credit memo that is created when you choose Cancel.\\To ensure that job information is available on a sales credit memo, use the Create Sales Credit Memo action on the job planning line.\\';

    PROCEDURE CancelInvoice@1(VAR SalesInvoiceHeader@1002 : Record 112) : Boolean;
    VAR
      SalesCrMemoHeader@1004 : Record 114;
      CorrectPostedSalesInvoice@1003 : Codeunit 1303;
    BEGIN
      CorrectPostedSalesInvoice.TestCorrectInvoiceIsAllowed(SalesInvoiceHeader,TRUE);
      IF CONFIRM(CreateConfirmQuestion(SalesInvoiceHeader)) THEN
        IF CorrectPostedSalesInvoice.CancelPostedInvoice(SalesInvoiceHeader) THEN
          IF CONFIRM(OpenPostedCreditMemoQst) THEN BEGIN
            SalesCrMemoHeader.GET(SalesInvoiceHeader."Canceled By");
            PAGE.RUN(PAGE::"Mini Posted Sales Credit Memo",SalesCrMemoHeader);
            EXIT(TRUE);
          END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CreateConfirmQuestion@2(SalesInvoiceHeader@1005 : Record 112) : Text;
    BEGIN
      IF SalesLinesContainsJob(SalesInvoiceHeader) THEN
        EXIT(CancelIfContainingJobQst + CancelPostedInvoiceQst);

      EXIT(CancelPostedInvoiceQst);
    END;

    LOCAL PROCEDURE SalesLinesContainsJob@30(SalesInvoiceHeader@1001 : Record 112) : Boolean;
    VAR
      SalesInvoiceLine@1002 : Record 113;
    BEGIN
      SalesInvoiceLine.SETRANGE("Document No.",SalesInvoiceHeader."No.");
      IF SalesInvoiceLine.FIND('-') THEN
        REPEAT
          IF SalesInvoiceLine."Job No." <> '' THEN
            EXIT(TRUE);
        UNTIL SalesInvoiceLine.NEXT = 0;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

