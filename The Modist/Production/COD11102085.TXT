OBJECT Codeunit 11102085 OM - Administration Tasks
{
  OBJECT-PROPERTIES
  {
    Date=01/04/14;
    Time=12:00:00 PM;
    Version List=OMA9.02;
  }
  PROPERTIES
  {
    TableNo=11102056;
    OnRun=VAR
            No@1000 : Integer;
            ctConfirm@1001 : TextConst 'DEU=Task ''%1'' will be executed.\Are you sure?;ENU=Task ''%1'' will be executed.\Are you sure?;ESP=Task ''%1'' will be executed.\Are you sure?;FRA=Task ''%1'' will be executed.\Are you sure?;NLD=Taak ''%1'' zal worden uitgevoegd.\Weet u het zeker?;DEA=Task ''%1'' will be executed.\Are you sure?';
          BEGIN
            IF NOT CONFIRM(ctConfirm, TRUE, Name) THEN
              ERROR('');

            EVALUATE(No, Code);

            CASE No OF
              TaskType::Remove : RemoveObjectManager;
              TaskType::"Check Settings" : CheckSettings;
              TaskType::"Check CAL" : CheckCALHistory;
              TaskType::"Delete WU" : DeleteAllWhereUsedData;
              TaskType::"Add Trace Trigger" : AddTraceSQLTrigger;
              TaskType::"Remove Trace Trigger" : RemoveTraceSQLTrigger;
              TaskType::"Add Ask Trigger" : AddAskSQLTrigger;
              TaskType::"Remove Ask Trigger" : RemoveAskSQLTrigger;
              TaskType::"Add DB Conv" : AddDbConvTrigger;
              TaskType::"Remove DB Conv" : RemoveDbConvTrigger;
              TaskType::"Empty Deleted Fields" : EmptyDeletedFieldsAndTables;
            END;
          END;

  }
  CODE
  {
    VAR
      TaskType@1000 : ' ,Remove,Check Settings,Check CAL,Delete WU,Add Trace Trigger,Remove Trace Trigger,Add Ask Trigger,Remove Ask Trigger,Add DB Conv,Remove DB Conv,Empty Deleted Fields';

    PROCEDURE AddTasks@1(VAR Task@1000 : Record 11102056);
    VAR
      ctAddAskSQLTrigger@1001 : TextConst 'DEU=Add Ask SQL trigger;ENU=Add Ask SQL trigger;ESP=Add Ask SQL trigger;FRA=Add Ask SQL trigger;NLD=Ask SQL trigger toevoegen;DEA=Add Ask SQL trigger';
      ctAddDbConvSQLTrigger@1002 : TextConst 'DEU=Add Block Database Conversion trigger;ENU=Add Block Database Conversion trigger;ESP=Add Block Database Conversion trigger;FRA=Add Block Database Conversion trigger;NLD=Blokkeer-database-converteer-trigger toevoegen;DEA=Add Block Database Conversion trigger';
      ctAddTraceTrigger@1003 : TextConst 'DEU=Add Trace Modification trigger;ENU=Add Trace Modification trigger;ESP=Add Trace Modification trigger;FRA=Add Trace Modification trigger;NLD=Wijzigingentraceertrigger toevoegen;DEA=Add Trace Modification trigger';
      ctCheckCALHistory@1004 : TextConst 'DEU=Check C/AL History;ENU=Check C/AL History;ESP=Check C/AL History;FRA=Check C/AL History;NLD=C/AL historie controleren;DEA=Check C/AL History';
      ctCheckSettings@1005 : TextConst 'DEU=Check Settings;ENU=Check Settings;ESP=Check Settings;FRA=Check Settings;NLD=Instellingen controleren;DEA=Check Settings';
      ctDeleteAllWhereUsedData@1006 : TextConst 'DEU=Delete all where used data;ENU=Delete all where used data;ESP=Delete all where used data;FRA=Delete all where used data;NLD=Waar-gebruikt data verwijderen;DEA=Delete all where used data';
      ctEmptyDeletedFieldsAndTables@1007 : TextConst 'DEU=Empty deleted fields and tables;ENU=Empty deleted fields and tables;ESP=Empty deleted fields and tables;FRA=Empty deleted fields and tables;NLD=Verwijderde velden en tabellen leegmaken;DEA=Empty deleted fields and tables';
      ctRemoveAskSQLTrigger@1008 : TextConst 'DEU=Remove Ask SQL trigger;ENU=Remove Ask SQL trigger;ESP=Remove Ask SQL trigger;FRA=Remove Ask SQL trigger;NLD=Ask SQL trigger verwijderen;DEA=Remove Ask SQL trigger';
      ctRemoveDbConvTrigger@1009 : TextConst 'DEU=Remove Block Database Conversion trigger;ENU=Remove Block Database Conversion trigger;ESP=Remove Block Database Conversion trigger;FRA=Remove Block Database Conversion trigger;NLD=Blokkeer-database-converteer-trigger verwijderen;DEA=Remove Block Database Conversion trigger';
      ctRemoveObjectManager@1010 : TextConst 'DEU=Remove Object Manager from database;ENU=Remove Object Manager from database;ESP=Remove Object Manager from database;FRA=Remove Object Manager from database;NLD=De Object Manager uit de database verwijderen;DEA=Remove Object Manager from database';
      ctRemoveTraceTrigger@1011 : TextConst 'DEU=Remove Trace Modification trigger;ENU=Remove Trace Modification trigger;ESP=Remove Trace Modification trigger;FRA=Remove Trace Modification trigger;NLD=Wijzigingentraceertrigger verwijderen;DEA=Remove Trace Modification trigger';
    BEGIN
      AddTask(Task, TaskType::Remove, ctRemoveObjectManager);
      AddTask(Task, TaskType::"Check Settings", ctCheckSettings);
      AddTask(Task, TaskType::"Check CAL", ctCheckCALHistory);
      AddTask(Task, TaskType::"Delete WU", ctDeleteAllWhereUsedData);
      AddTask(Task, TaskType::"Add Trace Trigger", ctAddTraceTrigger);
      AddTask(Task, TaskType::"Remove Trace Trigger", ctRemoveTraceTrigger);
      AddTask(Task, TaskType::"Add Ask Trigger", ctAddAskSQLTrigger);
      AddTask(Task, TaskType::"Remove Ask Trigger", ctRemoveAskSQLTrigger);
      AddTask(Task, TaskType::"Add DB Conv", ctAddDbConvSQLTrigger);
      AddTask(Task, TaskType::"Remove DB Conv", ctRemoveDbConvTrigger);
      AddTask(Task, TaskType::"Empty Deleted Fields", ctEmptyDeletedFieldsAndTables);
    END;

    LOCAL PROCEDURE AddTask@2(VAR Task@1000 : Record 11102056;Number@1001 : Integer;Description@1002 : Text);
    BEGIN
      Task.Code := COPYSTR('00' + FORMAT(Number), STRLEN(FORMAT(Number)));
      Task.Name := Description;
      Task.INSERT;
    END;

    PROCEDURE RemoveObjectManager@3();
    VAR
      TmpAction@1000 : TEMPORARY Record 11102048;
      TmpActionField@1001 : TEMPORARY Record 11102049;
      Object@1002 : Record 2000000001;
      ActionMgt@1003 : Codeunit 11102038;
      SQLMgt@1004 : Codeunit 11102075;
      ctAllDataIsRemoved@1005 : TextConst 'DEU=All data is removed from the Object Manager tables.\Now you can delete the objects in the Development Environment.;ENU=All data is removed from the Object Manager tables.\Now you can delete the objects in the Development Environment.;ESP=All data is removed from the Object Manager tables.\Now you can delete the objects in the Development Environment.;FRA=All data is removed from the Object Manager tables.\Now you can delete the objects in the Development Environment.;NLD=Alle data is verwijderd uit de Object Manager tabellen.\Nu kunt u de objecten verwijderen in de Development Environment.;DEA=All data is removed from the Object Manager tables.\Now you can delete the objects in the Development Environment.';
      ctAreYouReallySure@1006 : TextConst 'DEU=Absolutely sure?;ENU=Absolutely sure?;ESP=Absolutely sure?;FRA=Absolutely sure?;NLD=Echt zeker weten?;DEA=Absolutely sure?';
      ctRemoveSQLTrigger@1007 : TextConst 'DEU=Please remove the sql trigger to track object modifications from the object table.;ENU=Please remove the sql trigger to track object modifications from the object table.;ESP=Please remove the sql trigger to track object modifications from the object table.;FRA=Please remove the sql trigger to track object modifications from the object table.;NLD=De SQL trigger om objectwijzigingen te traceren moet van de object tabel worden verwijderd.;DEA=Please remove the sql trigger to track object modifications from the object table.';
      ctTryRemoveSQLTrigger@1008 : TextConst 'DEU=The sql trigger to track object modifications is still present.\Do you want to remove it?;ENU=The sql trigger to track object modifications is still present.\Do you want to remove it?;ESP=The sql trigger to track object modifications is still present.\Do you want to remove it?;FRA=The sql trigger to track object modifications is still present.\Do you want to remove it?;NLD=De SQL Trigger om objectwijzigingen te traceren is nog aanwezig.\Wilt u deze verwijderen?;DEA=The sql trigger to track object modifications is still present.\Do you want to remove it?';
    BEGIN
      IF NOT CONFIRM(ctAreYouReallySure, TRUE) THEN
        ERROR('');

      IF SQLMgt.RUN THEN BEGIN
        IF SQLMgt.GetTraceTriggerStatus IN [3, 4] THEN
          IF CONFIRM(ctTryRemoveSQLTrigger, TRUE) THEN
            SQLMgt.RemoveTraceTrigger(TRUE);
        IF SQLMgt.GetTraceTriggerStatus IN [3, 4] THEN
          IF CONFIRM(ctTryRemoveSQLTrigger, TRUE) THEN
            ERROR(ctRemoveSQLTrigger);
      END;

      Object.SETRANGE(Type, Object.Type::Table);
      Object.SETFILTER(Name, 'OM - *');
      IF Object.FINDSET THEN
        REPEAT
          TmpAction."Line No." := Object.ID;
          TmpAction."Action Type" := TmpAction."Action Type"::"Delete Data";
          TmpAction."Object No." := Object.ID;
          TmpAction.INSERT;
        UNTIL Object.NEXT = 0;

      ActionMgt.SetSkipConfirm(TRUE);
      ActionMgt.Start(TmpAction, TmpActionField);

      //Object.SETFILTER(Type, '<>%1', Object.Type::TableData);
      //Object.DELETEALL;

      MESSAGE(ctAllDataIsRemoved);
    END;

    PROCEDURE CheckSettings@11();
    VAR
      TestSettings@1000 : Codeunit 11102064;
    BEGIN
      TestSettings.RUN;
    END;

    PROCEDURE CheckCALHistory@4();
    VAR
      Object@1000 : Record 2000000001;
      CheckCALHistory@1001 : Codeunit 11102063;
    BEGIN
      Object.SETFILTER(Type, '<>%1', Object.Type::TableData);
      CheckCALHistory.RunWithObjectRange(Object);
    END;

    PROCEDURE DeleteAllWhereUsedData@5();
    VAR
      WhereUsedObject@1000 : Record 11102057;
      Progressbar@1001 : Codeunit 11102044;
      ctDeleting@1002 : TextConst 'DEU=Deleting where used data...;ENU=Deleting where used data...;ESP=Deleting where used data...;FRA=Deleting where used data...;NLD=Waar-gebruikt data verwijderen...;DEA=Deleting where used data...';
    BEGIN
      Progressbar.Open(ctDeleting, 0);
      Progressbar.BarSize := WhereUsedObject.COUNT;
      IF WhereUsedObject.FINDSET THEN
        REPEAT
          Progressbar.AddToBar(1);
          WhereUsedObject.DELETE(TRUE);
        UNTIL WhereUsedObject.NEXT = 0;
      Progressbar.Close;
    END;

    PROCEDURE AddTraceSQLTrigger@6();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctTriggerAlreadyInstalled@1001 : TextConst 'DEU=The trigger is already installed.;ENU=The trigger is already installed.;ESP=The trigger is already installed.;FRA=The trigger is already installed.;NLD=De trigger is reeds aanwezig.;DEA=The trigger is already installed.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetTraceTriggerStatus = 3 THEN
        ERROR(ctTriggerAlreadyInstalled);

      SQLMgt.AddTraceTrigger(TRUE);
    END;

    PROCEDURE RemoveTraceSQLTrigger@7();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctNoTriggerPresent@1001 : TextConst 'DEU=No trigger present.;ENU=No trigger present.;ESP=No trigger present.;FRA=No trigger present.;NLD=Geen trigger aanwezig.;DEA=No trigger present.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetTraceTriggerStatus = 1 THEN
        ERROR(ctNoTriggerPresent);

      SQLMgt.RemoveTraceTrigger(TRUE);
    END;

    PROCEDURE AddAskSQLTrigger@8();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctTriggerAlreadyInstalled@1001 : TextConst 'DEU=The trigger is already installed.;ENU=The trigger is already installed.;ESP=The trigger is already installed.;FRA=The trigger is already installed.;NLD=De trigger is reeds aanwezig.;DEA=The trigger is already installed.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetAskSqlTriggerStatus = 3 THEN
        ERROR(ctTriggerAlreadyInstalled);

      SQLMgt.AddAskSqlTrigger(TRUE);
    END;

    PROCEDURE RemoveAskSQLTrigger@9();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctTriggerNotFound@1001 : TextConst 'DEU=Trigger not found.;ENU=Trigger not found.;ESP=Trigger not found.;FRA=Trigger not found.;NLD=Trigger niet gevonden.;DEA=Trigger not found.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetAskSqlTriggerStatus = 1 THEN
        ERROR(ctTriggerNotFound);

      SQLMgt.RemoveAskSqlTrigger(TRUE);
    END;

    PROCEDURE AddDbConvTrigger@13();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctTriggerAlreadyInstalled@1001 : TextConst 'DEU=The trigger is already installed.;ENU=The trigger is already installed.;ESP=The trigger is already installed.;FRA=The trigger is already installed.;NLD=De trigger is reeds aanwezig.;DEA=The trigger is already installed.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetDbConvTriggerStatus = 3 THEN
        ERROR(ctTriggerAlreadyInstalled);

      SQLMgt.AddDbConvTrigger(TRUE);
    END;

    PROCEDURE RemoveDbConvTrigger@12();
    VAR
      SQLMgt@1000 : Codeunit 11102075;
      ctTriggerNotFound@1001 : TextConst 'DEU=Trigger not found.;ENU=Trigger not found.;ESP=Trigger not found.;FRA=Trigger not found.;NLD=Trigger niet gevonden.;DEA=Trigger not found.';
    BEGIN
      SQLMgt.RUN;
      IF SQLMgt.GetDbConvTriggerStatus = 1 THEN
        ERROR(ctTriggerNotFound);

      SQLMgt.RemoveDbConvTrigger(TRUE);
    END;

    PROCEDURE EmptyDeletedFieldsAndTables@10();
    VAR
      Initialize@1000 : Codeunit 11102041;
    BEGIN
      Initialize.EmptyDeletedFieldsAndTables;
    END;

    BEGIN
    END.
  }
}

