OBJECT Codeunit 9652 Edit RDLC Report Layout
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=9650;
    OnRun=BEGIN
            EditReportLayout(Rec);
          END;

  }
  CODE
  {
    VAR
      LoadDocQst@1000 : TextConst 'ENU=The report layout has been opened in SQL Report Builder.\\Edit the report layout in SQL Report Builder and save the changes. Then return to this message and choose Yes to import the changes or No to cancel the changes.\Do you want to import the changes?;ENG=The report layout has been opened in SQL Report Builder.\\Edit the report layout in SQL Report Builder and save the changes. Then return to this message and choose Yes to import the changes or No to cancel the changes.\Do you want to import the changes?';
      NoReportBuilderPresentErr@1001 : TextConst 'ENU=Microsoft Report Builder is not installed on this computer.;ENG=Microsoft Report Builder is not installed on this computer.';

    LOCAL PROCEDURE EditReportLayout@4(VAR CustomReportLayout@1015 : Record 9650);
    VAR
      TempBlob@1009 : Record 99008535;
      FileMgt@1005 : Codeunit 419;
      Process@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Diagnostics.Process" RUNONCLIENT;
      FileName@1003 : Text;
      RBFileName@1000 : Text;
      LoadModifiedDoc@1002 : Boolean;
    BEGIN
      CustomReportLayout.CALCFIELDS(Layout);
      CustomReportLayout.TESTFIELD(Layout);

      RBFileName := GetReportBuilderExe;
      IF RBFileName = '' THEN
        ERROR(NoReportBuilderPresentErr);

      TempBlob.INIT;
      TempBlob.Blob := CustomReportLayout.Layout;

      FileName := FileMgt.BLOBExport(TempBlob,'report.rdl',FALSE);

      Process := Process.Start(RBFileName,'"' + FileName + '"');

      LoadModifiedDoc := CONFIRM(LoadDocQst);

      IF LoadModifiedDoc THEN BEGIN
        FileMgt.BLOBImport(TempBlob,FileName);
        CustomReportLayout.ImportLayoutBlob(TempBlob,'');
      END;

      IF NOT Process.HasExited THEN
        Process.CloseMainWindow;

      FileMgt.DeleteClientFile(FileName);
    END;

    LOCAL PROCEDURE GetReportBuilderExe@1() : Text;
    VAR
      Registry@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.Registry" RUNONCLIENT;
      FileName@1002 : Text;
      i@1001 : Integer;
    BEGIN
      FileName := Registry.GetValue('HKEY_CLASSES_ROOT\MSReportBuilder_ReportFile_32\shell\Open\command','','');
      i := STRPOS(UPPERCASE(FileName),'.EXE');
      IF i > 0 THEN
        FileName := DELCHR(PADSTR(FileName,i + 3),'>')
      ELSE
        FileName := '';

      EXIT(FileName);
    END;

    BEGIN
    END.
  }
}

