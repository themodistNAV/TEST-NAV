OBJECT Table 9021634 Content Type Attribute
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:03 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               PageContentRec.RESET;
               PageContentRec.SETRANGE("Content Type Code","Content Type Code");
               IF PageContentRec.FIND('-') THEN
               REPEAT
                 PageContentValueRec.INIT;
                 PageContentValueRec."Label Code" := PageContentRec."Label Code";
                 PageContentValueRec."Page GroupGuID" := PageContentRec."Page GroupGuid";
                 PageContentValueRec.PlaceholderID := PageContentRec.PlaceholderID;
                 PageContentValueRec."Content Line No." := PageContentRec."Line No.";
                 PageContentValueRec."Content Type Code" := "Content Type Code";
                 PageContentValueRec.Attribute := Attribute;
                 PageContentValueRec."Html Allowed" := "Html Allowed";
                 PageContentValueRec."Shared Html" := "Shared Html";
                 PageContentValueRec.Required := Required;
                 PageContentValueRec."Is Master Data"  := PageContentRec."Is Master Data";
                 PageContentValueRec.INSERT;
               UNTIL PageContentRec.NEXT = 0;
             END;

    OnDelete=BEGIN
               PageContentValueRec.RESET;
               PageContentValueRec.SETRANGE("Content Type Code","Content Type Code");
               PageContentValueRec.SETRANGE(Attribute,Attribute);
               IF PageContentValueRec.FINDFIRST THEN BEGIN
                 IF CONFIRM(Text001,FALSE,"Content Type Code",Attribute) THEN
                     PageContentValueRec.DELETEALL
                  ELSE
                    ERROR(Text002);
               END;
             END;

    CaptionML=[ENU=Content Type Attribute;
               ENG=Content Type Attribute];
    LookupPageID=Page9021656;
    DrillDownPageID=Page9021656;
  }
  FIELDS
  {
    { 1   ;   ;Content Type Code   ;Code20        ;TableRelation="Content Type";
                                                   CaptionML=[ENU=Content Type Code;
                                                              ENG=Content Type Code] }
    { 10  ;   ;Attribute           ;Text100       ;CaptionML=[ENU=Attribute;
                                                              ENG=Attribute];
                                                   NotBlank=Yes }
    { 15  ;   ;Validate-As Type    ;Option        ;CaptionML=[ENU=Validate-As Type;
                                                              ENG=Validate-As Type];
                                                   OptionCaptionML=[ENU=" ,Boolean,Date,Decimal,Integer,Time";
                                                                    ENG=" ,Boolean,Date,Decimal,Integer,Time"];
                                                   OptionString=[ ,Boolean,Date,Decimal,Integer,Time];
                                                   Description=MA20110424 }
    { 17  ;   ;Value Is Mandatory  ;Boolean       ;CaptionML=[ENU=Value Is Mandatory;
                                                              ENG=Value Is Mandatory];
                                                   Description=MA20110424 }
    { 20  ;   ;Html Allowed        ;Boolean       ;OnValidate=BEGIN
                                                                PageContentValueRec.RESET;
                                                                PageContentValueRec.SETRANGE("Content Type Code","Content Type Code");
                                                                PageContentValueRec.SETRANGE(Attribute,Attribute);
                                                                IF PageContentValueRec.FIND('-') THEN
                                                                REPEAT
                                                                  PageContentValueRec."Html Allowed" := "Html Allowed";
                                                                  PageContentValueRec.MODIFY;
                                                                UNTIL PageContentValueRec.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Html Allowed;
                                                              ENG=Html Allowed] }
    { 21  ;   ;Shared Html         ;Boolean       ;CaptionML=[ENU=Shared Html;
                                                              ENG=Shared Html] }
    { 23  ;   ;Advanced Feature    ;Boolean       ;CaptionML=[ENU=Advanced Feature;
                                                              ENG=Advanced Feature] }
    { 30  ;   ;Default Value       ;Text250       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF ("Default Value" <> '') AND
                                                                  ("Multilanguage Allowed" = TRUE) THEN
                                                                  MESSAGE(Text005);
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Default Value;
                                                              ENG=Default Value] }
    { 40  ;   ;Required            ;Boolean       ;OnValidate=BEGIN
                                                                PageContentValueRec.RESET;
                                                                PageContentValueRec.SETRANGE("Content Type Code","Content Type Code");
                                                                PageContentValueRec.SETRANGE(Attribute,Attribute);
                                                                IF PageContentValueRec.FIND('-') THEN
                                                                REPEAT
                                                                  PageContentValueRec.Required := Required;
                                                                  PageContentValueRec.MODIFY;
                                                                UNTIL PageContentValueRec.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Required;
                                                              ENG=Required] }
    { 50  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 80  ;   ;Multilanguage Key   ;Code80        ;OnValidate=BEGIN
                                                                IF "Multilanguage Allowed" = FALSE THEN
                                                                  ERROR(Text004,FIELDCAPTION("Multilanguage Allowed"));
                                                                CalcMultiLanguageLabelCode;
                                                                IF MultiLanguageLabelCode = '' THEN BEGIN
                                                                  IF "Multilanguage Key" <> '' THEN
                                                                    ERROR(Text003,FIELDCAPTION("Multilanguage Key"));
                                                                END ELSE BEGIN
                                                                  IF "Multilanguage Key" = '?' THEN
                                                                    "Multilanguage Key" :=
                                                                      MultiLangFunc.CreateKey(9021634,MultiLanguageLabelCode,
                                                                      (FORMAT("Content Type Code") + '_' + Attribute),0);
                                                                END;
                                                                IF "Multilanguage Key" <> '' THEN
                                                                  MultiLangFunc.UpdateMultiLang("Multilanguage Key",MultiLanguageLabelCode);
                                                              END;

                                                   OnLookup=BEGIN
                                                              CalcMultiLanguageLabelCode;
                                                              IF MultiLanguageLabelCode = '' THEN
                                                                ERROR(Text003,FIELDCAPTION("Multilanguage Key"));
                                                              VALIDATE("Multilanguage Key",
                                                                       MultiLangFunc.ReturnResxName("Multilanguage Key",MultiLanguageLabelCode));
                                                            END;

                                                   CaptionML=[ENU=Multilanguage Key;
                                                              ENG=Multilanguage Key];
                                                   Description=GW add 120705 }
    { 81  ;   ;Multilanguage Allowed;Boolean      ;CaptionML=[ENU=Multilanguage Allowed;
                                                              ENG=Multilanguage Allowed];
                                                   Description=GW add 120705 }
  }
  KEYS
  {
    {    ;Content Type Code,Attribute             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PageContentValueRec@1040000 : Record 9021635;
      Text001@1040001 : TextConst 'ENU=CMS Content Values with Content Type %1, Attribute %2.  If you delete this, the system will delete all the CMS Content Vaules.\\Are you sure you want to delete this? (1);ENG=CMS Content Values with Content Type %1, Attribute %2.  If you delete this, the system will delete all the CMS Content Vaules.\\Are you sure you want to delete this? (1)';
      Text002@1040002 : TextConst 'ENU=Delete abandoned. (2);ENG=Delete abandoned. (2)';
      PageContentRec@1040003 : Record 9021630;
      LabelRec@1190000008 : Record 9021618;
      MultiLangFunc@1190000005 : Codeunit 9021603;
      MultiLanguageLabelCode@1190000007 : Code[10];
      Text003@1190000006 : TextConst 'ENU=The %1 field has no function as you are not in Multi Language Mode.;ENG=The %1 field has no function as you are not in Multi Language Mode.';
      Text004@1190000009 : TextConst 'ENU=You cannot do this as %1 is not TRUE.;ENG=You cannot do this as %1 is not TRUE.';
      Text005@1000000000 : TextConst 'ENU=This has no effect as Multilanguage is TRUE.;ENG=This has no effect as Multilanguage is TRUE.';

    PROCEDURE CalcMultiLanguageLabelCode@1190000005() : Code[10];
    BEGIN
      MultiLanguageLabelCode := '';
      LabelRec.RESET;
      LabelRec.SETRANGE("Used-in Company",COMPANYNAME);
      LabelRec.SETRANGE("Multi-Language Mode",TRUE);
      LabelRec.SETRANGE("Default Label in Company",TRUE);
      IF LabelRec.FINDFIRST THEN BEGIN
        MultiLanguageLabelCode := LabelRec.Code;
        EXIT(MultiLanguageLabelCode);
      END ELSE
        EXIT('');
    END;

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - Add Multilanguage functionality
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

