OBJECT Table 9062233 Smart Filter
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD(ID);
               Rec.TESTFIELD(Pattern);
             END;

    CaptionML=[ENU=Smart Filter;
               ENG=Smart Filter];
    LookupPageID=Page9062240;
    DrillDownPageID=Page9062240;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ENG=Code] }
    { 11  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 20  ;   ;Pattern             ;Text250       ;CaptionML=[ENU=Pattern;
                                                              ENG=Pattern] }
    { 21  ;   ;Use Regular Expressions;Boolean    ;CaptionML=[ENU=Use Regular Expressions;
                                                              ENG=Use Regular Expressions] }
    { 30  ;   ;Syntax              ;Text250       ;CaptionML=[ENU=Syntax;
                                                              ENG=Syntax] }
    { 40  ;   ;Help                ;BLOB          ;CaptionML=[ENU=Help;
                                                              ENG=Help];
                                                   SubType=Memo }
    { 50  ;   ;Category            ;Option        ;CaptionML=[ENU=Category;
                                                              ENG=Category];
                                                   OptionCaptionML=[ENU=" ,Dates and Times,Periods,Period Bounds,Accounting Periods,Accounting Period Bounds,My";
                                                                    ENG=" ,Dates and Times,Periods,Period Bounds,Accounting Periods,Accounting Period Bounds,My"];
                                                   OptionString=[ ,Dates and Times,Periods,Period Bounds,Accounting Periods,Accounting Period Bounds,My] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BlobToolkit@1000000000 : Codeunit 9062242;
      LastId@1000000001 : Integer;
      CurrentCategory@1000000002 : Integer;

    PROCEDURE Add@1000000000(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pHelp@1000000001 : Text) : Integer;
    BEGIN
      //DOC NA2016.12 - Adding an entry
      //  -> pCode: Code
      //  -> pDescription: Description
      //  -> pHelp: Help
      //  <- ID of entry added

      EXIT(AddInternal(pCode, pDescription, pCode, pCode, pHelp, FALSE));
    END;

    PROCEDURE AddRegEx@1000000001(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pPattern@1000000003 : Text;pSyntax@1000000001 : Text;pHelp@1000000004 : Text) : Integer;
    BEGIN
      //DOC NA2016.12 - Adding regular-expression entry
      //  -> pCode: Code
      //  -> pDescription: Description
      //  -> pPattern: Pattern
      //  -> pSyntax: Syntax
      //  -> pHelp: Help
      //  <- ID of entry added

      EXIT(AddInternal(pCode, pDescription, pPattern, pSyntax, pHelp, TRUE));
    END;

    LOCAL PROCEDURE AddInternal@1000000003(pCode@1000000000 : Code[20];pDescription@1000000002 : Text;pPattern@1000000003 : Text;pSyntax@1000000005 : Text;pHelp@1000000001 : Text;pUseRegularExpressions@1000000004 : Boolean) : Integer;
    VAR
      lHelpBlob@1000000006 : Record 99008535;
    BEGIN
      //DOC NA2016.12 - Adding an entry (internal version)
      //  -> pCode: Code
      //  -> pDescription: Description
      //  -> pPattern: Pattern
      //  -> pSyntax: Syntax
      //  -> pHelp: Help
      //  -> pUseRegularExpressions: Flag to use regular expressions
      //  <- ID of entry added

      LastId += 1;

      Rec.INIT();
      Rec.VALIDATE(ID, LastId);
      Rec.VALIDATE(Code, pCode);
      Rec.VALIDATE(Description, COPYSTR(pDescription, 1, MAXSTRLEN(Rec.Description)));
      Rec.VALIDATE(Pattern, pPattern);
      Rec.VALIDATE(Syntax, pSyntax);
      BlobToolkit.TextToBlob(pHelp, lHelpBlob);
      Rec.VALIDATE(Help, lHelpBlob.Blob);
      Rec.VALIDATE(Category, CurrentCategory);
      Rec.VALIDATE("Use Regular Expressions", pUseRegularExpressions);
      Rec.INSERT(TRUE);

      EXIT(LastId);
    END;

    PROCEDURE SetCurrentCategory@1000000002(pCurrentCategory@1000000000 : Integer);
    BEGIN
      //DOC NA2016.12 - Setting current category
      //  -> pCurrentCategory: The category

      CurrentCategory := pCurrentCategory;
    END;

    BEGIN
    {
      //DOC NA2016.12 JH 28/01/2016 - Created
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

