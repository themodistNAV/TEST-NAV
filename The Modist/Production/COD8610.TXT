OBJECT Codeunit 8610 Questionnaire Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The value of the key field %1 has not been filled in for questionnaire %2.;ENG=The value of the key field %1 has not been filled in for questionnaire %2.';
      XMLDOMMgt@1002 : Codeunit 6224;
      ConfigProgressBar@1023 : Codeunit 8615;
      ConfigValidateMgt@1003 : Codeunit 8617;
      FileMgt@1016 : Codeunit 419;
      Text001@1008 : TextConst 'ENU=Exporting questionnaire;ENG=Exporting questionnaire';
      Text002@1009 : TextConst 'ENU=Importing questionnaire;ENG=Importing questionnaire';
      Text005@1010 : TextConst 'ENU=Could not create the XML schema.;ENG=Could not create the XML schema.';
      Text007@1026 : TextConst 'ENU=Applying answers;ENG=Applying answers';
      Text008@1025 : TextConst 'ENU=Updating questionnaire;ENG=Updating questionnaire';
      Text019@1017 : TextConst 'ENU=Could not create an Excel instance.;ENG=Could not create an Excel instance.';
      Text020@1014 : TextConst 'ENU=Excel does not contain an XML schema.;ENG=Excel does not contain an XML schema.';
      Text022@1005 : TextConst 'ENU=Creating Excel worksheet;ENG=Creating Excel worksheet';
      XlApp@1015 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ApplicationClass" RUNONCLIENT;
      XlBook@1007 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.WorkbookClass" RUNONCLIENT;
      XlSheet@1006 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.WorksheetClass" RUNONCLIENT;
      WrkBkWriter@1012 : DotNet "'Microsoft.Dynamics.Nav.OpenXml, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.OpenXml.Spreadsheet.WorkbookWriter" RUNONCLIENT;
      XlHelper@1001 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Excel.ExcelHelper" RUNONCLIENT;
      ExportToExcel@1018 : Boolean;
      Text024@1020 : TextConst 'ENU=Download;ENG=Download';
      Text025@1021 : TextConst 'ENU=*.*|*.*;ENG=*.*|*.*';
      Text026@1022 : TextConst 'ENU=Default;ENG=Default';
      CalledFromCode@1024 : Boolean;
      Text028@1019 : TextConst 'ENU=Import File;ENG=Import File';
      Text029@1004 : TextConst '@@@="Only translate ''XML Files'' {Split=r""[\|\(]\*\.[^ |)]*[|) ]?""}";ENU=XML file (*.xml)|*.xml;ENG=XML file (*.xml)|*.xml';

    PROCEDURE UpdateQuestions@1(ConfigQuestionArea@1000 : Record 8611);
    VAR
      ConfigQuestion@1002 : Record 8612;
      Field@1001 : Record 2000000041;
      ConfigPackageMgt@1003 : Codeunit 8611;
      NextQuestionNo@1007 : Integer;
    BEGIN
      IF ConfigQuestionArea."Table ID" = 0 THEN
        EXIT;

      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);
      IF ConfigQuestion.FINDLAST THEN
        NextQuestionNo := ConfigQuestion."No." + 1
      ELSE
        NextQuestionNo := 1;

      ConfigPackageMgt.SetFieldFilter(Field,ConfigQuestionArea."Table ID",0);
      IF Field.FINDSET THEN
        REPEAT
          ConfigQuestion.INIT;
          ConfigQuestion."Questionnaire Code" := ConfigQuestionArea."Questionnaire Code";
          ConfigQuestion."Question Area Code" := ConfigQuestionArea.Code;
          ConfigQuestion."No." := NextQuestionNo;
          ConfigQuestion."Table ID" := ConfigQuestionArea."Table ID";
          ConfigQuestion."Field ID" := Field."No.";
          IF NOT QuestionExist(ConfigQuestion) THEN BEGIN
            UpdateQuestion(ConfigQuestion);
            ConfigQuestion."Answer Option" := BuildAnswerOption(ConfigQuestionArea."Table ID",Field."No.");
            ConfigQuestion.INSERT;
            NextQuestionNo := NextQuestionNo + 1;
          END;
        UNTIL Field.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateQuestion@2(VAR ConfigQuestion@1000 : Record 8612);
    VAR
      RecRef@1002 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      WITH ConfigQuestion DO BEGIN
        IF Question <> '' THEN
          EXIT;
        IF "Table ID" = 0 THEN
          EXIT;
        RecRef.OPEN("Table ID");
        FieldRef := RecRef.FIELD("Field ID");
        Question := FieldRef.CAPTION + '?';
      END;
    END;

    PROCEDURE UpdateQuestionnaire@3(ConfigQuestionnaire@1000 : Record 8610) : Boolean;
    VAR
      ConfigQuestionArea@1001 : Record 8611;
    BEGIN
      IF ConfigQuestionnaire.Code = '' THEN
        EXIT;

      ConfigQuestionArea.RESET;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      IF ConfigQuestionArea.FINDSET THEN BEGIN
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text008);
        REPEAT
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          UpdateQuestions(ConfigQuestionArea);
        UNTIL ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE QuestionExist@12(ConfigQuestion@1000 : Record 8612) : Boolean;
    VAR
      ConfigQuestion2@1001 : Record 8612;
    BEGIN
      ConfigQuestion2.RESET;
      ConfigQuestion2.SETCURRENTKEY("Questionnaire Code","Question Area Code","Field ID");
      ConfigQuestion2.SETRANGE("Questionnaire Code",ConfigQuestion."Questionnaire Code");
      ConfigQuestion2.SETRANGE("Question Area Code",ConfigQuestion."Question Area Code");
      ConfigQuestion2.SETRANGE("Field ID",ConfigQuestion."Field ID");
      EXIT(NOT ConfigQuestion2.ISEMPTY);
    END;

    PROCEDURE BuildAnswerOption@8(TableID@1007 : Integer;FieldID@1006 : Integer) : Text[250];
    VAR
      Field@1008 : Record 2000000041;
      RecRef@1001 : RecordRef;
      FieldRef@1000 : FieldRef;
      BooleanText@1002 : Text[30];
    BEGIN
      Field.SETRANGE(TableNo,TableID);
      Field.SETRANGE("No.",FieldID);

      IF NOT Field.FINDFIRST THEN
        EXIT;

      CASE Field.Type OF
        Field.Type::Option:
          BEGIN
            RecRef.OPEN(Field.TableNo);
            FieldRef := RecRef.FIELD(Field."No.");
            EXIT(FieldRef.OPTIONCAPTION);
          END;
        Field.Type::Boolean:
          BEGIN
            BooleanText := FORMAT(TRUE) + ',' + FORMAT(FALSE);
            EXIT(BooleanText)
          END;
        ELSE
          EXIT(FORMAT(Field.Type));
      END;
    END;

    PROCEDURE ApplyAnswers@30(ConfigQuestionnaire@1001 : Record 8610) : Boolean;
    VAR
      ConfigQuestionArea@1000 : Record 8611;
    BEGIN
      ConfigQuestionArea.RESET;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      IF ConfigQuestionArea.FINDSET THEN BEGIN
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text007);
        REPEAT
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          ApplyAnswer(ConfigQuestionArea);
        UNTIL ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE ApplyAnswer@7(ConfigQuestionArea@1001 : Record 8611);
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      IF ConfigQuestionArea."Table ID" = 0 THEN
        EXIT;

      RecRef.OPEN(ConfigQuestionArea."Table ID");
      RecRef.INIT;

      InsertRecordWithKeyFields(RecRef,ConfigQuestionArea);
      ModifyRecordWithOtherFields(RecRef,ConfigQuestionArea);
    END;

    LOCAL PROCEDURE InsertRecordWithKeyFields@40(VAR RecRef@1000 : RecordRef;ConfigQuestionArea@1002 : Record 8611);
    VAR
      ConfigQuestion@1001 : Record 8612;
      RecRef1@1006 : RecordRef;
      KeyRef@1003 : KeyRef;
      FieldRef@1004 : FieldRef;
      KeyFieldCount@1005 : Integer;
    BEGIN
      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        ConfigQuestion.SETRANGE("Field ID",FieldRef.NUMBER);
        IF ConfigQuestion.FINDFIRST THEN BEGIN
          ConfigValidateMgt.ValidateFieldValue(RecRef,FieldRef,ConfigQuestion.Answer,FALSE,GLOBALLANGUAGE);
        END ELSE
          IF KeyRef.FIELDCOUNT <> 1 THEN
            ERROR(STRSUBSTNO(Text000,FieldRef.NAME,ConfigQuestionArea.Code));
      END;

      RecRef1 := RecRef.DUPLICATE;

      IF RecRef1.FIND THEN BEGIN
        RecRef := RecRef1;
        EXIT
      END;

      RecRef.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ModifyRecordWithOtherFields@39(VAR RecRef@1004 : RecordRef;ConfigQuestionArea@1002 : Record 8611);
    VAR
      ConfigQuestion@1003 : Record 8612;
      TempConfigPackageField@1006 : TEMPORARY Record 8616;
      ConfigPackageManagement@1005 : Codeunit 8611;
      FieldRef@1001 : FieldRef;
      ErrorText@1000 : Text[250];
    BEGIN
      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

      IF ConfigQuestion.FINDSET THEN
        REPEAT
          TempConfigPackageField.DELETEALL;
          IF ConfigQuestion.Answer <> '' THEN BEGIN
            FieldRef := RecRef.FIELD(ConfigQuestion."Field ID");
            ConfigValidateMgt.ValidateFieldValue(RecRef,FieldRef,ConfigQuestion.Answer,FALSE,GLOBALLANGUAGE);
            ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
            ErrorText := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);
            IF ErrorText <> '' THEN
              ERROR(ErrorText);
          END;
        UNTIL ConfigQuestion.NEXT = 0;
      RecRef.MODIFY(TRUE);
    END;

    PROCEDURE ExportQuestionnaireAsXML@4(XMLDataFile@1000 : Text;VAR ConfigQuestionnaire@1001 : Record 8610) : Boolean;
    VAR
      QuestionnaireXML@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ToFile@1007 : Text[1024];
      FileName@1006 : Text;
      Exported@1009 : Boolean;
    BEGIN
      QuestionnaireXML := QuestionnaireXML.XmlDocument;

      GenerateQuestionnaireXMLDocument(QuestionnaireXML,ConfigQuestionnaire);

      Exported := TRUE;
      IF NOT ExportToExcel THEN BEGIN
        FileName := XMLDataFile;
        ToFile := Text026 + '.xml';

        IF NOT CalledFromCode THEN
          FileName := FileMgt.ServerTempFileName('.xml');
        QuestionnaireXML.Save(FileName);
        IF NOT CalledFromCode THEN
          Exported := FileMgt.DownloadHandler(FileName,Text024,'',Text025,ToFile);
      END ELSE BEGIN
        FileName := XMLDataFile;
        QuestionnaireXML.Save(FileName);
      END;

      EXIT(Exported);
    END;

    PROCEDURE GenerateQuestionnaireXMLDocument@1100(QuestionnaireXML@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR ConfigQuestionnaire@1001 : Record 8610);
    VAR
      ConfigQuestionArea@1005 : Record 8611;
      RecRef@1002 : RecordRef;
      DocumentNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      QuestionnaireXML.LoadXml(
        '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><Questionnaire></Questionnaire>');

      DocumentNode := QuestionnaireXML.DocumentElement;

      RecRef.GETTABLE(ConfigQuestionnaire);
      CreateFieldSubtree(RecRef,DocumentNode);

      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      IF ConfigQuestionArea.FINDSET THEN BEGIN
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text001);
        REPEAT
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          CreateQuestionNodes(QuestionnaireXML,ConfigQuestionArea);
        UNTIL ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
      END;
    END;

    PROCEDURE ImportQuestionnaireAsXMLFromClient@15() : Boolean;
    VAR
      ServerFileName@1000 : Text;
    BEGIN
      ServerFileName := FileMgt.ServerTempFileName('.xml');
      IF UPLOAD(Text028,'',Text029,'',ServerFileName) THEN
        EXIT(ImportQuestionnaireAsXML(ServerFileName));

      EXIT(FALSE);
    END;

    PROCEDURE ImportQuestionnaireAsXML@5(XMLDataFile@1000 : Text) : Boolean;
    VAR
      QuestionnaireXML@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      QuestionnaireXML := QuestionnaireXML.XmlDocument;
      QuestionnaireXML.Load(XMLDataFile);

      EXIT(ImportQuestionnaireXMLDocument(QuestionnaireXML));
    END;

    PROCEDURE ImportQuestionnaireXMLDocument@1108(QuestionnaireXML@1104 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument") : Boolean;
    VAR
      ConfigQuestionnaire@1003 : Record 8610;
      ConfigQuestionArea@1007 : Record 8611;
      ConfigQuestion@1000 : Record 8612;
      QuestionAreaNodes@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      QuestionAreaNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      QuestionNodes@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      QuestionnaireNode@1103 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AreaNodeCount@1009 : Integer;
      NodeCount@1010 : Integer;
    BEGIN
      QuestionnaireNode := QuestionnaireXML.SelectSingleNode('//Questionnaire');

      UpdateInsertQuestionnaireField(ConfigQuestionnaire,QuestionnaireNode);
      QuestionAreaNodes := QuestionnaireNode.SelectNodes('child::*[position() >= 3]');

      ConfigProgressBar.Init(QuestionAreaNodes.Count,1,Text002);

      FOR AreaNodeCount := 0 TO QuestionAreaNodes.Count - 1 DO BEGIN
        QuestionAreaNode := QuestionAreaNodes.Item(AreaNodeCount);
        ConfigProgressBar.Update(GetNodeValue(QuestionAreaNode,'Code'));
        ConfigQuestionArea."Questionnaire Code" := ConfigQuestionnaire.Code;
        UpdateInsertQuestionAreaFields(ConfigQuestionArea,QuestionAreaNode);

        QuestionNodes := QuestionAreaNode.SelectNodes('ConfigQuestion');
        FOR NodeCount := 0 TO QuestionNodes.Count - 1 DO BEGIN
          ConfigQuestion.INIT;
          ConfigQuestion."Questionnaire Code" := ConfigQuestionArea."Questionnaire Code";
          ConfigQuestion."Question Area Code" := ConfigQuestionArea.Code;
          ConfigQuestion."Table ID" := ConfigQuestionArea."Table ID";
          UpdateInsertQuestionFields(ConfigQuestion,QuestionNodes.Item(NodeCount))
        END;
      END;

      ConfigProgressBar.Close;
      EXIT(TRUE);
    END;

    PROCEDURE ExportQuestionnaireToExcel@9(ExcelFile@1001 : Text;VAR ConfigQuestionnaire@1000 : Record 8610) : Boolean;
    VAR
      ConfigQuestionnaire1@1019 : Record 8610;
      ConfigQuestionArea@1018 : Record 8611;
      ConfigQuestion@1002 : Record 8612;
      ConfigQuestionnaireSchema@1017 : XMLport 8611;
      ListObject@1012 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListObject" RUNONCLIENT;
      ListColumns@1008 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListColumns" RUNONCLIENT;
      ListColumn@1006 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListColumn" RUNONCLIENT;
      Range@1005 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.Range" RUNONCLIENT;
      XMLMap@1004 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMap" RUNONCLIENT;
      YesNo@1003 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XlYesNoGuess" RUNONCLIENT;
      OStream@1016 : OutStream;
      TempSchemaFile@1015 : File;
      TempConfigQuestionnaireFileName@1007 : Text;
      TempConfigQuestionnaireFNCL@1009 : Text;
      TempSchemaFileName@1010 : Text;
      TempSchemaFileNameCL@1011 : Text;
      TempXLSFile@1020 : Text;
      WrkShtNo@1014 : Integer;
    BEGIN
      ConfigQuestionnaire1.SETRANGE(Code,ConfigQuestionnaire.Code);
      TempSchemaFile.CREATETEMPFILE;
      TempSchemaFileName := TempSchemaFile.NAME + '.xsd';
      TempSchemaFile.CLOSE;

      TempSchemaFile.CREATE(TempSchemaFileName);
      TempSchemaFile.CREATEOUTSTREAM(OStream);
      ConfigQuestionnaireSchema.SETDESTINATION(OStream);
      ConfigQuestionnaireSchema.SETTABLEVIEW(ConfigQuestionnaire1);
      IF NOT ConfigQuestionnaireSchema.EXPORT THEN
        ERROR(Text005);

      TempSchemaFile.CLOSE;

      ExportToExcel := TRUE;
      CalledFromCode := TRUE;
      TempConfigQuestionnaireFileName := FileMgt.ServerTempFileName('');
      ExportQuestionnaireAsXML(TempConfigQuestionnaireFileName,ConfigQuestionnaire);
      ExportToExcel := FALSE;

      TempConfigQuestionnaireFNCL := FileMgt.DownloadTempFile(TempConfigQuestionnaireFileName);
      TempSchemaFileNameCL := FileMgt.DownloadTempFile(TempSchemaFileName);
      WrkShtNo := 1;

      TempXLSFile := FileMgt.ClientTempFileName('xlsx');
      CreateBook(TempXLSFile);

      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      IF ConfigQuestionArea.FINDSET THEN
        REPEAT
          WrkShtNo += 1;
          WrkBkWriter.AddWorksheet(FORMAT(WrkShtNo));
        UNTIL ConfigQuestionArea.NEXT = 0;
      WrkBkWriter.Close;

      OpenBook(TempXLSFile,FALSE);
      XlApp.DisplayAlerts(FALSE);

      XlBook.XmlMaps.Add(TempSchemaFileNameCL,'Questionnaire');
      XMLMap := XlBook.XmlMaps.Item(1);

      XlSheet := XlBook.Worksheets.Item(ConfigQuestionArea.COUNT + 1);

      IF ConfigQuestionnaire.Description <> '' THEN
        XlSheet.Name := ConfigQuestionnaire.Description
      ELSE
        XlSheet.Name := ConfigQuestionnaire.Code;

      Range := XlSheet.Range('A1');
      Range.Value := ConfigQuestionnaire.Code;
      Range.XPath.SetValue(XMLMap,'/Questionnaire/' + GetElementName(ConfigQuestionnaire.FIELDNAME(Code)),'',FALSE);

      Range := XlSheet.Range('B1');
      Range.Value := ConfigQuestionnaire.Description;
      Range.XPath.SetValue(XMLMap,'/Questionnaire/' + GetElementName(ConfigQuestionnaire.FIELDNAME(Description)),'',FALSE);

      WrkShtNo := 1;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text022);
      IF ConfigQuestionArea.FINDSET THEN BEGIN
        REPEAT
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
          ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

          XlSheet := XlBook.Worksheets.Item(WrkShtNo);
          WrkShtNo += 1;

          IF ConfigQuestionArea.Description <> '' THEN
            XlSheet.Name := ConfigQuestionArea.Description
          ELSE
            IF ConfigQuestionArea.Code <> ConfigQuestionArea."Questionnaire Code" THEN
              XlSheet.Name := ConfigQuestionArea.Code
            ELSE
              XlSheet.Name := ConfigQuestionArea."Questionnaire Code" + ' ' + ConfigQuestionArea.Code;

          Range := XlSheet.Range('A1');
          Range.Value := ConfigQuestionArea.Code;
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME(Code)),
            '',FALSE);

          Range := XlSheet.Range('B1');
          Range.Value := ConfigQuestionArea.Description;
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME(Description)),
            '',FALSE);

          Range := XlSheet.Range('C1');
          Range.Value := ConfigQuestionArea."Table ID";
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME("Table ID")),
            '',FALSE);

          IF ConfigQuestion.FINDFIRST THEN BEGIN
            Range := XlSheet.Range('A2',FORMAT(GetXLColumnID(7)) + '2');

            ListObject := XlSheet.ListObjects.Add(1,Range,TRUE,YesNo.xlNo,'');
            ListColumns := ListObject.ListColumns;

            ListColumn := ListColumns.Item(1);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("No.");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("No.")),'',TRUE);

            ListColumn := ListColumns.Item(2);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Question);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Question)),'',TRUE);

            ListColumn := ListColumns.Item(3);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Answer Option");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Answer Option")),'',TRUE);

            ListColumn := ListColumns.Item(4);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Answer);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Answer)),'',TRUE);

            ListColumn := ListColumns.Item(5);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Reference);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Reference)),'',TRUE);

            ListColumn := ListColumns.Item(6);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Field ID");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Field ID")),'',TRUE);

            ListColumn := ListColumns.Item(7);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Question Origin");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Question Origin")),'',TRUE);

            XMLMap.Import(TempConfigQuestionnaireFNCL,TRUE);
          END;

          XlSheet.Columns.Range('F:F').EntireColumn.Hidden := TRUE;
        UNTIL ConfigQuestionArea.NEXT = 0;
      END;

      IF FileMgt.GetExtension(ExcelFile) = '' THEN
        ExcelFile += '.xlsx';
      XlBook.SaveCopyAs(ExcelFile);

      CloseXlApp;

      FILE.ERASE(TempSchemaFileName);
      FILE.ERASE(TempConfigQuestionnaireFileName);

      ConfigProgressBar.Close;

      EXIT(TRUE);
    END;

    PROCEDURE ImportQuestionnaireFromExcel@24(XLSDataFile@1000 : Text) Imported : Boolean;
    VAR
      XmlMaps@1003 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMaps" RUNONCLIENT;
      XmlMap@1002 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMap" RUNONCLIENT;
      TmpXmlFile@1005 : File;
      InStream@1006 : InStream;
      XMLDataFileClient@1004 : Text;
      XMLDataFileServer@1001 : Text;
    BEGIN
      XlApp := XlApp.ApplicationClass;
      IF ISNULL(XlApp) THEN
        ERROR(Text019);

      XlHelper.CallOpen(XlApp,XLSDataFile);
      XlBook := XlApp.ActiveWorkbook;
      XMLDataFileClient := '';
      XmlMaps := XlBook.XmlMaps;
      IF XmlMaps.Count <> 0 THEN BEGIN
        TmpXmlFile.CREATETEMPFILE;
        TmpXmlFile.CREATEINSTREAM(InStream);
        DOWNLOADFROMSTREAM(InStream,'',FileMgt.Magicpath,'',XMLDataFileClient);
        TmpXmlFile.CLOSE;

        XmlMap := XmlMaps.Item(1);
        XmlMap.Export(XMLDataFileClient,TRUE);

        XMLDataFileServer := FileMgt.ServerTempFileName('.xml');
        UPLOAD('',FileMgt.Magicpath,'',XMLDataFileClient,XMLDataFileServer);

        IF ImportQuestionnaireAsXML(XMLDataFileServer) THEN
          Imported := TRUE;
        CloseXlApp;
      END ELSE BEGIN
        CloseXlApp;
        ERROR(Text020);
      END;

      EXIT(Imported);
    END;

    LOCAL PROCEDURE CreateQuestionNodes@10(QuestionnaireXML@1104 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";ConfigQuestionArea@1000 : Record 8611);
    VAR
      ConfigQuestion@1001 : Record 8612;
      DocumentElement@1103 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      QuestionAreaNode@1102 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      QuestionNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      RecRef@1012 : RecordRef;
      QuestionRecRef@1002 : RecordRef;
    BEGIN
      DocumentElement := QuestionnaireXML.DocumentElement;
      QuestionAreaNode := QuestionnaireXML.CreateElement(GetElementName(ConfigQuestionArea.Code + 'Questions'));
      DocumentElement.AppendChild(QuestionAreaNode);

      RecRef.GETTABLE(ConfigQuestionArea);
      CreateFieldSubtree(RecRef,QuestionAreaNode);

      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);
      IF ConfigQuestion.FINDSET THEN
        REPEAT
          QuestionNode := QuestionnaireXML.CreateElement(GetElementName(ConfigQuestion.TABLENAME));
          QuestionAreaNode.AppendChild(QuestionNode);

          QuestionRecRef.GETTABLE(ConfigQuestion);
          CreateFieldSubtree(QuestionRecRef,QuestionNode);
        UNTIL ConfigQuestion.NEXT = 0;
    END;

    PROCEDURE GetElementName@22(NameIn@1000 : Text[250]) : Text[250];
    BEGIN
      NameIn := DELCHR(NameIn,'=','?''`');
      NameIn := DELCHR(CONVERTSTR(NameIn,'<>,./\+-&()%:','             '),'=',' ');
      NameIn := DELCHR(NameIn,'=',' ');
      EXIT(NameIn);
    END;

    LOCAL PROCEDURE CreateFieldSubtree@6(VAR RecRef@1001 : RecordRef;VAR Node@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement");
    VAR
      Field@1006 : Record 2000000041;
      FieldRef@1003 : FieldRef;
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlDom@1102 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      i@1002 : Integer;
    BEGIN
      XmlDom := Node.OwnerDocument;
      FOR i := 1 TO RecRef.FIELDCOUNT DO BEGIN
        FieldRef := RecRef.FIELDINDEX(i);
        IF NOT FieldException(RecRef.NUMBER,FieldRef.NUMBER) THEN BEGIN
          FieldNode := XmlDom.CreateElement(GetElementName(FieldRef.NAME));

          IF Field.GET(RecRef.NUMBER,FieldRef.NUMBER) THEN BEGIN
            IF Field.Class = Field.Class::FlowField THEN
              FieldRef.CALCFIELD;
            FieldNode.InnerText := FORMAT(FieldRef.VALUE);

            XMLDOMMgt.AddAttribute(FieldNode,'fieldlength',FORMAT(Field.Len));
          END;
          Node.AppendChild(FieldNode);
        END;
      END;
    END;

    LOCAL PROCEDURE GetNodeValue@26(VAR RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";FieldNodeName@1001 : Text[250]) : Text[250];
    VAR
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      FieldNode := RecordNode.SelectSingleNode(FieldNodeName);
      EXIT(FieldNode.InnerText);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionnaireField@18(VAR ConfigQuestionnaire@1011 : Record 8610;RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.OPEN(DATABASE::"Config. Questionnaire");

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestionnaire);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionAreaFields@25(VAR ConfigQuestionArea@1002 : Record 8611;RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(ConfigQuestionArea);

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestionArea);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionFields@27(VAR ConfigQuestion@1011 : Record 8612;RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      Field@1001 : Record 2000000041;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(ConfigQuestion);

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestion);

      IF Field.GET(ConfigQuestion."Table ID",ConfigQuestion."Field ID") THEN
        ModifyConfigQuestionAnswer(ConfigQuestion,Field);
    END;

    LOCAL PROCEDURE FieldNodeExists@36(VAR RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";FieldNodeName@1001 : Text[250]) : Boolean;
    VAR
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      FieldNode := RecordNode.SelectSingleNode(FieldNodeName);

      IF NOT ISNULL(FieldNode) THEN
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateBook@33(XlsFile@1000 : Text);
    BEGIN
      WrkBkWriter := WrkBkWriter.Create(XlsFile);
      IF ISNULL(WrkBkWriter) THEN
        ERROR(Text019);
    END;

    LOCAL PROCEDURE GetXLColumnID@14(ColumnNo@1003 : Integer) : Text[10];
    VAR
      ExcelBuf@1000 : Record 370;
    BEGIN
      ExcelBuf.INIT;
      ExcelBuf.VALIDATE("Column No.",ColumnNo);
      EXIT(ExcelBuf.xlColID);
    END;

    LOCAL PROCEDURE FieldException@11(TableID@1000 : Integer;FieldID@1001 : Integer) : Boolean;
    VAR
      ConfigQuestionArea@1004 : Record 8611;
      ConfigQuestion@1005 : Record 8612;
    BEGIN
      CASE TableID OF
        DATABASE::"Config. Questionnaire":
          EXIT(FALSE);
        DATABASE::"Config. Question Area":
          EXIT(FieldID IN [ConfigQuestionArea.FIELDNO("Questionnaire Code"),
                           ConfigQuestionArea.FIELDNO("Table Name")]);
        DATABASE::"Config. Question":
          EXIT(FieldID IN [ConfigQuestion.FIELDNO("Questionnaire Code"),
                           ConfigQuestion.FIELDNO("Question Area Code"),
                           ConfigQuestion.FIELDNO("Table ID")]);
      END;
    END;

    PROCEDURE SetCalledFromCode@13();
    BEGIN
      CalledFromCode := TRUE;
    END;

    LOCAL PROCEDURE CloseXlApp@19();
    BEGIN
      CLEAR(WrkBkWriter);
      CLEAR(XlSheet);
      CLEAR(XlBook);
      XlApp.Quit;
      CLEAR(XlApp);
    END;

    LOCAL PROCEDURE ValidateKeyFields@16(RecRef@1000 : RecordRef;RecordNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      KeyRef@1001 : KeyRef;
      FieldRef@1002 : FieldRef;
      KeyFieldCount@1003 : Integer;
    BEGIN
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        IF FieldNodeExists(RecordNode,GetElementName(FieldRef.NAME)) THEN
          ConfigValidateMgt.ValidateFieldValue(
            RecRef,FieldRef,GetNodeValue(RecordNode,GetElementName(FieldRef.NAME)),FALSE,GLOBALLANGUAGE);
      END;
    END;

    LOCAL PROCEDURE ValidateFields@20(RecRef@1000 : RecordRef;RecordNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      Field@1001 : Record 2000000041;
      FieldRef@1002 : FieldRef;
    BEGIN
      Field.SETRANGE(TableNo,RecRef.NUMBER);
      IF Field.FINDSET THEN
        REPEAT
          FieldRef := RecRef.FIELD(Field."No.");
          IF FieldNodeExists(RecordNode,GetElementName(FieldRef.NAME)) THEN
            ConfigValidateMgt.ValidateFieldValue(
              RecRef,FieldRef,GetNodeValue(RecordNode,GetElementName(FieldRef.NAME)),FALSE,GLOBALLANGUAGE)
        UNTIL Field.NEXT = 0;
    END;

    LOCAL PROCEDURE ValidateRecordFields@21(RecRef@1000 : RecordRef;RecordNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef1@1001 : RecordRef;
    BEGIN
      ValidateKeyFields(RecRef,RecordNode);

      RecRef1 := RecRef.DUPLICATE;
      IF NOT RecRef1.FIND THEN
        RecRef.INSERT(TRUE);

      ValidateFields(RecRef,RecordNode);

      RecRef.MODIFY(TRUE);
    END;

    PROCEDURE ModifyConfigQuestionAnswer@23(VAR ConfigQuestion@1000 : Record 8612;Field@1001 : Record 2000000041);
    VAR
      DateFormula@1003 : DateFormula;
      OptionInt@1002 : Integer;
    BEGIN
      CASE Field.Type OF
        Field.Type::Option,
        Field.Type::Boolean:
          BEGIN
            IF ConfigQuestion.Answer <> '' THEN BEGIN
              OptionInt := ConfigValidateMgt.GetOptionNo(ConfigQuestion.Answer,ConfigQuestion."Answer Option");
              ConfigQuestion."Answer Option" :=
                BuildAnswerOption(ConfigQuestion."Table ID",ConfigQuestion."Field ID");
              IF OptionInt <> -1 THEN
                ConfigQuestion.Answer := SELECTSTR(OptionInt + 1,ConfigQuestion."Answer Option");
            END ELSE BEGIN
              ConfigQuestion.Answer := '';
              ConfigQuestion."Answer Option" :=
                BuildAnswerOption(ConfigQuestion."Table ID",ConfigQuestion."Field ID");
            END;
            ConfigQuestion.MODIFY;
          END;
        Field.Type::DateFormula:
          BEGIN
            EVALUATE(DateFormula,ConfigQuestion.Answer);
            ConfigQuestion.Answer := FORMAT(DateFormula);
            ConfigQuestion.MODIFY;
          END;
      END;
    END;

    LOCAL PROCEDURE OpenBook@34(XLSDataFile@1000 : Text;Visible@1001 : Boolean);
    BEGIN
      XlApp := XlApp.ApplicationClass;
      IF ISNULL(XlApp) THEN
        ERROR(Text019);
      XlHelper.CallOpen(XlApp,XLSDataFile);
      XlBook := XlApp.ActiveWorkbook;
      XlApp.Visible(Visible);
    END;

    BEGIN
    END.
  }
}

