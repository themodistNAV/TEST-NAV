OBJECT Table 9062368 Tile Frame
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Rec.TESTFIELD("Panel Code");
               Rec.TESTFIELD("Tile ID");
               CheckModifyPermission();
               IF (Rec.ID = 0) THEN
                 Rec.ID := NewId();
               IF (Rec.Sequence = 0) THEN
                 Rec.VALIDATE(Sequence, NewSequence());
               UpdateControlName();
               PanelTimestampToolkit.Add(Rec."Panel Code");
               TnpToolkit.UpdateCreatedBy(Rec."Created By", Rec."Created Date Time");
             END;

    OnModify=BEGIN
               CheckModifyPermission();
               PanelTimestampToolkit.Add(Rec."Panel Code");
               TnpToolkit.UpdateModifiedBy(Rec."Modified By", Rec."Modified Date Time");
             END;

    OnDelete=BEGIN
               CheckModifyPermission();
               PanelProperty.RESET();
               PanelProperty.SETRANGE("Parent Table ID", DATABASE::"Tile Frame");
               PanelProperty.SETRANGE("Parent Code", Rec."Panel Code");
               PanelProperty.SETRANGE("Parent ID", Rec."Tile ID");
               PanelProperty.SETRANGE("Parent ID 2", Rec.ID);
               PanelProperty.DELETEALL(TRUE);

               PanelTimestampToolkit.Add(Rec."Panel Code");
             END;

    OnRename=BEGIN
               CheckModifyPermission();
               ProcessRename(xRec."Panel Code", xRec."Tile ID", xRec.ID, Rec."Panel Code", Rec."Tile ID", Rec.ID);
             END;

    CaptionML=[ENU=Tile Frame;
               ENG=Tile Frame];
    LookupPageID=Page9062456;
    DrillDownPageID=Page9062456;
  }
  FIELDS
  {
    { 1   ;   ;Panel Code          ;Code20        ;TableRelation=Panel;
                                                   OnValidate=BEGIN
                                                                UpdateControlName();
                                                              END;

                                                   CaptionML=[ENU=Panel Code;
                                                              ENG=Panel Code] }
    { 3   ;   ;Tile ID             ;Integer       ;TableRelation=Tile.ID WHERE (Panel Code=FIELD(Panel Code),
                                                                                ID=FIELD(Tile ID));
                                                   OnValidate=BEGIN
                                                                UpdateControlName();
                                                              END;

                                                   CaptionML=[ENU=Tile ID;
                                                              ENG=Tile ID] }
    { 4   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 5   ;   ;Control Name        ;Text50        ;CaptionML=[ENU=Control Name;
                                                              ENG=Control Name] }
    { 6   ;   ;Text                ;Text250       ;CaptionML=[ENU=Text;
                                                              ENG=Text] }
    { 7   ;   ;Info                ;Text250       ;CaptionML=[ENU=Info;
                                                              ENG=Info] }
    { 8   ;   ;Image Id            ;Integer       ;CaptionML=[ENU=Image Id;
                                                              ENG=Image Id] }
    { 9   ;   ;Image               ;BLOB          ;CaptionML=[ENU=Image;
                                                              ENG=Image];
                                                   SubType=Bitmap }
    { 10  ;   ;Sequence            ;Integer       ;OnValidate=BEGIN
                                                                UpdateDescription();
                                                              END;

                                                   CaptionML=[ENU=Sequence;
                                                              ENG=Sequence];
                                                   MinValue=0 }
    { 11  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 12  ;   ;Text Colour Code    ;Code20        ;TableRelation="System Colour";
                                                   OnLookup=BEGIN
                                                              IF (SystemColourManagement.SystemColourLookup(Rec."Text Colour Code")) THEN
                                                                Rec.VALIDATE("Text Colour Code");
                                                            END;

                                                   CaptionML=[ENU=Text Colour Code;
                                                              ENG=Text Colour Code] }
    { 13  ;   ;Info Colour Code    ;Code20        ;TableRelation="System Colour";
                                                   OnLookup=BEGIN
                                                              IF (SystemColourManagement.SystemColourLookup(Rec."Info Colour Code")) THEN
                                                                Rec.VALIDATE("Info Colour Code");
                                                            END;

                                                   CaptionML=[ENU=Info Colour Code;
                                                              ENG=Info Colour Code] }
    { 14  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ENG=Blocked] }
    { 15  ;   ;Panel Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Panel.Description WHERE (Code=FIELD(Panel Code)));
                                                   CaptionML=[ENU=Panel Description;
                                                              ENG=Panel Description];
                                                   Editable=No }
    { 16  ;No ;Bitmap              ;BLOB          ;CaptionML=[ENU=Bitmap;
                                                              ENG=Bitmap] }
    { 17  ;   ;Tile Text           ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Tile.Text WHERE (Panel Code=FIELD(Panel Code),
                                                                                       ID=FIELD(Tile ID)));
                                                   CaptionML=[ENU=Panel Tile Text;
                                                              ENG=Panel Tile Text];
                                                   Editable=No }
    { 30  ;   ;Text Font Size      ;Integer       ;CaptionML=[ENU=Text Font Size;
                                                              ENG=Text Font Size];
                                                   MinValue=0 }
    { 31  ;   ;Text Font Style     ;Option        ;CaptionML=[ENU=Text Font Style;
                                                              ENG=Text Font Style];
                                                   OptionCaptionML=[ENU=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline";
                                                                    ENG=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline"];
                                                   OptionString=[ ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline] }
    { 40  ;   ;Info Font Size      ;Integer       ;CaptionML=[ENU=Info Font Size;
                                                              ENG=Info Font Size];
                                                   MinValue=0 }
    { 41  ;   ;Info Font Style     ;Option        ;CaptionML=[ENU=Info Font Style;
                                                              ENG=Info Font Style];
                                                   OptionCaptionML=[ENU=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline";
                                                                    ENG=" ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline"];
                                                   OptionString=[ ,Bold,Italic,Underline,Bold + Italic,Bold + Underline,Italic + Underline,Bold + Italic + Underline] }
    { 50  ;   ;Property Description;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Panel Property".Description WHERE (Parent Table ID=CONST(9062368),
                                                                                                          Parent Code=FIELD(Panel Code),
                                                                                                          Parent ID=FIELD(Tile ID),
                                                                                                          Parent ID 2=FIELD(ID),
                                                                                                          Property ID=FIELD(Property ID Filter)));
                                                   CaptionML=[ENU=Property Description;
                                                              ENG=Property Description];
                                                   Editable=No }
    { 51  ;   ;Property ID Filter  ;Integer       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Property ID Filter;
                                                              ENG=Property ID Filter] }
    { 60  ;   ;Text Colour Value   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("System Colour".Value WHERE (Code=FIELD(Text Colour Code)));
                                                   CaptionML=[ENU=Text Colour Value;
                                                              ENG=Text Colour Value];
                                                   Editable=No }
    { 61  ;   ;Info Colour Value   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("System Colour".Value WHERE (Code=FIELD(Info Colour Code)));
                                                   CaptionML=[ENU=Info Colour Value;
                                                              ENG=Info Colour Value];
                                                   Editable=No }
    { 1000;   ;Created By          ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Created By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Created By;
                                                              ENG=Created By] }
    { 1001;   ;Created Date Time   ;DateTime      ;CaptionML=[ENU=Created Date Time;
                                                              ENG=Created Date Time] }
    { 1002;   ;Modified By         ;Code50        ;OnLookup=BEGIN
                                                              TnpToolkit.UserLookup(Rec."Modified By", FALSE);
                                                            END;

                                                   CaptionML=[ENU=Modified By;
                                                              ENG=Modified By] }
    { 1003;   ;Modified Date Time  ;DateTime      ;CaptionML=[ENU=Modified Date Time;
                                                              ENG=Modified Date Time] }
  }
  KEYS
  {
    {    ;Panel Code,Tile ID,ID                   ;Clustered=Yes }
    {    ;Panel Code,Tile ID,Sequence,ID           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Panel@1000000009 : Record 9062363;
      PanelProperty@1000000002 : Record 9062433;
      EnhancedPermissionsMgt@1000000010 : Codeunit 9062277;
      PanelPropertyManagement@1000000006 : Codeunit 9062355;
      PanelTimestampToolkit@1000000007 : Codeunit 9062385;
      PanelTools@1000000000 : Codeunit 9062320;
      SystemColourManagement@1000000008 : Codeunit 9062344;
      TnpToolkit@1000000001 : Codeunit 9062230;

    PROCEDURE "--- Initialisation ---"@1000000003();
    BEGIN
    END;

    PROCEDURE NewId@1000000015() : Integer;
    VAR
      lTileFrame@1000000000 : Record 9062368;
    BEGIN
      //DOC NA2015.1 - Returning new ID
      //  <- The ID

      Rec.TESTFIELD("Panel Code");
      Rec.TESTFIELD("Tile ID");

      lTileFrame.RESET();
      lTileFrame.SETRANGE("Panel Code", Rec."Panel Code");
      lTileFrame.SETRANGE("Tile ID", Rec."Tile ID");
      IF (lTileFrame.FINDLAST()) THEN
        EXIT(lTileFrame.ID + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE NewIdFor@1000000002(pTilePanelCode@1000000001 : Code[20];pTileId@1000000003 : Integer) : Integer;
    BEGIN
      //DOC NA2015.1 - Returning new ID for specified panel code, panel control ID and panel tile ID (using Rec)
      //  -> pTilePanelCode: Tile panel code
      //  -> plTileId: Tile ID
      //  <- The ID

      Rec.RESET();
      Rec.SETRANGE("Panel Code", pTilePanelCode);
      Rec.SETRANGE("Tile ID", pTileId);
      IF (Rec.FINDLAST()) THEN
        EXIT(Rec.ID + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE NewSequence@1000000000() : Integer;
    VAR
      lTileFrame@1000000001 : Record 9062368;
    BEGIN
      //DOC NA2015.1 - Returning new sequence
      //  <- The sequence

      lTileFrame.RESET();
      lTileFrame.SETCURRENTKEY("Panel Code", "Tile ID", Sequence);
      lTileFrame.SETRANGE("Panel Code", Rec."Panel Code");
      lTileFrame.SETRANGE("Tile ID", Rec.ID);
      IF (lTileFrame.FINDLAST()) THEN
        EXIT(lTileFrame.Sequence + 10)
      ELSE
        EXIT(10);
    END;

    PROCEDURE "--- General ---"@1000000004();
    BEGIN
    END;

    PROCEDURE ProcessRename@1000000006(pFromPanelCode@1000000000 : Code[20];pFromTileId@1000000006 : Integer;pFromId@1000000002 : Integer;pToPanelCode@1000000001 : Code[20];pToTileId@1000000007 : Integer;pToId@1000000003 : Integer);
    VAR
      lFromPanelProperty@1000000005 : Record 9062433;
      lToPanelProperty@1000000004 : Record 9062433;
    BEGIN
      //DOC NA2015.2 - Processing rename (tables with non-standard relations)
      //  -> pFromPanelCode: From panel code
      //  -> pFromTileId: From tile ID
      //  -> pFromId: From ID
      //  -> pToPanelCode: To panel code
      //  -> pToTileId: To tile ID
      //  -> pToId: To ID

      IF ((pFromPanelCode = pToPanelCode) AND (pFromTileId = pToTileId) AND (pFromId = pToId)) THEN
        EXIT;

      lFromPanelProperty.RESET();
      lFromPanelProperty.SETRANGE("Parent Table ID", DATABASE::"Tile Frame");
      lFromPanelProperty.SETRANGE("Parent Code", Rec."Panel Code");
      lFromPanelProperty.SETRANGE("Parent ID", Rec."Tile ID");
      lFromPanelProperty.SETRANGE("Parent ID 2", Rec.ID);
      IF (lFromPanelProperty.FINDSET()) THEN
      BEGIN
        REPEAT
          lFromPanelProperty.ProcessRename(lFromPanelProperty."Parent Table ID", pFromPanelCode, pFromTileId, pFromId, lFromPanelProperty.ID,
            lFromPanelProperty."Parent Table ID", pToPanelCode, pToTileId, pToId, lFromPanelProperty.ID);
          lToPanelProperty := lFromPanelProperty;
          lToPanelProperty."Parent Code" := pToPanelCode;
          lToPanelProperty."Parent ID" := pToTileId;
          lToPanelProperty."Parent ID 2" := pToId;
          lToPanelProperty.INSERT();
        UNTIL (lFromPanelProperty.NEXT() = 0);
        lFromPanelProperty.DELETEALL();
      END;

      UpdateControlName();
    END;

    LOCAL PROCEDURE UpdateDescription@1000000005();
    VAR
      eDescription@1000000000 : TextConst 'ENU=Frame %1;ENG=Frame %1';
    BEGIN
      //DOC NA2015.1 - Updating description

      IF ((Rec.Description <> '') AND (Rec.Description <> STRSUBSTNO(eDescription, xRec.Sequence))) THEN
        EXIT;

      Rec.VALIDATE(Description, STRSUBSTNO(eDescription, Rec.Sequence));
    END;

    PROCEDURE UpdateControlName@1000000013();
    VAR
      eName@1000000000 : TextConst 'ENU=Control_%1_%2;ENG=Control_%1_%2';
    BEGIN
      //DOC NA2015.1 - Updating control name

      Rec."Control Name" := PanelTools.CodeIdIdToControlName(DATABASE::"Tile Frame", Rec."Panel Code", Rec."Tile ID", Rec.ID);
    END;

    PROCEDURE LoadImageFromLibrary@1000000047() : Boolean;
    VAR
      lSystemImage@1000000001 : Record 9062240;
      lSystemImageManagement@1000000000 : Codeunit 9062244;
      lImageSelection@1000000003 : Integer;
      lResult@1000000002 : Boolean;
    BEGIN
      //DOC NA2015.1 - Loading image from library
      //  <- Result

      lResult := FALSE;

      IF (lSystemImageManagement.LoadImageFromLibrary(lSystemImage, lImageSelection)) THEN
      BEGIN
        CASE (lImageSelection) OF
          lSystemImage."Image Selection"::Image:
            Rec.VALIDATE(Image, lSystemImage.Image);
          lSystemImage."Image Selection"::"Alternative Image":
            Rec.VALIDATE(Image, lSystemImage."Alternative Image");
        END;
        Rec.MODIFY(TRUE);
        lResult := TRUE;
      END;

      EXIT(lResult);
    END;

    LOCAL PROCEDURE CheckModifyPermission@1000000041();
    BEGIN
      //DOC NA2016.15 - Checking modify permission (given to the panel)

      IF ( NOT (Panel.GET(Rec."Panel Code"))) THEN
        EXIT;
      EnhancedPermissionsMgt.CheckPanelModifyPermission(Panel, USERID());
    END;

    PROCEDURE "--- Properties ---"@1000000001();
    BEGIN
    END;

    PROCEDURE ShowProperties@1000000012();
    BEGIN
      //DOC NA2015.1 - Showing properties

      PanelPropertyManagement.ShowProperties(DATABASE::"Tile Frame", Rec."Panel Code", Rec."Tile ID", Rec.ID);
    END;

    PROCEDURE ShowTextProperty@1000000033();
    BEGIN
      //DOC NA2015.1 - Showing text property

      ShowProperty(Rec.FIELDNO(Text));
    END;

    PROCEDURE ShowInfoProperty@1000000034();
    BEGIN
      //DOC NA2015.1 - Showing info property

      ShowProperty(Rec.FIELDNO(Info));
    END;

    PROCEDURE ShowProperty@1000000009(pPropertyId@1000000000 : Integer);
    BEGIN
      //DOC NA2015.2 - Showing property
      //  -> pPropertyId: Property ID

      PanelPropertyManagement.ShowProperty(DATABASE::"Tile Frame", Rec."Panel Code", Rec."Tile ID", Rec.ID, DATABASE::"Tile Frame", pPropertyId, TRUE);
    END;

    BEGIN
    {
      //DOC NA2015.1  JH 17/07/2013 - Created
      //DOC NA2015.1  JH 14/04/2014 - Panels 3
      //DOC NA2015.2  JH 18/07/2014 - Fixes
      //DOC NA2015.5  JH 14/03/2015 - "Bitmap", "Text Colour Value", "Info Colour Value" added (for backward-compatibility with 2009)
      //DOC NA2016.5  JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.15 JH 05/05/2016 - Enhanced permissions
    }
    END.
  }
}

