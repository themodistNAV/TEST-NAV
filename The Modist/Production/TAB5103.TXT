OBJECT Table 5103 Interaction Tmpl. Language
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               InteractionTemplate@1000 : Record 5064;
             BEGIN
               InteractionTemplate.GET("Interaction Template Code");
               IF xRec."Language Code" <> "Language Code" THEN
                 IF InteractionTemplate."Language Code (Default)" = xRec."Language Code" THEN BEGIN
                   InteractionTemplate."Language Code (Default)" := "Language Code";
                   InteractionTemplate.MODIFY;
                 END;
             END;

    CaptionML=[ENU=Interaction Tmpl. Language;
               ENG=Interaction Tmpl. Language];
    LookupPageID=Page5154;
  }
  FIELDS
  {
    { 1   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              ENG=Interaction Template Code];
                                                   Editable=No }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ENG=Language Code] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 4   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              ENG=Attachment No.] }
  }
  KEYS
  {
    {    ;Interaction Template Code,Language Code ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AttachmentRecord@1003 : Record 5062;
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;ENG=You have cancelled the create process.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;ENG=Replace existing attachment?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;ENG=You have cancelled the import process.';
      Text005@1004 : TextConst 'ENU=Export Attachment;ENG=Export Attachment';

    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 5062;
      InteractTmplLanguage@1004 : Record 5103;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      NewAttachNo := WordManagement.CreateWordAttachment("Interaction Template Code" + ' ' + Description,"Language Code");
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        IF InteractTmplLanguage.GET("Interaction Template Code","Language Code") THEN
          MODIFY
        ELSE
          INSERT;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("Interaction Template Code" + ' ' + Description,FALSE,"Language Code");
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      InteractTmplLanguage@1000 : Record 5103;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      InteractTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,InteractTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(InteractTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      TempBlob@1002 : Record 99008535;
      MarketingSetup@1004 : Record 5079;
      FileMgt@1003 : Codeunit 419;
      FileName@1001 : Text[1024];
      FileFilter@1005 : Text;
      ExportToFile@1006 : Text;
    BEGIN
      MarketingSetup.GET;
      ExportToFile := '';
      IF AttachmentRecord.GET("Attachment No.") THEN
        WITH AttachmentRecord DO BEGIN
          IF "Storage Type" = "Storage Type"::Embedded THEN BEGIN
            CALCFIELDS(Attachment);
            IF Attachment.HASVALUE THEN BEGIN
              FileName := "Interaction Template Code" + '.' + "File Extension";
              TempBlob.Blob := Attachment;
              FileMgt.BLOBExport(TempBlob,FileName,TRUE);
            END;
          END ELSE BEGIN
            IF "Storage Type" = "Storage Type"::"Disk File" THEN BEGIN
              IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
                MarketingSetup.TESTFIELD("Attachment Storage Location");
              FileFilter := UPPERCASE("File Extension") + ' (*.' + "File Extension" + ')|*.' + "File Extension";
              ExportToFile := "Interaction Template Code" + '.' + "File Extension";
              DOWNLOAD(ConstDiskFileName,Text005,'',FileFilter,ExportToFile);
            END;
          END;
        END;
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    LOCAL PROCEDURE ConstDiskFileName@4() DiskFileName@1000 : Text[1024];
    BEGIN
      DiskFileName := AttachmentRecord."Storage Pointer" + '\' + FORMAT(AttachmentRecord."No.") + '.';
    END;

    BEGIN
    END.
  }
}

