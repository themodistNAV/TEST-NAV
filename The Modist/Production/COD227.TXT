OBJECT Codeunit 227 VendEntry-Apply Posted Entries
{
  OBJECT-PROPERTIES
  {
    Date=29/01/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44974;
  }
  PROPERTIES
  {
    TableNo=25;
    Permissions=TableData 25=rimd;
    OnRun=BEGIN
            Apply(Rec,"Document No.",0D);
          END;

  }
  CODE
  {
    VAR
      PostingApplicationMsg@1000 : TextConst 'ENU=Posting application...;ENG=Posting application...';
      MustNotBeBeforeErr@1002 : TextConst 'ENU=The posting date entered must not be before the posting date on the vendor ledger entry.;ENG=The posting date entered must not be before the posting date on the vendor ledger entry.';
      NoEntriesAppliedErr@1003 : TextConst 'ENU=The application could not be posted, because one or more selected entries have the same sign as the entry you are applying to.;ENG=The application could not be posted, because one or more selected entries have the same sign as the entry you are applying to.';
      UnapplyPostedAfterThisEntryErr@1018 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries that were posted after this entry.;ENG=Before you can unapply this entry, you must first unapply all application entries that were posted after this entry.';
      NoApplicationEntryErr@1017 : TextConst 'ENU=Vendor Ledger Entry No. %1 does not have an application entry.;ENG=Vendor Ledger Entry No. %1 does not have an application entry.';
      UnapplyingMsg@1015 : TextConst 'ENU=Unapplying and posting...;ENG=Unapplying and posting...';
      UnapplyAllPostedAfterThisEntryErr@1021 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries in Vendor Ledger Entry No. %1 that were posted after this entry.;ENG=Before you can unapply this entry, you must first unapply all application entries in Vendor Ledger Entry No. %1 that were posted after this entry.';
      NotAllowedPostingDatesErr@1019 : TextConst 'ENU=Posting date is not within the range of allowed posting dates.;ENG=Posting date is not within the range of allowed posting dates.';
      LatestEntryMustBeApplicationErr@1023 : TextConst 'ENU=The latest Transaction No. must be an application in Vendor Ledger Entry No. %1.;ENG=The latest Transaction No. must be an application in Vendor Ledger Entry No. %1.';
      CannotUnapplyExchRateErr@1025 : TextConst 'ENU=You cannot unapply the entry with the posting date %1, because the exchange rate for the additional reporting currency has been changed.;ENG=You cannot unapply the entry with the posting date %1, because the exchange rate for the additional reporting currency has been changed.';
      CannotUnapplyInReversalErr@1026 : TextConst 'ENU=You cannot unapply Vendor Ledger Entry No. %1 because the entry is part of a reversal.;ENG=You cannot unapply Vendor Ledger Entry No. %1 because the entry is part of a reversal.';
      CannotApplyClosedEntriesErr@1102601000 : TextConst 'ENU=One or more of the entries that you selected is closed. You cannot apply closed entries.;ENG=One or more of the entries that you selected is closed. You cannot apply closed entries.';
      PreviewMode@1001 : Boolean;

    [TryFunction]
    PROCEDURE Apply@17(VendLedgEntry@1000 : Record 25;DocumentNo@1001 : Code[20];ApplicationDate@1002 : Date);
    VAR
      PaymentToleranceMgt@1011 : Codeunit 426;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        PaymentToleranceMgt.ApplyPostedEntriesMode;
        IF NOT PreviewMode THEN
          IF NOT PaymentToleranceMgt.PmtTolVend(VendLedgEntry) THEN
            EXIT;
        GET("Entry No.");

        IF ApplicationDate = 0D THEN
          ApplicationDate := GetApplicationDate(VendLedgEntry)
        ELSE
          IF ApplicationDate < GetApplicationDate(VendLedgEntry) THEN
            ERROR(MustNotBeBeforeErr);

        IF DocumentNo = '' THEN
          DocumentNo := "Document No.";

        VendPostApplyVendLedgEntry(VendLedgEntry,DocumentNo,ApplicationDate);
      END;
    END;

    PROCEDURE GetApplicationDate@14(VendLedgEntry@1000 : Record 25) ApplicationDate : Date;
    VAR
      ApplyToVendLedgEntry@1001 : Record 25;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        ApplicationDate := 0D;
        ApplyToVendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID");
        ApplyToVendLedgEntry.SETRANGE("Vendor No.","Vendor No.");
        ApplyToVendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
        ApplyToVendLedgEntry.FIND('-');
        REPEAT
          IF ApplyToVendLedgEntry."Posting Date" > ApplicationDate THEN
            ApplicationDate := ApplyToVendLedgEntry."Posting Date";
        UNTIL ApplyToVendLedgEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE VendPostApplyVendLedgEntry@12(VendLedgEntry@1000 : Record 25;DocumentNo@1002 : Code[20];ApplicationDate@1003 : Date);
    VAR
      SourceCodeSetup@1004 : Record 242;
      GenJnlLine@1005 : Record 81;
      UpdateAnalysisView@1001 : Codeunit 410;
      GenJnlPostLine@1006 : Codeunit 12;
      GenJnlPostPreview@1008 : Codeunit 19;
      Window@1007 : Dialog;
      EntryNoBeforeApplication@1010 : Integer;
      EntryNoAfterApplication@1009 : Integer;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        Window.OPEN(PostingApplicationMsg);

        SourceCodeSetup.GET;

        GenJnlLine.INIT;
        GenJnlLine."Document No." := DocumentNo;
        GenJnlLine."Posting Date" := ApplicationDate;
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Vendor;
        GenJnlLine."Account No." := "Vendor No.";
        CALCFIELDS("Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");
        GenJnlLine.Correction :=
          ("Debit Amount" < 0) OR ("Credit Amount" < 0) OR
          ("Debit Amount (LCY)" < 0) OR ("Credit Amount (LCY)" < 0);
        GenJnlLine."Document Type" := "Document Type";
        GenJnlLine.Description := Description;
        GenJnlLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := "Dimension Set ID";
        GenJnlLine."Posting Group" := "Vendor Posting Group";
        GenJnlLine."Source Type" := GenJnlLine."Source Type"::Vendor;
        GenJnlLine."Source No." := "Vendor No.";
        GenJnlLine."Source Code" := SourceCodeSetup."Purchase Entry Application";
        GenJnlLine."System-Created Entry" := TRUE;

        EntryNoBeforeApplication := FindLastApplDtldVendLedgEntry;

        GenJnlPostLine.VendPostApplyVendLedgEntry(GenJnlLine,VendLedgEntry);

        EntryNoAfterApplication := FindLastApplDtldVendLedgEntry;
        IF EntryNoAfterApplication = EntryNoBeforeApplication THEN
          ERROR(NoEntriesAppliedErr);

        IF PreviewMode THEN
          ERROR(GenJnlPostPreview.GetPreviewModeErrMessage);

        COMMIT;
        Window.CLOSE;
        UpdateAnalysisView.UpdateAll(0,TRUE);
      END;
    END;

    LOCAL PROCEDURE FindLastApplDtldVendLedgEntry@1() : Integer;
    VAR
      DtldVendLedgEntry@1000 : Record 380;
    BEGIN
      DtldVendLedgEntry.LOCKTABLE;
      IF DtldVendLedgEntry.FINDLAST THEN
        EXIT(DtldVendLedgEntry."Entry No.");

      EXIT(0);
    END;

    LOCAL PROCEDURE FindLastApplEntry@2(VendLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record 380;
      ApplicationEntryNo@1000 : Integer;
    BEGIN
      WITH DtldVendLedgEntry DO BEGIN
        SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
        SETRANGE("Entry Type","Entry Type"::Application);
        SETRANGE(Unapplied,FALSE);
        ApplicationEntryNo := 0;
        IF FIND('-') THEN
          REPEAT
            IF "Entry No." > ApplicationEntryNo THEN
              ApplicationEntryNo := "Entry No.";
          UNTIL NEXT = 0;
      END;
      EXIT(ApplicationEntryNo);
    END;

    LOCAL PROCEDURE FindLastTransactionNo@6(VendLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record 380;
      LastTransactionNo@1000 : Integer;
    BEGIN
      WITH DtldVendLedgEntry DO BEGIN
        SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
        SETRANGE(Unapplied,FALSE);
        SETFILTER("Entry Type",'<>%1&<>%2',"Entry Type"::"Unrealized Loss","Entry Type"::"Unrealized Gain");
        LastTransactionNo := 0;
        IF FINDSET THEN
          REPEAT
            IF LastTransactionNo < "Transaction No." THEN
              LastTransactionNo := "Transaction No.";
          UNTIL NEXT = 0;
      END;
      EXIT(LastTransactionNo);
    END;

    PROCEDURE UnApplyDtldVendLedgEntry@3(DtldVendLedgEntry@1000 : Record 380);
    VAR
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      DtldVendLedgEntry.TESTFIELD("Entry Type",DtldVendLedgEntry."Entry Type"::Application);
      DtldVendLedgEntry.TESTFIELD(Unapplied,FALSE);
      ApplicationEntryNo := FindLastApplEntry(DtldVendLedgEntry."Vendor Ledger Entry No.");

      IF DtldVendLedgEntry."Entry No." <> ApplicationEntryNo THEN
        ERROR(UnapplyPostedAfterThisEntryErr);
      CheckReversal(DtldVendLedgEntry."Vendor Ledger Entry No.");
      UnApplyVendor(DtldVendLedgEntry);
    END;

    PROCEDURE UnApplyVendLedgEntry@7(VendLedgEntryNo@1000 : Integer);
    VAR
      DtldVendLedgEntry@1002 : Record 380;
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      CheckReversal(VendLedgEntryNo);
      ApplicationEntryNo := FindLastApplEntry(VendLedgEntryNo);
      IF ApplicationEntryNo = 0 THEN
        ERROR(NoApplicationEntryErr,VendLedgEntryNo);
      DtldVendLedgEntry.GET(ApplicationEntryNo);
      UnApplyVendor(DtldVendLedgEntry);
    END;

    LOCAL PROCEDURE UnApplyVendor@19(DtldVendLedgEntry@1000000000 : Record 380);
    VAR
      UnapplyVendEntries@1000 : Page 624;
    BEGIN
      WITH DtldVendLedgEntry DO BEGIN
        TESTFIELD("Entry Type","Entry Type"::Application);
        TESTFIELD(Unapplied,FALSE);
        UnapplyVendEntries.SetDtldVendLedgEntry("Entry No.");
        UnapplyVendEntries.LOOKUPMODE(TRUE);
        UnapplyVendEntries.RUNMODAL;
      END;
    END;

    [TryFunction]
    PROCEDURE PostUnApplyVendor@4(DtldVendLedgEntry2@1007 : Record 380;DocNo@1000 : Code[20];PostingDate@1001 : Date);
    VAR
      GLEntry@1013 : Record 17;
      VendLedgEntry@1004 : Record 25;
      DtldVendLedgEntry@1010 : Record 380;
      SourceCodeSetup@1006 : Record 242;
      GenJnlLine@1005 : Record 81;
      DateComprReg@1014 : Record 87;
      GenJnlPostLine@1003 : Codeunit 12;
      GenJnlPostPreview@1011 : Codeunit 19;
      Window@1002 : Dialog;
      LastTransactionNo@1009 : Integer;
      AddCurrChecked@1012 : Boolean;
      MaxPostingDate@1008 : Date;
    BEGIN
      MaxPostingDate := 0D;
      GLEntry.LOCKTABLE;
      DtldVendLedgEntry.LOCKTABLE;
      VendLedgEntry.LOCKTABLE;
      VendLedgEntry.GET(DtldVendLedgEntry2."Vendor Ledger Entry No.");
      CheckPostingDate(PostingDate,MaxPostingDate);
      IF PostingDate < DtldVendLedgEntry2."Posting Date" THEN
        ERROR(MustNotBeBeforeErr);
      IF DtldVendLedgEntry2."Transaction No." = 0 THEN BEGIN
        DtldVendLedgEntry.SETCURRENTKEY("Application No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Application No.",DtldVendLedgEntry2."Application No.");
      END ELSE BEGIN
        DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Transaction No.",DtldVendLedgEntry2."Transaction No.");
      END;
      DtldVendLedgEntry.SETRANGE("Vendor No.",DtldVendLedgEntry2."Vendor No.");
      DtldVendLedgEntry.SETFILTER("Entry Type",'<>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
      DtldVendLedgEntry.SETRANGE(Unapplied,FALSE);
      IF DtldVendLedgEntry.FIND('-') THEN
        REPEAT
          IF NOT AddCurrChecked THEN BEGIN
            CheckAdditionalCurrency(PostingDate,DtldVendLedgEntry."Posting Date");
            AddCurrChecked := TRUE;
          END;
          CheckReversal(DtldVendLedgEntry."Vendor Ledger Entry No.");
          IF DtldVendLedgEntry."Transaction No." <> 0 THEN BEGIN
            IF DtldVendLedgEntry."Entry Type" = DtldVendLedgEntry."Entry Type"::Application THEN BEGIN
              LastTransactionNo :=
                FindLastApplTransactionEntry(DtldVendLedgEntry."Vendor Ledger Entry No.");
              IF (LastTransactionNo <> 0) AND (LastTransactionNo <> DtldVendLedgEntry."Transaction No.") THEN
                ERROR(UnapplyAllPostedAfterThisEntryErr,DtldVendLedgEntry."Vendor Ledger Entry No.");
            END;
            LastTransactionNo := FindLastTransactionNo(DtldVendLedgEntry."Vendor Ledger Entry No.");
            IF (LastTransactionNo <> 0) AND (LastTransactionNo <> DtldVendLedgEntry."Transaction No.") THEN
              ERROR(LatestEntryMustBeApplicationErr,DtldVendLedgEntry."Vendor Ledger Entry No.");
          END;
        UNTIL DtldVendLedgEntry.NEXT = 0;

      DateComprReg.CheckMaxDateCompressed(MaxPostingDate,0);

      WITH DtldVendLedgEntry2 DO BEGIN
        SourceCodeSetup.GET;
        VendLedgEntry.GET("Vendor Ledger Entry No.");
        GenJnlLine."Document No." := DocNo;
        GenJnlLine."Posting Date" := PostingDate;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Vendor;
        GenJnlLine."Account No." := "Vendor No.";
        GenJnlLine.Correction := TRUE;
        GenJnlLine."Document Type" := "Document Type";
        GenJnlLine.Description := VendLedgEntry.Description;
        GenJnlLine."Shortcut Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := VendLedgEntry."Dimension Set ID";
        GenJnlLine."Posting Group" := VendLedgEntry."Vendor Posting Group";
        GenJnlLine."Source Type" := GenJnlLine."Source Type"::Vendor;
        GenJnlLine."Source No." := "Vendor No.";
        GenJnlLine."Source Code" := SourceCodeSetup."Unapplied Purch. Entry Appln.";
        GenJnlLine."Source Currency Code" := "Currency Code";
        GenJnlLine."System-Created Entry" := TRUE;
        Window.OPEN(UnapplyingMsg);
        GenJnlPostLine.UnapplyVendLedgEntry(GenJnlLine,DtldVendLedgEntry2);

        IF PreviewMode THEN
          ERROR(GenJnlPostPreview.GetPreviewModeErrMessage);

        COMMIT;
        Window.CLOSE;
      END;
    END;

    LOCAL PROCEDURE CheckPostingDate@5(PostingDate@1001 : Date;VAR MaxPostingDate@1005 : Date);
    VAR
      GenJnlCheckLine@1000 : Codeunit 11;
    BEGIN
      IF GenJnlCheckLine.DateNotAllowed(PostingDate) THEN
        ERROR(NotAllowedPostingDatesErr);

      IF PostingDate > MaxPostingDate THEN
        MaxPostingDate := PostingDate;
    END;

    LOCAL PROCEDURE CheckAdditionalCurrency@8(OldPostingDate@1000 : Date;NewPostingDate@1001 : Date);
    VAR
      GLSetup@1002 : Record 98;
      CurrExchRate@1003 : Record 330;
    BEGIN
      IF OldPostingDate = NewPostingDate THEN
        EXIT;
      GLSetup.GET;
      IF GLSetup."Additional Reporting Currency" <> '' THEN
        IF CurrExchRate.ExchangeRate(OldPostingDate,GLSetup."Additional Reporting Currency") <>
           CurrExchRate.ExchangeRate(NewPostingDate,GLSetup."Additional Reporting Currency")
        THEN
          ERROR(CannotUnapplyExchRateErr,NewPostingDate);
    END;

    LOCAL PROCEDURE CheckReversal@9(VendLedgEntryNo@1000 : Integer);
    VAR
      VendLedgEntry@1001 : Record 25;
    BEGIN
      VendLedgEntry.GET(VendLedgEntryNo);
      IF VendLedgEntry.Reversed THEN
        ERROR(CannotUnapplyInReversalErr,VendLedgEntryNo);
    END;

    PROCEDURE ApplyVendEntryFormEntry@10(VAR ApplyingVendLedgEntry@1000 : Record 25);
    VAR
      VendLedgEntry@1002 : Record 25;
      ApplyVendEntries@1001 : Page 233;
      VendEntryApplID@1004 : Code[50];
    BEGIN
      IF NOT ApplyingVendLedgEntry.Open THEN
        ERROR(CannotApplyClosedEntriesErr);

      VendEntryApplID := USERID;
      IF VendEntryApplID = '' THEN
        VendEntryApplID := '***';
      IF ApplyingVendLedgEntry."Remaining Amount" = 0 THEN
        ApplyingVendLedgEntry.CALCFIELDS("Remaining Amount");

      ApplyingVendLedgEntry."Applying Entry" := TRUE;
      IF ApplyingVendLedgEntry."Applies-to ID" = '' THEN
        ApplyingVendLedgEntry."Applies-to ID" := VendEntryApplID;
      ApplyingVendLedgEntry."Amount to Apply" := ApplyingVendLedgEntry."Remaining Amount";
      CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",ApplyingVendLedgEntry);
      COMMIT;

      VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
      VendLedgEntry.SETRANGE("Vendor No.",ApplyingVendLedgEntry."Vendor No.");
      VendLedgEntry.SETRANGE(Open,TRUE);
      IF VendLedgEntry.FINDFIRST THEN BEGIN
        ApplyVendEntries.SetVendLedgEntry(ApplyingVendLedgEntry);
        ApplyVendEntries.SETRECORD(VendLedgEntry);
        ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
        IF ApplyingVendLedgEntry."Applies-to ID" <> VendEntryApplID THEN
          ApplyVendEntries.SetAppliesToID(ApplyingVendLedgEntry."Applies-to ID");
        ApplyVendEntries.RUNMODAL;
        CLEAR(ApplyVendEntries);
        ApplyingVendLedgEntry."Applying Entry" := FALSE;
        ApplyingVendLedgEntry."Applies-to ID" := '';
        ApplyingVendLedgEntry."Amount to Apply" := 0;
      END;
    END;

    LOCAL PROCEDURE FindLastApplTransactionEntry@11(VendLedgEntryNo@1000 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record 380;
      LastTransactionNo@1002 : Integer;
    BEGIN
      DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
      DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
      DtldVendLedgEntry.SETRANGE("Entry Type",DtldVendLedgEntry."Entry Type"::Application);
      LastTransactionNo := 0;
      IF DtldVendLedgEntry.FIND('-') THEN
        REPEAT
          IF (DtldVendLedgEntry."Transaction No." > LastTransactionNo) AND NOT DtldVendLedgEntry.Unapplied THEN
            LastTransactionNo := DtldVendLedgEntry."Transaction No.";
        UNTIL DtldVendLedgEntry.NEXT = 0;
      EXIT(LastTransactionNo);
    END;

    PROCEDURE PreviewApply@16(VendorLedgerEntry@1003 : Record 25;DocumentNo@1002 : Code[20];ApplicationDate@1001 : Date);
    VAR
      GenJnlPostPreview@1004 : Codeunit 19;
    BEGIN
      PreviewMode := TRUE;
      GenJnlPostPreview.Start;
      IF NOT Apply(VendorLedgerEntry,DocumentNo,ApplicationDate) THEN BEGIN
        GenJnlPostPreview.Finish;
        IF GETLASTERRORTEXT <> GenJnlPostPreview.GetPreviewModeErrMessage THEN
          ERROR(GETLASTERRORTEXT);
        GenJnlPostPreview.ShowAllEntries;
        ERROR('');
      END;
    END;

    PROCEDURE PreviewUnapply@15(DetailedVendorLedgEntry@1002 : Record 380;DocumentNo@1001 : Code[20];ApplicationDate@1000 : Date);
    VAR
      GenJnlPostPreview@1003 : Codeunit 19;
    BEGIN
      PreviewMode := TRUE;
      GenJnlPostPreview.Start;
      IF NOT PostUnApplyVendor(DetailedVendorLedgEntry,DocumentNo,ApplicationDate) THEN BEGIN
        GenJnlPostPreview.Finish;
        IF GETLASTERRORTEXT <> GenJnlPostPreview.GetPreviewModeErrMessage THEN
          ERROR(GETLASTERRORTEXT);
        GenJnlPostPreview.ShowAllEntries;
        ERROR('');
      END;
    END;

    BEGIN
    END.
  }
}

