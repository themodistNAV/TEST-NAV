OBJECT Codeunit 416 Release Service Document
{
  OBJECT-PROPERTIES
  {
    Date=23/03/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45480;
  }
  PROPERTIES
  {
    TableNo=5900;
    OnRun=VAR
            ServLine@1000 : Record 5902;
            TempVATAmountLine0@1001 : TEMPORARY Record 290;
            TempVATAmountLine1@1002 : TEMPORARY Record 290;
          BEGIN
            IF "Release Status" = "Release Status"::"Released to Ship" THEN
              EXIT;
            IF "Document Type" = "Document Type"::Quote THEN
              TESTFIELD("Bill-to Customer No.");
            ServLine.SETRANGE("Document Type","Document Type");
            ServLine.SETRANGE("Document No.","No.");
            ServLine.SETFILTER(Type,'<>%1',ServLine.Type::" ");
            ServLine.SETFILTER(Quantity,'<>0');
            IF ServLine.ISEMPTY THEN
              ERROR(Text001,"Document Type","No.");
            InvtSetup.GET;
            IF InvtSetup."Location Mandatory" THEN BEGIN
              ServLine.SETCURRENTKEY(Type);
              ServLine.SETRANGE(Type,ServLine.Type::Item);
              IF ServLine.FINDSET THEN
                REPEAT
                  ServLine.TESTFIELD("Location Code");
                UNTIL ServLine.NEXT = 0;
              ServLine.SETFILTER(Type,'<>%1',ServLine.Type::" ");
            END;
            ServLine.RESET;
            VALIDATE("Release Status","Release Status"::"Released to Ship");
            ServLine.SetServHeader(Rec);
            ServLine.CalcVATAmountLines(0,Rec,ServLine,TempVATAmountLine0,ServLine.IsShipment);
            ServLine.CalcVATAmountLines(1,Rec,ServLine,TempVATAmountLine1,ServLine.IsShipment);
            ServLine.UpdateVATOnLines(0,Rec,ServLine,TempVATAmountLine0);
            ServLine.UpdateVATOnLines(1,Rec,ServLine,TempVATAmountLine1);
            MODIFY(TRUE);

            IF "Document Type" = "Document Type"::Order THEN
              WhseServiceRelease.Release(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst '@@@=Example: There is nothing to release for Order 12345.;ENU=There is nothing to release for %1 %2.;ENG=There is nothing to release for %1 %2.';
      InvtSetup@1006 : Record 313;
      WhseServiceRelease@1007 : Codeunit 5770;

    PROCEDURE Reopen@3(VAR ServHeader@1000 : Record 5900);
    BEGIN
      WITH ServHeader DO BEGIN
        IF "Release Status" = "Release Status"::Open THEN
          EXIT;
        VALIDATE("Release Status","Release Status"::Open);
        MODIFY(TRUE);
        IF "Document Type" IN ["Document Type"::Order] THEN
          WhseServiceRelease.Reopen(ServHeader);
      END;
    END;

    PROCEDURE PerformManualRelease@1(VAR ServHeader@1000 : Record 5900);
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Release Service Document",ServHeader);
    END;

    PROCEDURE PerformManualReopen@6(VAR ServHeader@1000 : Record 5900);
    BEGIN
      Reopen(ServHeader);
    END;

    BEGIN
    END.
  }
}

