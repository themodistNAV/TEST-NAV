OBJECT Codeunit 9062650 POS UI Management
{
  OBJECT-PROPERTIES
  {
    Date=11/02/16;
    Time=12:00:00 PM;
    Version List=NH2016.12;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      cnfCreateButtons@9062231 : TextConst 'ENU="Do you want to create buttons for Buttons Gropup ''%1''? ";ENG="Do you want to create buttons for Buttons Gropup ''%1''? "';

    PROCEDURE CreateButtons@9062231(VAR ButtonGroup@9062230 : Record 9062787);
    VAR
      Button@9062231 : Record 9062788;
      RowNo@9062232 : Integer;
      ColumnNo@9062233 : Integer;
      ButtonNo@9062234 : Integer;
    BEGIN
      IF (GUIALLOWED) THEN
        IF (NOT(CONFIRM(STRSUBSTNO(cnfCreateButtons, ButtonGroup.Code)))) THEN
          EXIT;

      Button.RESET();
      Button.SETRANGE("Button Group Code", ButtonGroup.Code);

      Button.SETFILTER("Column No.", '>%1', ButtonGroup."No. of Columns");
      Button.DELETEALL(TRUE);
      Button.SETRANGE("Column No.");

      Button.SETFILTER("Row No.", '>%1', ButtonGroup."No. of Rows");
      Button.DELETEALL(TRUE);
      Button.SETRANGE("Row No.");

      ButtonNo := 0;
      IF (Button.FINDLAST()) THEN
        ButtonNo := Button."No.";

      FOR RowNo := 1 TO ButtonGroup."No. of Rows" DO BEGIN
        FOR ColumnNo := 1 TO ButtonGroup."No. of Columns" DO BEGIN
          Button.RESET();
          Button.SETRANGE("Button Group Code", ButtonGroup.Code);
          Button.SETRANGE("Row No.", RowNo);
          Button.SETRANGE("Column No.", ColumnNo);
          IF (Button.ISEMPTY) THEN BEGIN
            ButtonNo += 1;
            Button.INIT();
            Button."Button Group Code" := ButtonGroup.Code;
            Button."No." := ButtonNo;
            Button."Row No." := RowNo;
            Button."Column No." := ColumnNo;
            Button.INSERT();
          END;
        END;
      END;
    END;

    PROCEDURE CopyButtonsGroup@9062230(VAR SrcButtonGroup@9062230 : Record 9062787;VAR DestBtnGroup@9062231 : Record 9062787);
    VAR
      SrcButton@9062232 : Record 9062788;
      DestBtn@9062233 : Record 9062788;
      SrcButtonCmd@9062234 : Record 9062790;
      DestButtonCmd@9062235 : Record 9062790;
    BEGIN
      DestBtn.RESET();
      DestBtn.SETRANGE("Button Group Code", DestBtnGroup.Code);
      DestBtn.DELETEALL(TRUE);

      DestBtnGroup."No. of Rows" := SrcButtonGroup."No. of Rows";
      DestBtnGroup."No. of Columns" := SrcButtonGroup."No. of Columns";
      DestBtnGroup.MODIFY();

      SrcButton.RESET();
      SrcButton.SETRANGE("Button Group Code", SrcButtonGroup.Code);
      IF (SrcButton.FINDSET()) THEN BEGIN
        REPEAT
          SrcButton.CALCFIELDS("Icon Image");
          DestBtn.TRANSFERFIELDS(SrcButton);
          DestBtn."Button Group Code" := DestBtnGroup.Code;
          DestBtn.INSERT();

          SrcButtonCmd.RESET();
          SrcButtonCmd.SETRANGE("Button Group Code", SrcButton."Button Group Code");
          SrcButtonCmd.SETRANGE("Button No.", SrcButton."No.");
          IF (SrcButtonCmd.FINDSET()) THEN BEGIN
            REPEAT
              DestButtonCmd.TRANSFERFIELDS(SrcButtonCmd);
              DestButtonCmd."Button Group Code" := DestBtn."Button Group Code";
              DestButtonCmd."Button No." := DestBtn."No.";
              DestButtonCmd.INSERT();
            UNTIL (SrcButtonCmd.NEXT() = 0);
          END;

        UNTIL (SrcButton.NEXT() = 0);
      END;
    END;

    BEGIN
    {
      //DOC NH2016.12 BM 11/02/2016 - Created
    }
    END.
  }
}

