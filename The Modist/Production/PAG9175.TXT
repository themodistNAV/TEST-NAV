OBJECT Page 9175 Connect Online
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Connect Online;
               ENG=Connect Online];
    PageType=CardPart;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                CaptionML=[ENU=Connect Online;
                           ENG=Connect Online];
                SourceExpr=Parameters;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.DynamicsOnlineConnect;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      CurrentRole@1000 : Code[30];
      CurrentRoleID@1001 : Integer;
      CurrentLanguageID@1002 : Integer;
      Text000@1003 : TextConst '@@@="version=";ENU="version=";ENG="version="';
      Text001@1004 : TextConst '@@@="locale=";ENU="locale=";ENG="locale="';
      Text002@1005 : TextConst '@@@="role=";ENU="role=";ENG="role="';
      Text003@1006 : TextConst '@@@="roleid=";ENU="roleid=";ENG="roleid="';
      Text004@1007 : TextConst '@@@="Serial=";ENU="Serial=";ENG="Serial="';

    LOCAL PROCEDURE Parameters@26() : Text[350];
    BEGIN
      InitCurrentRoleValues;

      EXIT(Add(Version) + Add(Locale) + Add(Role) + Add(RoleID) + Add(Serial));
    END;

    LOCAL PROCEDURE Add@23(Parameter@1000 : Text[200]) : Text[200];
    BEGIN
      EXIT(Parameter + Separator);
    END;

    LOCAL PROCEDURE Version@1() : Text[170];
    VAR
      ApplicationManagement@1000 : Codeunit 1;
    BEGIN
      EXIT(Text000 + FORMAT(ApplicationManagement.ApplicationVersion + ':' + ApplicationManagement.ApplicationBuild,0,XMLFormat));
    END;

    LOCAL PROCEDURE Locale@2() : Text[30];
    BEGIN
      // Windows Language ID
      EXIT(Text001 + FORMAT(CurrentLanguageID,0,XMLFormat));
    END;

    LOCAL PROCEDURE Role@4() : Text[40];
    BEGIN
      // Profile ID (Any text entered in Profile ID)
      EXIT(Text002 + FORMAT(DELCHR(CurrentRole,'=',Separator),0,XMLFormat));
    END;

    LOCAL PROCEDURE RoleID@7() : Text[30];
    BEGIN
      // Role Center ID (Page ID)
      EXIT(Text003 + FORMAT(CurrentRoleID,0,XMLFormat));
    END;

    LOCAL PROCEDURE Serial@6() : Text[80];
    BEGIN
      // License ID
      EXIT(Text004 + FORMAT(SERIALNUMBER,0,XMLFormat));
    END;

    LOCAL PROCEDURE Separator@15() : Text[1];
    BEGIN
      EXIT(';');
    END;

    LOCAL PROCEDURE XMLFormat@5() : Integer;
    BEGIN
      EXIT(9);
    END;

    LOCAL PROCEDURE InitCurrentRoleValues@9();
    VAR
      UserPersonalization@1001 : Record 2000000073;
      Profile@1002 : Record 2000000072;
      ApplicationManagement@1000 : Codeunit 1;
    BEGIN
      CurrentLanguageID := GLOBALLANGUAGE;
      CurrentRoleID := ApplicationManagement.DefaultRoleCenter;
      CurrentRole := FORMAT(CurrentRoleID);

      Profile.SETRANGE("Default Role Center",TRUE);
      IF Profile.FINDFIRST THEN BEGIN
        CurrentRoleID := Profile."Role Center ID";
        CurrentRole := Profile."Profile ID";
      END;

      UserPersonalization.SETFILTER("User ID",'%1',UserFilter);
      IF UserPersonalization.FINDFIRST THEN BEGIN
        IF (UserPersonalization."Language ID" <> 0) AND (UserPersonalization."Language ID" <> CurrentLanguageID) THEN
          CurrentLanguageID := UserPersonalization."Language ID";
        IF UserPersonalization."Profile ID" <> '' THEN BEGIN
          CurrentRole := UserPersonalization."Profile ID";
          IF Profile.GET(UserPersonalization."Profile ID") THEN
            CurrentRoleID := Profile."Role Center ID";
        END;
      END;
    END;

    LOCAL PROCEDURE UserFilter@8() : Text[80];
    BEGIN
      EXIT('*\' + USERID);
    END;

    BEGIN
    END.
  }
}

