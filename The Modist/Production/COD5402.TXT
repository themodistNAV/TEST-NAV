OBJECT Codeunit 5402 Unit of Measure Management
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ItemUnitOfMeasure@1000 : Record 5404;
      ResourceUnitOfMeasure@1001 : Record 205;
      Text001@1002 : TextConst 'ENU=Quantity per unit of measure must be defined.;ENG=Quantity per unit of measure must be defined.';

    PROCEDURE GetQtyPerUnitOfMeasure@1(Item@1000 : Record 27;UnitOfMeasureCode@1001 : Code[10]) : Decimal;
    BEGIN
      Item.TESTFIELD("No.");
      IF UnitOfMeasureCode IN [Item."Base Unit of Measure",''] THEN
        EXIT(1);
      IF (Item."No." <> ItemUnitOfMeasure."Item No.") OR
         (UnitOfMeasureCode <> ItemUnitOfMeasure.Code)
      THEN
        ItemUnitOfMeasure.GET(Item."No.",UnitOfMeasureCode);
      ItemUnitOfMeasure.TESTFIELD("Qty. per Unit of Measure");
      EXIT(ItemUnitOfMeasure."Qty. per Unit of Measure");
    END;

    PROCEDURE GetResQtyPerUnitOfMeasure@2(Resource@1000 : Record 156;UnitOfMeasureCode@1001 : Code[10]) : Decimal;
    BEGIN
      Resource.TESTFIELD("No.");
      IF UnitOfMeasureCode IN [Resource."Base Unit of Measure",''] THEN
        EXIT(1);
      IF (Resource."No." <> ResourceUnitOfMeasure."Resource No.") OR
         (UnitOfMeasureCode <> ResourceUnitOfMeasure.Code)
      THEN
        ResourceUnitOfMeasure.GET(Resource."No.",UnitOfMeasureCode);
      ResourceUnitOfMeasure.TESTFIELD("Qty. per Unit of Measure");
      EXIT(ResourceUnitOfMeasure."Qty. per Unit of Measure");
    END;

    PROCEDURE CalcBaseQty@3(Qty@1001 : Decimal;QtyPerUOM@1000 : Decimal) : Decimal;
    BEGIN
      IF QtyPerUOM = 0 THEN
        ERROR(Text001);

      EXIT(RoundQty(Qty * QtyPerUOM));
    END;

    PROCEDURE CalcQtyFromBase@10(QtyBase@1001 : Decimal;QtyPerUOM@1000 : Decimal) : Decimal;
    BEGIN
      IF QtyPerUOM = 0 THEN
        ERROR(Text001);

      EXIT(RoundQty(QtyBase / QtyPerUOM));
    END;

    PROCEDURE RoundQty@11(Qty@1000 : Decimal) : Decimal;
    BEGIN
      EXIT(ROUND(Qty,QtyRndPrecision));
    END;

    PROCEDURE QtyRndPrecision@4() : Decimal;
    BEGIN
      EXIT(0.00001);
    END;

    BEGIN
    END.
  }
}

