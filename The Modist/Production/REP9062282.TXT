OBJECT Report 9062282 Create Workflow Approval Resp.
{
  OBJECT-PROPERTIES
  {
    Date=26/08/15;
    Time=[ 2:20:39 PM];
    Version List=NA2016.4;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Workflow Approval Resp.;
               ENG=Create Workflow Approval Resp.];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   mOK := FALSE;
                   mLastTableId := 0;
                 END;

    OnPreReport=VAR
                  eReportNotInitialised@1000000000 : TextConst 'ENU=The report hasn''t been initialised. It can be only run from workflow card.;ENG=The report hasn''t been initialised. It can be only run from workflow card.';
                BEGIN
                  IF ( NOT (mInitialised)) THEN
                    ERROR(eReportNotInitialised);
                  mOK := TRUE;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=ContentAreaContainer;
                  CaptionML=ENU=Content Area;
                  ContainerType=ContentArea }

      { 1000000001;1;Field  ;
                  Name=WorkflowNoField;
                  CaptionML=ENU=Workflow No.;
                  SourceExpr=mWorkflow."No.";
                  Editable=FALSE }

      { 1000000002;1;Field  ;
                  Name=WorkflowDescriptionField;
                  CaptionML=ENU=Workflow Description;
                  SourceExpr=mWorkflow.Description;
                  Editable=FALSE }

      { 1000000005;1;Field  ;
                  Name=ResponseWorkflowNoField;
                  CaptionML=[ENU=Response Workflow No.;
                             ENG=Response Workflow No.];
                  SourceExpr=mResponseWorkflowNo;
                  TableRelation="Enhanced Workflow" }

      { 1000000003;1;Field  ;
                  Name=TableNameField;
                  CaptionML=ENU=Table;
                  SourceExpr=mTableName;
                  OnValidate=BEGIN
                               mObjectFieldToolkit.ValidateTableName(mTableId, mTableName, FALSE);
                               ValidateTable();
                             END;

                  OnLookup=BEGIN
                             IF (mObjectFieldToolkit.TableLookup(mTableId)) THEN
                             BEGIN
                               mTableName := mObjectFieldToolkit.GetTableName(mTableId);
                               ValidateTable();
                             END;
                           END;
                            }

      { 1000000004;1;Field  ;
                  Name=FieldNameFIeld;
                  CaptionML=ENU=Field;
                  SourceExpr=mFieldName;
                  OnValidate=BEGIN
                               mObjectFieldToolkit.ValidateFieldName(mTableId, mFieldId, mFieldName, FALSE);
                             END;

                  OnLookup=BEGIN
                             IF (mObjectFieldToolkit.FieldLookup(mTableId, mFieldId)) THEN
                               mFieldName := mObjectFieldToolkit.GetFieldName(mTableId, mFieldId);
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      mWorkflow@1000000000 : Record 9062258;
      mObjectFieldToolkit@1000000007 : Codeunit 9062343;
      mTableId@1000000001 : Integer;
      mTableName@1000000003 : Text;
      mFieldId@1000000002 : Integer;
      mFieldName@1000000004 : Text;
      mResponseWorkflowNo@1000000009 : Code[20];
      mOK@1000000005 : Boolean;
      mInitialised@1000000006 : Boolean;
      mLastTableId@1000000008 : Integer;
      tResponseWorkflow@1000000010 : TextConst 'ENU=RESPONSE;ENG=RESPONSE';

    PROCEDURE Initialise@1000000000(pWorkflow@1000000000 : Record 9062258);
    VAR
      lField@1000000002 : Record 2000000041;
      lWorkflow@1000000001 : Record 9062258;
    BEGIN
      mWorkflow := pWorkflow;
      mTableId := mWorkflow."Table ID";
      mTableName := mObjectFieldToolkit.GetTableName(mTableId);
      CLEAR(mFieldId);
      CLEAR(mFieldName);
      IF (mTableId <> 0) THEN
      BEGIN
        lField.RESET();
        lField.SETRANGE(TableNo, mTableId);
        lField.SETFILTER(FieldName, '@Approv*');
        IF (lField.FINDFIRST()) THEN
        BEGIN
          mFieldId := lField."No.";
          mFieldName := lField.FieldName;
        END;
      END;
      IF (lWorkflow.GET(tResponseWorkflow)) THEN
        mResponseWorkflowNo := tResponseWorkflow;
      mInitialised := TRUE;
    END;

    PROCEDURE OK@1000000001() : Boolean;
    BEGIN
      EXIT(mOK);
    END;

    PROCEDURE GetResponseWorkflowNo@1000000002() : Code[20];
    BEGIN
      EXIT(mResponseWorkflowNo);
    END;

    PROCEDURE GetTableId@1000000003() : Integer;
    BEGIN
      EXIT(mTableId);
    END;

    PROCEDURE GetFieldId@1000000004() : Integer;
    BEGIN
      EXIT(mFieldId);
    END;

    LOCAL PROCEDURE ValidateTable@1000000009();
    BEGIN
      IF (mTableId <> mLastTableId) THEN
      BEGIN
        CLEAR(mFieldId);
        CLEAR(mFieldName);
        mLastTableId := mTableId;
      END;
    END;

    BEGIN
    {
      //DOC NA2015.4 JH 02/01/2015 - Created
      //DOC NA2016.4 JH 26/08/2015 - Upgraded to 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

