OBJECT Codeunit 1321 Mini Getting Started Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            IF IsGettingStartedVisible THEN
              DisableGettingStartedForCurrentUser
            ELSE
              EnableGettingStartedForCurrentUser;

            MESSAGE(GettingStartedRefreshThePageMsg);
          END;

  }
  CODE
  {
    VAR
      GettingStartedRefreshThePageMsg@1000 : TextConst 'ENU=Refresh the page to see the change.;ENG=Refresh the page to see the change.';
      WelcomePageTxt@1001 : TextConst 'ENU=Welcome!;ENG=Welcome!';
      SettingUpYourSystemPageTxt@1002 : TextConst 'ENU=Setting up Your System;ENG=Setting up Your System';

    LOCAL PROCEDURE EnableGettingStartedForCurrentUser@9();
    VAR
      MiniUserRemovedInstructions@1000 : Record 1306;
    BEGIN
      MiniUserRemovedInstructions.EnableInstruction(GetGettingStartedCode);
    END;

    LOCAL PROCEDURE DisableGettingStartedForCurrentUser@4();
    VAR
      MiniUserRemovedInstructions@1000 : Record 1306;
    BEGIN
      MiniUserRemovedInstructions.DisableInstruction(GetGettingStartedCode);
    END;

    PROCEDURE IsGettingStartedVisible@1() : Boolean;
    VAR
      MiniUserRemovedInstructions@1000 : Record 1306;
    BEGIN
      EXIT(NOT MiniUserRemovedInstructions.GET(USERID,GetGettingStartedCode) AND NotDevice);
    END;

    PROCEDURE PlayWelcomeVideoOnFirstLogin@3();
    BEGIN
      IF ShouldWelcomeVideoBePlayed THEN BEGIN
        SetWelcomeVideoPlayed;
        PlayWelcomeVideoForWebClient;
      END;
    END;

    PROCEDURE PlayWelcomeVideoForWebClient@5();
    BEGIN
      PlayVideo(WelcomePageTxt,GetLanguageSpecificURL('https://go.microsoft.com/fwlink/?LinkID=506729'));
    END;

    PROCEDURE PlaySettingUpYourSystemVideoForWebClient@10();
    BEGIN
      PlayVideo(SettingUpYourSystemPageTxt,GetLanguageSpecificURL('https://go.microsoft.com/fwlink/?LinkID=506736'));
    END;

    PROCEDURE PlaySettingUpYourSystemVideoForTablet@7();
    BEGIN
      PlayVideoTablet(
        SettingUpYourSystemPageTxt,
        GetLanguageSpecificURL('https://go.microsoft.com/fwlink/?LinkID=506791'),
        GetLanguageSpecificURL('https://go.microsoft.com/fwlink/?LinkID=507484'));
    END;

    LOCAL PROCEDURE PlayVideo@8(PageCaption@1001 : Text;Src@1000 : Text);
    VAR
      MiniVideoPlayerPage@1006 : Page 1395;
      Height@1005 : Integer;
      Width@1004 : Integer;
    BEGIN
      Height := 415;
      Width := 740;

      MiniVideoPlayerPage.SetParameters(Height,Width,Src,PageCaption);
      MiniVideoPlayerPage.RUN;
    END;

    LOCAL PROCEDURE PlayVideoTablet@12(PageCaption@1001 : Text;Src@1000 : Text;SrcLink@1002 : Text);
    VAR
      MiniVideoPlayerPageTablet@1006 : Page 1396;
      Height@1005 : Integer;
      Width@1004 : Integer;
    BEGIN
      Height := 415;
      Width := 740;

      MiniVideoPlayerPageTablet.SetParameters(Height,Width,Src,SrcLink,PageCaption);
      MiniVideoPlayerPageTablet.RUN;
    END;

    PROCEDURE ShouldWelcomeVideoBePlayed@18() : Boolean;
    VAR
      MiniUserRemovedInstructions@1000 : Record 1306;
    BEGIN
      EXIT(NOT MiniUserRemovedInstructions.GET(USERID,GetWelcomeVideoCode));
    END;

    LOCAL PROCEDURE SetWelcomeVideoPlayed@19();
    VAR
      MiniUserRemovedInstructions@1000 : Record 1306;
    BEGIN
      MiniUserRemovedInstructions.DisableInstruction(GetWelcomeVideoCode);
    END;

    LOCAL PROCEDURE GetGettingStartedCode@2() : Code[20];
    BEGIN
      EXIT('GETTINGSTARTED');
    END;

    LOCAL PROCEDURE GetWelcomeVideoCode@15() : Code[20];
    BEGIN
      EXIT('WELCOMEVIDEOPLAYED');
    END;

    LOCAL PROCEDURE GetLanguageSpecificURL@14(NonLanguageSpecificURL@1000 : Text) : Text;
    BEGIN
      EXIT(NonLanguageSpecificURL + GetLanguageURLParameter);
    END;

    LOCAL PROCEDURE GetLanguageURLParameter@6() : Text;
    VAR
      Convert@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      LanguageHexaDecimalCode@1001 : Text;
    BEGIN
      LanguageHexaDecimalCode := Convert.ToString(GLOBALLANGUAGE,16);
      EXIT(STRSUBSTNO('&clcid=0x%1',LanguageHexaDecimalCode));
    END;

    LOCAL PROCEDURE NotDevice@11() : Boolean;
    BEGIN
      EXIT(NOT (CURRENTCLIENTTYPE IN [CLIENTTYPE::Tablet,CLIENTTYPE::Phone]));
    END;

    BEGIN
    END.
  }
}

