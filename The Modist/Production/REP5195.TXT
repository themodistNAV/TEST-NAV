OBJECT Report 5195 Create Conts. from Customers
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Conts. from Customers;
               ENG=Create Conts. from Customers];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RMSetup.GET;
                  RMSetup.TESTFIELD("Bus. Rel. Code for Customers");
                END;

  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000 +
                                 Text001,"No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1);

                                  WITH ContBusRel DO BEGIN
                                    SETRANGE("Link to Table","Link to Table"::Customer);
                                    SETRANGE("No.",Customer."No.");
                                    IF FINDFIRST THEN
                                      CurrReport.SKIP;
                                  END;

                                  Cont.INIT;
                                  Cont.TRANSFERFIELDS(Customer);
                                  Cont."No." := '';
                                  Cont.SetSkipDefault(FALSE);
                                  Cont.INSERT(TRUE);
                                  DuplMgt.MakeContIndex(Cont);

                                  IF NOT DuplicateContactExist THEN
                                    DuplicateContactExist := DuplMgt.DuplicateExist(Cont);

                                  WITH ContBusRel DO BEGIN
                                    INIT;
                                    "Contact No." := Cont."No.";
                                    "Business Relation Code" := RMSetup."Bus. Rel. Code for Customers";
                                    "Link to Table" := "Link to Table"::Customer;
                                    "No." := Customer."No.";
                                    INSERT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF DuplicateContactExist THEN BEGIN
                                  COMMIT;
                                  PAGE.RUNMODAL(PAGE::"Contact Duplicates");
                                END;
                              END;

               ReqFilterFields=No.,Search Name,Customer Posting Group,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing customers...\\;ENG=Processing customers...\\';
      Text001@1001 : TextConst 'ENU=Customer No.    #1##########;ENG=Customer No.    #1##########';
      RMSetup@1002 : Record 5079;
      Cont@1003 : Record 5050;
      ContBusRel@1004 : Record 5054;
      Window@1005 : Dialog;
      DuplMgt@1006 : Codeunit 5060;
      DuplicateContactExist@1007 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

