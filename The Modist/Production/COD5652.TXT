OBJECT Codeunit 5652 Insurance Jnl.-Post Line
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5635;
    Permissions=TableData 5629=rimd,
                TableData 5636=rimd;
    OnRun=BEGIN
            GLSetup.GET;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      FA@1001 : Record 5600;
      Insurance@1002 : Record 5628;
      InsuranceJnlLine@1003 : Record 5635;
      InsCoverageLedgEntry@1004 : Record 5629;
      InsCoverageLedgEntry2@1005 : Record 5629;
      InsuranceReg@1006 : Record 5636;
      InsuranceJnlCheckLine@1008 : Codeunit 5651;
      MakeInsCoverageLedgEntry@1009 : Codeunit 5657;
      NextEntryNo@1011 : Integer;

    PROCEDURE RunWithCheck@3(VAR InsuranceJnlLine2@1000 : Record 5635);
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(TRUE);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    PROCEDURE RunWithOutCheck@1(VAR InsuranceJnlLine2@1000 : Record 5635);
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(FALSE);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    LOCAL PROCEDURE Code@2(CheckLine@1000 : Boolean);
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        IF "Insurance No." = '' THEN
          EXIT;
        IF CheckLine THEN
          InsuranceJnlCheckLine.RunCheck(InsuranceJnlLine);
        Insurance.GET("Insurance No.");
        Insurance.TESTFIELD(Blocked,FALSE);
        FA.GET("FA No.");
        FA.TESTFIELD("Budgeted Asset",FALSE);
        FA.TESTFIELD(Blocked,FALSE);
        FA.TESTFIELD(Inactive,FALSE);
        MakeInsCoverageLedgEntry.CopyFromJnlLine(InsCoverageLedgEntry,InsuranceJnlLine);
        MakeInsCoverageLedgEntry.CopyFromInsuranceCard(InsCoverageLedgEntry,Insurance);
      END;
      IF NextEntryNo = 0 THEN BEGIN
        InsCoverageLedgEntry.LOCKTABLE;
        IF InsCoverageLedgEntry2.FINDLAST THEN
          NextEntryNo := InsCoverageLedgEntry2."Entry No.";
        InsuranceReg.LOCKTABLE;
        IF InsuranceReg.FINDLAST THEN
          InsuranceReg."No." := InsuranceReg."No." + 1
        ELSE
          InsuranceReg."No." := 1;
        InsuranceReg.INIT;
        InsuranceReg."From Entry No." := NextEntryNo + 1;
        InsuranceReg."Creation Date" := TODAY;
        InsuranceReg."Source Code" := InsuranceJnlLine."Source Code";
        InsuranceReg."Journal Batch Name" := InsuranceJnlLine."Journal Batch Name";
        InsuranceReg."User ID" := USERID;
      END;
      NextEntryNo := NextEntryNo + 1;
      InsCoverageLedgEntry."Entry No." := NextEntryNo;
      InsCoverageLedgEntry."Dimension Set ID" := InsuranceJnlLine."Dimension Set ID";
      InsCoverageLedgEntry.INSERT;
      IF InsuranceReg."To Entry No." = 0 THEN BEGIN
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.INSERT;
      END ELSE BEGIN
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

