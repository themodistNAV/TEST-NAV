OBJECT Codeunit 5321 Exchange Web Services Server
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Service@1000 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ExchangeServiceWrapper";

    PROCEDURE Initialize@3(AutodiscoveryEmail@1000 : Text[250];ServiceUri@1003 : Text;Credentials@1007 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.NetworkCredential";Rediscover@1001 : Boolean) Result : Boolean;
    VAR
      ServiceFactory@1002 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ServiceWrapperFactory";
    BEGIN
      IF ISNULL(Service) THEN
        Service := ServiceFactory.CreateServiceWrapper;

      IF ServiceUri <> '' THEN
        Service.ExchangeServiceUrl := ServiceUri;

      IF NOT ISNULL(Credentials) THEN
        Service.SetNetworkCredential(Credentials);

      IF (Service.ExchangeServiceUrl = '') OR Rediscover THEN
        Result := Service.AutodiscoverServiceUrl(AutodiscoveryEmail)
      ELSE
        Result := TRUE;
    END;

    PROCEDURE GetEmailFolder@4(FolderId@1000 : Text;VAR Folder@1001 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailFolder") : Boolean;
    BEGIN
      Folder := Service.GetEmailFolder(FolderId);
      EXIT(NOT ISNULL(Folder));
    END;

    PROCEDURE IdenticalMailExists@9(SampleMessage@1006 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailMessage";TargetFolder@1000 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailFolder";VAR TargetMessage@1005 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailMessage") : Boolean;
    VAR
      FindResults@1004 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IFindEmailsResults";
      Enumerator@1003 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.IEnumerator";
      FolderOffset@1002 : Integer;
    BEGIN
      TargetFolder.UseSampleEmailAsFilter(SampleMessage);
      FolderOffset := 0;
      REPEAT
        FindResults := TargetFolder.FindEmailMessages(50,FolderOffset);
        IF FindResults.TotalCount > 0 THEN BEGIN
          Enumerator := FindResults.GetEnumerator;
          WHILE Enumerator.MoveNext DO BEGIN
            TargetMessage := Enumerator.Current;
            IF SampleMessage.Subject = TargetMessage.Subject THEN
              IF SampleMessage.Body = TargetMessage.Body THEN BEGIN
                IF CompareEmailAttachments(SampleMessage,TargetMessage) THEN
                  EXIT(TRUE);
              END;
          END;
          FolderOffset := FindResults.NextPageOffset;
        END;
      UNTIL NOT FindResults.MoreAvailable;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CompareEmailAttachments@10(LeftMsg@1001 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailMessage";RightMsg@1000 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IEmailMessage") : Boolean;
    VAR
      LeftEnum@1007 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.IEnumerator";
      RightEnum@1006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.IEnumerator";
      LeftAttrib@1005 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IAttachment";
      RightAttrib@1004 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.IAttachment";
      LeftFlag@1003 : Boolean;
      RightFlag@1002 : Boolean;
    BEGIN
      LeftEnum := LeftMsg.Attachments.GetEnumerator;
      RightEnum := RightMsg.Attachments.GetEnumerator;

      LeftFlag := LeftEnum.MoveNext;
      RightFlag := RightEnum.MoveNext;
      WHILE LeftFlag AND RightFlag DO BEGIN
        LeftAttrib := LeftEnum.Current;
        RightAttrib := RightEnum.Current;
        IF (LeftAttrib.ContentId <> RightAttrib.ContentId) OR (LeftAttrib.ContentType <> RightAttrib.ContentType) THEN
          EXIT(FALSE);

        LeftFlag := LeftEnum.MoveNext;
        RightFlag := RightEnum.MoveNext;
      END;

      EXIT(LeftFlag = RightFlag);
    END;

    BEGIN
    END.
  }
}

