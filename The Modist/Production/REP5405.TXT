OBJECT Report 5405 Calc. Consumption
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calc. Consumption;
               ENG=Calc. Consumption];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4824;    ;DataItem;                    ;
               DataItemTable=Table5405;
               DataItemTableView=SORTING(Status,No.)
                                 WHERE(Status=CONST(Released));
               OnPreDataItem=BEGIN
                               ItemJnlLine.SETRANGE("Journal Template Name",ToTemplateName);
                               ItemJnlLine.SETRANGE("Journal Batch Name",ToBatchName);
                               IF ItemJnlLine.FINDLAST THEN
                                 NextConsumpJnlLineNo := ItemJnlLine."Line No." + 10000
                               ELSE
                                 NextConsumpJnlLineNo := 10000;

                               Window.OPEN(
                                 Text000 +
                                 Text001 +
                                 Text002 +
                                 Text003);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                END;

               ReqFilterFields=No. }

    { 7771;1   ;DataItem;                    ;
               DataItemTable=Table5407;
               OnPreDataItem=BEGIN
                               SETFILTER("Flushing Method",'<>%1&<>%2',"Flushing Method"::Backward,"Flushing Method"::"Pick + Backward");
                             END;

               OnAfterGetRecord=VAR
                                  NeededQty@1000 : Decimal;
                                BEGIN
                                  Window.UPDATE(2,"Item No.");

                                  CLEAR(ItemJnlLine);
                                  Item.GET("Item No.");
                                  ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.");

                                  NeededQty := GetNeededQty(CalcBasedOn,TRUE);

                                  IF NeededQty <> 0 THEN BEGIN
                                    IF LocationCode <> '' THEN
                                      CreateConsumpJnlLine(LocationCode,'',NeededQty)
                                    ELSE
                                      CreateConsumpJnlLine("Location Code","Bin Code",NeededQty);
                                    LastItemJnlLine := ItemJnlLine;
                                  END;
                                END;

               ReqFilterFields=Item No.;
               DataItemLink=Status=FIELD(Status),
                            Prod. Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   InitializeRequest(WORKDATE,CalcBasedOn::"Expected Output");
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ENG=Posting Date];
                  SourceExpr=PostingDate }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Calculation Based on;
                             ENG=Calculation Based on];
                  OptionCaptionML=[ENU=Actual Output,Expected Output;
                                   ENG=Actual Output,Expected Output];
                  SourceExpr=CalcBasedOn }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Picking Location;
                             ENG=Picking Location];
                  SourceExpr=LocationCode;
                  OnLookup=VAR
                             Location@1002 : Record 14;
                           BEGIN
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Text := Location.Code;
                               EXIT(TRUE);
                             END;
                             EXIT(FALSE);
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Calculating consumption...\\;ENG=Calculating consumption...\\';
      Text001@1001 : TextConst 'ENU=Prod. Order No.   #1##########\;ENG=Prod. Order No.   #1##########\';
      Text002@1002 : TextConst 'ENU=Item No.          #2##########\;ENG=Item No.          #2##########\';
      Text003@1003 : TextConst 'ENU=Quantity          #3##########;ENG=Quantity          #3##########';
      Item@1004 : Record 27;
      ProdOrderLine@1005 : Record 5406;
      ItemJnlLine@1006 : Record 83;
      LastItemJnlLine@1017 : Record 83;
      Window@1009 : Dialog;
      PostingDate@1010 : Date;
      CalcBasedOn@1011 : 'Actual Output,Expected Output';
      LocationCode@1013 : Code[10];
      ToTemplateName@1014 : Code[10];
      ToBatchName@1015 : Code[10];
      NextConsumpJnlLineNo@1016 : Integer;

    PROCEDURE InitializeRequest@2(NewPostingDate@1001 : Date;NewCalcBasedOn@1000 : Option);
    BEGIN
      PostingDate := NewPostingDate;
      CalcBasedOn := NewCalcBasedOn;
    END;

    LOCAL PROCEDURE CreateConsumpJnlLine@4(LocationCode@1000 : Code[10];BinCode@1001 : Code[20];QtyToPost@1002 : Decimal);
    VAR
      Location@1004 : Record 14;
      ItemTrackingMgt@1003 : Codeunit 6500;
    BEGIN
      Window.UPDATE(3,QtyToPost);

      IF Location.GET(LocationCode) AND Location."Require Pick" AND Location."Require Shipment" THEN
        "Prod. Order Component".AdjustQtyToQtyPicked(QtyToPost);

      IF (ItemJnlLine."Item No." = "Prod. Order Component"."Item No.") AND
         (LocationCode = ItemJnlLine."Location Code") AND
         (BinCode = ItemJnlLine."Bin Code")
      THEN BEGIN
        IF Item."Rounding Precision" > 0 THEN
          ItemJnlLine.VALIDATE(Quantity,ItemJnlLine.Quantity + ROUND(QtyToPost,Item."Rounding Precision",'>'))
        ELSE
          ItemJnlLine.VALIDATE(Quantity,ItemJnlLine.Quantity + ROUND(QtyToPost,0.00001));
        ItemJnlLine.MODIFY;
      END ELSE BEGIN
        ItemJnlLine.INIT;
        ItemJnlLine."Journal Template Name" := ToTemplateName;
        ItemJnlLine."Journal Batch Name" := ToBatchName;
        ItemJnlLine.SetUpNewLine(LastItemJnlLine);
        ItemJnlLine."Line No." := NextConsumpJnlLineNo;

        ItemJnlLine.VALIDATE("Entry Type",ItemJnlLine."Entry Type"::Consumption);
        ItemJnlLine.VALIDATE("Order Type",ItemJnlLine."Order Type"::Production);
        ItemJnlLine.VALIDATE("Order No.","Prod. Order Component"."Prod. Order No.");
        ItemJnlLine.VALIDATE("Source No.",ProdOrderLine."Item No.");
        ItemJnlLine.VALIDATE("Posting Date",PostingDate);
        ItemJnlLine.VALIDATE("Item No.","Prod. Order Component"."Item No.");
        ItemJnlLine.VALIDATE("Unit of Measure Code","Prod. Order Component"."Unit of Measure Code");
        ItemJnlLine.Description := "Prod. Order Component".Description;
        IF Item."Rounding Precision" > 0 THEN
          ItemJnlLine.VALIDATE(Quantity,ROUND(QtyToPost,Item."Rounding Precision",'>'))
        ELSE
          ItemJnlLine.VALIDATE(Quantity,ROUND(QtyToPost,0.00001));
        ItemJnlLine."Variant Code" := "Prod. Order Component"."Variant Code";
        ItemJnlLine.VALIDATE("Location Code",LocationCode);
        IF BinCode <> '' THEN
          ItemJnlLine."Bin Code" := BinCode;
        ItemJnlLine.VALIDATE("Order Line No.","Prod. Order Component"."Prod. Order Line No.");
        ItemJnlLine.VALIDATE("Prod. Order Comp. Line No.","Prod. Order Component"."Line No.");

        ItemJnlLine.INSERT;
        IF Item."Item Tracking Code" <> '' THEN
          ItemTrackingMgt.CopyItemTracking("Prod. Order Component".RowID1,ItemJnlLine.RowID1,FALSE);
      END;

      NextConsumpJnlLineNo := NextConsumpJnlLineNo + 10000;
    END;

    PROCEDURE SetTemplateAndBatchName@1(TemplateName@1000 : Code[10];BatchName@1001 : Code[10]);
    BEGIN
      ToTemplateName := TemplateName;
      ToBatchName := BatchName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

