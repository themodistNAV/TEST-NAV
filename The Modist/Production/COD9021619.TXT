OBJECT Codeunit 9021619 Label Functions
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:17 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    Permissions=TableData 21=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MQGlobalFunc@1190000005 : Codeunit 9021624;

    PROCEDURE ResetePageDataWithLocalLang@1040002(LabelCode@1040000 : Code[10]);
    VAR
      LangRec@1040001 : Record 8;
      CatRec@1040002 : Record 9021628;
      CatRec1@1040003 : Record 9021628;
      LabelAttRec@1040004 : Record 9021623;
      LabelAttRec1@1040007 : Record 9021623;
      LabelAttValueRec@1040005 : Record 9021624;
      Text000@1040006 : TextConst 'ENU=Can not find language. (0);ENG=Can not find language. (0)';
    BEGIN
    END;

    PROCEDURE EmailOK@1040003(Email@1040000 : Text[80]) : Boolean;
    VAR
      Pos@1040002 : Integer;
      TextL000@1000000000 : TextConst 'ENU=Sorry, you have used an invalid character of %1 in the email, please try again.\\You can only use A to Z, a to z, 0 to 9, .-_ and *.;ENG=Sorry, you have used an invalid character of %1 in the email, please try again.\\You can only use A to Z, a to z, 0 to 9, .-_ and *.';
      j@1000000001 : Integer;
    BEGIN
      IF STRPOS(Email,' ') <> 0 THEN EXIT(FALSE);
      IF STRPOS(Email,';') <> 0 THEN EXIT(FALSE);

      IF STRLEN(Email) < 7 THEN EXIT(FALSE);
      Pos := STRPOS(Email,'@');
      IF Pos < 2 THEN EXIT(FALSE);
      Pos := Pos + 1;
      Email := COPYSTR(Email,Pos);
      IF STRLEN(Email) < 5 THEN EXIT(FALSE);
      Pos := STRPOS(Email,'.');
      IF Pos < 3 THEN EXIT(FALSE);
      Pos := Pos + 1;
      Email := COPYSTR(Email,Pos);
      IF STRLEN(Email) < 2 THEN EXIT(FALSE);
      IF STRPOS(Email,'@') <> 0 THEN EXIT(FALSE);

      //DOC EC2013.3.60 -
      FOR Pos := 1 TO STRLEN(Email) DO
        IF COPYSTR(Email,Pos,1) IN ['0'..'9','A'..'Z','a'..'z','_','.','-','@'] THEN
          j := 1
        ELSE
          ERROR(TextL000,COPYSTR(Email,Pos,1));
      //DOC EC2013.3.60 +

      EXIT(TRUE);
    END;

    PROCEDURE InsertPageItem@1000000000(ItemNo@1000000000 : Code[20];OnlyInsertIfNotAlreadyOnWebPag@1000000001 : Boolean;PutAtBottom@1000000003 : Boolean;OverrideePageGuID@1000000004 : Integer;LabelCode@1000000002 : Code[10]);
    VAR
      Item@1040009 : Record 27;
      Item1@1040008 : Record 27;
      ProdGroup@1040007 : Record 5723;
      PageItem@1040006 : Record 9021629;
      PageItem1@1040005 : Record 9021629;
      Label@1040004 : Record 9021618;
      ePage@1040003 : Record 9021628;
      ePageGuID@1040002 : Integer;
      FoundOrder@1040001 : Boolean;
      OrderNo@1040000 : Integer;
      Text000@1040015 : TextConst 'ENU=You cannot request Put At Top and Put At Bottom simultaneously.  See item %1.;ENG=You cannot request Put At Top and Put At Bottom simultaneously.  See item %1.';
      Text001@1040014 : TextConst 'ENU=Item No. must not be blank.;ENG=Item No. must not be blank.';
      Text002@1040013 : TextConst 'ENU=Unexpected error, ePageGuID is blank on Item No. %1.;ENG=Unexpected error, ePageGuID is blank on Item No. %1.';
      Text003@1040012 : TextConst 'ENU=There is more than one label so you need to specify the label code.;ENG=There is more than one label so you need to specify the label code.';
      Text004@1040011 : TextConst 'ENU=Cannot find ePage (Label Code %2, Page GuID %3) for Item No. %1;ENG=Cannot find ePage (Label Code %2, Page GuID %3) for Item No. %1';
      Text005@1040010 : TextConst 'ENU=Error in finding order no. on item %1.;ENG=Error in finding order no. on item %1.';
    BEGIN
      IF LabelCode = '' THEN BEGIN
        LabelCode:= MQGlobalFunc.GetLabelCode;
        IF LabelCode='' THEN EXIT; //this only runs if no guiallowed so error was supressed inside GetLabelCode...
      END ELSE
        Label.GET(LabelCode);

      IF ItemNo = '' THEN
        ERROR(Text001);

      Item.GET(ItemNo);
      IF OnlyInsertIfNotAlreadyOnWebPag THEN BEGIN
        Item.CALCFIELDS("No. of Web Pages");
        IF Item."No. of Web Pages" > 0 THEN
          EXIT;
      END;
      IF OverrideePageGuID = 0 THEN BEGIN
        IF (Item."Product Group Code" = '') OR
           (Item."Item Category Code" = '') THEN
          EXIT;
        ProdGroup.GET(Item."Item Category Code",Item."Product Group Code");
        IF ProdGroup."ePage GuID" = 0 THEN
          EXIT
        ELSE
          ePageGuID := ProdGroup."ePage GuID";
      END ELSE
        ePageGuID := OverrideePageGuID;

      IF ePageGuID = 0 THEN
        ERROR(Text002,ItemNo);

      IF NOT ePage.GET(LabelCode,ePageGuID) THEN
        ERROR(Text004,ItemNo,LabelCode,ePageGuID);

      FoundOrder := FALSE;
      OrderNo := 0;
      IF NOT PageItem1.GET(LabelCode,ePageGuID,PageItem1.Type::Item,'',ItemNo) THEN BEGIN
        PageItem1.INIT;
        PageItem1."Label Code" := LabelCode;
        PageItem1."Page GroupGuid" := ePageGuID;
        PageItem1.Type := PageItem1.Type::Item;
        PageItem1.VALIDATE("No.",ItemNo);
        PageItem1."Inserted by Auto Process" := TRUE;
        PageItem1.INSERT(TRUE);
      END;

      PageItem.RESET;
      PageItem.SETCURRENTKEY("Label Code","Page GroupGuid",Type,Order);
      PageItem.SETRANGE("Label Code",LabelCode);
      PageItem.SETRANGE("Page GroupGuid",ePageGuID);
      PageItem.SETRANGE(Type,PageItem.Type::Item);

      IF PageItem.FINDFIRST THEN BEGIN
        Item.CALCFIELDS("No. of Promotion Groups");
        IF Item."No. of Promotion Groups" = 0 THEN BEGIN
          //Working out order if item being inserted has no promo code
          IF PutAtBottom THEN BEGIN
            PageItem.FINDLAST;
            OrderNo := PageItem.Order + 1;
          END ELSE BEGIN
            //working out order if PutAtTop but below Items on Promo
            PageItem.FIND('-');
            REPEAT
              IF FoundOrder = FALSE THEN BEGIN
                Item1.GET(PageItem."No.");
                Item1.CALCFIELDS("No. of Promotion Groups");
                IF Item1."No. of Promotion Groups" = 0 THEN BEGIN
                  FoundOrder := TRUE;
                  IF OrderNo = 0 THEN
                    OrderNo := 1;
                END ELSE
                  OrderNo := PageItem.Order + 1;
              END;
            UNTIL PageItem.NEXT = 0;
          END;
        END ELSE BEGIN
        //Working out order if item being inserted has a promo code
          IF PutAtBottom THEN BEGIN
            PageItem.FIND('-');
            REPEAT
              IF FoundOrder = FALSE THEN BEGIN
                Item1.GET(PageItem."No.");
                Item1.CALCFIELDS("No. of Promotion Groups");
                IF Item1."No. of Promotion Groups" = 0 THEN BEGIN
                  FoundOrder := TRUE;
                  IF OrderNo = 0 THEN
                    OrderNo := 1;
                END ELSE
                  OrderNo := PageItem.Order - 1;
              END;
            UNTIL PageItem.NEXT = 0;
          END ELSE
            OrderNo := 1;
        END;
        IF OrderNo = 0 THEN
          ERROR(Text005,ItemNo);
        PageItem1.Order := OrderNo;
      END ELSE
        PageItem1.Order := 2;

      IF NOT PutAtBottom THEN
        PageItem1.Reorder(LabelCode,ePageGuID);

      PageItem1.MODIFY;
    END;

    PROCEDURE CleanProductDataPageItems@1190000005(LabelPar@1190000005 : Record 9021618);
    VAR
      PageItem@1190000007 : Record 9021629;
      PageItem1@1190000008 : Record 9021629;
      PageItem2@1190000015 : Record 9021629;
      i@1190000006 : Integer;
      j@1190000012 : Integer;
      CarryOn@1190000009 : Boolean;
      TextL000@1190000010 : TextConst 'ENU=Item %1 %2 is in the Master Data but nowhere else.\\Do you wish to delete it out of the Master Data page?;ENG=Item %1 %2 is in the Master Data but nowhere else.\\Do you wish to delete it out of the Master Data page?';
      TextL001@1190000011 : TextConst 'ENU=You have checked 10 orphan page items.\\Do you wish to contine checking?\\(Note: if you answer no, other orphaned page items will not be deleted.);ENG=You have checked 10 orphan page items.\\Do you wish to contine checking?\\(Note: if you answer no, other orphaned page items will not be deleted.)';
      TextL002@1190000013 : TextConst 'ENU=Done;ENG=Done';
      TextL003@1190000014 : TextConst 'ENU=Done with %1 page items left as possible orphans.;ENG=Done with %1 page items left as possible orphans.';
    BEGIN
      LabelPar.TESTFIELD("Master ePage GuID for Items");
      PageItem1.RESET;
      PageItem1.SETRANGE("Label Code",LabelPar.Code);
      PageItem1.SETFILTER("Page GroupGuid",'<>%1',LabelPar."Master ePage GuID for Items");
      PageItem1.SETFILTER(Type,'%1|%2',PageItem.Type::Item,PageItem.Type::"Promo Item");
      i := 0;
      j := 0;
      CarryOn := TRUE;
      PageItem.RESET;
      PageItem.SETRANGE("Label Code",LabelPar.Code);
      PageItem.SETRANGE("Page GroupGuid",LabelPar."Master ePage GuID for Items");
      PageItem.SETFILTER(Type,'%1|%2',PageItem.Type::Item,PageItem.Type::"Promo Item");
      PageItem.SETFILTER("No.",'<>%1','');
      IF PageItem.FIND('-') THEN
      REPEAT
        IF CarryOn THEN BEGIN
          PageItem1.SETRANGE("No.",PageItem."No.");
          IF PageItem1.COUNT = 0 THEN BEGIN
            i := i + 1;
            PageItem.CALCFIELDS("Item Description");
            IF CONFIRM(TextL000,FALSE,PageItem."No.",PageItem."Item Description") THEN BEGIN
              PageItem2 := PageItem;
              PageItem2.DELETE(TRUE);
            END;
            IF i = 10 THEN BEGIN
              IF CONFIRM(TextL001) THEN
                i := 0
              ELSE
                CarryOn := FALSE;
            END;
          END;
        END ELSE BEGIN
          PageItem1.SETRANGE("No.",PageItem."No.");
          IF PageItem1.COUNT = 0 THEN
            j := j + 1;
        END;
      UNTIL PageItem.NEXT = 0;


      IF j = 0 THEN
        MESSAGE(TextL002)
      ELSE
        MESSAGE(TextL003,j);
    END;

    BEGIN
    {
      //DOC EC2013.3.00 MQ 01/01/2013 - Remove reference to PageItem."Page Item Type Code"; Removed Errored Functions
      //DOC EC2013.3.60 MQ 17/05/2013 - GW130517 Character checks for Email.
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

