OBJECT Page 11102078 OM - Import Transport
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Import Transport;
               ENU=Import Transport;
               ESP=Import Transport;
               FRA=Import Transport;
               NLD=Transport importeren;
               DEA=Import Transport];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11102043;
    PageType=NavigatePage;
    OnInit=BEGIN
             NextEnable := TRUE;
             ProjectsVisible := TRUE;
           END;

    OnOpenPage=VAR
                 ImportTransport@1000 : Codeunit 11102087;
                 i@1001 : Integer;
               BEGIN
                 IF Transport."No." = '' THEN
                   IF NOT OpenFile THEN BEGIN
                     CurrPage.CLOSE;
                     EXIT;
                   END;

                 ImportTransport.GetObjects(TmpSection, TmpObject);
                 ImportTransport.GetImportSteps(Transport, NoOfSteps, StepType, StepDescription);
                 ImportTransport.TestTextExportNeeded(Transport);

                 NoOfPages := NoOfSteps + 2;

                 FOR i := 1 TO 10 DO
                   SetCheckBox(i, i <= NoOfSteps, FALSE);

                 FILTERGROUP(2);
                 SETRANGE("No.", Transport."No.");
                 FILTERGROUP(0);
                 FINDFIRST;

                 ActivatePage(1);

                 BackupMade := TRUE;
               END;

    OnQueryClosePage=VAR
                       ctConfirm@1000 : TextConst 'DEU=Importing of this transport is not finished.\Are you sure?;ENU=Importing of this transport is not finished.\Are you sure?;ESP=Importing of this transport is not finished.\Are you sure?;FRA=Importing of this transport is not finished.\Are you sure?;NLD=Het importeren van dit transport is nog niet gereed.\Weet u het zeker?;DEA=Importing of this transport is not finished.\Are you sure?';
                     BEGIN
                       IF ActivePage IN [1, NoOfPages] THEN
                         EXIT(TRUE);

                       EXIT(CONFIRM(ctConfirm, TRUE));
                     END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=&Back;
                                 ENU=&Back;
                                 ESP=&Back;
                                 FRA=&Back;
                                 NLD=&Vorige;
                                 DEA=&Back];
                      Enabled=FALSE;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 ActivatePage(ActivePage - 1);
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Next;
                                 ENU=&Next;
                                 ESP=&Next;
                                 FRA=&Next;
                                 NLD=Vo&lgende;
                                 DEA=&Next];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=VAR
                                 ctConfirmNoBackupMade@1000 : TextConst 'DEU=No backup made.\Are you sure?;ENU=No backup made.\Are you sure?;ESP=No backup made.\Are you sure?;FRA=No backup made.\Are you sure?;NLD=Geen backup gemaakt.\Weet u het zeker?;DEA=No backup made.\Are you sure?';
                               BEGIN
                                 IF (ActivePage = 1) AND NOT BackupMade THEN
                                   IF NOT CONFIRM(ctConfirmNoBackupMade, TRUE) THEN
                                     EXIT;

                                 IF (ActivePage > 1) AND (ActivePage - 1 <= NoOfSteps) THEN BEGIN
                                   IF NOT ExecuteStep(ActivePage - 1) THEN
                                     EXIT;
                                   StepExecuted[ActivePage - 1] := TRUE;
                                 END;

                                 ActivatePage(ActivePage + 1);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=&Finish;
                                 ENU=&Finish;
                                 ESP=&Finish;
                                 FRA=&Finish;
                                 NLD=Voltooie&n;
                                 DEA=&Finish];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 ImportTransport@1000 : Codeunit 11102087;
                               BEGIN
                                 IF ActivePage = 1 THEN BEGIN
                                   ImportTransport.SetServerFileName(ServerFileName);
                                   ImportTransport.RUN;
                                 END;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 5   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=Step1;
                CaptionML=[DEU=This wizard helps you to import a transport.;
                           ENU=This wizard helps you to import a transport.;
                           ESP=This wizard helps you to import a transport.;
                           FRA=This wizard helps you to import a transport.;
                           NLD=Deze wizard begeleidt u bij het importeren van een transport.;
                           DEA=This wizard helps you to import a transport.];
                Visible=Step1Visible }

    { 7   ;2   ;Field     ;
                CaptionML=[DEU=Import Transport;
                           ENU=Import Transport;
                           ESP=Import Transport;
                           FRA=Import Transport;
                           NLD=Transport importeren;
                           DEA=Import Transport];
                MultiLine=Yes }

    { 8   ;2   ;Field     ;
                SourceExpr="No.";
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 10  ;2   ;Part      ;
                Name=Projects;
                CaptionML=[DEU=Projects;
                           ENU=Projects;
                           ESP=Projects;
                           FRA=Projects;
                           NLD=Projecten;
                           DEA=Projects];
                SubPageView=SORTING(Transport No.);
                SubPageLink=Transport No.=FIELD(No.);
                PagePartID=Page11102079;
                Visible=ProjectsVisible;
                PartType=Page }

    { 11  ;2   ;Field     ;
                CaptionML=[DEU=I have made a backup recently.;
                           ENU=I have made a backup recently.;
                           ESP=I have made a backup recently.;
                           FRA=I have made a backup recently.;
                           NLD=Ik heb recent een backup gemaakt.;
                           DEA=I have made a backup recently.];
                SourceExpr=BackupMade;
                MultiLine=Yes }

    { 12  ;2   ;Field     ;
                CaptionML=[DEU=Press next to go to the first step.;
                           ENU=Press next to go to the first step.;
                           ESP=Press next to go to the first step.;
                           FRA=Press next to go to the first step.;
                           NLD=Druk op volgende om naar de eerste stap te gaan.;
                           DEA=Press next to go to the first step.];
                MultiLine=Yes }

    { 13  ;1   ;Group     ;
                Name=Step2Visible;
                CaptionML=[DEU=When you click next the following steps wil be executed.;
                           ENU=When you click next the following steps wil be executed.;
                           ESP=When you click next the following steps wil be executed.;
                           FRA=When you click next the following steps wil be executed.;
                           NLD=De volgende stappen zullen worden uitgevoerd als u op volgende klikt.;
                           DEA=When you click next the following steps wil be executed.];
                Visible=Step2Visible;
                GroupType=Group }

    { 14  ;2   ;Field     ;
                SourceExpr=StepExecuted[1];
                CaptionClass=DisplayStepDescription[1];
                Visible=cChecked2Visible;
                Editable=FALSE;
                Style=Attention;
                StyleExpr=TRUE }

    { 15  ;2   ;Field     ;
                SourceExpr=StepExecuted[2];
                CaptionClass=DisplayStepDescription[2];
                Visible=cChecked2Visible;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr=StepExecuted[3];
                CaptionClass=DisplayStepDescription[3];
                Visible=cChecked3Visible;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                SourceExpr=StepExecuted[4];
                CaptionClass=DisplayStepDescription[4];
                Visible=cChecked4Visible;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr=StepExecuted[5];
                CaptionClass=DisplayStepDescription[5];
                Visible=cChecked5Visible;
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr=StepExecuted[6];
                CaptionClass=DisplayStepDescription[6];
                Visible=cChecked6Visible;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr=StepExecuted[7];
                CaptionClass=DisplayStepDescription[7];
                Visible=cChecked7Visible;
                Editable=FALSE }

    { 21  ;2   ;Field     ;
                SourceExpr=StepExecuted[8];
                CaptionClass=DisplayStepDescription[8];
                Visible=cChecked8Visible;
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                SourceExpr=StepExecuted[9];
                CaptionClass=DisplayStepDescription[9];
                Visible=cChecked9Visible;
                Editable=FALSE }

    { 27  ;2   ;Field     ;
                SourceExpr=StepExecuted[10];
                CaptionClass=DisplayStepDescription[10];
                Visible=cChecked10Visible;
                Editable=FALSE }

    { 23  ;1   ;Group     ;
                Name=Step3;
                Visible=Step3Visible }

    { 24  ;2   ;Field     ;
                CaptionML=[DEU=The transport is imported.;
                           ENU=The transport is imported.;
                           ESP=The transport is imported.;
                           FRA=The transport is imported.;
                           NLD=Het transport is ge‹mporteerd.;
                           DEA=The transport is imported.];
                MultiLine=Yes }

    { 25  ;2   ;Field     ;
                CaptionML=[DEU=Press finish.;
                           ENU=Press finish.;
                           ESP=Press finish.;
                           FRA=Press finish.;
                           NLD=Druk op voltooien.;
                           DEA=Press finish.];
                MultiLine=Yes }

    { 26  ;1   ;Field     ;
                SourceExpr=ImportFileName;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Transport@1000 : Record 11102043;
      TmpObject@1001 : TEMPORARY Record 2000000001;
      TmpSection@1002 : TEMPORARY Record 2000000001;
      DisplayStepDescription@1003 : ARRAY [10] OF Text[1024];
      ImportFileName@1004 : Text;
      ServerFileName@1005 : Text;
      StepDescription@1006 : ARRAY [10] OF Text[1024];
      ActivePage@1007 : Integer;
      NoOfPages@1008 : Integer;
      NoOfSteps@1009 : Integer;
      StepType@1010 : ARRAY [10] OF 'Back Up Original Objects,Merge Objects,Import Objects Part 1,Fab before,Import Objects Part 2,Import Objects,Fab after,Permissions,Back Up New Objects,Reset Project Status,Compile Objects';
      BackupMade@1011 : Boolean;
      cChecked10Visible@1012 : Boolean INDATASET;
      cChecked1Visible@1013 : Boolean INDATASET;
      cChecked2Visible@1014 : Boolean INDATASET;
      cChecked3Visible@1015 : Boolean INDATASET;
      cChecked4Visible@1016 : Boolean INDATASET;
      cChecked5Visible@1017 : Boolean INDATASET;
      cChecked6Visible@1018 : Boolean INDATASET;
      cChecked7Visible@1019 : Boolean INDATASET;
      cChecked8Visible@1020 : Boolean INDATASET;
      cChecked9Visible@1021 : Boolean INDATASET;
      FinishEnable@1022 : Boolean INDATASET;
      NextEnable@1023 : Boolean INDATASET;
      ProjectsVisible@1024 : Boolean INDATASET;
      Step1Visible@1025 : Boolean INDATASET;
      Step2Visible@1026 : Boolean INDATASET;
      Step3Visible@1027 : Boolean INDATASET;
      StepExecuted@1028 : ARRAY [10] OF Boolean;

    LOCAL PROCEDURE ActivatePage@1(PageNo@1000 : Integer);
    VAR
      ActiveStep@1001 : Integer;
      i@1002 : Integer;
      ScreenNo@1003 : Integer;
    BEGIN
      CASE TRUE OF
        PageNo = 1 :
          ScreenNo := 1;

        PageNo - 1 <= NoOfSteps :
          ScreenNo := 2;

        ELSE
          ScreenNo := 3;
      END;

      Step1Visible := ScreenNo = 1;
      ProjectsVisible := ScreenNo = 1;
      Step2Visible := ScreenNo = 2;
      Step3Visible := ScreenNo = 3;

      ActivePage := PageNo;
      NextEnable := ActivePage < NoOfPages;
      FinishEnable := (ActivePage = 1) OR (ActivePage = NoOfPages);

      IF (PageNo > 1) AND (PageNo - 1 <= NoOfSteps) THEN
        ActiveStep := PageNo - 1;

      FOR i := 1 TO 10 DO
        IF i = ActiveStep THEN
          DisplayStepDescription[i] := ' > ' + StepDescription[i]
        ELSE
          IF StepDescription[i] = '' THEN
            DisplayStepDescription[i] := ''
          ELSE
            DisplayStepDescription[i] := '    ' + StepDescription[i];
    END;

    LOCAL PROCEDURE SetCheckBox@2(Number@1000 : Integer;Visible@1001 : Boolean;Checked@1002 : Boolean);
    BEGIN
      CASE Number OF
        1 : cChecked1Visible := Visible;
        2 : cChecked2Visible := Visible;
        3 : cChecked3Visible := Visible;
        4 : cChecked4Visible := Visible;
        5 : cChecked5Visible := Visible;
        6 : cChecked6Visible := Visible;
        7 : cChecked7Visible := Visible;
        8 : cChecked8Visible := Visible;
        9 : cChecked9Visible := Visible;
      END;
      StepExecuted[Number] := Checked;
    END;

    LOCAL PROCEDURE OpenFile@3() : Boolean;
    VAR
      CommonDialog@1000 : Codeunit 11102059;
      ImportTransport@1001 : Codeunit 11102087;
    BEGIN
      CommonDialog.AddKnownExtension('fib');
      IF NOT CommonDialog.OpenFile(ImportFileName, ServerFileName) THEN
        EXIT;

      IF NOT ImportTransport.GetContent(ServerFileName, Transport, TmpSection, TRUE) THEN
        EXIT;

      EXIT(TRUE);
    END;

    PROCEDURE SetTransport@4(NewTransport@1000 : Record 11102043);
    BEGIN
      Transport := NewTransport;
    END;

    PROCEDURE SetSections@5(VAR Object@1000 : Record 2000000001);
    BEGIN
      IF Object.FINDSET THEN
        REPEAT
          Object.CALCFIELDS("BLOB Reference");
          TmpSection.TRANSFERFIELDS(Object);
          TmpSection.INSERT;
        UNTIL Object.NEXT = 0;
    END;

    LOCAL PROCEDURE ExecuteStep@6(Step@1000 : Integer) : Boolean;
    VAR
      ImportTransport@1001 : Codeunit 11102087;
    BEGIN
      EXIT(ImportTransport.ExecuteStep(StepType[Step], Transport, TmpSection, TmpObject));
    END;

    PROCEDURE SetServerFileName@7(NewValue@1000 : Text);
    BEGIN
      ServerFileName := NewValue;
    END;

    BEGIN
    END.
  }
}

