OBJECT Report 9087005 Archive Buying Staging
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=12:29:52 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Archive Buying Staging;
               ENG=Archive Buying Staging];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1000000000;;DataItem;dBuyingStagingHeader;
               DataItemTable=Table9087016;
               ReqFilterHeadingML=[ENU=dBuyingStagingHeader;
                                   ENG=dBuyingStagingHeader];
               OnPreDataItem=VAR
                               lCount@1000000000 : Integer;
                             BEGIN
                               BuyingStagingHeader.COPYFILTERS(dBuyingStagingHeader);
                               lCount := BuyingStagingHeader.COUNT;
                               CASE (lCount) OF
                                 0:
                                   ERROR(tNothingToArchive);
                                 1:
                                   BEGIN
                                     BuyingStagingHeader.FINDFIRST();
                                     IF ( NOT (CONFIRM(tConfirmationOne, TRUE, BuyingStagingHeader."No."))) THEN
                                       ERROR('');
                                   END;
                                 ELSE
                                   IF ( NOT (CONFIRM(tConfirmationMany, TRUE, lCount))) THEN
                                     ERROR('');
                               END;

                               BuyingStagingHeaderToArchive.RESET();
                               BuyingStagingHeaderToArchive.DELETEALL();
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ( NOT (BuyingStagingHeaderToArchive.GET(dBuyingStagingHeader."No."))) THEN
                                  BEGIN
                                    BuyingStagingHeaderToArchive := dBuyingStagingHeader;
                                    BuyingStagingHeaderToArchive.INSERT();
                                  END;
                                END;

               ReqFilterFields=No. }

    { 1000000001;;DataItem;dInteger          ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=FILTER(1..));
               ReqFilterHeadingML=[ENU=dInteger;
                                   ENG=dInteger];
               OnPreDataItem=BEGIN
                               BuyingStagingHeaderToArchive.RESET();
                               dInteger.SETRANGE(Number, 1, BuyingStagingHeaderToArchive.COUNT);
                             END;

               OnAfterGetRecord=VAR
                                  lFound@1000000000 : Boolean;
                                BEGIN
                                  IF (dInteger.Number = 1) THEN
                                    lFound := BuyingStagingHeaderToArchive.FINDSET()
                                  ELSE
                                    lFound := (BuyingStagingHeaderToArchive.NEXT() <> 0);
                                  IF (lFound) THEN
                                  BEGIN
                                    BuyingStagingHeader.GET(BuyingStagingHeaderToArchive."No.");
                                    CLEAR(BuyingStagingManagement);
                                    BuyingStagingManagement.ArchiveBuyingStaging(BuyingStagingHeader, MoveToArchive, FALSE);
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  ContainerType=ContentArea }

      { 1000000002;1;Group  ;
                  Name=Options;
                  CaptionML=[ENU=Options;
                             ENG=Options];
                  GroupType=Group }

      { 1000000001;2;Field  ;
                  Name=MoveToArchiveField;
                  CaptionML=[ENU=Move to Archive;
                             ENG=Move to Archive];
                  SourceExpr=MoveToArchive }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      tNothingToArchive@1000000003 : TextConst 'ENU=There are no buying stagings to archive within the filter.;ENG=There are no buying stagings to archive within the filter.';
      tConfirmationOne@1000000000 : TextConst 'ENU=Do you want to archive buying staging "%1"?;ENG=Do you want to archive buying staging "%1"?';
      tConfirmationMany@1000000001 : TextConst 'ENU=Do you want to archive %1 buying stagings?;ENG=Do you want to archive %1 buying stagings?';
      BuyingStagingHeader@1000000002 : Record 9087016;
      BuyingStagingHeaderToArchive@1000000004 : TEMPORARY Record 9087016;
      BuyingStagingManagement@1000000006 : Codeunit 9087010;
      MoveToArchive@1000000005 : Boolean;

    BEGIN
    {
      //DOC NS2015.1  JH 06/02/2014 - Created
      //DOC NS2016.16 JH 14/09/2016 - nSemble 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

