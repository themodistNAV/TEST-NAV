OBJECT Codeunit 260 Document-Mailing
{
  OBJECT-PROPERTIES
  {
    Date=26/11/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.43897;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EmailSubjectCapTxt@1001 : TextConst '@@@="%1 = Customer Name. %2 = Document Type %3 = Invoice No.";ENU=%1 - %2 %3;ENG=%1 - %2 %3';
      ReportAsPdfFileNameMsg@1000 : TextConst '@@@="%1 = Document Type %2 = Invoice No.";ENU=Sales %1 %2.pdf;ENG=Sales %1 %2.pdf';
      InvoiceTxt@1003 : TextConst 'ENU=Invoice;ENG=Invoice';
      ShipmentTxt@1004 : TextConst 'ENU=Shipment;ENG=Shipment';
      CrMemoTxt@1002 : TextConst 'ENU=Credit Memos;ENG=Credit Memos';

    PROCEDURE EmailFileFromIssuedFinChrgMemo@1(IssuedFinChargeMemoHeader@1000 : Record 304;AttachmentFilePath@1001 : Text[250];HideDialog@1004 : Boolean);
    VAR
      CustomReportSelection@1002 : Record 9657;
      IssuedFinanceChargeMemo@1003 : Page 450;
    BEGIN
      CustomReportSelection.INIT;
      WITH IssuedFinChargeMemoHeader DO
        EmailFile(AttachmentFilePath,'',"No.","Customer No.",IssuedFinanceChargeMemo.CAPTION,HideDialog,CustomReportSelection);
    END;

    PROCEDURE EmailFileFromIssuedReminder@2(IssuedReminderHeader@1000 : Record 297;AttachmentFilePath@1001 : Text[250];HideDialog@1004 : Boolean);
    VAR
      CustomReportSelection@1002 : Record 9657;
      IssuedReminder@1003 : Page 438;
    BEGIN
      CustomReportSelection.INIT;
      WITH IssuedReminderHeader DO
        EmailFile(AttachmentFilePath,'',"No.","Customer No.",IssuedReminder.CAPTION,HideDialog,CustomReportSelection);
    END;

    PROCEDURE EmailFileFromSalesHeader@12(SalesHeader@1000 : Record 36;AttachmentFilePath@1001 : Text[250];CustomReportSelection@1003 : Record 9657);
    BEGIN
      WITH SalesHeader DO
        EmailFile(
          AttachmentFilePath,'',"No.","Bill-to Customer No.",FORMAT("Document Type"),FALSE,CustomReportSelection);
    END;

    PROCEDURE EmailFileFromSalesInvoiceHeader@5(SalesInvoiceHeader@1000 : Record 112;AttachmentFilePath@1001 : Text[250];CustomReportSelection@1003 : Record 9657);
    BEGIN
      WITH SalesInvoiceHeader DO
        EmailFile(AttachmentFilePath,'',"No.","Bill-to Customer No.",InvoiceTxt,FALSE,CustomReportSelection);
    END;

    PROCEDURE EmailSalesInvoiceFileFromOrderHeader@6(SalesHeader@1000 : Record 36;AttachmentFilePath@1001 : Text[250]);
    VAR
      CustomReportSelection@1003 : Record 9657;
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      CustomReportSelection.INIT;
      IF SalesHeader."Document Type" = SalesHeader."Document Type"::Order THEN
        SalesInvoiceHeader.SETRANGE("Order No.",SalesHeader."No.")
      ELSE
        SalesInvoiceHeader.SETRANGE("Pre-Assigned No.",SalesHeader."No.");
      SalesInvoiceHeader.FINDLAST;
      WITH SalesHeader DO
        EmailFile(
          AttachmentFilePath,'',SalesInvoiceHeader."No.","Bill-to Customer No.",InvoiceTxt,FALSE,
          CustomReportSelection);
    END;

    PROCEDURE EmailSalesShipmentFileFromOrderHeader@7(SalesHeader@1000 : Record 36;AttachmentFilePath@1001 : Text[250]);
    VAR
      CustomReportSelection@1003 : Record 9657;
      SalesShipmentHeader@1002 : Record 110;
    BEGIN
      CustomReportSelection.INIT;
      SalesShipmentHeader.SETRANGE("Order No.",SalesHeader."No.");
      SalesShipmentHeader.FINDLAST;
      WITH SalesHeader DO
        EmailFile(
          AttachmentFilePath,'',SalesShipmentHeader."No.","Bill-to Customer No.",ShipmentTxt,FALSE,
          CustomReportSelection);
    END;

    PROCEDURE EmailFileFromSalesCrMemoHeader@4(SalesCrMemoHeader@1000 : Record 114;AttachmentFilePath@1001 : Text[250];CustomReportSelection@1003 : Record 9657);
    BEGIN
      WITH SalesCrMemoHeader DO
        EmailFile(AttachmentFilePath,'',"No.","Bill-to Customer No.",CrMemoTxt,FALSE,CustomReportSelection);
    END;

    PROCEDURE EmailFile@3(AttachmentFilePath@1001 : Text[250];AttachmentFileName@1005 : Text[250];PostedDocNo@1000 : Code[20];SendEmaillToCustNo@1006 : Code[20];EmailDocName@1008 : Text[150];HideDialog@1003 : Boolean;CustomReportSelection@1009 : Record 9657);
    VAR
      TempEmailItem@1002 : TEMPORARY Record 9500;
      CompanyInformation@1004 : Record 79;
    BEGIN
      IF AttachmentFileName = '' THEN
        AttachmentFileName := STRSUBSTNO(ReportAsPdfFileNameMsg,EmailDocName,PostedDocNo);
      CompanyInformation.GET;
      WITH TempEmailItem DO BEGIN
        IF CustomReportSelection."Send To Email" <> '' THEN
          "Send to" := CustomReportSelection."Send To Email"
        ELSE
          "Send to" := GetToAddressFromCustomer(SendEmaillToCustNo);
        Subject := COPYSTR(
            STRSUBSTNO(
              EmailSubjectCapTxt,CompanyInformation.Name,EmailDocName,PostedDocNo),1,
            MAXSTRLEN(Subject));
        "Attachment File Path" := AttachmentFilePath;
        "Attachment Name" := AttachmentFileName;
        Send(HideDialog);
      END;
    END;

    LOCAL PROCEDURE GetToAddressFromCustomer@16(BillToCustomerNo@1000 : Code[20]) : Text[250];
    VAR
      Customer@1002 : Record 18;
      ToAddress@1001 : Text;
    BEGIN
      IF Customer.GET(BillToCustomerNo) THEN
        ToAddress := Customer."E-Mail";

      EXIT(ToAddress);
    END;

    BEGIN
    END.
  }
}

