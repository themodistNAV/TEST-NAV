OBJECT Page 9063169 POS New Cust Card
{
  OBJECT-PROPERTIES
  {
    Date=17/08/16;
    Time=[ 4:34:36 PM];
    Version List=NH2016.16.1;
  }
  PROPERTIES
  {
    CaptionML=[ENU=POS New Cust Card;
               ENG=POS New Cust Card];
    DeleteAllowed=No;
    SourceTable=Table18;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             CustNo:=COPYSTR(USERID,1,20);
           END;

    OnOpenPage=BEGIN
                 Rec.INIT;
                 Rec."No.":=CustNo;
                 Rec.INSERT();
                 CurrPage.UPDATE(FALSE);
               END;

    OnQueryClosePage=VAR
                       Customer@1000000000 : Record 18;
                     BEGIN
                       //DOC PC NA2016.16.1 >>>
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF NOT ExistingCustomer THEN BEGIN
                           Customer.INIT;
                           Customer.TRANSFERFIELDS(Rec);
                           Customer."No." := '';

                           POSSetup.GET;
                           Customer.VALIDATE("Customer Posting Group",POSSetup."Customer Posting Group");
                           Customer.VALIDATE("VAT Bus. Posting Group",POSSetup."VAT Bus. Posting Group");
                           Customer.VALIDATE("Gen. Bus. Posting Group",POSSetup."Gen. Bus. Posting Group");

                           Customer.INSERT(TRUE);
                         END ELSE BEGIN
                           Customer.GET("No.");
                           Customer.TRANSFERFIELDS(Rec);
                           Customer.MODIFY(TRUE);
                         END;
                       END;
                       //DOC PC NA2016.16.1 <<<
                     END;

  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Details;
                CaptionML=[ENU=Details;
                           ENG=Details];
                GroupType=Group }

    { 1000000002;2;Field  ;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             IF "E-Mail"<>'' THEN FindCust;
                           END;
                            }

    { 1000000003;2;Field  ;
                SourceExpr=Name;
                OnValidate=BEGIN
                             IF (STRLEN(Name) > 4) AND (STRLEN("Post Code") >3) THEN FindCust;
                           END;
                            }

    { 1000000004;2;Field  ;
                SourceExpr=Address }

    { 1000000005;2;Field  ;
                SourceExpr="Address 2" }

    { 1000000006;2;Field  ;
                SourceExpr=City }

    { 1000000009;2;Field  ;
                SourceExpr=County }

    { 1000000008;2;Field  ;
                SourceExpr="Post Code";
                OnValidate=BEGIN
                             IF (STRLEN(Name) > 4) AND (STRLEN("Post Code") >3) THEN FindCust;

                             IF PageMode=PageMode::AddCust THEN BEGIN
                               //PostcodeMgt.GetAddressByID(
                               END;
                           END;
                            }

    { 1000000007;2;Field  ;
                SourceExpr="Phone No." }

  }
  CODE
  {
    VAR
      Cust@1000000004 : Record 18;
      FindStr@1000000003 : Text;
      TNPUtils@1000000002 : Codeunit 9062229;
      StringMgt@1000000006 : Codeunit 9062236;
      CustNo@1000000001 : Code[20];
      PostcodeMgt@1000000000 : Codeunit 9062361;
      PageMode@1000000005 : 'FindCust,AddCust';
      ExistingCustomer@1000000007 : Boolean;
      POSSetup@1000000008 : Record 9062777;

    LOCAL PROCEDURE FindCust@1000000000();
    BEGIN

      IF STRLEN("E-Mail") > 4 THEN BEGIN
        IF STRPOS("E-Mail",'@') > 0 THEN BEGIN
          Cust.SETCURRENTKEY("E-Mail");
          FindStr:=StringMgt.ReplaceAll("E-Mail",'@','*');
          Cust.SETFILTER("E-Mail",FindStr);
          IF Cust.FINDFIRST THEN BEGIN
            Cust.SETRECFILTER;
            Rec.COPY(Cust);
            ExistingCustomer := TRUE; //DOC PC NA2016.16.1
            EXIT;
            END;
        END;
      END;

      //Can we find by post code and surname
      IF (STRLEN(Name) > 4) AND (STRLEN("Post Code") >3) THEN BEGIN
      Cust.RESET;
      Cust.SETFILTER("Search Name",'%1','*'+ Name +'*');
      Cust.SETFILTER("Post Code","Post Code");
      IF Cust.FINDFIRST THEN BEGIN
        Cust.SETRECFILTER;
        Rec.COPY(Cust);
        ExistingCustomer := TRUE; //DOC PC NA2016.16.1
        END;
      END;
    END;

    BEGIN
    {
      //Needs post code integration completing
      //DOC NH2016.12 BM 11/02/2016 - Created
      //DOC NH2016.15 JH 10/05/2016 - CfMD amendments
      //DOC NA2016.16.1 PC 17/08/2016 - Added code to OnQueryClosePage to Update/Insert Customer
    }
    END.
  }
}

