OBJECT Report 9087009 Add Items to Buying Sheet
{
  OBJECT-PROPERTIES
  {
    Date=05/10/16;
    Time=12:29:53 PM;
    Version List=NS2016.16;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Add Items to Buying Sheet;
               ENG=Add Items to Buying Sheet];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  dItem.FILTERGROUP(2);
                  BuyingSheetNo := dItem.GETRANGEMIN("Buying Sheet No.");
                  dItem.FILTERGROUP(0);
                END;

    OnPostReport=BEGIN
                   IF (LinesAdded > 0) THEN
                     MESSAGE(tLinesAdded, LinesAdded)
                   ELSE
                     MESSAGE(tNoLinesAdded);
                 END;

  }
  DATASET
  {
    { 1000000000;;DataItem;dItem             ;
               DataItemTable=Table27;
               ReqFilterHeadingML=[ENU=dItem;
                                   ENG=dItem];
               OnPreDataItem=BEGIN
                               CLEAR(LinesAdded);
                               BuyingSheetSummaryLine.LOCKTABLE();
                               Dlg.OPEN(tAdding + ' ' + PADSTR('#1', 20, '#'));
                             END;

               OnAfterGetRecord=BEGIN
                                  Dlg.UPDATE(1, dItem."No.");

                                  BuyingSheetSummaryLine.RESET();
                                  BuyingSheetSummaryLine.SETRANGE("Buying Sheet No.", BuyingSheetNo);
                                  BuyingSheetSummaryLine.SETRANGE("Item No.", dItem."No.");
                                  IF (BuyingSheetSummaryLine.ISEMPTY) THEN
                                  BEGIN
                                    IF (BuyingSheetSummaryLineNo = 0) THEN
                                    BEGIN
                                      BuyingSheetSummaryLine.RESET();
                                      BuyingSheetSummaryLine.SETRANGE("Buying Sheet No.", BuyingSheetNo);
                                      IF (BuyingSheetSummaryLine.FINDLAST()) THEN
                                        BuyingSheetSummaryLineNo := BuyingSheetSummaryLine."Line No." + 10000
                                      ELSE
                                        BuyingSheetSummaryLineNo := 10000;
                                    END;

                                    CLEAR(BuyingSheetSummaryLine);
                                    BuyingSheetSummaryLine.INIT();
                                    BuyingSheetSummaryLine.VALIDATE("Buying Sheet No.", BuyingSheetNo);
                                    BuyingSheetSummaryLine.VALIDATE("Line No.", BuyingSheetSummaryLineNo);
                                    BuyingSheetSummaryLine.VALIDATE("Item No.", dItem."No.");
                                    BuyingSheetSummaryLine.INSERT(TRUE);

                                    BuyingSheetSummaryLineNo += 10000;
                                    LinesAdded += 1;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Dlg.CLOSE();
                              END;

               ReqFilterFields=No.,Vendor No.,Vendor Item No.,Brand Code,Division Code,Department Code,Group Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      BuyingSheetSummaryLine@1000000002 : Record 9087021;
      BuyingSheetNo@1000000000 : Code[10];
      BuyingSheetSummaryLineNo@1000000004 : Integer;
      LinesAdded@1000000005 : Integer;
      tLinesAdded@1000000006 : TextConst 'ENU=%1 lines have been added to your buying sheet.;ENG=%1 lines have been added to your buying sheet.';
      tNoLinesAdded@1000000007 : TextConst 'ENU=No lines have been added to your buying sheet.;ENG=No lines have been added to your buying sheet.';
      Dlg@1000000008 : Dialog;
      tAdding@1000000009 : TextConst 'ENU=Adding;ENG=Adding';

    BEGIN
    {
      //DOC NS2015.3  JH 01/08/2014 - Created
      //DOC NS2016.16 JH 14/09/2016 - nSemble 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

