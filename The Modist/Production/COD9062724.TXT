OBJECT Codeunit 9062724 Setup Default Users
{
  OBJECT-PROPERTIES
  {
    Date=11/04/16;
    Time=[ 1:34:00 PM];
    Version List=NA2016.14;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CreateEnablementUsers(TRUE);
          END;

  }
  CODE
  {
    VAR
      tDefaultLocation@1000000000 : TextConst 'ENU=WMS;ENG=WMS';
      EnablementUser@1000000006 : Record 9062729;
      Location@1000000001 : Record 14;
      User@1000000002 : Record 2000000120;
      WarehouseEmployee@1000000003 : Record 7301;
      EnablementUserManagement@1000000005 : Codeunit 9062255;
      Relationships@1000000004 : ARRAY [20] OF Boolean;

    PROCEDURE CreateEnablementUsers@1000000003(pInteraction@1000000000 : Boolean);
    VAR
      lInteraction@1000000001 : Boolean;
      eConfirmation@1000000002 : TextConst 'ENU=Do you want to setup default users?;ENG=Do you want to setup default users?';
      eFinalMessages@1000000003 : TextConst 'ENU=Users have been set up.;ENG=Users have been set up.';
    BEGIN
      lInteraction := pInteraction AND GUIALLOWED;
      IF lInteraction THEN
        IF NOT CONFIRM(eConfirmation) THEN
          EXIT;

      Relationships[EnablementUser.Relationship::User] := TRUE;
      Relationships[EnablementUser.Relationship::"User Setup"] := TRUE;
      Relationships[EnablementUser.Relationship::Contact] := FALSE;
      Relationships[EnablementUser.Relationship::"Salesperson/Purchaser"] := FALSE;
      Relationships[EnablementUser.Relationship::Vendor] := FALSE;
      Relationships[EnablementUser.Relationship::Resource] := FALSE;
      Relationships[EnablementUser.Relationship::Employee] := FALSE;

      User.RESET;
      IF User.FINDSET THEN REPEAT
        IF NOT EnablementUser.GET(User."User Name") THEN
          EnablementUserManagement.AddNavUser(User."User Security ID", '', Relationships);
        CreateWarehouseEmployeeForUser(User."User Name");
      UNTIL User.NEXT = 0;

      IF lInteraction THEN
        MESSAGE(eFinalMessages);
    END;

    LOCAL PROCEDURE CreateWarehouseEmployeeForUser@1000000000(pUserId@1000000000 : Code[60]);
    BEGIN
      Location.SETRANGE("Require Receive",TRUE);
      IF Location.FINDSET THEN REPEAT
        IF NOT WarehouseEmployee.GET(pUserId, Location.Code) THEN BEGIN
          WarehouseEmployee.INIT;
          WarehouseEmployee.VALIDATE("User ID", pUserId);
          WarehouseEmployee.VALIDATE("Location Code", Location.Code);
          IF Location.Code = tDefaultLocation THEN
            WarehouseEmployee.VALIDATE(Default,TRUE);
          WarehouseEmployee.INSERT(TRUE);
        END
        ELSE IF Location.Code = tDefaultLocation THEN
          IF NOT WarehouseEmployee.Default THEN BEGIN
            WarehouseEmployee.VALIDATE(Default,TRUE);
            WarehouseEmployee.MODIFY(TRUE);
          END;
      UNTIL Location.NEXT = 0;
    END;

    BEGIN
    {
      //DOC NA2016.14 JH 11/04/2016 - Bugfixes
    }
    END.
  }
}

