OBJECT Table 9062798 Record Browser Line Field
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Record Browser Line No.");
             END;

    OnDelete=BEGIN
               IF ("Visibility Criteria ID" <> 0) THEN
                 CriteriaMgt.DeleteCriteria("Visibility Criteria ID");
             END;

    CaptionML=[ENU=Record Browser Line Field;
               ENG=Record Browser Line Field];
  }
  FIELDS
  {
    { 1   ;   ;Record Browser Code ;Code20        ;TableRelation="Record Browser Header".Code;
                                                   CaptionML=[ENU=Record Browser Code;
                                                              ENG=Record Browser Code] }
    { 2   ;   ;Record Browser Line No.;Integer    ;TableRelation="Record Browser Line"."Line No." WHERE (Record Browser Code=FIELD(Record Browser Code));
                                                   OnValidate=BEGIN
                                                                UpdateSourceTable();
                                                              END;

                                                   CaptionML=[ENU=Record Browser Line No.;
                                                              ENG=Record Browser Line No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 4   ;   ;Field Type          ;Option        ;CaptionML=[ENU=Field Type;
                                                              ENG=Field Type];
                                                   OptionCaptionML=[ENU=Field,Text,Enhanced Field;
                                                                    ENG=Field,Text,Enhanced Field];
                                                   OptionString=Field,Text,Enhanced Field }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 6   ;   ;Name                ;Text80        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 7   ;   ;Text Value          ;Text250       ;CaptionML=[ENU=Text Value;
                                                              ENG=Text Value] }
    { 8   ;   ;Source Table ID     ;Integer       ;OnValidate=BEGIN
                                                                IF ("Source Table ID" <> xRec."Source Table ID") THEN
                                                                  VALIDATE("Source Field ID", 0);
                                                              END;

                                                   CaptionML=[ENU=Source Table ID;
                                                              ENG=Source Table ID] }
    { 9   ;   ;Source Field ID     ;Integer       ;TableRelation=IF (Field Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(Source Table ID))
                                                                 ELSE IF (Field Type=CONST(Enhanced Field)) "Enhanced Field Definition"."Enhanced Field No." WHERE (Table No.=FIELD(Source Table ID));
                                                   OnValidate=VAR
                                                                Field@1000000001 : Record 2000000041;
                                                                EnhFieldDef@1000000000 : Record 9062327;
                                                              BEGIN
                                                                IF ("Source Field ID" <> 0) THEN BEGIN
                                                                  CASE ("Field Type") OF
                                                                    "Field Type"::Field:
                                                                      BEGIN
                                                                        Field.GET("Source Table ID", "Source Field ID");
                                                                        "Source Field Name" := Field.FieldName;
                                                                      END;
                                                                    "Field Type"::"Enhanced Field":
                                                                      BEGIN
                                                                        EnhFieldDef.GET("Source Table ID", "Source Field ID");
                                                                        "Source Field Name" := EnhFieldDef.Name;
                                                                      END;
                                                                  END;
                                                                END;

                                                                IF ("Line Type" = "Line Type"::"Filter Field") THEN
                                                                  Name := "Source Field Name";
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupField();
                                                            END;

                                                   CaptionML=[ENU=Source Field ID;
                                                              ENG=Source Field ID] }
    { 10  ;   ;Source Field Name   ;Text80        ;OnValidate=BEGIN
                                                                ValidateFieldName();
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupField();
                                                            END;

                                                   CaptionML=[ENU=Source Field Name;
                                                              ENG=Source Field Name] }
    { 11  ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                UpdateSourceTable();
                                                              END;

                                                   CaptionML=[ENU=Line Type;
                                                              ENG=Line Type];
                                                   OptionCaptionML=[ENU=Browser Field,Selection Field,Details Field,Filter Field;
                                                                    ENG=Browser Field,Selection Field,Details Field,Filter Field];
                                                   OptionString=Browser Field,Selection Field,Details Field,Filter Field }
    { 12  ;   ;Visibility Criteria ID;Integer     ;CaptionML=[ENU=Visibility Criteria ID;
                                                              ENG=Visibility Criteria ID] }
    { 13  ;   ;Visibility Criteria Desc.;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Criteria.Description WHERE (ID=FIELD(Visibility Criteria ID)));
                                                   CaptionML=[ENU=Visibility Criteria Desc.;
                                                              ENG=Visibility Criteria Desc.];
                                                   Editable=No }
    { 14  ;   ;No. of Visib. Criteria Fields;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Criteria Field" WHERE (Criteria ID=FIELD(Visibility Criteria ID)));
                                                   CaptionML=[ENU=No. of Visib. Criteria Fields;
                                                              ENG=No. of Visib. Criteria Fields];
                                                   Editable=No }
    { 15  ;   ;Visibility Source Table No.;Integer;OnValidate=BEGIN
                                                                IF ("Visibility Source Table No." <> xRec."Visibility Source Table No.") AND ("Visibility Criteria ID" <> 0) THEN BEGIN
                                                                  CriteriaMgt.DeleteCriteria("Visibility Criteria ID");
                                                                  "Visibility Criteria ID" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Visibility Source Table No.;
                                                              ENG=Visibility Source Table No.] }
  }
  KEYS
  {
    {    ;Record Browser Code,Line Type,Record Browser Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BrowserHeader@1000000001 : Record 9062795;
      BrowserLine@1000000000 : Record 9062796;
      CriteriaMgt@1000000002 : Codeunit 9062462;
      ObjectFieldToolkit@1000000003 : Codeunit 9062343;

    PROCEDURE GetTextValue@1000000000(VAR RecRef@1000000000 : RecordRef) : Text;
    VAR
      Field@1000000003 : Record 2000000041;
      EnhancedFieldMgt@1000000002 : Codeunit 9062298;
      FldRef@1000000001 : FieldRef;
    BEGIN
      CASE ("Field Type") OF
        "Field Type"::Text:
          EXIT("Text Value");
        "Field Type"::Field:
          IF ("Source Field ID" <> 0) THEN BEGIN
            FldRef := RecRef.FIELD("Source Field ID");
            Field.GET(RecRef.NUMBER, FldRef.NUMBER);
            IF (Field.Class = Field.Class::FlowField) THEN
              FldRef.CALCFIELD();
            EXIT(FORMAT(FldRef.VALUE, 0, 9));
          END;
        "Field Type"::"Enhanced Field":
          EXIT(EnhancedFieldMgt.CalculateFieldValue(RecRef.NUMBER, "Source Field ID", RecRef.GETPOSITION));
      END;
      EXIT('');
    END;

    PROCEDURE EditVisibilityCriteria@1000000001();
    BEGIN
      CriteriaMgt.EditCriteriaAttached(
        Rec."Visibility Criteria ID", Rec."Visibility Source Table No.",
        DATABASE::"Record Browser Line Field", Rec.GETPOSITION(FALSE), FIELDNO("Visibility Criteria ID"),
        TRUE);
      Rec.CALCFIELDS("Visibility Criteria Desc.");
    END;

    LOCAL PROCEDURE UpdateSourceTable@1000000003();
    BEGIN
      IF ("Record Browser Line No." <> 0) AND
        ("Line Type" IN [
          "Line Type"::"Browser Field",
          "Line Type"::"Selection Field",
          "Line Type"::"Details Field"])
      THEN BEGIN
        BrowserLine.GET("Record Browser Code", "Record Browser Line No.");
        VALIDATE("Source Table ID", BrowserLine."Source Table No.");
        VALIDATE("Visibility Source Table No.", BrowserLine."Source Table No.");
      END ELSE IF (BrowserHeader.GET("Record Browser Code")) THEN BEGIN
        IF (BrowserLine.GET("Record Browser Code", "Record Browser Line No.")) THEN
          VALIDATE("Visibility Source Table No.", BrowserLine."Source Table No.")
        ELSE
            VALIDATE("Visibility Source Table No.", BrowserHeader."Table No.");
        VALIDATE("Source Table ID", BrowserHeader."Table No.");
      END ELSE
        VALIDATE("Source Table ID", 0);
    END;

    LOCAL PROCEDURE LookupField@1000000004();
    VAR
      TableField@1000000002 : Record 9062231;
      SourceFieldType@1000000000 : Option;
      SourceFieldId@1000000001 : Integer;
    BEGIN
      IF ("Field Type" = "Field Type"::"Enhanced Field") THEN
        SourceFieldType := TableField."Field Type"::"Enhanced Field"
      ELSE
        SourceFieldType := TableField."Field Type"::Field;
      SourceFieldId := Rec."Source Field ID";

      IF (ObjectFieldToolkit.FieldLookupAdv(Rec."Source Table ID", SourceFieldType, SourceFieldId)) THEN BEGIN
        SelectTableField(SourceFieldType, SourceFieldId);
      END;
    END;

    LOCAL PROCEDURE ValidateFieldName@1000000002();
    VAR
      TableField@1000000002 : Record 9062231;
      SourceFieldType@1000000001 : Option;
      SourceFieldId@1000000000 : Integer;
      SourceFieldName@1000000003 : Text;
    BEGIN
      CASE ("Field Type") OF
        "Field Type"::Field:
          SourceFieldType := TableField."Field Type"::Field;
        "Field Type"::"Enhanced Field":
          SourceFieldType := TableField."Field Type"::"Enhanced Field";
      ELSE
        EXIT;
      END;

      SourceFieldId := "Source Field ID";
      SourceFieldName := "Source Field Name";
      ObjectFieldToolkit.ValidateFieldNameAdv("Source Table ID", SourceFieldType, SourceFieldId, SourceFieldName);
      SelectTableField(SourceFieldType, SourceFieldId);
    END;

    LOCAL PROCEDURE SelectTableField@1000000008(TableFieldFieldType@1000000000 : Integer;TableFieldFieldId@1000000001 : Integer);
    VAR
      TableField@1000000002 : Record 9062231;
    BEGIN
      IF (TableFieldFieldType = TableField."Field Type"::"Enhanced Field") THEN
        VALIDATE("Field Type", "Field Type"::"Enhanced Field")
      ELSE
        VALIDATE("Field Type", "Field Type"::Field);
      VALIDATE("Source Field ID", TableFieldFieldId);
    END;

    BEGIN
    {
      //DOC NA2016.15 JH 06/05/2016 - CfMD amendments
    }
    END.
  }
}

