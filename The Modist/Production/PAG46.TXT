OBJECT Page 46 Sales Order Subform
{
  OBJECT-PROPERTIES
  {
    Date=17/12/18;
    Time=[ 1:57:26 PM];
    Modified=Yes;
    Version List=NAVW19.00.00.45480,NH2016.12,NS2016.16,M0063,M0062,OP9031;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ENG=Lines];
    MultipleNewLines=Yes;
    LinksAllowed=No;
    SourceTable=Table37;
    DelayedInsert=Yes;
    SourceTableView=WHERE(Document Type=FILTER(Order));
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnAfterGetRecord=BEGIN
                       ShowShortcutDimCode(ShortcutDimCode);
                       CLEAR(DocumentTotals);
                     END;

    OnNewRecord=BEGIN
                  InitType;
                  CLEAR(ShortcutDimCode);
                END;

    OnDeleteRecord=VAR
                     ReserveSalesLine@1000 : Codeunit 99000832;
                   BEGIN
                     IF (Quantity <> 0) AND ItemExists("No.") THEN BEGIN
                       COMMIT;
                       IF NOT ReserveSalesLine.DeleteLineConfirm(Rec) THEN
                         EXIT(FALSE);
                       ReserveSalesLine.DeleteLine(Rec);
                     END;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IF SalesHeader.GET("Document Type","Document No.") THEN;

                           DocumentTotals.SalesUpdateTotalsControls(Rec,TotalSalesHeader,TotalSalesLine,RefreshMessageEnabled,
                             TotalAmountStyle,RefreshMessageText,InvDiscAmountEditable,CurrPage.EDITABLE,VATAmount);

                           TypeChosen := HasTypeToFillMandatotyFields;
                           SetLocationCodeMandatory;
                         END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1907935204;1 ;ActionGroup;
                      CaptionML=[ENU=&Line;
                                 ENG=&Line];
                      Image=Line }
      { 1900580804;2 ;ActionGroup;
                      CaptionML=[ENU=Item Availability by;
                                 ENG=Item Availability by];
                      Image=ItemAvailability }
      { 5       ;3   ;Action    ;
                      Name=<Action3>;
                      CaptionML=[ENU=Event;
                                 ENG=Event];
                      Image=Event;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromSalesLine(Rec,ItemAvailFormsMgt.ByEvent)
                               END;
                                }
      { 1904522204;3 ;Action    ;
                      CaptionML=[ENU=Period;
                                 ENG=Period];
                      Image=Period;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromSalesLine(Rec,ItemAvailFormsMgt.ByPeriod)
                               END;
                                }
      { 1902056104;3 ;Action    ;
                      CaptionML=[ENU=Variant;
                                 ENG=Variant];
                      Image=ItemVariant;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromSalesLine(Rec,ItemAvailFormsMgt.ByVariant)
                               END;
                                }
      { 1900639404;3 ;Action    ;
                      AccessByPermission=TableData 14=R;
                      CaptionML=[ENU=Location;
                                 ENG=Location];
                      Image=Warehouse;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromSalesLine(Rec,ItemAvailFormsMgt.ByLocation)
                               END;
                                }
      { 15      ;3   ;Action    ;
                      CaptionML=[ENU=BOM Level;
                                 ENG=BOM Level];
                      Image=BOMLevel;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromSalesLine(Rec,ItemAvailFormsMgt.ByBOM)
                               END;
                                }
      { 1000000023;2 ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Ledger E&ntries;
                                 ENG=Ledger E&ntries];
                      RunObject=Page 38;
                      RunPageView=SORTING(Item No.);
                      RunPageLink=Item No.=FIELD(No.),
                                  Variant Code=FIELD(Variant Code);
                      Promoted=No;
                      Image=ItemLedger;
                      PromotedCategory=Process }
      { 1000000024;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=Card;
                                 ENG=Card];
                      RunObject=Page 30;
                      RunPageLink=No.=FIELD(No.);
                      Image=EditLines }
      { 1900186704;2 ;Action    ;
                      AccessByPermission=TableData 27=R;
                      CaptionML=[ENU=Reservation Entries;
                                 ENG=Reservation Entries];
                      Image=ReservationLedger;
                      OnAction=BEGIN
                                 ShowReservationEntries(TRUE);
                               END;
                                }
      { 1905987604;2 ;Action    ;
                      Name=ItemTrackingLines;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=[ENU=Item &Tracking Lines;
                                 ENG=Item &Tracking Lines];
                      Image=ItemTrackingLines;
                      OnAction=BEGIN
                                 OpenItemTrackingLines;
                               END;
                                }
      { 1901633104;2 ;Action    ;
                      AccessByPermission=TableData 5715=R;
                      CaptionML=[ENU=Select Item Substitution;
                                 ENG=Select Item Substitution];
                      Image=SelectItemSubstitution;
                      OnAction=BEGIN
                                 ShowItemSub;
                               END;
                                }
      { 1902085804;2 ;Action    ;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 ENG=Dimensions];
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 1903418704;2 ;Action    ;
                      CaptionML=[ENU=Co&mments;
                                 ENG=Co&mments];
                      Image=ViewComments;
                      OnAction=BEGIN
                                 ShowLineComments;
                               END;
                                }
      { 1907184504;2 ;Action    ;
                      AccessByPermission=TableData 5800=R;
                      CaptionML=[ENU=Item Charge &Assignment;
                                 ENG=Item Charge &Assignment];
                      Image=ItemCosts;
                      OnAction=BEGIN
                                 ItemChargeAssgnt;
                               END;
                                }
      { 1905403704;2 ;Action    ;
                      Name=OrderPromising;
                      AccessByPermission=TableData 99000880=R;
                      CaptionML=[ENU=Order &Promising;
                                 ENG=Order &Promising];
                      Image=OrderPromising;
                      OnAction=BEGIN
                                 OrderPromisingLine;
                               END;
                                }
      { 1000000010;2 ;Action    ;
                      Name=StoreAllocationsAction;
                      CaptionML=[ENU=Store Allocations;
                                 ENG=Store Allocations];
                      Description=NS2015.1;
                      Promoted=Yes;
                      Visible=FALSE;
                      PromotedIsBig=Yes;
                      Image=Allocations;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowStoreAllocations();
                               END;
                                }
      { 1000000021;2 ;Action    ;
                      Name=GAC Updates;
                      RunObject=Page 50016;
                      RunPageLink=Document Type=CONST(Sales Order),
                                  NAV Doc No.=FIELD(Document No.),
                                  Document Line No.=FIELD(Line No.);
                      Image=Warehouse }
      { 1000000019;2 ;Action    ;
                      Name=CancelLine;
                      AccessByPermission=TableData 99000880=R;
                      CaptionML=[ENU=Cancel Line;
                                 ENG=Cancel Line];
                      Image=Error;
                      OnAction=VAR
                                 OrderUpdateFunctions@1000000000 : Codeunit 50017;
                                 ErrCancelNotAllowed@1000000001 : TextConst 'ENU=Item %1 is already packed or shipped, so cancellation not allowed.;ENG=Item %1 is already packed or shipped, so cancellation not allowed.';
                               BEGIN
                                 //DOC OP9031 RK 01/03/17 -
                                 IF NOT OrderUpdateFunctions.CheckCancelChangeAllowed("Document No.","Line No.") THEN
                                   ERROR(STRSUBSTNO(ErrCancelNotAllowed,"No."));
                                 //DOC OP9031 RK 01/03/17 +
                                 //DOC OP9031 LF 02/11/2016 -
                                 IF CONFIRM(STRSUBSTNO('Cancel Line with Item %1?',Description)) THEN BEGIN
                                   "Line Status" := "Line Status"::Cancelled;
                                 END;
                                 //DOC OP9031 LF 02/11/2016 +
                               END;
                                }
      { 7       ;2   ;ActionGroup;
                      CaptionML=[ENU=Assemble to Order;
                                 ENG=Assemble to Order];
                      ActionContainerType=NewDocumentItems;
                      Image=AssemblyBOM }
      { 9       ;3   ;Action    ;
                      Name=AssembleToOrderLines;
                      AccessByPermission=TableData 90=R;
                      CaptionML=[ENU=Assemble-to-Order Lines;
                                 ENG=Assemble-to-Order Lines];
                      OnAction=BEGIN
                                 ShowAsmToOrderLines;
                               END;
                                }
      { 11      ;3   ;Action    ;
                      AccessByPermission=TableData 90=R;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Roll Up &Price;
                                 ENG=Roll Up &Price];
                      OnAction=BEGIN
                                 RollupAsmPrice;
                               END;
                                }
      { 13      ;3   ;Action    ;
                      AccessByPermission=TableData 90=R;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Roll Up &Cost;
                                 ENG=Roll Up &Cost];
                      OnAction=BEGIN
                                 RollUpAsmCost;
                               END;
                                }
      { 31      ;2   ;Action    ;
                      Name=DeferralSchedule;
                      CaptionML=[ENU=Deferral Schedule;
                                 ENG=Deferral Schedule];
                      Enabled="Deferral Code" <> '';
                      Image=PaymentPeriod;
                      OnAction=BEGIN
                                 SalesHeader.GET("Document Type","Document No.");
                                 ShowDeferrals(SalesHeader."Posting Date",SalesHeader."Currency Code");
                               END;
                                }
      { 1906587504;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ENG=F&unctions];
                      Image=Action }
      { 1905623604;2 ;Action    ;
                      Name=GetPrice;
                      AccessByPermission=TableData 7002=R;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Get Price;
                                 ENG=Get Price];
                      Image=Price;
                      OnAction=BEGIN
                                 ShowPrices;
                               END;
                                }
      { 1901770504;2 ;Action    ;
                      AccessByPermission=TableData 7004=R;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Get Li&ne Discount;
                                 ENG=Get Li&ne Discount];
                      Image=LineDiscount;
                      OnAction=BEGIN
                                 ShowLineDisc
                               END;
                                }
      { 1901741804;2 ;Action    ;
                      Name=ExplodeBOM_Functions;
                      AccessByPermission=TableData 90=R;
                      CaptionML=[ENU=E&xplode BOM;
                                 ENG=E&xplode BOM];
                      Image=ExplodeBOM;
                      OnAction=BEGIN
                                 ExplodeBOM;
                               END;
                                }
      { 1903099004;2 ;Action    ;
                      Name=Insert Ext. Texts;
                      AccessByPermission=TableData 279=R;
                      CaptionML=[ENU=Insert &Ext. Texts;
                                 ENG=Insert &Ext. Texts];
                      Image=Text;
                      OnAction=BEGIN
                                 InsertExtendedText(TRUE);
                               END;
                                }
      { 1905427504;2 ;Action    ;
                      Name=Reserve;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Reserve;
                                 ENG=&Reserve];
                      Image=Reserve;
                      OnAction=BEGIN
                                 FIND;
                                 ShowReservation;
                               END;
                                }
      { 1903502504;2 ;Action    ;
                      Name=OrderTracking;
                      CaptionML=[ENU=Order &Tracking;
                                 ENG=Order &Tracking];
                      Image=OrderTracking;
                      OnAction=BEGIN
                                 ShowTracking;
                               END;
                                }
      { 1905968604;2 ;Action    ;
                      AccessByPermission=TableData 5718=R;
                      CaptionML=[ENU=Nonstoc&k Items;
                                 ENG=Nonstoc&k Items];
                      Image=NonStockItem;
                      OnAction=BEGIN
                                 ShowNonstockItems;
                               END;
                                }
      { 1905926804;1 ;ActionGroup;
                      CaptionML=[ENU=O&rder;
                                 ENG=O&rder];
                      Image=Order }
      { 1903645604;2 ;ActionGroup;
                      CaptionML=[ENU=Dr&op Shipment;
                                 ENG=Dr&op Shipment];
                      Image=Delivery }
      { 1907981104;3 ;Action    ;
                      AccessByPermission=TableData 120=R;
                      CaptionML=[ENU=Purchase &Order;
                                 ENG=Purchase &Order];
                      Image=Document;
                      OnAction=BEGIN
                                 OpenPurchOrderForm;
                               END;
                                }
      { 1903587004;2 ;ActionGroup;
                      CaptionML=[ENU=Speci&al Order;
                                 ENG=Speci&al Order];
                      Image=SpecialOrder }
      { 1903192904;3 ;Action    ;
                      Name=OpenSpecialPurchaseOrder;
                      AccessByPermission=TableData 120=R;
                      CaptionML=[ENU=Purchase &Order;
                                 ENG=Purchase &Order];
                      Image=Document;
                      OnAction=BEGIN
                                 OpenSpecialPurchOrderForm;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             TypeOnAfterValidate;
                             NoOnAfterValidate;
                             TypeChosen := HasTypeToFillMandatotyFields;
                             SetLocationCodeMandatory;

                             IF xRec."No." <> '' THEN
                               RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 4   ;2   ;Field     ;
                SourceExpr="No.";
                OnValidate=BEGIN
                             ShowShortcutDimCode(ShortcutDimCode);
                             NoOnAfterValidate;

                             IF xRec."No." <> '' THEN
                               RedistributeTotalsOnAfterValidate;
                           END;

                ShowMandatory=TypeChosen }

    { 9062231;2;Field     ;
                Description=NH2016.12;
                SourceExpr="Catalogue Code" }

    { 68  ;2   ;Field     ;
                SourceExpr="Cross-Reference No.";
                Visible=FALSE;
                OnValidate=BEGIN
                             CrossReferenceNoOnAfterValidat;
                             NoOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           CrossReferenceNoLookUp;
                           InsertExtendedText(FALSE);
                           NoOnAfterValidate;
                         END;
                          }

    { 1136;2   ;Field     ;
                SourceExpr="IC Partner Code";
                Visible=FALSE }

    { 130 ;2   ;Field     ;
                SourceExpr="IC Partner Ref. Type";
                Visible=FALSE }

    { 132 ;2   ;Field     ;
                SourceExpr="IC Partner Reference";
                Visible=FALSE }

    { 30  ;2   ;Field     ;
                Description=NS2016.16;
                SourceExpr="Variant Code";
                Visible=TRUE;
                OnValidate=BEGIN
                             VariantCodeOnAfterValidate;
                           END;
                            }

    { 1000000002;2;Field  ;
                SourceExpr="Brand Code";
                Visible=FALSE }

    { 1000000022;2;Field  ;
                CaptionML=[ENU=Brand Name;
                           ENG=Brand Name];
                SourceExpr=ShowBrandName;
                Editable=FALSE }

    { 104 ;2   ;Field     ;
                SourceExpr="Substitution Available";
                Visible=FALSE }

    { 74  ;2   ;Field     ;
                SourceExpr="Purchasing Code";
                Visible=FALSE }

    { 70  ;2   ;Field     ;
                SourceExpr=Nonstock;
                Visible=FALSE }

    { 78  ;2   ;Field     ;
                SourceExpr="VAT Prod. Posting Group";
                Visible=FALSE;
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 6   ;2   ;Field     ;
                SourceExpr=Description;
                QuickEntry=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="Drop Shipment";
                Visible=FALSE }

    { 106 ;2   ;Field     ;
                SourceExpr="Special Order";
                Visible=FALSE }

    { 110 ;2   ;Field     ;
                SourceExpr="Return Reason Code";
                Visible=FALSE }

    { 42  ;2   ;Field     ;
                SourceExpr="Location Code";
                OnValidate=BEGIN
                             LocationCodeOnAfterValidate;
                           END;

                ShowMandatory=LocationCodeMandatory;
                QuickEntry=FALSE }

    { 114 ;2   ;Field     ;
                SourceExpr="Bin Code";
                Visible=FALSE }

    { 50  ;2   ;Field     ;
                SourceExpr=Reserve;
                Visible=FALSE;
                OnValidate=BEGIN
                             ReserveOnAfterValidate;
                           END;
                            }

    { 8   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             QuantityOnAfterValidate;
                             RedistributeTotalsOnAfterValidate;
                           END;

                ShowMandatory=TypeChosen }

    { 3   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Qty. to Assemble to Order";
                Visible=FALSE;
                OnValidate=BEGIN
                             QtyToAsmToOrderOnAfterValidate;
                           END;

                OnDrillDown=BEGIN
                              ShowAsmToOrderLines;
                            END;
                             }

    { 48  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Reserved Quantity";
                QuickEntry=FALSE }

    { 44  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code";
                OnValidate=BEGIN
                             UnitofMeasureCodeOnAfterValida;
                             RedistributeTotalsOnAfterValidate;
                           END;

                QuickEntry=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Unit of Measure";
                Visible=FALSE }

    { 38  ;2   ;Field     ;
                SourceExpr="Unit Cost (LCY)";
                Visible=FALSE }

    { 112 ;2   ;Field     ;
                Name=SalesPriceExist;
                CaptionML=[ENU=Sales Price Exists;
                           ENG=Sales Price Exists];
                SourceExpr=PriceExists;
                Visible=FALSE;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Unit Price";
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;

                ShowMandatory=TypeChosen }

    { 76  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Line Amount";
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 1000000018;2;Field  ;
                SourceExpr="Gen. Bus. Posting Group" }

    { 1000000020;2;Field  ;
                SourceExpr="Gen. Prod. Posting Group" }

    { 116 ;2   ;Field     ;
                Name=SalesLineDiscExists;
                CaptionML=[ENU=Sales Line Disc. Exists;
                           ENG=Sales Line Disc. Exists];
                SourceExpr=LineDiscExists;
                Visible=FALSE;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Line Discount %";
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 52  ;2   ;Field     ;
                SourceExpr="Line Discount Amount";
                Visible=FALSE;
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 136 ;2   ;Field     ;
                SourceExpr="Prepayment %";
                Visible=FALSE;
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 138 ;2   ;Field     ;
                SourceExpr="Prepmt. Line Amount";
                Visible=FALSE;
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 140 ;2   ;Field     ;
                SourceExpr="Prepmt. Amt. Inv.";
                Visible=FALSE;
                OnValidate=BEGIN
                             RedistributeTotalsOnAfterValidate;
                           END;
                            }

    { 54  ;2   ;Field     ;
                SourceExpr="Allow Invoice Disc.";
                Visible=FALSE }

    { 1000000017;2;Field  ;
                SourceExpr="Line Status";
                Editable=FALSE }

    { 90  ;2   ;Field     ;
                SourceExpr="Inv. Discount Amount";
                Visible=FALSE }

    { 18  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Qty. to Ship";
                OnValidate=BEGIN
                             IF "Qty. to Asm. to Order (Base)" <> 0 THEN BEGIN
                               CurrPage.SAVERECORD;
                               CurrPage.UPDATE(FALSE);
                             END;
                           END;
                            }

    { 20  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Quantity Shipped";
                QuickEntry=FALSE }

    { 22  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Qty. to Invoice" }

    { 24  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Quantity Invoiced" }

    { 142 ;2   ;Field     ;
                SourceExpr="Prepmt Amt to Deduct";
                Visible=FALSE }

    { 144 ;2   ;Field     ;
                SourceExpr="Prepmt Amt Deducted";
                Visible=FALSE }

    { 46  ;2   ;Field     ;
                SourceExpr="Allow Item Charge Assignment";
                Visible=FALSE }

    { 5800;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Qty. to Assign";
                OnDrillDown=BEGIN
                              CurrPage.SAVERECORD;
                              ShowItemChargeAssgnt;
                              UpdateForm(FALSE);
                            END;

                QuickEntry=FALSE }

    { 102 ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Qty. Assigned";
                OnDrillDown=BEGIN
                              CurrPage.SAVERECORD;
                              ShowItemChargeAssgnt;
                              CurrPage.UPDATE(FALSE);
                            END;

                QuickEntry=FALSE }

    { 82  ;2   ;Field     ;
                SourceExpr="Requested Delivery Date";
                Visible=FALSE }

    { 1000000016;2;Field  ;
                SourceExpr="Shipment ID" }

    { 84  ;2   ;Field     ;
                SourceExpr="Promised Delivery Date";
                Visible=FALSE }

    { 86  ;2   ;Field     ;
                SourceExpr="Planned Delivery Date";
                QuickEntry=FALSE }

    { 88  ;2   ;Field     ;
                SourceExpr="Planned Shipment Date" }

    { 100 ;2   ;Field     ;
                SourceExpr="Shipment Date";
                OnValidate=BEGIN
                             ShipmentDateOnAfterValidate;
                           END;

                QuickEntry=FALSE }

    { 92  ;2   ;Field     ;
                SourceExpr="Shipping Agent Code";
                Visible=FALSE }

    { 94  ;2   ;Field     ;
                SourceExpr="Shipping Agent Service Code";
                Visible=FALSE }

    { 96  ;2   ;Field     ;
                SourceExpr="Shipping Time";
                Visible=FALSE }

    { 134 ;2   ;Field     ;
                SourceExpr="Work Type Code";
                Visible=FALSE }

    { 17  ;2   ;Field     ;
                SourceExpr="Whse. Outstanding Qty.";
                Visible=FALSE }

    { 58  ;2   ;Field     ;
                SourceExpr="Whse. Outstanding Qty. (Base)";
                Visible=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr="ATO Whse. Outstanding Qty.";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                SourceExpr="ATO Whse. Outstd. Qty. (Base)";
                Visible=FALSE }

    { 98  ;2   ;Field     ;
                SourceExpr="Outbound Whse. Handling Time";
                Visible=FALSE }

    { 1000000012;2;Field  ;
                SourceExpr=Campaign;
                Visible=FALSE }

    { 1000000013;2;Field  ;
                SourceExpr="Coupon Code";
                Visible=FALSE }

    { 1000000014;2;Field  ;
                SourceExpr=Gift }

    { 1000000015;2;Field  ;
                SourceExpr="Gift Message" }

    { 62  ;2   ;Field     ;
                SourceExpr="Blanket Order No.";
                Visible=FALSE }

    { 66  ;2   ;Field     ;
                SourceExpr="Blanket Order Line No.";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="FA Posting Date";
                Visible=FALSE }

    { 40  ;2   ;Field     ;
                SourceExpr="Depr. until FA Posting Date";
                Visible=FALSE }

    { 36  ;2   ;Field     ;
                SourceExpr="Depreciation Book Code";
                Visible=FALSE }

    { 80  ;2   ;Field     ;
                SourceExpr="Use Duplication List";
                Visible=FALSE }

    { 56  ;2   ;Field     ;
                SourceExpr="Duplicate in Depreciation Book";
                Visible=FALSE }

    { 108 ;2   ;Field     ;
                SourceExpr="Appl.-from Item Entry";
                Visible=FALSE }

    { 60  ;2   ;Field     ;
                SourceExpr="Appl.-to Item Entry";
                Visible=FALSE }

    { 29  ;2   ;Field     ;
                SourceExpr="Deferral Code";
                TableRelation="Deferral Template"."Deferral Code";
                Enabled=(Type <> Type::"Fixed Asset") AND (Type <> Type::" ") }

    { 32  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 34  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 300 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[3];
                CaptionClass='1,2,3';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(3),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(3,ShortcutDimCode[3]);
                           END;
                            }

    { 302 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[4];
                CaptionClass='1,2,4';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(4),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(4,ShortcutDimCode[4]);
                           END;
                            }

    { 304 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[5];
                CaptionClass='1,2,5';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(5),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(5,ShortcutDimCode[5]);
                           END;
                            }

    { 306 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[6];
                CaptionClass='1,2,6';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(6),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(6,ShortcutDimCode[6]);
                           END;
                            }

    { 308 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[7];
                CaptionClass='1,2,7';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(7),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(7,ShortcutDimCode[7]);
                           END;
                            }

    { 310 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[8];
                CaptionClass='1,2,8';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(8),
                                                            Dimension Value Type=CONST(Standard),
                                                            Blocked=CONST(No));
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(8,ShortcutDimCode[8]);
                           END;
                            }

    { 146 ;2   ;Field     ;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE }

    { 148 ;2   ;Field     ;
                SourceExpr="Line No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1170000000;2;Field  ;
                SourceExpr="Main Line No.";
                Visible=FALSE }

    { 1000000025;2;Field  ;
                SourceExpr="Customer Price Group";
                Visible=FALSE;
                Editable=FALSE }

    { 1170000001;2;Field  ;
                SourceExpr="Is Returnable" }

    { 51  ;1   ;Group     ;
                GroupType=Group }

    { 45  ;2   ;Group     ;
                GroupType=Group }

    { 43  ;3   ;Field     ;
                Name=Invoice Discount Amount;
                CaptionML=[ENU=Invoice Discount Amount;
                           ENG=Invoice Discount Amount];
                SourceExpr=TotalSalesLine."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr=TotalSalesHeader."Currency Code";
                Editable=InvDiscAmountEditable;
                Style=Subordinate;
                StyleExpr=RefreshMessageEnabled;
                OnValidate=VAR
                             SalesHeader@1000 : Record 36;
                           BEGIN
                             SalesHeader.GET("Document Type","Document No.");
                             SalesCalcDiscByType.ApplyInvDiscBasedOnAmt(TotalSalesLine."Inv. Discount Amount",SalesHeader);
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 41  ;3   ;Field     ;
                Name=Invoice Disc. Pct.;
                CaptionML=[ENU=Invoice Discount %;
                           ENG=Invoice Discount %];
                DecimalPlaces=0:2;
                SourceExpr=SalesCalcDiscByType.GetCustInvoiceDiscountPct(Rec);
                Visible=TRUE;
                Editable=FALSE;
                Style=Subordinate;
                StyleExpr=RefreshMessageEnabled }

    { 28  ;2   ;Group     ;
                GroupType=Group }

    { 27  ;3   ;Field     ;
                Name=Total Amount Excl. VAT;
                DrillDown=No;
                CaptionML=[ENU=Total Amount Excl. VAT;
                           ENG=Total Amount Excl. VAT];
                SourceExpr=TotalSalesLine.Amount;
                AutoFormatType=1;
                AutoFormatExpr=TotalSalesHeader."Currency Code";
                CaptionClass=DocumentTotals.GetTotalExclVATCaption(SalesHeader."Currency Code");
                Editable=FALSE;
                Style=Subordinate;
                StyleExpr=RefreshMessageEnabled }

    { 25  ;3   ;Field     ;
                Name=Total VAT Amount;
                CaptionML=[ENU=Total VAT;
                           ENG=Total VAT];
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr=TotalSalesHeader."Currency Code";
                CaptionClass=DocumentTotals.GetTotalVATCaption(SalesHeader."Currency Code");
                Editable=FALSE;
                Style=Subordinate;
                StyleExpr=RefreshMessageEnabled }

    { 23  ;3   ;Field     ;
                Name=Total Amount Incl. VAT;
                CaptionML=[ENU=Total Amount Incl. VAT;
                           ENG=Total Amount Incl. VAT];
                SourceExpr=TotalSalesLine."Amount Including VAT";
                AutoFormatType=1;
                AutoFormatExpr=TotalSalesHeader."Currency Code";
                CaptionClass=DocumentTotals.GetTotalInclVATCaption(SalesHeader."Currency Code");
                Editable=FALSE;
                StyleExpr=TotalAmountStyle }

    { 61  ;3   ;Field     ;
                Name=RefreshTotals;
                DrillDown=Yes;
                CaptionML=[ENU=RefreshTotals;
                           ENG=RefreshTotals];
                SourceExpr=RefreshMessageText;
                Enabled=RefreshMessageEnabled;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DocumentTotals.SalesRedistributeInvoiceDiscountAmounts(Rec,VATAmount,TotalSalesLine);
                              DocumentTotals.SalesUpdateTotalsControls(Rec,TotalSalesHeader,TotalSalesLine,RefreshMessageEnabled,
                                TotalAmountStyle,RefreshMessageText,InvDiscAmountEditable,CurrPage.EDITABLE,VATAmount);
                            END;

                ShowCaption=No }

    { 1000000000;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Style No.";
                Visible=FALSE }

    { 1000000001;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Season Code";
                Visible=FALSE }

    { 1000000011;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Sub-Season Code";
                Visible=FALSE }

    { 1000000003;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Division Code";
                Visible=FALSE }

    { 1000000004;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Department Code";
                Visible=FALSE }

    { 1000000005;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Group Code";
                Visible=FALSE }

    { 1000000006;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Category Code";
                Visible=FALSE }

    { 1000000007;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Sub-Category Code";
                Visible=FALSE }

    { 1000000009;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Store Allocation Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000008;2;Field  ;
                Description=NS2015.1;
                SourceExpr="Quantity Allocated";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      TotalSalesHeader@1016 : Record 36;
      TotalSalesLine@1009 : Record 37;
      SalesHeader@1000 : Record 36;
      SalesPriceCalcMgt@1006 : Codeunit 7000;
      TransferExtendedText@1002 : Codeunit 378;
      ItemAvailFormsMgt@1001 : Codeunit 353;
      SalesCalcDiscByType@1008 : Codeunit 56;
      DocumentTotals@1007 : Codeunit 57;
      VATAmount@1005 : Decimal;
      ShortcutDimCode@1003 : ARRAY [8] OF Code[20];
      Text001@1004 : TextConst 'ENU=You cannot use the Explode BOM function because a prepayment of the sales order has been invoiced.;ENG=You cannot use the Explode BOM function because a prepayment of the sales order has been invoiced.';
      ItemPanelVisible@19014573 : Boolean INDATASET;
      TypeChosen@1018 : Boolean;
      LocationCodeMandatory@1017 : Boolean;
      InvDiscAmountEditable@1014 : Boolean;
      TotalAmountStyle@1013 : Text;
      RefreshMessageEnabled@1012 : Boolean;
      RefreshMessageText@1011 : Text;

    PROCEDURE ApproveCalcInvDisc@1();
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Sales-Disc. (Yes/No)",Rec);
    END;

    PROCEDURE CalcInvDisc@6();
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Sales-Calc. Discount",Rec);
    END;

    PROCEDURE ExplodeBOM@3();
    BEGIN
      IF "Prepmt. Amt. Inv." <> 0 THEN
        ERROR(Text001);
      CODEUNIT.RUN(CODEUNIT::"Sales-Explode BOM",Rec);
    END;

    PROCEDURE OpenPurchOrderForm@4();
    VAR
      PurchHeader@1000 : Record 38;
      PurchOrder@1001 : Page 50;
    BEGIN
      TESTFIELD("Purchase Order No.");
      PurchHeader.SETRANGE("No.","Purchase Order No.");
      PurchOrder.SETTABLEVIEW(PurchHeader);
      PurchOrder.EDITABLE := FALSE;
      PurchOrder.RUN;
    END;

    PROCEDURE OpenSpecialPurchOrderForm@14();
    VAR
      PurchHeader@1001 : Record 38;
      PurchRcptHeader@1002 : Record 120;
      PurchOrder@1000 : Page 50;
    BEGIN
      TESTFIELD("Special Order Purchase No.");
      PurchHeader.SETRANGE("No.","Special Order Purchase No.");
      IF NOT PurchHeader.ISEMPTY THEN BEGIN
        PurchOrder.SETTABLEVIEW(PurchHeader);
        PurchOrder.EDITABLE := FALSE;
        PurchOrder.RUN;
      END ELSE BEGIN
        PurchRcptHeader.SETRANGE("Order No.","Special Order Purchase No.");
        IF PurchRcptHeader.COUNT = 1 THEN
          PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader)
        ELSE
          PAGE.RUN(PAGE::"Posted Purchase Receipts",PurchRcptHeader);
      END;
    END;

    PROCEDURE InsertExtendedText@5(Unconditionally@1000 : Boolean);
    BEGIN
      IF TransferExtendedText.SalesCheckIfAnyExtText(Rec,Unconditionally) THEN BEGIN
        CurrPage.SAVERECORD;
        COMMIT;
        TransferExtendedText.InsertSalesExtText(Rec);
      END;
      IF TransferExtendedText.MakeUpdate THEN
        UpdateForm(TRUE);
    END;

    PROCEDURE ShowNonstockItems@11();
    BEGIN
      ShowNonstock;
    END;

    PROCEDURE ShowTracking@13();
    VAR
      TrackingForm@1000 : Page 99000822;
    BEGIN
      TrackingForm.SetSalesLine(Rec);
      TrackingForm.RUNMODAL;
    END;

    PROCEDURE ItemChargeAssgnt@5800();
    BEGIN
      ShowItemChargeAssgnt;
    END;

    PROCEDURE UpdateForm@12(SetSaveRecord@1000 : Boolean);
    BEGIN
      CurrPage.UPDATE(SetSaveRecord);
    END;

    PROCEDURE ShowPrices@15();
    BEGIN
      SalesHeader.GET("Document Type","Document No.");
      CLEAR(SalesPriceCalcMgt);
      SalesPriceCalcMgt.GetSalesLinePrice(SalesHeader,Rec);
    END;

    PROCEDURE ShowLineDisc@16();
    BEGIN
      SalesHeader.GET("Document Type","Document No.");
      CLEAR(SalesPriceCalcMgt);
      SalesPriceCalcMgt.GetSalesLineLineDisc(SalesHeader,Rec);
    END;

    PROCEDURE OrderPromisingLine@17();
    VAR
      OrderPromisingLine@1000 : TEMPORARY Record 99000880;
      OrderPromisingLines@1001 : Page 99000959;
    BEGIN
      OrderPromisingLine.SETRANGE("Source Type","Document Type");
      OrderPromisingLine.SETRANGE("Source ID","Document No.");
      OrderPromisingLine.SETRANGE("Source Line No.","Line No.");

      OrderPromisingLines.SetSourceType(OrderPromisingLine."Source Type"::Sales);
      OrderPromisingLines.SETTABLEVIEW(OrderPromisingLine);
      OrderPromisingLines.RUNMODAL;
    END;

    LOCAL PROCEDURE TypeOnAfterValidate@19069045();
    BEGIN
      ItemPanelVisible := Type = Type::Item;
    END;

    LOCAL PROCEDURE NoOnAfterValidate@19066594();
    BEGIN
      InsertExtendedText(FALSE);
      IF (Type = Type::"Charge (Item)") AND ("No." <> xRec."No.") AND
         (xRec."No." <> '')
      THEN
        CurrPage.SAVERECORD;

      SaveAndAutoAsmToOrder;

      IF Reserve = Reserve::Always THEN BEGIN
        CurrPage.SAVERECORD;
        IF ("Outstanding Qty. (Base)" <> 0) AND ("No." <> xRec."No.") THEN BEGIN
          AutoReserve;
          CurrPage.UPDATE(FALSE);
        END;
      END;
    END;

    LOCAL PROCEDURE CrossReferenceNoOnAfterValidat@19048248();
    BEGIN
      InsertExtendedText(FALSE);
    END;

    LOCAL PROCEDURE VariantCodeOnAfterValidate@21();
    BEGIN
      SaveAndAutoAsmToOrder;
    END;

    LOCAL PROCEDURE LocationCodeOnAfterValidate@19034787();
    BEGIN
      SaveAndAutoAsmToOrder;

      IF (Reserve = Reserve::Always) AND
         ("Outstanding Qty. (Base)" <> 0) AND
         ("Location Code" <> xRec."Location Code")
      THEN BEGIN
        CurrPage.SAVERECORD;
        AutoReserve;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE ReserveOnAfterValidate@19004502();
    BEGIN
      IF (Reserve = Reserve::Always) AND ("Outstanding Qty. (Base)" <> 0) THEN BEGIN
        CurrPage.SAVERECORD;
        AutoReserve;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE QuantityOnAfterValidate@19032465();
    VAR
      UpdateIsDone@1001 : Boolean;
    BEGIN
      IF Type = Type::Item THEN
        CASE Reserve OF
          Reserve::Always:
            BEGIN
              CurrPage.SAVERECORD;
              AutoReserve;
              CurrPage.UPDATE(FALSE);
              UpdateIsDone := TRUE;
            END;
          Reserve::Optional:
            IF (Quantity < xRec.Quantity) AND (xRec.Quantity > 0) THEN BEGIN
              CurrPage.SAVERECORD;
              CurrPage.UPDATE(FALSE);
              UpdateIsDone := TRUE;
            END;
        END;

      IF (Type = Type::Item) AND
         (Quantity <> xRec.Quantity) AND
         NOT UpdateIsDone
      THEN
        CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE QtyToAsmToOrderOnAfterValidate@19();
    BEGIN
      CurrPage.SAVERECORD;
      IF Reserve = Reserve::Always THEN
        AutoReserve;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE UnitofMeasureCodeOnAfterValida@19057939();
    BEGIN
      IF Reserve = Reserve::Always THEN BEGIN
        CurrPage.SAVERECORD;
        AutoReserve;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE ShipmentDateOnAfterValidate@19068710();
    BEGIN
      IF (Reserve = Reserve::Always) AND
         ("Outstanding Qty. (Base)" <> 0) AND
         ("Shipment Date" <> xRec."Shipment Date")
      THEN BEGIN
        CurrPage.SAVERECORD;
        AutoReserve;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE SaveAndAutoAsmToOrder@20();
    BEGIN
      IF (Type = Type::Item) AND IsAsmToOrderRequired THEN BEGIN
        CurrPage.SAVERECORD;
        AutoAsmToOrder;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE SetLocationCodeMandatory@22();
    VAR
      InventorySetup@1000 : Record 313;
    BEGIN
      InventorySetup.GET;
      LocationCodeMandatory := InventorySetup."Location Mandatory" AND (Type = Type::Item);
    END;

    LOCAL PROCEDURE RedistributeTotalsOnAfterValidate@2();
    BEGIN
      CurrPage.SAVERECORD;

      SalesHeader.GET("Document Type","Document No.");
      IF DocumentTotals.SalesCheckNumberOfLinesLimit(SalesHeader) THEN
        DocumentTotals.SalesRedistributeInvoiceDiscountAmounts(Rec,VATAmount,TotalSalesLine);
      CurrPage.UPDATE;
    END;

    BEGIN
    {
      //DOC NH2016.12 JH 18/11/2015 - "Catalogue Code" field added
      //DOC NS2015.1  AZ 23/01/2014 - nSemble fields added
      //DOC NS2015.6  JH 09/04/2015 - "Stock Alloctions" renamed to "Store Allocations"
      //DOC NS2016.16 JH 04/10/2016 - nSemble 2016, "Variant Code" visible as default
      //DOC M0063 DD 26/10/16 - Added fields Campaign and "Coupon Code".
      //DOC M0062 DD 27/10/16 - Added fields Gift and "Gift Message".
      //DOC OP9031 LF 31/10/2016 - Added field "Shipment ID"
      //DOC OP9031 LF 02/11/2016 - Added field "Line Status" and line function to cancel line
      //DOC OP9031 RK 23/02/17 - Added brand code on lines.
      //DOC OP9031 RK 01/03/17 - Disallow cancelling of line of GAC has sent pack, pick or ship status.
    }
    END.
  }
}

