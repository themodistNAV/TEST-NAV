OBJECT Codeunit 1032 Job Planning Line-Reserve
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 337=rimd,
                TableData 99000850=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Reserved quantity cannot be greater than %1.;ENG=Reserved quantity cannot be greater than %1.';
      Text002@1001 : TextConst '@@@=starts with "Planning Date";ENU=must be filled in when a quantity is reserved;ENG=must be filled in when a quantity is reserved';
      Text004@1003 : TextConst '@@@=starts with some field name;ENU=must not be changed when a quantity is reserved;ENG=must not be changed when a quantity is reserved';
      Text005@1004 : TextConst 'ENU=Codeunit is not initialized correctly.;ENG=Codeunit is not initialised correctly.';
      CreateReservEntry@1005 : Codeunit 99000830;
      ReservEngineMgt@1006 : Codeunit 99000831;
      ReservMgt@1007 : Codeunit 99000845;
      Blocked@1008 : Boolean;
      SetFromType@1009 : ' ,Sales,Requisition Line,Purchase,Item Journal,BOM Journal,Item Ledger Entry,Service,Job';
      SetFromSubtype@1010 : Integer;
      SetFromID@1011 : Code[20];
      SetFromBatchName@1012 : Code[10];
      SetFromProdOrderLine@1013 : Integer;
      SetFromRefNo@1014 : Integer;
      SetFromVariantCode@1015 : Code[10];
      SetFromLocationCode@1016 : Code[10];
      SetFromSerialNo@1018 : Code[20];
      SetFromLotNo@1019 : Code[20];
      SetFromQtyPerUOM@1020 : Decimal;
      DeleteItemTracking@1026 : Boolean;

    PROCEDURE CreateReservation@1(VAR JobPlanningLine@1000 : Record 1003;Description@1001 : Text[50];ExpectedReceiptDate@1002 : Date;Quantity@1008 : Decimal;QuantityBase@1003 : Decimal;ForSerialNo@1005 : Code[20];ForLotNo@1004 : Code[20]);
    VAR
      PlanningDate@1007 : Date;
      SignFactor@1006 : Integer;
    BEGIN
      IF SetFromType = 0 THEN
        ERROR(Text005);
      JobPlanningLine.TESTFIELD(Type,JobPlanningLine.Type::Item);
      JobPlanningLine.TESTFIELD("No.");
      JobPlanningLine.TESTFIELD("Planning Date");

      JobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
      IF ABS(JobPlanningLine."Remaining Qty. (Base)") < ABS(JobPlanningLine."Reserved Qty. (Base)") + QuantityBase THEN
        ERROR(
          Text000,
          ABS(JobPlanningLine."Remaining Qty. (Base)") - ABS(JobPlanningLine."Reserved Qty. (Base)"));

      JobPlanningLine.TESTFIELD("Variant Code",SetFromVariantCode);
      JobPlanningLine.TESTFIELD("Location Code",SetFromLocationCode);

      SignFactor := -1;

      IF QuantityBase * SignFactor < 0 THEN
        PlanningDate := JobPlanningLine."Planning Date"
      ELSE BEGIN
        PlanningDate := ExpectedReceiptDate;
        ExpectedReceiptDate := JobPlanningLine."Planning Date";
      END;

      CreateReservEntry.CreateReservEntryFor(
        DATABASE::"Job Planning Line",JobPlanningLine.Status,
        JobPlanningLine."Job No.",'',0,JobPlanningLine."Job Contract Entry No.",JobPlanningLine."Qty. per Unit of Measure",
        Quantity,QuantityBase,ForSerialNo,ForLotNo);
      CreateReservEntry.CreateReservEntryFrom(
        SetFromType,SetFromSubtype,SetFromID,SetFromBatchName,SetFromProdOrderLine,SetFromRefNo,
        SetFromQtyPerUOM,SetFromSerialNo,SetFromLotNo);
      CreateReservEntry.CreateReservEntry(
        JobPlanningLine."No.",JobPlanningLine."Variant Code",JobPlanningLine."Location Code",
        Description,ExpectedReceiptDate,PlanningDate);

      SetFromType := 0;
    END;

    PROCEDURE CreateReservationSetFrom@7(TrackingSpecificationFrom@1000 : Record 336);
    BEGIN
      WITH TrackingSpecificationFrom DO BEGIN
        SetFromType := "Source Type";
        SetFromSubtype := "Source Subtype";
        SetFromID := "Source ID";
        SetFromBatchName := "Source Batch Name";
        SetFromProdOrderLine := "Source Prod. Order Line";
        SetFromRefNo := "Source Ref. No.";
        SetFromVariantCode := "Variant Code";
        SetFromLocationCode := "Location Code";
        SetFromSerialNo := "Serial No.";
        SetFromLotNo := "Lot No.";
        SetFromQtyPerUOM := "Qty. per Unit of Measure";
      END;
    END;

    PROCEDURE SetBinding@8(Binding@1000 : ' ,Order-to-Order');
    BEGIN
      CreateReservEntry.SetBinding(Binding);
    END;

    PROCEDURE FilterReservFor@12(VAR FilterReservEntry@1000 : Record 337;JobPlanningLine@1001 : Record 1003);
    BEGIN
      FilterReservEntry.SETRANGE("Source Type",DATABASE::"Job Planning Line");
      FilterReservEntry.SETRANGE("Source Subtype",JobPlanningLine.Status);
      FilterReservEntry.SETRANGE("Source ID",JobPlanningLine."Job No.");
      FilterReservEntry.SETRANGE("Source Batch Name",'');
      FilterReservEntry.SETRANGE("Source Prod. Order Line",0);
      FilterReservEntry.SETRANGE("Source Ref. No.",JobPlanningLine."Job Contract Entry No.");
    END;

    PROCEDURE ReservQuantity@21(JobPlanningLine@1001 : Record 1003;VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1002 : Decimal);
    BEGIN
      CASE JobPlanningLine.Status OF
        JobPlanningLine.Status::Planning,
        JobPlanningLine.Status::Quote,
        JobPlanningLine.Status::Order,
        JobPlanningLine.Status::Completed:
          BEGIN
            QtyToReserve := JobPlanningLine."Remaining Qty.";
            QtyToReserveBase := JobPlanningLine."Remaining Qty. (Base)";
          END;
      END;
    END;

    PROCEDURE Caption@30(JobPlanningLine@1001 : Record 1003) CaptionText@1000 : Text[80];
    BEGIN
      CaptionText :=
        STRSUBSTNO('%1 %2 %3',JobPlanningLine.Status,JobPlanningLine."Job No.",JobPlanningLine."No.");
    END;

    PROCEDURE FindReservEntry@4(JobPlanningLine@1000 : Record 1003;VAR ReservEntry@1001 : Record 337) : Boolean;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      FilterReservFor(ReservEntry,JobPlanningLine);
      EXIT(ReservEntry.FINDLAST);
    END;

    LOCAL PROCEDURE ReservEntryExist@17(JobPlanningLine@1000 : Record 1003) : Boolean;
    VAR
      ReservEntry@1001 : Record 337;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      FilterReservFor(ReservEntry,JobPlanningLine);
      EXIT(NOT ReservEntry.ISEMPTY);
    END;

    PROCEDURE VerifyChange@59(VAR NewJobPlanningLine@1000 : Record 1003;VAR OldJobPlanningLine@1001 : Record 1003);
    VAR
      JobPlanningLine@1005 : Record 1003;
      ReservEntry@1002 : Record 337;
      ShowError@1003 : Boolean;
      HasError@1004 : Boolean;
    BEGIN
      IF (NewJobPlanningLine.Type <> NewJobPlanningLine.Type::Item) AND (OldJobPlanningLine.Type <> OldJobPlanningLine.Type::Item) THEN
        EXIT;
      IF Blocked THEN
        EXIT;
      IF NewJobPlanningLine."Job Contract Entry No." = 0 THEN
        IF NOT JobPlanningLine.GET(
             NewJobPlanningLine."Job No.",
             NewJobPlanningLine."Job Task No.",
             NewJobPlanningLine."Line No.")
        THEN
          EXIT;

      NewJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
      ShowError := NewJobPlanningLine."Reserved Qty. (Base)" <> 0;

      IF NewJobPlanningLine."Usage Link" <> OldJobPlanningLine."Usage Link" THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("Usage Link",Text004);
        HasError := TRUE;
      END;

      IF (NewJobPlanningLine."Planning Date" = 0D) AND (OldJobPlanningLine."Planning Date" <> 0D) THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("Planning Date",Text002);
        HasError := TRUE;
      END;

      IF NewJobPlanningLine."No." <> OldJobPlanningLine."No." THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("No.",Text004);
        HasError := TRUE;
      END;

      IF NewJobPlanningLine."Variant Code" <> OldJobPlanningLine."Variant Code" THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("Variant Code",Text004);
        HasError := TRUE;
      END;

      IF NewJobPlanningLine."Location Code" <> OldJobPlanningLine."Location Code" THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("Location Code",Text004);
        HasError := TRUE;
      END;

      IF NewJobPlanningLine."Bin Code" <> OldJobPlanningLine."Bin Code" THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR("Bin Code",Text004);
        HasError := TRUE;
      END;

      IF NewJobPlanningLine."Line No." <> OldJobPlanningLine."Line No." THEN
        HasError := TRUE;

      IF NewJobPlanningLine.Type <> OldJobPlanningLine.Type THEN BEGIN
        IF ShowError THEN
          NewJobPlanningLine.FIELDERROR(Type,Text004);
        HasError := TRUE;
      END;

      IF HasError THEN
        IF (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") OR
           FindReservEntry(NewJobPlanningLine,ReservEntry)
        THEN BEGIN
          IF (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") OR (NewJobPlanningLine.Type <> OldJobPlanningLine.Type) THEN BEGIN
            ReservMgt.SetJobPlanningLine(OldJobPlanningLine);
            ReservMgt.DeleteReservEntries(TRUE,0);
            ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
          END ELSE BEGIN
            ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
            ReservMgt.DeleteReservEntries(TRUE,0);
          END;
          ReservMgt.AutoTrack(NewJobPlanningLine."Remaining Qty. (Base)");
        END;

      IF HasError OR (NewJobPlanningLine."Planning Date" <> OldJobPlanningLine."Planning Date")
      THEN BEGIN
        AssignForPlanning(NewJobPlanningLine);
        IF (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") OR
           (NewJobPlanningLine."Variant Code" <> OldJobPlanningLine."Variant Code") OR
           (NewJobPlanningLine."Location Code" <> OldJobPlanningLine."Location Code")
        THEN
          AssignForPlanning(OldJobPlanningLine);
      END;
    END;

    PROCEDURE VerifyQuantity@2(VAR NewJobPlanningLine@1000 : Record 1003;VAR OldJobPlanningLine@1001 : Record 1003);
    VAR
      JobPlanningLine@1002 : Record 1003;
    BEGIN
      IF Blocked THEN
        EXIT;

      WITH NewJobPlanningLine DO BEGIN
        IF Type <> Type::Item THEN
          EXIT;
        IF Status = OldJobPlanningLine.Status THEN
          IF "Line No." = OldJobPlanningLine."Line No." THEN
            IF "Quantity (Base)" = OldJobPlanningLine."Quantity (Base)" THEN
              EXIT;
        IF "Line No." = 0 THEN
          IF NOT JobPlanningLine.GET("Job No.","Job Task No.","Line No.") THEN
            EXIT;
        ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
        IF "Qty. per Unit of Measure" <> OldJobPlanningLine."Qty. per Unit of Measure" THEN
          ReservMgt.ModifyUnitOfMeasure;
        IF "Remaining Qty. (Base)" * OldJobPlanningLine."Remaining Qty. (Base)" < 0 THEN
          ReservMgt.DeleteReservEntries(TRUE,0)
        ELSE
          ReservMgt.DeleteReservEntries(FALSE,"Remaining Qty. (Base)");
        ReservMgt.ClearSurplus;
        ReservMgt.AutoTrack("Remaining Qty. (Base)");
        AssignForPlanning(NewJobPlanningLine);
      END;
    END;

    PROCEDURE TransferJobLineToItemJnlLine@35(VAR JobPlanningLine@1000 : Record 1003;VAR NewItemJnlLine@1001 : Record 83;TransferQty@1002 : Decimal) : Decimal;
    VAR
      OldReservEntry@1003 : Record 337;
      ItemTrackingFilterIsSet@1009 : Boolean;
      EndLoop@1008 : Boolean;
      TrackedQty@1005 : Decimal;
      UnTrackedQty@1007 : Decimal;
      xTransferQty@1006 : Decimal;
    BEGIN
      IF NOT FindReservEntry(JobPlanningLine,OldReservEntry) THEN
        EXIT(TransferQty);

      // Store initial values
      OldReservEntry.CALCSUMS("Quantity (Base)");
      TrackedQty := -OldReservEntry."Quantity (Base)";
      xTransferQty := TransferQty;

      OldReservEntry.Lock;

      // Handle Item Tracking on job planning line:
      CLEAR(CreateReservEntry);
      IF NewItemJnlLine."Entry Type" = NewItemJnlLine."Entry Type"::"Negative Adjmt." THEN
        IF NewItemJnlLine.TrackingExists THEN BEGIN
          CreateReservEntry.SetNewSerialLotNo(NewItemJnlLine."Serial No.",NewItemJnlLine."Lot No.");
          // Try to match against Item Tracking on the job planning line:
          OldReservEntry.SETRANGE("Serial No.",NewItemJnlLine."Serial No.");
          OldReservEntry.SETRANGE("Lot No.",NewItemJnlLine."Lot No.");
          IF OldReservEntry.ISEMPTY THEN BEGIN
            OldReservEntry.SETRANGE("Serial No.");
            OldReservEntry.SETRANGE("Lot No.");
          END ELSE
            ItemTrackingFilterIsSet := TRUE;
        END;

      NewItemJnlLine.TESTFIELD("Item No.",JobPlanningLine."No.");
      NewItemJnlLine.TESTFIELD("Variant Code",JobPlanningLine."Variant Code");
      NewItemJnlLine.TESTFIELD("Location Code",JobPlanningLine."Location Code");

      IF TransferQty = 0 THEN
        EXIT;

      IF ReservEngineMgt.InitRecordSet2(OldReservEntry,NewItemJnlLine."Serial No.",NewItemJnlLine."Lot No.") THEN
        REPEAT
          OldReservEntry.TESTFIELD("Item No.",JobPlanningLine."No.");
          OldReservEntry.TESTFIELD("Variant Code",JobPlanningLine."Variant Code");
          OldReservEntry.TESTFIELD("Location Code",JobPlanningLine."Location Code");

          TransferQty :=
            CreateReservEntry.TransferReservEntry(DATABASE::"Item Journal Line",
              NewItemJnlLine."Entry Type",NewItemJnlLine."Journal Template Name",NewItemJnlLine."Journal Batch Name",0,
              NewItemJnlLine."Line No.",NewItemJnlLine."Qty. per Unit of Measure",OldReservEntry,TransferQty);

          IF ReservEngineMgt.NEXTRecord(OldReservEntry) = 0 THEN
            IF ItemTrackingFilterIsSet THEN BEGIN
              OldReservEntry.SETRANGE("Serial No.");
              OldReservEntry.SETRANGE("Lot No.");
              ItemTrackingFilterIsSet := FALSE;
              EndLoop := NOT ReservEngineMgt.InitRecordSet(OldReservEntry);
            END ELSE
              EndLoop := TRUE;
        UNTIL EndLoop OR (TransferQty = 0);

      // Handle remaining transfer quantity
      IF TransferQty <> 0 THEN BEGIN
        TrackedQty -= (xTransferQty - TransferQty);
        UnTrackedQty := JobPlanningLine."Remaining Qty. (Base)" - TrackedQty;
        IF TransferQty > UnTrackedQty THEN BEGIN
          ReservMgt.SetJobPlanningLine(JobPlanningLine);
          ReservMgt.DeleteReservEntries(FALSE,JobPlanningLine."Remaining Qty. (Base)");
        END;
      END;
      EXIT(TransferQty);
    END;

    LOCAL PROCEDURE TransferJobLineToJobLine@31(VAR OldJobPlanningLine@1000 : Record 1003;VAR NewJobPlanningLine@1001 : Record 1003;TransferQty@1002 : Decimal);
    VAR
      OldReservEntry@1003 : Record 337;
      Status@1004 : 'Reservation,Tracking,Surplus,Prospect';
    BEGIN
      // Used for transfering a job planning to another job planning on another job
      IF NOT FindReservEntry(OldJobPlanningLine,OldReservEntry) THEN
        EXIT;

      OldReservEntry.Lock;

      NewJobPlanningLine.TESTFIELD("No.",OldJobPlanningLine."No.");
      NewJobPlanningLine.TESTFIELD("Variant Code",OldJobPlanningLine."Variant Code");
      NewJobPlanningLine.TESTFIELD("Location Code",OldJobPlanningLine."Location Code");

      FOR Status := Status::Reservation TO Status::Prospect DO BEGIN
        IF TransferQty = 0 THEN
          EXIT;
        OldReservEntry.SETRANGE("Reservation Status",Status);
        IF OldReservEntry.FINDSET THEN
          REPEAT
            OldReservEntry.TESTFIELD("Item No.",OldJobPlanningLine."No.");
            OldReservEntry.TESTFIELD("Variant Code",OldJobPlanningLine."Variant Code");
            OldReservEntry.TESTFIELD("Location Code",OldJobPlanningLine."Location Code");

            TransferQty :=
              CreateReservEntry.TransferReservEntry(DATABASE::"Job Planning Line",
                NewJobPlanningLine.Status,NewJobPlanningLine."Job No.",'',0,
                NewJobPlanningLine."Job Contract Entry No.",NewJobPlanningLine."Qty. per Unit of Measure",OldReservEntry,TransferQty);

          UNTIL (OldReservEntry.NEXT = 0) OR (TransferQty = 0);
      END; // DO
    END;

    LOCAL PROCEDURE RenameLine@16(VAR NewJobPlanningLine@1001 : Record 1003;VAR OldJobPlanningLine@1000 : Record 1003);
    BEGIN
      ReservEngineMgt.RenamePointer(DATABASE::"Job Planning Line",
        OldJobPlanningLine.Status,
        OldJobPlanningLine."Job No.",
        '',
        0,
        OldJobPlanningLine."Job Contract Entry No.",
        NewJobPlanningLine.Status,
        NewJobPlanningLine."Job No.",
        '',
        0,
        NewJobPlanningLine."Job Contract Entry No.");
    END;

    LOCAL PROCEDURE DeleteLineConfirm@20(VAR JobPlanningLine@1000 : Record 1003) : Boolean;
    BEGIN
      WITH JobPlanningLine DO BEGIN
        IF NOT ReservEntryExist(JobPlanningLine) THEN
          EXIT(TRUE);

        ReservMgt.SetJobPlanningLine(JobPlanningLine);
        IF ReservMgt.DeleteItemTrackingConfirm THEN
          DeleteItemTracking := TRUE;
      END;

      EXIT(DeleteItemTracking);
    END;

    PROCEDURE DeleteLine@3(VAR JobPlanningLine@1000 : Record 1003);
    BEGIN
      WITH JobPlanningLine DO BEGIN
        ReservMgt.SetJobPlanningLine(JobPlanningLine);
        IF DeleteItemTracking THEN
          ReservMgt.SetItemTrackingHandling(1); // Allow Deletion
        ReservMgt.DeleteReservEntries(TRUE,0);
        CALCFIELDS("Reserved Qty. (Base)");
        AssignForPlanning(JobPlanningLine);
      END;
    END;

    LOCAL PROCEDURE AssignForPlanning@5(VAR JobPlanningLine@1000 : Record 1003);
    VAR
      PlanningAssignment@1001 : Record 99000850;
    BEGIN
      WITH JobPlanningLine DO BEGIN
        IF Status <> Status::Order THEN
          EXIT;
        IF Type <> Type::Item THEN
          EXIT;
        IF "No." <> '' THEN
          PlanningAssignment.ChkAssignOne("No.","Variant Code","Location Code","Planning Date");
      END;
    END;

    LOCAL PROCEDURE Block@22(SetBlocked@1000 : Boolean);
    BEGIN
      Blocked := SetBlocked;
    END;

    BEGIN
    END.
  }
}

