OBJECT Codeunit 9062359 Consignment-Archive
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    TableNo=9062447;
    OnRun=BEGIN
            IF NOT UIDisabled THEN BEGIN
              IF (CONFIRM(STRSUBSTNO(cnfArchiveDocument, Rec."No."))) THEN BEGIN
                ArchiveDocument(Rec);
                MESSAGE(txtDocumentArchived);
              END;
            END ELSE BEGIN
              ArchiveDocument(Rec);
            END;
          END;

  }
  CODE
  {
    VAR
      cnfArchiveDocument@1000000000 : TextConst 'ENU=Do you want to archive consignment no. %1;ENG=Do you want to archive consignment no. %1';
      txtDocumentArchived@1000000001 : TextConst 'ENU=Document archived.;ENG=Document archived.';
      UIDisabled@9062230 : Boolean;

    PROCEDURE ArchiveDocument@1000000000(VAR ConsignmentHeader@1000000000 : Record 9062447);
    VAR
      ConsignmentLine@1000000001 : Record 9062448;
      ConsignmentStatusChange@1000000002 : Record 9062483;
      ArchivedConsHeader@1000000003 : Record 9062480;
      ArchivedConsLine@1000000004 : Record 9062481;
      ArchivedConsStatChange@1000000005 : Record 9062482;
      PurchaseLine@1000000006 : Record 39;
      FileAttachmentManagement@9062231 : Codeunit 9062267;
    BEGIN
      ArchivedConsHeader.TRANSFERFIELDS(ConsignmentHeader);
      ArchivedConsHeader.INSERT();

      ConsignmentLine.RESET();
      ConsignmentLine.SETRANGE("Consignment No.", ConsignmentHeader."No.");
      IF (ConsignmentLine.FINDSET()) THEN
        REPEAT
          ArchivedConsLine.TRANSFERFIELDS(ConsignmentLine);
          ArchivedConsLine.INSERT();
        UNTIL (ConsignmentLine.NEXT() = 0);

      ConsignmentStatusChange.RESET();
      ConsignmentStatusChange.SETRANGE("Consignment No.", ConsignmentHeader."No.");
      IF (ConsignmentStatusChange.FINDSET()) THEN
        REPEAT
          ArchivedConsStatChange.TRANSFERFIELDS(ConsignmentStatusChange);
          ArchivedConsStatChange.INSERT();
        UNTIL (ConsignmentStatusChange.NEXT() = 0);

      ConsignmentStatusChange.DELETEALL();
      ConsignmentLine.DELETEALL();
      ConsignmentHeader.DELETE();

      FileAttachmentManagement.MoveToRecord(DATABASE::"Consignment Archive Header",ArchivedConsHeader.GETPOSITION,DATABASE::"Consignment Header",ConsignmentHeader.GETPOSITION);
    END;

    PROCEDURE DisableUserInteraction@9062230();
    BEGIN
      UIDisabled := TRUE;
    END;

    BEGIN
    {
      //DOC NH2016.11 PC 05/11/2015 - Migrated from 2015
      //DOC NH2016.15 JH 18/04/2016 - CfMD amendments
    }
    END.
  }
}

