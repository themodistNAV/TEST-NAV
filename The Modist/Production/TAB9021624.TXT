OBJECT Table 9021624 Label Attrib Translation Value
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:01 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LabelAttRec.GET("Label Attribute Entry No.");
               "Html Allowed" := LabelAttRec."Allow HTML";
               IF LabelAttRec."Type Code" = 'TEMPLATE' THEN BEGIN
                 IF "Language Code" <> '' THEN
                   ERROR(Text003);
               END ELSE BEGIN
                 IF ("Language Code" = '') AND
                   (LabelAttRec."Allow Blank Language Code" = FALSE) AND
                   ("Html Allowed" = FALSE) THEN
                   ERROR(Text002);
               END;
               "Do Not Extract" := LabelAttRec."Do Not Extract";  //DOC EC2015.5.03

               LabAttValRec.RESET;
               LabAttValRec.SETRANGE("Label Code","Label Code");
               LabAttValRec.SETRANGE("Label Attribute Entry No.","Label Attribute Entry No.");
               IF "HTML Entry No." = 0 THEN
                 LabAttValRec.SETFILTER("Language Code",'%1','')
               ELSE
                 LabAttValRec.SETFILTER("Language Code",'<>%1','');
               IF LabAttValRec.FINDFIRST THEN
                 ERROR(Text008);

               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021624);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    OnModify=BEGIN
               LabAttValRec.RESET;
               LabAttValRec.SETRANGE("Label Code","Label Code");
               LabAttValRec.SETRANGE("Label Attribute Entry No.","Label Attribute Entry No.");
               IF "HTML Entry No." = 0 THEN
                 LabAttValRec.SETFILTER("Language Code",'%1','')
               ELSE
                 LabAttValRec.SETFILTER("Language Code",'<>%1','');
               IF LabAttValRec.FINDFIRST THEN
                 ERROR(Text008);

               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021624);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    OnDelete=BEGIN
               //DOC EC2013.3.80 SyncMgt.DeletionMgt(9021624,FORMAT("Label Code"),FORMAT("Label Attribute Entry No."),
               //DOC EC2013.3.80                     "Language Code",'','','','','',"Sync Log No.","Sync Version No.");
             END;

    OnRename=BEGIN
               IF "Language Code" = '' THEN BEGIN
                 LabelAttRec.GET("Label Attribute Entry No.");
                 IF LabelAttRec."Allow Blank Language Code" = FALSE THEN
                   ERROR(Text002);
               END;

               //DOC EC2013.3.80 "Sync Log No." := SyncMgt.InsertModifyRenameMgt(9021624);
               //DOC EC2013.3.80 "Sync Version No." := CREATEGUID;
             END;

    CaptionML=[ENU=Label Attrib Translation Value;
               ENG=Label Attrib Translation Value];
    LookupPageID=Page9021634;
    DrillDownPageID=Page9021634;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Label Attribute Entry No.;Integer  ;TableRelation="Label Attribute"."Entry No." WHERE (Label Code=FIELD(Label Code),
                                                                                                      Entry No.=FIELD(Label Attribute Entry No.));
                                                   CaptionML=[ENU=Label Attribute Entry No.;
                                                              ENG=Label Attribute Entry No.];
                                                   NotBlank=Yes }
    { 10  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   OnValidate=BEGIN
                                                                IF ("HTML Entry No." <> 0) AND ("Language Code" <> '') THEN
                                                                  ERROR(Text005,FIELDCAPTION("Language Code"));
                                                              END;

                                                   CaptionML=[ENU=Language Code;
                                                              ENG=Language Code] }
    { 20  ;   ;Value               ;Text250       ;OnValidate=BEGIN
                                                                IF Value = '<html entry>' THEN
                                                                  ERROR(Text007,Value);
                                                                IF ("HTML Entry No." <> 0) AND (Value <> '') AND
                                                                  (Value <> '<html entry>') THEN
                                                                  ERROR(Text005,FIELDCAPTION(Value));
                                                                IF "Language Code" = '' THEN BEGIN
                                                                  LabelAttRec.GET("Label Attribute Entry No.");
                                                                  IF (LabelAttRec."Allow Blank Language Code" = FALSE) AND
                                                                    (LabelAttRec."Type Code" <> 'TEMPLATE') THEN
                                                                    ERROR(Text002);
                                                                END ELSE BEGIN
                                                                  IF "HTML Entry No." <> 0 THEN
                                                                    ERROR(Text005,FIELDCAPTION("Language Code"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Value;
                                                              ENG=Value] }
    { 30  ;   ;HTML Entry No.      ;Integer       ;OnValidate=BEGIN
                                                                IF "HTML Entry No." = 0 THEN BEGIN
                                                                  Value := '';
                                                                END ELSE BEGIN
                                                                  IF "Language Code" <> '' THEN
                                                                    ERROR(Text005,FIELDCAPTION("Language Code"));
                                                                  IF (Value <> '') AND (Value <> '<html entry>')  THEN
                                                                    ERROR(Text005,FIELDCAPTION(Value));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=HTML Entry No.;
                                                              ENG=HTML Entry No.] }
    { 31  ;   ;Html Allowed        ;Boolean       ;CaptionML=[ENU=Html Allowed;
                                                              ENG=Html Allowed] }
    { 32  ;   ;Shared Html         ;Boolean       ;CaptionML=[ENU=Shared Html;
                                                              ENG=Shared Html] }
    { 200 ;   ;Do Not Extract      ;Boolean       ;CaptionML=[ENU=Do Not Extract;
                                                              ENG=Do Not Extract];
                                                   Description=GW add 150602 }
  }
  KEYS
  {
    {    ;Label Code,Label Attribute Entry No.,Language Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LabelRec@1000000000 : Record 9021618;
      LangRec@1040000 : Record 8;
      LabelAttRec@1040001 : Record 9021623;
      LabelLookup@1190000007 : Record 9021622;
      LabAttValRec@1040003 : Record 9021624;
      Text000@1040004 : TextConst 'ENU=Can not find Language - see SyncAttributes function in Label Attribute Translation Value.;ENG=Can not find Language - see SyncAttributes function in Label Attribute Translation Value.';
      Text001@1040005 : TextConst 'ENU=Can not find Entry No. - see SyncAttributes function in Label Attribute Translation Value.;ENG=Can not find Entry No. - see SyncAttributes function in Label Attribute Translation Value.';
      LanguageCode@1040006 : Code[10];
      Text002@1040007 : TextConst 'ENU=The language code cannot be blank.;ENG=The language code cannot be blank.';
      Text003@1040008 : TextConst 'ENU=The Atttribute Value for Templates must have a blank language code.;ENG=The Atttribute Value for Templates must have a blank language code.';
      GlobalFuncs@1000000002 : Codeunit 9021624;
      LabelAttRecEntryNo@1000000001 : Integer;
      Text004@1000000003 : TextConst 'ENU=The Value must be blank if this is an HTML Entry.;ENG=The Value must be blank if this is an HTML Entry.';
      Text005@1000000004 : TextConst 'ENU=The %1 must be blank if this is an HTML Entry No.;ENG=The %1 must be blank if this is an HTML Entry No.';
      Text006@1000000005 : TextConst 'ENU=HTML is not allowed against this Attribute Type.;ENG=HTML is not allowed against this Attribute Type.';
      Text007@1000000006 : TextConst 'ENU=You cannot manually enter %1.;ENG=You cannot manually enter %1.';
      Text008@1000000007 : TextConst 'ENU=You cannot do this as there is an existing Label Attribute Translation Value incompatible with this one.;ENG=You cannot do this as there is an existing Label Attribute Translation Value incompatible with this one.';

    PROCEDURE SyncAttributes@1040007(TypeIn@1040000 : Code[20];ValueIn@1040001 : Text[250];OwnerRef@1040003 : Code[20];LabelCode@1040004 : Code[10];AttributeTypeIn@1190000005 : Code[10]);
    BEGIN
      IF OwnerRef = '' THEN
        EXIT;

      IF NOT LabelLookup.GET(AttributeTypeIn,TypeIn) THEN BEGIN
        LabelLookup.INIT;
        LabelLookup."Attribute Type" := AttributeTypeIn;
        LabelLookup.Code := TypeIn;
        LabelLookup.Description := TypeIn;
        LabelLookup.INSERT;
      END;

      LangRec.RESET;
      LangRec.SETRANGE("Windows Language ID",GLOBALLANGUAGE);
      LangRec.SETFILTER(Code,'<>%1','CPL');
      IF NOT LangRec.FINDFIRST THEN
        ERROR(Text000);

      LabelAttRec.RESET;
      LabelAttRec.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      LabelAttRec.SETRANGE("Label Code",LabelCode);
      LabelAttRec.SETRANGE("Attribute Type",AttributeTypeIn);
      LabelAttRec.SETRANGE("Attribute Owner Reference",OwnerRef);
      LabelAttRec.SETRANGE("Type Code",TypeIn);
      IF LabelAttRec.FINDFIRST THEN BEGIN
        LabelAttRecEntryNo := LabelAttRec."Entry No.";
      END ELSE BEGIN
        IF ValueIn = '' THEN
          EXIT;
        LabelAttRec.INIT;
        LabelAttRec."Label Code" := LabelCode;
        LabelAttRec."Attribute Type" := AttributeTypeIn;
        LabelAttRec."Attribute Owner Reference" := OwnerRef;
        LabelAttRec."Type Code" := TypeIn;
        LabelAttRec."First Saved Value" := ValueIn;
        LabelAttRec.INSERT(TRUE);
        LabelAttRecEntryNo := LabelAttRec."Entry No.";
      END;
      IF LabelAttRecEntryNo = 0 THEN
        ERROR(Text001);
      LabAttValRec.RESET;
      IF TypeIn = 'TEMPLATE' THEN
        LanguageCode := ''
      ELSE
        LanguageCode := LangRec.Code;
      IF LabAttValRec.GET(LabelCode,LabelAttRecEntryNo,LanguageCode) THEN BEGIN
        IF ValueIn = '' THEN
          LabAttValRec.DELETE
        ELSE BEGIN
          LabAttValRec.Value := ValueIn;
          LabAttValRec.MODIFY;
        END;
      END ELSE BEGIN
        IF ValueIn <> '' THEN BEGIN
          LabAttValRec.INIT;
          LabAttValRec."Label Code" := LabelCode;
          LabAttValRec."Label Attribute Entry No." := LabelAttRecEntryNo;
          LabAttValRec."Language Code" := LanguageCode;
          LabAttValRec.Value := ValueIn;
          LabAttValRec.INSERT(TRUE);
        END;
      END;
    END;

    PROCEDURE CreateAttributesIfNotExist@1040001(TypeIn@1040000 : Code[20];ValueIn@1040001 : Text[250];OwnerRef@1040003 : Code[20];LabelCodeIn@1040004 : Code[10];AttributeTypeIn@1190000005 : Code[10]) LabelAttEntryNo : Integer;
    BEGIN
      IF OwnerRef = '' THEN
        EXIT;
      LangRec.RESET;
      LangRec.SETRANGE("Windows Language ID",GLOBALLANGUAGE);
      LangRec.SETFILTER(Code,'<>%1','CPL');
      IF NOT LangRec.FINDFIRST THEN
        ERROR(Text000);

      IF NOT LabelLookup.GET(AttributeTypeIn,TypeIn) THEN BEGIN
        LabelLookup.INIT;
        LabelLookup."Attribute Type" := AttributeTypeIn;
        LabelLookup.Code := TypeIn;
        LabelLookup.Description := TypeIn;
        LabelLookup.INSERT;
      END;

      LabelAttRec.RESET;
      LabelAttRec.SETCURRENTKEY("Label Code","Attribute Type","Attribute Owner Reference","Type Code");
      LabelAttRec.SETRANGE("Label Code",LabelCodeIn);
      LabelAttRec.SETRANGE("Attribute Type",AttributeTypeIn);
      LabelAttRec.SETRANGE("Attribute Owner Reference",OwnerRef);
      LabelAttRec.SETRANGE("Type Code",TypeIn);

      IF LabelAttRec.FINDFIRST THEN
        LabelAttEntryNo := LabelAttRec."Entry No."
      ELSE BEGIN
        LabelAttRec.INIT;
        LabelAttRec."Label Code" := LabelCodeIn;
        LabelAttRec."Attribute Type" := AttributeTypeIn;
        LabelAttRec."Attribute Owner Reference" := OwnerRef;
        LabelAttRec."Type Code" := TypeIn;
        LabelAttRec."First Saved Value" := ValueIn;
        LabelAttRec.INSERT(TRUE);
        LabelAttEntryNo := LabelAttRec."Entry No.";
      END;
      IF LabelAttEntryNo = 0 THEN
        ERROR(Text001);

      LabAttValRec.RESET;
      IF TypeIn = 'TEMPLATE' THEN
        LanguageCode := ''
      ELSE
        LanguageCode := LangRec.Code;
      IF LabAttValRec.GET(LabelCodeIn,LabelAttEntryNo,LanguageCode) THEN BEGIN
        IF ValueIn <> '' THEN BEGIN
          LabAttValRec.Value := ValueIn;
          LabAttValRec.MODIFY;
        END;
      END ELSE BEGIN
        IF ValueIn <> '' THEN BEGIN
          LabAttValRec.INIT;
          LabAttValRec."Label Code" := LabelCodeIn;
          LabAttValRec."Label Attribute Entry No." := LabelAttEntryNo;
          LabAttValRec."Language Code" := LanguageCode;
          LabAttValRec.Value := ValueIn;
          LabAttValRec.INSERT(TRUE);
        END;
      END;
    END;

    PROCEDURE HtmlEntry@1040000();
    BEGIN
      //pass "Html Entry No." to function where it's updated if 0...
      IF (Value <> '') AND (Value <> '<html entry>') THEN
        ERROR(Text004);
      IF LabAttValRec.GET("Label Code","Label Attribute Entry No.",'') THEN BEGIN
        IF "HTML Entry No." = 0 THEN BEGIN
          IF "Html Allowed" THEN BEGIN
            LabelAttRec.GET("Label Attribute Entry No.");
            LabelRec.EditHtmlEntry("Html Allowed","HTML Entry No.","Label Code","Shared Html",
                                LabelAttRec."Attribute Type",LabelAttRec."Attribute Owner Reference",
                                LabelAttRec."Type Code",GlobalFuncs.GetLanguage);
            MODIFY(TRUE);
          END ELSE
            ERROR(Text006);
        END ELSE BEGIN
          LabelAttRec.GET("Label Attribute Entry No.");
          LabelRec.EditHtmlEntry("Html Allowed","HTML Entry No.","Label Code","Shared Html",
                                LabelAttRec."Attribute Type",LabelAttRec."Attribute Owner Reference",
                                LabelAttRec."Type Code",GlobalFuncs.GetLanguage);
        END;
      END ELSE BEGIN
        "Language Code" := '';
        LabelAttRec.GET("Label Attribute Entry No.");
        LabelAttRec.EditHtmlEntry;
      END;
    END;

    BEGIN
    {
      //DOC EC2013.3.80 MQ 20/08/2013 - GW130820 Remove fields, key, global and use of Sync Mgt
      //DOC EC2015.5.03 MQ 02/06/2015 - GW150602 Add Do Not Extract field and Code
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

