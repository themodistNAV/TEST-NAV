OBJECT Table 9021636 Page Item Content
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:03 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "DateTime Created":=CURRENTDATETIME;
               SetModifiedDateTime();
             END;

    OnModify=BEGIN
               SetModifiedDateTime();
             END;

    OnDelete=BEGIN
               PageItemRec.RESET;
               PageItemRec.SETRANGE("Label Code","Label Code");
               PageItemRec.SETRANGE(Type,PageItemRec.Type::Content);
               //DOC EC2013.3.00 PageItemRec.SETRANGE("Page Item Type Code","Page Item Type Code");
               PageItemRec.SETRANGE("No.","No.");
               IF PageItemRec.FINDFIRST THEN
                 ERROR(Text001);

               //DOC EC2013.3.00 -
               LabelAttributeRec.RESET;
               LabelAttributeRec.SETRANGE("Label Code","Label Code");
               LabelAttributeRec.SETRANGE("Attribute Type",'CONTENT');
               LabelAttributeRec.SETRANGE("Attribute Owner Reference","No.");
               IF LabelAttributeRec.FIND('-') THEN
               REPEAT
                 LabelAttributeRec.DELETE(TRUE);
               UNTIL LabelAttributeRec.NEXT = 0;
               //DOC EC2013.3.00+
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);  //DOC EC2013.3.00 so do not have to deal with links in Label Attributes
             END;

    CaptionML=[ENU=Page Item Content;
               ENG=Page Item Content];
    LookupPageID=Page9021666;
    DrillDownPageID=Page9021666;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                Label.GET("Label Code");
                                                                IF Label."Used-in Company" <> COMPANYNAME THEN
                                                                  ERROR(Text006);
                                                                IF "No." <> '' THEN
                                                                  ERROR(Text005,FIELDCAPTION("Label Code"),TABLECAPTION,FIELDCAPTION("No."));
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Page Item Type Code ;Code10        ;TableRelation="Page Item Content Type".Code WHERE (Label Code=FIELD(Label Code));
                                                   OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "Label Code" = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("Label Code"));
                                                                IF "No." <> '' THEN
                                                                  ERROR(Text005,FIELDCAPTION("Page Item Type Code"),TABLECAPTION,FIELDCAPTION("No."));
                                                                IF "Page Item Type Code" = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("Page Item Type Code"));

                                                                PageItemContType.GET("Label Code","Page Item Type Code");
                                                                PageItemContType.TESTFIELD("Number Series");
                                                                "No." := "Page Item Type Code" + '-' +
                                                                         NoSeriesMgt.GetNextNo(PageItemContType."Number Series",TODAY,TRUE);
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Page Item Type Code;
                                                              ENG=Page Item Type Code];
                                                   NotBlank=Yes }
    { 3   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ENG=No.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 10  ;   ;Internal Description;Text100       ;CaptionML=[ENU=Internal Description;
                                                              ENG=Internal Description] }
    { 11  ;   ;Web Description     ;Text250       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('WEB_DESCRIPTION',"Web Description","No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Web Description;
                                                              ENG=Web Description] }
    { 15  ;   ;Title               ;Text100       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('TITLE',Title,"No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Title;
                                                              ENG=Title] }
    { 20  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ENG=Date] }
    { 21  ;   ;Expiry Date         ;Date          ;CaptionML=[ENU=Expiry Date;
                                                              ENG=Expiry Date] }
    { 30  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ENG=Blocked] }
    { 50  ;   ;Thumbnail Image     ;Text250       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('IMAGE_THUMBNAIL',"Thumbnail Image","No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Thumbnail Image;
                                                              ENG=Thumbnail Image] }
    { 52  ;   ;Main Image          ;Text250       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('IMAGE_MAIN',"Main Image","No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Main Image;
                                                              ENG=Main Image] }
    { 55  ;   ;Image Alternative Text;Text100     ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('IMAGE_ALT_TEXT',"Image Alternative Text","No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Image Alternative Text;
                                                              ENG=Image Alternative Text] }
    { 56  ;   ;Image Title         ;Text100       ;OnValidate=BEGIN
                                                                //DOC EC2013.3.00 -
                                                                IF "No." = '' THEN
                                                                  ERROR(Text004,FIELDCAPTION("No."))
                                                                ELSE
                                                                  LabelAttTransRec.SyncAttributes('IMAGE_TITLE',"Image Title","No.",
                                                                                                  "Label Code",'CONTENT');
                                                                //DOC EC2013.3.00 +
                                                              END;

                                                   CaptionML=[ENU=Image Title;
                                                              ENG=Image Title] }
    { 60  ;   ;Detail URL          ;Text100       ;CaptionML=[ENU=Detail URL;
                                                              ENG=Detail URL] }
    { 70  ;   ;Download URL        ;Text100       ;CaptionML=[ENU=Download URL;
                                                              ENG=Download URL];
                                                   Description=MA20111006 EC2009.2.70 }
    { 80  ;   ;Security Type       ;Option        ;OnValidate=BEGIN
                                                                IF ("Security Type"<>xRec."Security Type") THEN
                                                                  "Security Filter":='';
                                                              END;

                                                   CaptionML=[ENU=Security Type;
                                                              ENG=Security Type];
                                                   OptionCaptionML=[ENU=" ,User Type,Web Security Role";
                                                                    ENG=" ,User Type,Web Security Role"];
                                                   OptionString=[ ,User Type,Web Security Role];
                                                   Description=MA20110821 EC2009.2.69 }
    { 81  ;   ;Security Filter     ;Code10        ;OnValidate=BEGIN
                                                                IF "Security Type"="Security Type"::" " THEN
                                                                  ERROR(Text002,FIELDCAPTION("Security Type"),FIELDCAPTION("Security Filter"));
                                                              END;

                                                   OnLookup=VAR
                                                              WebSecurityRole@1190000005 : Record 9021730;
                                                            BEGIN
                                                            END;

                                                   CaptionML=[ENU=Security Filter;
                                                              ENG=Security Filter];
                                                   ValuesAllowed=[B2C;B2B];
                                                   Description=MA20110821 EC2009.2.69 }
    { 101 ;   ;Html Entry No.      ;Integer       ;CaptionML=[ENU=Html Entry No.;
                                                              ENG=Html Entry No.] }
    { 110 ;   ;Usage Count         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Page Item" WHERE (Type=FILTER(Content),
                                                                                        No.=FIELD(No.)));
                                                   CaptionML=[ENU=Usage Count;
                                                              ENG=Usage Count];
                                                   Editable=No }
    { 120 ;   ;DateTime Created    ;DateTime      ;CaptionML=[ENU=DateTime Created;
                                                              ENG=DateTime Created] }
    { 121 ;   ;DateTime Last Modified;DateTime    ;CaptionML=[ENU=DateTime Last Modified;
                                                              ENG=DateTime Last Modified] }
  }
  KEYS
  {
    {    ;Label Code,No.                          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Label@1000000000 : Record 9021618;
      PageItemRec@1040002 : Record 9021629;
      Text001@1040003 : TextConst 'ENU=Error.... You cannot delete a Content Item while it exists in Page Contents. (1);ENG=Error.... You cannot delete a Content Item while it exists in Page Contents. (1)';
      Text002@1190000006 : TextConst 'ENU=Please select a %1 before assigning a %2.;ENG=Please select a %1 before assigning a %2.';
      Text003@1190000007 : TextConst 'ENU=Error.. You are not allowed to rename a %1.  Please delete and create a new one.;ENG=Error.. You are not allowed to rename a %1.  Please delete and create a new one.';
      Text004@1190000008 : TextConst 'ENU=The %1 must be filled in before you can do this.;ENG=The %1 must be filled in before you can do this.';
      PageItemContType@1190000005 : Record 9021632;
      LabelAttTransRec@1190000013 : Record 9021624;
      LabelAttributeRec@1190000014 : Record 9021623;
      GlobalFuncs@1190000009 : Codeunit 9021624;
      NoSeriesMgt@1190000010 : Codeunit 396;
      Text005@1190000011 : TextConst 'ENU=You cannot change the %1 now the %2 has a %3.;ENG=You cannot change the %1 now the %2 has a %3.';
      MultiLangFunc@1190000012 : Codeunit 9021603;
      Text006@1190000016 : TextConst 'ENU=You cannot use a Label that is associated with another company.;ENG=You cannot use a Label that is associated with another company.';

    PROCEDURE HtmlEntry@1040000();
    VAR
      OwnerId@1190000005 : Code[30];
    BEGIN
      //pass "Html Entry No." to function where it's updated if 0...
      //<MA20110714>
      //Label.EditHtmlEntry(TRUE,"Html Entry No.","Label Code",FALSE);
      OwnerId:="Page Item Type Code"+'-'+"No.";
      Label.EditHtmlEntry(TRUE,"Html Entry No.","Label Code",FALSE,
                                'CONTENT',OwnerId,'DESCRIPTION',GlobalFuncs.GetLanguage);
      //</MA20110714>
      //save any change to "Html Entry No."
      MODIFY;
    END;

    PROCEDURE SetModifiedDateTime@1000000003();
    VAR
      Page@1000000000 : Record 9021628;
      PageItem@1000000001 : Record 9021629;
    BEGIN
      "DateTime Last Modified":=CURRENTDATETIME;
      PageItem.SETRANGE("Label Code","Label Code");
      PageItem.SETFILTER(Type,'%1',PageItem.Type::Content);
      //DOC EC2013.3.00 PageItem.SETRANGE("Page Item Type Code","Page Item Type Code");
      PageItem.SETRANGE("No.","No.");
      IF NOT PageItem.FINDFIRST THEN EXIT;

      //update all pages which have this content listed...
      REPEAT
        IF Page.GET("Label Code",PageItem."Page GroupGuid") THEN BEGIN
          Page.VALIDATE("DateTime Last Modified",CURRENTDATETIME);
          Page.MODIFY;
        END;
        PageItem.VALIDATE("DateTime Last Modified",CURRENTDATETIME);
        PageItem.MODIFY;
      UNTIL PageItem.NEXT=0;
    END;

    BEGIN
    {
      //DOC EC2009.2    MA 03/10/2010 - "Web Description" length increased from 100 to 250; "Title" length increased from 50 to 100
      //DOC EC2009.2.67 MA 14/07/2011 - MA20110714 mods to HtmlEntry function
      //DOC EC2009.2.69 MA 21/08/2011 - MA20110821 added Security Type and Security Filter fields
      //DOC EC2009.2.70 MA 06/10/2011 - MA20111006 added "Download URL"
      //DOC EC2009.2.70 MQ 17/10/2011 - GW111017 Removed fields 12 Render Description As Link Bo; 16 Render Title As Link Bo; 51 Render Thumbnail As Link Bo
      //DOC EC2013.3.00 MQ 01/01/2013 - Removed calls to removed field Page Item.Page Item Type Code; Removed Page Item Type Code from primary key; Error Rename
      //DOC EC2013.3.30 MQ 01/01/2013 - Enhanced Error message in AssistEditForAttributes
      //DOC EC2013.4.00 MQ 05/11/2013 - GW131105 Removed function AssistEditForAttributes, now in Version Calls CUs.  Not called from this table.
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

