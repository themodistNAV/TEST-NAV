OBJECT Codeunit 701 Data Type Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetRecordRefAndFieldRef@21(RecRelatedVariant@1001 : Variant;FieldNumber@1000 : Integer;VAR RecordRef@1002 : RecordRef;VAR FieldRef@1003 : FieldRef) : Boolean;
    BEGIN
      IF NOT GetRecordRef(RecRelatedVariant,RecordRef) THEN
        EXIT(FALSE);

      FieldRef := RecordRef.FIELD(FieldNumber);
      EXIT(TRUE);
    END;

    PROCEDURE GetRecordRef@24(RecRelatedVariant@1001 : Variant;VAR ResultRecordRef@1002 : RecordRef) : Boolean;
    VAR
      RecID@1000 : RecordID;
    BEGIN
      CASE TRUE OF
        RecRelatedVariant.ISRECORD:
          ResultRecordRef.GETTABLE(RecRelatedVariant);
        RecRelatedVariant.ISRECORDREF:
          ResultRecordRef := RecRelatedVariant;
        RecRelatedVariant.ISRECORDID:
          BEGIN
            RecID := RecRelatedVariant;
            IF RecID.TABLENO = 0 THEN
              EXIT(FALSE);
            IF NOT ResultRecordRef.GET(RecID) THEN
              ResultRecordRef.OPEN(RecID.TABLENO);
          END;
        ELSE
          EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE FindFieldByName@22(RecordRef@1001 : RecordRef;VAR FieldRef@1003 : FieldRef;FieldNameTxt@1000 : Text) : Boolean;
    VAR
      Field@1002 : Record 2000000041;
    BEGIN
      Field.SETRANGE(TableNo,RecordRef.NUMBER);
      Field.SETRANGE(FieldName,FieldNameTxt);

      IF NOT Field.FINDFIRST THEN
        EXIT(FALSE);

      FieldRef := RecordRef.FIELD(Field."No.");
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

