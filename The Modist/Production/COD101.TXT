OBJECT Codeunit 101 Cust. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CustEntryApplID@1000 : Code[50];

    PROCEDURE SetApplId@1(VAR CustLedgEntry@1000 : Record 21;ApplyingCustLedgEntry@1003 : Record 21;AppliesToID@1001 : Code[50]);
    BEGIN
      CustLedgEntry.LOCKTABLE;
      IF CustLedgEntry.FIND('-') THEN BEGIN
        // Make Applies-to ID
        IF CustLedgEntry."Applies-to ID" <> '' THEN
          CustEntryApplID := ''
        ELSE BEGIN
          CustEntryApplID := AppliesToID;
          IF CustEntryApplID = '' THEN BEGIN
            CustEntryApplID := USERID;
            IF CustEntryApplID = '' THEN
              CustEntryApplID := '***';
          END;
        END;

        // Set Applies-to ID
        REPEAT
          CustLedgEntry.TESTFIELD(Open,TRUE);
          CustLedgEntry."Applies-to ID" := CustEntryApplID;
          IF CustLedgEntry."Applies-to ID" = '' THEN BEGIN
            CustLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
            CustLedgEntry."Accepted Payment Tolerance" := 0;
          END;
          // Set Amount to Apply
          IF ((CustLedgEntry."Amount to Apply" <> 0) AND (CustEntryApplID = '')) OR
             (CustEntryApplID = '')
          THEN
            CustLedgEntry."Amount to Apply" := 0
          ELSE
            IF CustLedgEntry."Amount to Apply" = 0 THEN BEGIN
              CustLedgEntry.CALCFIELDS("Remaining Amount");
              CustLedgEntry."Amount to Apply" := CustLedgEntry."Remaining Amount"
            END;

          IF CustLedgEntry."Entry No." = ApplyingCustLedgEntry."Entry No." THEN
            CustLedgEntry."Applying Entry" := ApplyingCustLedgEntry."Applying Entry";
          CustLedgEntry.MODIFY;
        UNTIL CustLedgEntry.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

