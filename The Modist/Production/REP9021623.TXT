OBJECT Report 9021623 Create Internet Custs from Cus
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:51:06 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Internet Custs from Cus;
               ENG=Create Internet Custs from Cus];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CONFIRM(Text000) THEN
                    ERROR(Text001);
                  IF NOT CONFIRM(Text002) THEN
                    ERROR(Text001);

                  LabelCode := '';
                  LabelCode := MQGlobal.GetLabelCode;
                  IF NOT CONFIRM(Text003,FALSE,LabelCode) THEN
                    ERROR(Text001);
                END;

    OnPostReport=VAR
                   TxtL001@1000000000 : TextConst 'ENU=Internet Customers created for %1 customers.;ENG=Internet Customers created for %1 customers.';
                 BEGIN
                   MESSAGE(TxtL001,i);
                 END;

  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=WHERE(Is An Internet Customer=CONST(No));
               ReqFilterHeadingML=[ENU=6836;
                                   ENG=6836];
               OnAfterGetRecord=VAR
                                  TxtL001@1000000000 : TextConst 'ENU=Customer No. is wrong in ContBusRel, Customer No. %1;ENG=Customer No. is wrong in ContBusRel, Customer No. %1';
                                  TxtL002@1000000001 : TextConst 'ENU=Contact No. must not be blank in ContBusRel, Customer No. %1;ENG=Contact No. must not be blank in ContBusRel, Customer No. %1';
                                  TxtL003@1000000002 : TextConst 'ENU=Something has gone wrong, no User Guid on Customer %1;ENG=Something has gone wrong, no User Guid on Customer %1';
                                  TxtL004@1000000003 : TextConst 'ENU=INITIAL LOAD;ENG=INITIAL LOAD';
                                BEGIN
                                  Cust := Customer;
                                  i := i + 1;

                                  ContBusRel.SETCURRENTKEY("Link to Table","No.");
                                  ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
                                  ContBusRel.SETRANGE("No.",Cust."No.");
                                  IF NOT ContBusRel.FIND('-') THEN BEGIN
                                    UpdateContFromCust.InsertNewContact(Cust,FALSE);
                                    ContBusRel.FIND('-');
                                  END;

                                  IF ContBusRel."No." <> Cust."No." THEN
                                    ERROR(TxtL001,Cust."No.");
                                  IF ContBusRel."Contact No." = '' THEN
                                    ERROR(TxtL002,Cust."No.");
                                  Cust.VALIDATE("Label Code",LabelCode);
                                  Cust.VALIDATE("Is An Internet Customer",TRUE);
                                  IF Cust."User GUID" = '' THEN
                                    ERROR(TxtL003,Cust."No.");
                                  Cust.MODIFY;

                                  CLEAR(IntCust);
                                  IntCust.INIT;
                                  IntCust."No." := Cust."User GUID";
                                  IntCust."Customer No." := Cust."No.";
                                  IntCust."Contact No." := ContBusRel."Contact No.";
                                  IntCust.Name := Cust.Name;
                                  IntCust.Address := Cust.Address;
                                  IntCust."Address 2" := Cust."Address 2";
                                  IntCust.City := Cust.City;
                                  IntCust.County := Cust.County;
                                  IntCust."Country Code" := Cust."Country/Region Code";
                                  IntCust."Post Code" := Cust."Post Code";
                                  IntCust.Contact := Cust.Name;
                                  IntCust."Phone No." := Cust."Phone No.";
                                  IntCust."E-Mail" := Cust."E-Mail";
                                  IntCust.Created := TODAY;
                                  IntCust."Currency Code" := Cust."Currency Code";
                                  IntCust."Label Code" := LabelCode;
                                  IntCust.Login := Cust."E-Mail";
                                  IntCust."Last Date Modified" := TODAY;
                                  IntCust."Last DateTime Processed" := CURRENTDATETIME;
                                  IntCust."Last DateTime Modified" := CURRENTDATETIME;
                                  IntCust."Source Code User Created" := TxtL004;
                                  IntCust."Source Code Last Tracked" := TxtL004;
                                  IntCust."Source Code Last Tracked DateT" := CURRENTDATETIME;
                                  IntCust.INSERT;
                                END;

               ReqFilterFields=No.,Customer Posting Group,Customer Price Group }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000000000 : TextConst 'ENU=This report will make the selected customers, Internet Customers and create Internet Customers.  Are you sure?;ENG=This report will make the selected customers, Internet Customers and create Internet Customers.  Are you sure?';
      Text001@1000000001 : TextConst 'ENU=Abandonned.;ENG=Abandonned.';
      Text002@1000000002 : TextConst 'ENU=You will need to make sure the selected customers are exported to the web.  Are you sure you know how to do this?;ENG=You will need to make sure the selected customers are exported to the web.  Are you sure you know how to do this?';
      IntCust@1000000003 : Record 9021642;
      ContBusRel@1000000004 : Record 5054;
      Cust@1000000005 : Record 18;
      Cont@1000000010 : Record 5050;
      LabelCode@1000000007 : Code[10];
      i@1000000006 : Integer;
      MQGlobal@1000000008 : Codeunit 9021624;
      Text003@1000000009 : TextConst 'ENU=The will use Label Code %1.  Is that correct?;ENG=The will use Label Code %1.  Is that correct?';
      UpdateContFromCust@1000000011 : Codeunit 5056;

    BEGIN
    {
      //DOC EC2009.2    MQ 01/01/2012 - Data load report. Creates Internet Customers and Contacts from Customers. This process must be combined with the extract of Customers from NAV and then
      //DOC EC2009.2    MQ 01/01/2012 - the User records must be created online. The reason to do it this way is otherwise new Internet Customers will detect a duplication and it need manual intervention.
      //DOC EC2013.4.64 MQ 10/07/2014 - GW140710 New
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
  RDLDATA
  {
  }
}

