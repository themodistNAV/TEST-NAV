OBJECT Table 9021650 Items in Promotion Group
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:04 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               //DOC EC2015.5.01 -
               IF "Label Code" = '' THEN
                 ERROR(Text007,FIELDCAPTION("Label Code"));
               IF "Promotion Group Code" = '' THEN
                 ERROR(Text007,FIELDCAPTION("Promotion Group Code"));
               IF "Item No." = '' THEN
                 ERROR(Text007,FIELDCAPTION("Item No."));
               //DOC EC2015.5.01 +
               //DOC EC2013.3.60 -
               PromoGroup.GET("Label Code","Promotion Group Code");
               PromoQL.RESET;
               //DOC EC2015.5.01 -
               //PromoQL.SETRANGE("Applies-to Type",PromoQL."Applies-to Type"::"Promotion Group");
               PromoQL.SETFILTER("Applies-to Type",'%1|%2',PromoQL."Applies-to Type"::"Promotion Group",
                                 PromoQL."Applies-to Type"::"Distinct Promo Group Items");
               //DOC EC2015.5.01 +
               PromoQL.SETRANGE("No.",PromoGroup.Code);
               IF PromoQL.FIND('-') THEN
               REPEAT
                 Promo.GET(PromoQL."Promotion No.");
                 Item.GET("Item No.");
                 Item.CALCFIELDS("No. of Web Pages");
                 IF Promo.Released THEN BEGIN
                   MESSAGE(Text004,"Promotion Group Code",Promo."No.");
                   IF Item."No. of Web Pages" < 1 THEN
                     ERROR(Text005,Item."No.",Promo."No.");
                   IF Item."Blocked Online" THEN
                     ERROR(Text006,Item."No.",Promo."No.");
                 END;
               UNTIL PromoQL.NEXT = 0;
               //DOC EC2013.3.60 +
             END;

    OnModify=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    OnDelete=BEGIN
               //DOC EC2013.3.60 -
               PromoGroup.GET("Label Code","Promotion Group Code");
               PromoGroup.CALCFIELDS("No. of Items in Group");
               IF PromoGroup."No. of Items in Group" < 2 THEN BEGIN
                 PromoQL.RESET;

                 //DOC EC2015.5.01 -
                 //PromoQL.SETRANGE("Applies-to Type",PromoQL."Applies-to Type"::"Promotion Group");
                 PromoQL.SETFILTER("Applies-to Type",'%1|%2',PromoQL."Applies-to Type"::"Promotion Group",
                                   PromoQL."Applies-to Type"::"Distinct Promo Group Items");
                 //DOC EC2015.5.01 +

                 PromoQL.SETRANGE("No.",PromoGroup.Code);
                 IF PromoQL.FIND('-') THEN
                 REPEAT
                   Promo.GET(PromoQL."Promotion No.");
                   IF Promo.Released THEN
                     ERROR(Text002,"Promotion Group Code",Promo."No.");
                 UNTIL PromoQL.NEXT = 0;
               END;
               //DOC EC2013.3.60 +
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=[ENU=Items in Promotion Group;
               ENG=Items in Promotion Group];
    LookupPageID=Page9021695;
    DrillDownPageID=Page9021695;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                IF "Label Code" <> '' THEN BEGIN
                                                                  Label.GET("Label Code");
                                                                  IF Label."Used-in Company" <> COMPANYNAME THEN
                                                                    ERROR(Text000,Label."Used-in Company");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 7   ;   ;Promotion Group Code;Code10        ;TableRelation="Promotion Group".Code WHERE (Label Code=FIELD(Label Code));
                                                   CaptionML=[ENU=Promotion Group Code;
                                                              ENG=Promotion Group Code];
                                                   NotBlank=Yes }
    { 11  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.];
                                                   NotBlank=No }
    { 50  ;   ;Item Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Description;
                                                              ENG=Item Description];
                                                   Description=flowfield;
                                                   Editable=No }
    { 52  ;   ;Item Item Category  ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Item Category Code" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Item Category;
                                                              ENG=Item Item Category];
                                                   Description=flowfield;
                                                   Editable=No }
    { 54  ;   ;Item Product Group  ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Product Group Code" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Product Group;
                                                              ENG=Item Product Group];
                                                   Description=flowfield;
                                                   Editable=No }
    { 60  ;   ;Not On Active Promotion;Boolean    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Promotion Group"."Not On Active Promotion" WHERE (Label Code=FIELD(Label Code),
                                                                                                                         Code=FIELD(Promotion Group Code)));
                                                   CaptionML=[ENU=Not On Active Promotion;
                                                              ENG=Not On Active Promotion];
                                                   Description=flowfield EC2015.5.03 add field MQ 02/07/2015 - GW150702 }
    { 62  ;   ;On No. of Web Pages ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Page Item" WHERE (Label Code=FIELD(Label Code),
                                                                                        Type=FILTER(Item),
                                                                                        No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=On No. of Web Pages;
                                                              ENG=On No. of Web Pages];
                                                   Description=flowfield EC2015.5.03 add field MQ 02/07/2015 - GW150702 }
  }
  KEYS
  {
    {    ;Label Code,Promotion Group Code,Item No.;Clustered=Yes }
    {    ;Label Code,Item No.                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Label@1000000000 : Record 9021618;
      Text000@1000000001 : TextConst 'ENU=You cannot use this label as it can only be used in Company %1.;ENG=You cannot use this label as it can only be used in Company %1.';
      Text001@1000000003 : TextConst 'ENU=You cannot rename an Item Label Promotion Group;ENG=You cannot rename an Item Label Promotion Group';
      PromoGroup@1000000004 : Record 9021651;
      Promo@1000000005 : Record 9021656;
      PromoQL@1000000006 : Record 9021657;
      Text002@1000000007 : TextConst 'ENU=The Promotion Group %1 is being used on Released Promotion %2 so cannot have no Items in the Promotion.;ENG=The Promotion Group %1 is being used on Released Promotion %2 so cannot have no Items in the Promotion.';
      Text003@1000000008 : TextConst 'ENU=You cannot modify %1.;ENG=You cannot modify %1.';
      Text004@1000000002 : TextConst 'ENU=WARNING..... You have added an item in Promotion Group %1 which is being used on the Released Promotion %2.;ENG=WARNING..... You have added an item in Promotion Group %1 which is being used on the Released Promotion %2.';
      Text005@1000000009 : TextConst 'ENU=ERROR..... The item %1 is being used on the Released Promotion %2 but is not on any Web Page.;ENG=ERROR..... The item %1 is being used on the Released Promotion %2 but is not on any Web Page.';
      Text006@1000000010 : TextConst 'ENU=ERROR..... The item %1 is being used on the Released Promotion %2 but is Blocked Online.;ENG=ERROR..... The item %1 is being used on the Released Promotion %2 but is Blocked Online.';
      Item@1000000011 : Record 27;
      Text007@1000000012 : TextConst 'ENU=%1 must not be blank.;ENG=%1 must not be blank.';

    BEGIN
    {
      //DOC EC2013.3.60 MQ 01/07/2013 - GW130701 Enhanced error checking
      //DOC EC2015.5.01 MQ 23/12/2014 - GW141223 Enhanced for Distinct
      //DOC EC2015.5.03 MQ 02/07/2015 - GW150702 Add field 60 and 62
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

