OBJECT Report 5853 Copy Standard Cost Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Standard Cost Worksheet;
               ENG=Copy Standard Cost Worksheet];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  StdCostWkshName@1000 : Record 5840;
                BEGIN
                  IF ToStdCostWkshName = '' THEN
                    ERROR(STRSUBSTNO(Text003));
                  StdCostWkshName.GET(ToStdCostWkshName);

                  IF FromStdCostWkshName.Name = '' THEN
                    ERROR(STRSUBSTNO(Text004));
                  FromStdCostWkshName.GET(FromStdCostWkshName.Name);

                  ToStdCostWksh.LOCKTABLE
                END;

  }
  DATASET
  {
    { 4691;    ;DataItem;                    ;
               DataItemTable=Table5841;
               DataItemTableView=SORTING(Standard Cost Worksheet Name,Type,No.);
               OnPreDataItem=BEGIN
                               FromStdCostWkshName.GET(FromStdCostWkshName.Name);
                               SETFILTER("Standard Cost Worksheet Name",FromStdCostWkshName.Name);

                               WindowUpdateDateTime := CURRENTDATETIME;
                               Window.OPEN(Text007 + Text008 + Text009);
                             END;

               OnAfterGetRecord=BEGIN
                                  InsertStdCostWksh;
                                  IF CURRENTDATETIME - WindowUpdateDateTime >= 750 THEN BEGIN
                                    Window.UPDATE(1,Type);
                                    Window.UPDATE(2,"No.");

                                    WindowUpdateDateTime := CURRENTDATETIME;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NOT NoMessage THEN
                                  MESSAGE(Text010);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENG=Options] }

      { 1   ;2   ;Group     ;
                  CaptionML=[ENU=Copy from;
                             ENG=Copy from] }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Standard Cost Worksheet Name;
                             ENG=Standard Cost Worksheet Name];
                  SourceExpr=FromStdCostWkshName.Name;
                  TableRelation="Standard Cost Worksheet Name" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text003@1001 : TextConst 'ENU=You must specify a worksheet name to copy to.;ENG=You must specify a worksheet name to copy to.';
      Text004@1003 : TextConst 'ENU=You must specify a worksheet name to copy from.;ENG=You must specify a worksheet name to copy from.';
      Text007@1006 : TextConst 'ENU=Copying worksheet...\\;ENG=Copying worksheet...\\';
      Text008@1005 : TextConst 'ENU=Type               #1##########\;ENG=Type               #1##########\';
      Text009@1004 : TextConst 'ENU=No.             #2##########\;ENG=No.                #2##########\';
      Text010@1009 : TextConst 'ENU=The worksheet has been successfully copied.;ENG=The worksheet has been successfully copied.';
      ToStdCostWksh@1010 : Record 5841;
      FromStdCostWkshName@1000 : Record 5840;
      ToStdCostWkshName@1026 : Code[10];
      NoMessage@1040 : Boolean;
      Window@1011 : Dialog;
      WindowUpdateDateTime@1002 : DateTime;

    LOCAL PROCEDURE InsertStdCostWksh@2();
    BEGIN
      WITH ToStdCostWksh DO BEGIN
        ToStdCostWksh := "Standard Cost Worksheet";
        "Standard Cost Worksheet Name" := ToStdCostWkshName;
        IF NOT INSERT(TRUE) THEN
          MODIFY(TRUE);
      END;
    END;

    PROCEDURE SetCopyToWksh@3(ToStdCostWkshName2@1001 : Code[10]);
    BEGIN
      ToStdCostWkshName := ToStdCostWkshName2;
    END;

    PROCEDURE Initialize@1(FromStdCostWkshName2@1000 : Code[10];ToStdCostWkshName2@1004 : Code[10];NoMessage2@1010 : Boolean);
    BEGIN
      FromStdCostWkshName.Name := FromStdCostWkshName2;
      ToStdCostWkshName := ToStdCostWkshName2;
      NoMessage := NoMessage2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

