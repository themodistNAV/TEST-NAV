OBJECT Codeunit 97 Blanket Purch. Order to Order
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=VAR
            Vend@1001 : Record 23;
            PrepmtMgt@1003 : Codeunit 441;
            ShouldRedistributeInvoiceAmount@1000 : Boolean;
          BEGIN
            TESTFIELD("Document Type","Document Type"::"Blanket Order");
            ShouldRedistributeInvoiceAmount := PurchCalcDiscByType.ShouldRedistributeInvoiceDiscountAmount(Rec);

            Vend.GET("Buy-from Vendor No.");
            Vend.CheckBlockedVendOnDocs(Vend,FALSE);

            IF QtyToReceiveIsZero THEN
              ERROR(Text002);

            PurchSetup.GET;

            PurchOrderHeader := Rec;
            PurchOrderHeader."Document Type" := PurchOrderHeader."Document Type"::Order;
            PurchOrderHeader."No. Printed" := 0;
            PurchOrderHeader.Status := PurchOrderHeader.Status::Open;
            PurchOrderHeader."No." := '';

            PurchOrderLine.LOCKTABLE;
            PurchOrderHeader.INSERT(TRUE);

            IF "Order Date" = 0D THEN
              PurchOrderHeader."Order Date" := WORKDATE
            ELSE
              PurchOrderHeader."Order Date" := "Order Date";
            IF "Posting Date" <> 0D THEN
              PurchOrderHeader."Posting Date" := "Posting Date";
            PurchOrderHeader."Document Date" := "Document Date";
            PurchOrderHeader."Expected Receipt Date" := "Expected Receipt Date";
            PurchOrderHeader."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
            PurchOrderHeader."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
            PurchOrderHeader."Dimension Set ID" := "Dimension Set ID";
            PurchOrderHeader."Inbound Whse. Handling Time" := "Inbound Whse. Handling Time";
            PurchOrderHeader."Location Code" := "Location Code";
            PurchOrderHeader."Ship-to Name" := "Ship-to Name";
            PurchOrderHeader."Ship-to Name 2" := "Ship-to Name 2";
            PurchOrderHeader."Ship-to Address" := "Ship-to Address";
            PurchOrderHeader."Ship-to Address 2" := "Ship-to Address 2";
            PurchOrderHeader."Ship-to City" := "Ship-to City";
            PurchOrderHeader."Ship-to Post Code" := "Ship-to Post Code";
            PurchOrderHeader."Ship-to County" := "Ship-to County";
            PurchOrderHeader."Ship-to Country/Region Code" := "Ship-to Country/Region Code";
            PurchOrderHeader."Ship-to Contact" := "Ship-to Contact";

            PurchOrderHeader."Prepayment %" := Vend."Prepayment %";
            IF PurchOrderHeader."Posting Date" = 0D THEN
              PurchOrderHeader."Posting Date" := WORKDATE;

            PurchOrderHeader.MODIFY;

            PurchBlanketOrderLine.RESET;
            PurchBlanketOrderLine.SETRANGE("Document Type","Document Type");
            PurchBlanketOrderLine.SETRANGE("Document No.","No.");

            IF PurchBlanketOrderLine.FINDSET THEN
              REPEAT
                IF (PurchBlanketOrderLine.Type = PurchBlanketOrderLine.Type::" ") OR
                   (PurchBlanketOrderLine."Qty. to Receive" <> 0)
                THEN BEGIN
                  PurchLine.SETCURRENTKEY("Document Type","Blanket Order No.","Blanket Order Line No.");
                  PurchLine.SETRANGE("Blanket Order No.",PurchBlanketOrderLine."Document No.");
                  PurchLine.SETRANGE("Blanket Order Line No.",PurchBlanketOrderLine."Line No.");
                  QuantityOnOrders := 0;
                  IF PurchLine.FINDSET THEN
                    REPEAT
                      IF (PurchLine."Document Type" = PurchLine."Document Type"::"Return Order") OR
                         ((PurchLine."Document Type" = PurchLine."Document Type"::"Credit Memo") AND
                          (PurchLine."Return Shipment No." = ''))
                      THEN
                        QuantityOnOrders := QuantityOnOrders - PurchLine."Outstanding Qty. (Base)"
                      ELSE
                        IF (PurchLine."Document Type" = PurchLine."Document Type"::Order) OR
                           ((PurchLine."Document Type" = PurchLine."Document Type"::Invoice) AND
                            (PurchLine."Receipt No." = ''))
                        THEN
                          QuantityOnOrders := QuantityOnOrders + PurchLine."Outstanding Qty. (Base)";
                    UNTIL PurchLine.NEXT = 0;
                  IF (ABS(PurchBlanketOrderLine."Qty. to Receive (Base)" + QuantityOnOrders +
                        PurchBlanketOrderLine."Qty. Received (Base)") >
                      ABS(PurchBlanketOrderLine."Quantity (Base)")) OR
                     (PurchBlanketOrderLine."Quantity (Base)" * PurchBlanketOrderLine."Outstanding Qty. (Base)" < 0)
                  THEN
                    ERROR(
                      Text000 +
                      '%6\' +
                      '%7 - %8 = %9',
                      PurchBlanketOrderLine.FIELDCAPTION("Qty. to Receive (Base)"),
                      PurchBlanketOrderLine.Type,PurchBlanketOrderLine."No.",
                      PurchBlanketOrderLine.FIELDCAPTION("Line No."),PurchBlanketOrderLine."Line No.",
                      STRSUBSTNO(
                        Text001,
                        PurchBlanketOrderLine.FIELDCAPTION("Outstanding Qty. (Base)"),
                        PurchBlanketOrderLine.FIELDCAPTION("Qty. to Receive (Base)")),
                      PurchBlanketOrderLine."Outstanding Qty. (Base)",QuantityOnOrders,
                      PurchBlanketOrderLine."Outstanding Qty. (Base)" - QuantityOnOrders);

                  PurchOrderLine := PurchBlanketOrderLine;
                  ResetQuantityFields(PurchOrderLine);
                  PurchOrderLine."Document Type" := PurchOrderHeader."Document Type";
                  PurchOrderLine."Document No." := PurchOrderHeader."No.";
                  PurchOrderLine."Blanket Order No." := "No.";
                  PurchOrderLine."Blanket Order Line No." := PurchBlanketOrderLine."Line No.";

                  IF (PurchOrderLine."No." <> '') AND (PurchOrderLine.Type <> 0)THEN BEGIN
                    PurchOrderLine.Amount := 0;
                    PurchOrderLine."Amount Including VAT" := 0;
                    PurchOrderLine.VALIDATE(Quantity,PurchBlanketOrderLine."Qty. to Receive");
                    IF PurchBlanketOrderLine."Expected Receipt Date" <> 0D THEN
                      PurchOrderLine.VALIDATE("Expected Receipt Date",PurchBlanketOrderLine."Expected Receipt Date")
                    ELSE
                      PurchOrderLine.VALIDATE("Order Date",PurchOrderHeader."Order Date");
                    PurchOrderLine.VALIDATE("Direct Unit Cost",PurchBlanketOrderLine."Direct Unit Cost");
                    PurchOrderLine.VALIDATE("Line Discount %",PurchBlanketOrderLine."Line Discount %");
                    IF PurchOrderLine.Quantity <> 0 THEN
                      PurchOrderLine.VALIDATE("Inv. Discount Amount",PurchBlanketOrderLine."Inv. Discount Amount");
                    PurchBlanketOrderLine.CALCFIELDS("Reserved Qty. (Base)");
                    IF PurchBlanketOrderLine."Reserved Qty. (Base)" <> 0 THEN
                      ReservePurchLine.TransferPurchLineToPurchLine(
                        PurchBlanketOrderLine,PurchOrderLine,-PurchBlanketOrderLine."Qty. to Receive (Base)");
                  END;

                  IF Vend."Prepayment %" <> 0 THEN
                    PurchOrderLine."Prepayment %" := Vend."Prepayment %";
                  PrepmtMgt.SetPurchPrepaymentPct(PurchOrderLine,PurchOrderHeader."Posting Date");
                  PurchOrderLine.VALIDATE("Prepayment %");

                  PurchOrderLine."Shortcut Dimension 1 Code" := PurchBlanketOrderLine."Shortcut Dimension 1 Code";
                  PurchOrderLine."Shortcut Dimension 2 Code" := PurchBlanketOrderLine."Shortcut Dimension 2 Code";
                  PurchOrderLine."Dimension Set ID" := PurchBlanketOrderLine."Dimension Set ID";
                  PurchOrderLine.DefaultDeferralCode;
                  PurchOrderLine.INSERT;

                  IF PurchBlanketOrderLine."Qty. to Receive" <> 0 THEN BEGIN
                    PurchBlanketOrderLine.VALIDATE("Qty. to Receive",0);
                    PurchBlanketOrderLine.MODIFY;
                  END;
                END;
              UNTIL PurchBlanketOrderLine.NEXT = 0;

            IF PurchSetup."Default Posting Date" = PurchSetup."Default Posting Date"::"No Date" THEN BEGIN
              PurchOrderHeader."Posting Date" := 0D;
              PurchOrderHeader.MODIFY;
            END;

            CopyCommentsFromBlanketToOrder(Rec);

            IF NOT ShouldRedistributeInvoiceAmount THEN
              PurchCalcDiscByType.ResetRecalculateInvoiceDisc(PurchOrderHeader);
            COMMIT;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 of %2 %3 in %4 %5 cannot be more than %9.\;ENG=%1 of %2 %3 in %4 %5 cannot be more than %9.\';
      Text001@1001 : TextConst 'ENU="%1 - Unposted %1 = Possible %2";ENG="%1 - Unposted %1 = Possible %2"';
      PurchBlanketOrderLine@1003 : Record 39;
      PurchOrderHeader@1004 : Record 38;
      PurchOrderLine@1005 : Record 39;
      PurchCommentLine@1006 : Record 43;
      PurchCommentLine2@1007 : Record 43;
      PurchSetup@1008 : Record 312;
      PurchLine@1009 : Record 39;
      ReservePurchLine@1010 : Codeunit 99000834;
      PurchCalcDiscByType@1011 : Codeunit 66;
      QuantityOnOrders@1012 : Decimal;
      Text002@1002 : TextConst 'ENU=There is nothing to create.;ENG=There is nothing to create.';

    LOCAL PROCEDURE ResetQuantityFields@1(VAR TempPurchLine@1000 : Record 39);
    BEGIN
      TempPurchLine.Quantity := 0;
      TempPurchLine."Quantity (Base)" := 0;
      TempPurchLine."Qty. Rcd. Not Invoiced" := 0;
      TempPurchLine."Quantity Received" := 0;
      TempPurchLine."Quantity Invoiced" := 0;
      TempPurchLine."Qty. Rcd. Not Invoiced (Base)" := 0;
      TempPurchLine."Qty. Received (Base)" := 0;
      TempPurchLine."Qty. Invoiced (Base)" := 0;
    END;

    PROCEDURE GetPurchOrderHeader@2(VAR PurchHeader@1000 : Record 38);
    BEGIN
      PurchHeader := PurchOrderHeader;
    END;

    LOCAL PROCEDURE CopyCommentsFromBlanketToOrder@3(BlanketOrderPurchaseHeader@1000 : Record 38);
    VAR
      RecordLinkManagement@1001 : Codeunit 447;
    BEGIN
      IF PurchSetup."Copy Comments Blanket to Order" THEN BEGIN
        PurchCommentLine.SETRANGE("Document Type",PurchCommentLine."Document Type"::"Blanket Order");
        PurchCommentLine.SETRANGE("No.",BlanketOrderPurchaseHeader."No.");
        IF PurchCommentLine.FINDSET THEN
          REPEAT
            PurchCommentLine2 := PurchCommentLine;
            PurchCommentLine2."Document Type" := PurchOrderHeader."Document Type";
            PurchCommentLine2."No." := PurchOrderHeader."No.";
            PurchCommentLine2.INSERT;
          UNTIL PurchCommentLine.NEXT = 0;
        RecordLinkManagement.CopyLinks(BlanketOrderPurchaseHeader,PurchOrderHeader);
      END;
    END;

    BEGIN
    END.
  }
}

