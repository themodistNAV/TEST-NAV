OBJECT Table 5089 Contact Profile Answer
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:40:51 PM];
    Version List=NAVW19.00,EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContProfileAnswer@1005 : Record 5089;
               ProfileQuestnLine@1003 : Record 5088;
               ProfileQuestnLine2@1002 : Record 5088;
               ProfileQuestnLine3@1001 : Record 5088;
             BEGIN
               ProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");
               ProfileQuestnLine.TESTFIELD(Type,ProfileQuestnLine.Type::Answer);

               ProfileQuestnLine2.GET("Profile Questionnaire Code",QuestionLineNo);
               ProfileQuestnLine2.TESTFIELD("Auto Contact Classification",FALSE);

               IF NOT ProfileQuestnLine2."Multiple Answers" THEN BEGIN
                 ContProfileAnswer.RESET;
                 ProfileQuestnLine3.RESET;
                 ProfileQuestnLine3.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                 ProfileQuestnLine3.SETRANGE(Type,ProfileQuestnLine3.Type::Question);
                 ProfileQuestnLine3.SETFILTER("Line No.",'>%1',ProfileQuestnLine2."Line No.");
                 IF ProfileQuestnLine3.FINDFIRST THEN
                   ContProfileAnswer.SETRANGE(
                     "Line No.",ProfileQuestnLine2."Line No.",ProfileQuestnLine3."Line No.")
                 ELSE
                   ContProfileAnswer.SETFILTER("Line No.",'>%1',ProfileQuestnLine2."Line No.");
                 ContProfileAnswer.SETRANGE("Contact No.","Contact No.");
                 ContProfileAnswer.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                 IF ContProfileAnswer.FINDFIRST THEN
                   ERROR(Text000,ProfileQuestnLine2.FIELDCAPTION("Multiple Answers"));
               END;

               IF PartOfRating THEN BEGIN
                 INSERT;
                 UpdateContactClassification.UpdateRating("Contact No.");
                 DELETE;
               END;

               TouchContact("Contact No.");
             END;

    OnModify=BEGIN
               TouchContact("Contact No.");
             END;

    OnDelete=VAR
               ProfileQuestnLine@1001 : Record 5088;
             BEGIN
               ProfileQuestnLine.GET("Profile Questionnaire Code",QuestionLineNo);
               ProfileQuestnLine.TESTFIELD("Auto Contact Classification",FALSE);

               IF PartOfRating THEN BEGIN
                 DELETE;
                 UpdateContactClassification.UpdateRating("Contact No.");
                 INSERT;
               END;

               TouchContact("Contact No.");
             END;

    OnRename=BEGIN
               IF xRec."Contact No." = "Contact No." THEN
                 TouchContact("Contact No.")
               ELSE BEGIN
                 TouchContact("Contact No.");
                 TouchContact(xRec."Contact No.");
               END;
             END;

    CaptionML=[ENU=Contact Profile Answer;
               ENG=Contact Profile Answer];
    DrillDownPageID=Page5115;
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                Cont@1000 : Record 5050;
                                                              BEGIN
                                                                IF Cont.GET("Contact No.") THEN
                                                                  "Contact Company No." := Cont."Company No."
                                                                ELSE
                                                                  "Contact Company No." := '';
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              ENG=Contact No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[ENU=Contact Company No.;
                                                              ENG=Contact Company No.];
                                                   NotBlank=Yes }
    { 3   ;   ;Profile Questionnaire Code;Code10  ;TableRelation="Profile Questionnaire Header";
                                                   OnValidate=VAR
                                                                ProfileQuestnHeader@1000 : Record 5087;
                                                              BEGIN
                                                                ProfileQuestnHeader.GET("Profile Questionnaire Code");
                                                                "Profile Questionnaire Priority" := ProfileQuestnHeader.Priority;
                                                              END;

                                                   CaptionML=[ENU=Profile Questionnaire Code;
                                                              ENG=Profile Questionnaire Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Line No.            ;Integer       ;TableRelation="Profile Questionnaire Line"."Line No." WHERE (Profile Questionnaire Code=FIELD(Profile Questionnaire Code),
                                                                                                                Type=CONST(Answer));
                                                   OnValidate=VAR
                                                                ProfileQuestnLine@1000 : Record 5088;
                                                              BEGIN
                                                                ProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");
                                                                "Answer Priority" := ProfileQuestnLine.Priority;
                                                              END;

                                                   CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 5   ;   ;Answer              ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Profile Questionnaire Line".Description WHERE (Profile Questionnaire Code=FIELD(Profile Questionnaire Code),
                                                                                                                      Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Answer;
                                                              ENG=Answer];
                                                   Editable=No }
    { 6   ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Company Name" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Company Name;
                                                              ENG=Contact Company Name];
                                                   Editable=No }
    { 7   ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              ENG=Contact Name];
                                                   Editable=No }
    { 8   ;   ;Profile Questionnaire Priority;Option;
                                                   CaptionML=[ENU=Profile Questionnaire Priority;
                                                              ENG=Profile Questionnaire Priority];
                                                   OptionCaptionML=[ENU=Very Low,Low,Normal,High,Very High;
                                                                    ENG=Very Low,Low,Normal,High,Very High];
                                                   OptionString=Very Low,Low,Normal,High,Very High;
                                                   Editable=No }
    { 9   ;   ;Answer Priority     ;Option        ;CaptionML=[ENU=Answer Priority;
                                                              ENG=Answer Priority];
                                                   OptionCaptionML=[ENU=Very Low (Hidden),Low,Normal,High,Very High;
                                                                    ENG=Very Low (Hidden),Low,Normal,High,Very High];
                                                   OptionString=Very Low (Hidden),Low,Normal,High,Very High }
    { 10  ;   ;Last Date Updated   ;Date          ;CaptionML=[ENU=Last Date Updated;
                                                              ENG=Last Date Updated] }
    { 11  ;   ;Questions Answered (%);Decimal     ;CaptionML=[ENU=Questions Answered (%);
                                                              ENG=Questions Answered (%)];
                                                   DecimalPlaces=0:0;
                                                   BlankZero=Yes }
    { 9021208;;User GuID           ;Text38        ;CaptionML=[ENU=User GuID;
                                                              ENG=User GuID];
                                                   Description=MQ }
    { 9021272;;Text Answer         ;Text250       ;CaptionML=[ENU=Text Answer;
                                                              ENG=Text Answer];
                                                   Description=MQ }
  }
  KEYS
  {
    {    ;Contact No.,Profile Questionnaire Code,Line No.;
                                                   Clustered=Yes }
    {    ;Contact No.,Answer Priority,Profile Questionnaire Priority }
    {    ;Profile Questionnaire Code,Line No.      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Question does not allow %1.;ENG=This Question does not allow %1.';
      UpdateContactClassification@1007 : Report 5199;

    PROCEDURE Question@1() : Text[50];
    VAR
      ProfileQuestnLine@1000 : Record 5088;
    BEGIN
      IF ProfileQuestnLine.GET("Profile Questionnaire Code",QuestionLineNo) THEN
        EXIT(ProfileQuestnLine.Description)
    END;

    LOCAL PROCEDURE QuestionLineNo@3() : Integer;
    VAR
      ProfileQuestnLine@1000 : Record 5088;
    BEGIN
      WITH ProfileQuestnLine DO BEGIN
        RESET;
        SETRANGE("Profile Questionnaire Code",Rec."Profile Questionnaire Code");
        SETFILTER("Line No.",'<%1',Rec."Line No.");
        SETRANGE(Type,Type::Question);
        IF FINDLAST THEN
          EXIT("Line No.")
      END;
    END;

    LOCAL PROCEDURE TouchContact@2(ContactNo@1000 : Code[20]);
    VAR
      Cont@1001 : Record 5050;
    BEGIN
      Cont.LOCKTABLE;
      Cont.GET(ContactNo);
      Cont."Last Date Modified" := TODAY;
      Cont.MODIFY;
    END;

    LOCAL PROCEDURE PartOfRating@4() : Boolean;
    VAR
      Rating@1001 : Record 5111;
      ProfileQuestnLine@1000 : Record 5088;
      ProfileQuestnLine2@1002 : Record 5088;
    BEGIN
      Rating.SETCURRENTKEY("Rating Profile Quest. Code","Rating Profile Quest. Line No.");
      Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");

      ProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");
      ProfileQuestnLine.GET("Profile Questionnaire Code",ProfileQuestnLine.FindQuestionLine);

      ProfileQuestnLine2 := ProfileQuestnLine;
      ProfileQuestnLine2.SETRANGE(Type,ProfileQuestnLine2.Type::Question);
      ProfileQuestnLine2.SETRANGE("Profile Questionnaire Code",ProfileQuestnLine2."Profile Questionnaire Code");
      IF ProfileQuestnLine2.NEXT <> 0 THEN
        Rating.SETRANGE("Rating Profile Quest. Line No.",ProfileQuestnLine."Line No.",ProfileQuestnLine2."Line No.")
      ELSE
        Rating.SETFILTER("Rating Profile Quest. Line No.",'%1..',ProfileQuestnLine."Line No.");

      EXIT(Rating.FINDFIRST);
    END;

    PROCEDURE xxMQ@1230000000();
    BEGIN
    END;

    PROCEDURE GetQuestionLineNo@1040000(ProfileQuestionnaireCode@1040000 : Code[10];AnswerLineNo@1040001 : Integer) QuestionLineNo : Integer;
    VAR
      ProfileQLine@1040002 : Record 5088;
    BEGIN
      WITH ProfileQLine DO BEGIN
        RESET;
        SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
        SETFILTER("Line No.",'<%1',AnswerLineNo);
        SETRANGE(Type,Type::Question);
        IF FIND('+') THEN
          EXIT("Line No.")
      END;
    END;

    PROCEDURE GetNextQuestionLineNo@1040002(ProfileQuestionnaireCode@1040000 : Code[10];CurrentQuestionLineNo@1040001 : Integer) QuestionLineNo : Integer;
    VAR
      ProfileQLine@1040002 : Record 5088;
    BEGIN
      WITH ProfileQLine DO BEGIN
        RESET;
        SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
        SETFILTER("Line No.",'>%1',CurrentQuestionLineNo);
        SETRANGE(Type,Type::Question);
        IF FIND('-') THEN
          EXIT("Line No.")
      END;
    END;

    BEGIN
    {
      //DOC EC2015.5.03 MQ 05/05/2015 - GW150505 Simplified.  Now: just add fields,  don't need DateTimeInsertedModified
      //DOC EC2015.5.03 MQ 05/05/2015 - "Text Answer" field to capture free text responses
      //DOC EC2015.5.03 MQ 05/05/2015 - "User GUID" field to link back to internet customer source record
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

