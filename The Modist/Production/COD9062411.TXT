OBJECT Codeunit 9062411 Auto Clear Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    TableNo=9062734;
    OnRun=VAR
            AutoClear@1000000000 : Record 9062537;
          BEGIN
            IF AutoClear.FINDSET THEN
              REPEAT
                AutoClear.ClearEntries(FALSE);
              UNTIL AutoClear.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      TNLUtils@1000000000 : Codeunit 9062229;
      Logger@1000000001 : Record 9062280;
      tDeletedFile@1000000002 : TextConst 'ENU=Deleted File at %1 as older than date %2;ENG=Deleted File at %1 as older than date %2';

    PROCEDURE DeleteFilesInFolder@1000000033(FolderPath@1000000000 : Text[1024];IncludeSubfolders@1000000010 : Boolean;OlderThan@1000000007 : Date);
    VAR
      FileSystem@1000000012 : Codeunit 9062264;
      FileTmp@1000000013 : TEMPORARY Record 9062386;
      FileDate@1000000002 : Date;
      FullFilePath@1000000018 : Text;
      FolderTmp@1000000001 : TEMPORARY Record 9062386;
    BEGIN
      IF IncludeSubfolders THEN BEGIN
        FileSystem.GetFolders(FALSE,FolderPath,'',FolderTmp);
        IF FolderTmp.FINDSET THEN BEGIN
          REPEAT
            DeleteFilesInFolder(FolderTmp.GetPath(),TRUE,OlderThan);
          UNTIL FolderTmp.NEXT = 0;
        END;

      END;

      FileSystem.GetFiles(FALSE,FolderPath,'',FileTmp);

      IF FileTmp.FINDSET THEN BEGIN
        REPEAT
          FullFilePath := FileTmp.GetPath();

          CLEAR(FileDate);
          IF (FileTmp."Creation Date Time" <> 0DT) THEN
            FileDate := DT2DATE(FileTmp."Creation Date Time");
          IF (FileDate <> 0D) AND (FileDate < OlderThan) THEN BEGIN
            Logger.Log('AUTOCLEAR',Logger.Type::Debug,STRSUBSTNO(tDeletedFile,FullFilePath,OlderThan),USERID);
            FileSystem.DeleteFile(FALSE,FullFilePath);
            COMMIT;
          END;

        UNTIL FileTmp.NEXT = 0;
      END;
    END;

    BEGIN
    {
      //DOC NA2015.1  PC 26/03/2014 - Created
      //DOC NA2016.10 JH 28/08/2015 - Upgraded to 2016
      //DOC NA2016.11 JH 27/11/2015 - Folders structure changed in "FileSystem"
      //DOC NA2016.15 JH 09/05/2016 - CfMD modifications
    }
    END.
  }
}

