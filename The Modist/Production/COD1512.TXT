OBJECT Codeunit 1512 Workflow Create Payment Line
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            WorkflowStepArgument@1000 : Record 1523;
          BEGIN
            WorkflowStepArgument.GET("Record ID to Process");
            CreatePmtLine(WorkflowStepArgument);
          END;

  }
  CODE
  {
    VAR
      PaymentTxt@1000 : TextConst '@@@="%1 = Document Type (Eg. Invoice) %2 = Document No. (1201); Payment for Invoice 1201";ENU=Payment for %1 %2.;ENG=Payment for %1 %2.';

    PROCEDURE CreatePmtLine@1(WorkflowStepArgument@1005 : Record 1523);
    VAR
      GenJournalBatch@1013 : Record 232;
      GenJournalTemplate@1012 : Record 80;
      GenJournalLine@1011 : Record 81;
      Vendor@1010 : Record 23;
      VendorLedgerEntry@1009 : Record 25;
      PurchInvHeader@1008 : Record 122;
      WorkflowStepInstance@1000 : Record 1504;
      WorkflowStepInstanceArchive@1006 : Record 1530;
      RecRef@1004 : RecordRef;
      EmptyDateFormula@1003 : DateFormula;
      LastLineNo@1002 : Integer;
      LastDocNo@1001 : Code[20];
    BEGIN
      GenJournalTemplate.GET(WorkflowStepArgument."General Journal Template Name");
      GenJournalBatch.GET(GenJournalTemplate.Name,WorkflowStepArgument."General Journal Batch Name");

      WorkflowStepInstance.SETRANGE(Argument,WorkflowStepArgument.ID);
      IF WorkflowStepInstance.FINDFIRST THEN
        RecRef.GET(WorkflowStepInstance."Record ID")
      ELSE BEGIN
        WorkflowStepInstanceArchive.SETRANGE(Argument,WorkflowStepArgument.ID);
        IF NOT WorkflowStepInstanceArchive.FINDFIRST THEN
          EXIT;

        RecRef.GET(WorkflowStepInstanceArchive."Record ID")
      END;

      RecRef.SETTABLE(PurchInvHeader);
      PurchInvHeader.FIND;
      VendorLedgerEntry.GET(PurchInvHeader."Vendor Ledger Entry No.");
      Vendor.GET(VendorLedgerEntry."Vendor No.");

      GenJournalLine.SETRANGE("Journal Template Name",GenJournalBatch."Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",GenJournalBatch.Name);
      IF GenJournalLine.FINDLAST THEN BEGIN
        LastLineNo := GenJournalLine."Line No.";
        LastDocNo := GenJournalLine."Document No.";
      END;

      GenJournalLine.INIT;
      GenJournalLine."Journal Template Name" := GenJournalBatch."Journal Template Name";
      GenJournalLine."Journal Batch Name" := GenJournalBatch.Name;
      GenJournalLine."Line No." := LastLineNo + 10000;
      GenJournalLine."Document Type" := GenJournalLine."Document Type"::Payment;
      GenJournalLine."Posting No. Series" := GenJournalBatch."Posting No. Series";

      VendorLedgerEntry.CALCFIELDS(Amount);

      GenJournalLine."Account Type" := GenJournalLine."Account Type"::Vendor;
      GenJournalLine.VALIDATE("Account No.",VendorLedgerEntry."Vendor No.");
      GenJournalLine."Bal. Account Type" := GenJournalBatch."Bal. Account Type";
      GenJournalLine.VALIDATE("Bal. Account No.",GenJournalBatch."Bal. Account No.");
      GenJournalLine.VALIDATE("Currency Code",VendorLedgerEntry."Currency Code");
      GenJournalLine.Description := STRSUBSTNO(PaymentTxt,VendorLedgerEntry."Document Type",VendorLedgerEntry."Document No.");
      GenJournalLine."Source Line No." := VendorLedgerEntry."Entry No.";
      GenJournalLine."Shortcut Dimension 1 Code" := VendorLedgerEntry."Global Dimension 1 Code";
      GenJournalLine."Shortcut Dimension 2 Code" := VendorLedgerEntry."Global Dimension 2 Code";
      GenJournalLine."Dimension Set ID" := VendorLedgerEntry."Dimension Set ID";
      GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
      GenJournalLine."Reason Code" := GenJournalBatch."Reason Code";
      GenJournalLine.VALIDATE(Amount,-VendorLedgerEntry.Amount);
      GenJournalLine."Applies-to Doc. Type" := VendorLedgerEntry."Document Type";
      GenJournalLine."Applies-to Doc. No." := VendorLedgerEntry."Document No.";
      GenJournalLine."Payment Method Code" := VendorLedgerEntry."Payment Method Code";
      GenJournalLine."Creditor No." := VendorLedgerEntry."Creditor No.";
      GenJournalLine."Payment Reference" := VendorLedgerEntry."Payment Reference";
      GenJournalLine."Applies-to Ext. Doc. No." := VendorLedgerEntry."External Document No.";
      EVALUATE(EmptyDateFormula,'<0D>');
      GenJournalLine.SetPostingDateAsDueDate(GenJournalLine.GetAppliesToDocDueDate,EmptyDateFormula);
      GenJournalLine."Document No." := GetDocumentNo(GenJournalLine,LastDocNo);
      GenJournalLine.INSERT;
    END;

    PROCEDURE GetDocumentNo@86(VAR GenJournalLine@1003 : Record 81;LastDocNo@1000 : Code[20]) : Code[20];
    VAR
      GenJournalBatch@1002 : Record 232;
      NoSeriesMgt@1001 : Codeunit 396;
    BEGIN
      GenJournalBatch.GET(GenJournalLine."Journal Template Name",GenJournalLine."Journal Batch Name");
      IF GenJournalBatch."No. Series" = '' THEN
        EXIT(INCSTR(LastDocNo));

      CLEAR(NoSeriesMgt);
      EXIT(NoSeriesMgt.GetNextNo(GenJournalBatch."No. Series",GenJournalLine."Posting Date",FALSE));
    END;

    BEGIN
    END.
  }
}

