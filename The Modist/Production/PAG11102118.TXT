OBJECT Page 11102118 OM - Check Guidelines Code
{
  OBJECT-PROPERTIES
  {
    Date=19/11/15;
    Time=12:00:00 PM;
    Version List=OMA10.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Check Guidelines Code;
               ENU=Check Guidelines Code;
               ESP=Check Guidelines Code;
               FRA=Check Guidelines Code;
               NLD=Controleer richtlijnen code;
               DEA=Check Guidelines Code];
    SourceTable=Table11102053;
    DataCaptionExpr=CaptionExpr;
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 CALHistoryObject@1000 : Record 11102052;
               BEGIN
                 SetCodeChanged(FALSE);

                 FILTERGROUP(2);
                 CALHistoryObject.GET(GETRANGEMIN("Entry No."));
                 FILTERGROUP(0);

                 Object.GET(CALHistoryObject."Object Type", '', CALHistoryObject."Object No.");
                 Object.SETRECFILTER;

                 PopulateTmpRecords;
               END;

    OnAfterGetRecord=BEGIN
                       KnownComment := CalcKnownComment;

                       StyleExpr := '';

                       IF DELSTR(DELCHR(Code, '<'), 3) = '//' THEN
                         StyleExpr := 'Favorable';

                       IF TmpHighLightedLine.GET("Entry No.", "Line No.", 1) THEN
                         StyleExpr := 'Unfavorable';
                     END;

    OnNewRecord=BEGIN
                  "Entry No." := xRec."Entry No.";
                  "Line No." := xRec."Line No.";
                  "Sub Line No." := 999;
                END;

    OnInsertRecord=BEGIN
                     IF NOT ProceedModify THEN
                       EXIT(FALSE);

                     NewLine;

                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     IF NOT ProceedModify THEN
                       EXIT(FALSE);

                     SetCodeChanged(TRUE);

                     EXIT(TRUE);
                   END;

    OnDeleteRecord=BEGIN
                     IF NOT ProceedModify THEN
                       EXIT(FALSE);

                     SetCodeChanged(TRUE);

                     EXIT(TRUE);
                   END;

    OnQueryClosePage=BEGIN
                       EXIT(NOT KeepChanges);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           AnswerProceedModify := AnswerProceedModify::" ";
                         END;

    ActionList=ACTIONS
    {
      { 1       ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 2       ;1   ;Action    ;
                      CaptionML=[DEU=New Line;
                                 ENU=New Line;
                                 ESP=New Line;
                                 FRA=New Line;
                                 NLD=Nieuwe regel;
                                 DEA=New Line];
                      Image=New;
                      OnAction=BEGIN
                                 IF ProceedModify THEN BEGIN
                                   Code := '';
                                   NewLine;
                                 END;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      ShortCutKey=Ctrl+S;
                      CaptionML=[DEU=Save;
                                 ENU=Save;
                                 ESP=Save;
                                 FRA=Save;
                                 NLD=Op&slaan;
                                 DEA=Save];
                      Promoted=Yes;
                      Enabled=SaveEnabled;
                      PromotedIsBig=Yes;
                      Image=Save;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SaveObject;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      CaptionML=[DEU=Undo;
                                 ENU=Undo;
                                 ESP=Undo;
                                 FRA=Undo;
                                 NLD=On&gedaan maken;
                                 DEA=Undo];
                      Promoted=Yes;
                      Enabled=SaveEnabled;
                      PromotedIsBig=Yes;
                      Image=Undo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Undo;
                               END;
                                }
      { 5       ;1   ;Action    ;
                      ShortCutKey=Shift+Ctrl+L;
                      CaptionML=[DEU=Re&fresh Code;
                                 ENU=Re&fresh Code;
                                 ESP=Re&fresh Code;
                                 FRA=Re&fresh Code;
                                 NLD=Code vernieuwen;
                                 DEA=Re&fresh Code];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT KeepChanges THEN
                                   Refresh(TRUE);
                               END;
                                }
      { 6       ;1   ;ActionGroup;
                      CaptionML=[DEU=&Object;
                                 ENU=&Object;
                                 ESP=&Object;
                                 FRA=&Object;
                                 NLD=&Object;
                                 DEA=&Object] }
      { 7       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F2;
                      CaptionML=[DEU=Design;
                                 ENU=Design;
                                 ESP=Design;
                                 FRA=Design;
                                 NLD=Design;
                                 DEA=Design];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Design;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ObjectMgt@1000 : Codeunit 11102046;
                               BEGIN
                                 ObjectMgt.DesignObject(Object);
                               END;
                                }
      { 8       ;2   ;Action    ;
                      ShortCutKey=Ctrl+R;
                      CaptionML=[DEU=Run;
                                 ENU=Run;
                                 ESP=Run;
                                 FRA=Run;
                                 NLD=Run;
                                 DEA=Run];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ObjectMgt@1000 : Codeunit 11102046;
                               BEGIN
                                 ObjectMgt.RunObject(Object);
                               END;
                                }
      { 30      ;2   ;Action    ;
                      CaptionML=[DEU=Navigate;
                                 ENU=Navigate;
                                 ESP=Navigate;
                                 FRA=Navigate;
                                 NLD=Navigeren;
                                 DEA=Navigate];
                      Image=Navigate;
                      OnAction=VAR
                                 ObjectMgt@1000 : Codeunit 11102046;
                               BEGIN
                                 ObjectMgt.Navigate(Object);
                               END;
                                }
      { 9       ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+H;
                      CaptionML=[DEU=C/AL &History;
                                 ENU=C/AL &History;
                                 ESP=C/AL &History;
                                 FRA=C/AL &History;
                                 NLD=C/AL &Historie;
                                 DEA=C/AL &History];
                      Image=History;
                      OnAction=VAR
                                 CALHistoryObject@1000 : Record 11102052;
                                 CALHistoryObjects@1001 : Page 11102072;
                               BEGIN
                                 CALHistoryObject.SETCURRENTKEY("Object Type", "Object No.");
                                 CALHistoryObject.SETRANGE("Object Type", Object.Type);
                                 CALHistoryObject.SETRANGE("Object No.", Object.ID);

                                 CALHistoryObjects.SETTABLEVIEW(CALHistoryObject);
                                 CALHistoryObjects.RUN;
                               END;
                                }
      { 10      ;2   ;Action    ;
                      ShortCutKey=F11;
                      CaptionML=[DEU=Compile;
                                 ENU=Compile;
                                 ESP=Compile;
                                 FRA=Compile;
                                 NLD=Compileren;
                                 DEA=Compile];
                      Image=Recalculate;
                      OnAction=VAR
                                 ObjectMgt@1000 : Codeunit 11102046;
                               BEGIN
                                 ObjectMgt.CompileObject(Object);

                                 Refresh(FALSE);
                               END;
                                }
      { 11      ;2   ;Action    ;
                      ShortCutKey=Ctrl+T;
                      CaptionML=[DEU=Edit in External Editor;
                                 ENU=Edit in External Editor;
                                 ESP=Edit in External Editor;
                                 FRA=Edit in External Editor;
                                 NLD=In externe &editor wijzigen;
                                 DEA=Edit in External Editor];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=DocumentEdit;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 EditObject;
                               END;
                                }
      { 12      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unctions;
                                 ENU=F&unctions;
                                 ESP=F&unctions;
                                 FRA=F&unctions;
                                 NLD=F&uncties;
                                 DEA=F&unctions] }
      { 13      ;2   ;Action    ;
                      ShortCutKey=Ctrl+Up;
                      CaptionML=[DEU=Previous;
                                 ENU=Previous;
                                 ESP=Previous;
                                 FRA=Previous;
                                 NLD=Vo&rige;
                                 DEA=Previous];
                      Promoted=Yes;
                      Image=MoveUp;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 GoPrevious;
                               END;
                                }
      { 14      ;2   ;Action    ;
                      ShortCutKey=Ctrl+Down;
                      CaptionML=[DEU=Next;
                                 ENU=Next;
                                 ESP=Next;
                                 FRA=Next;
                                 NLD=Volgende;
                                 DEA=Next];
                      Promoted=Yes;
                      Image=MoveDown;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 GoNext;
                               END;
                                }
      { 15      ;2   ;Separator  }
      { 16      ;2   ;Action    ;
                      ShortCutKey=Ctrl+K;
                      CaptionML=[DEU=Set to &Known Comment;
                                 ENU=Set to &Known Comment;
                                 ESP=Set to &Known Comment;
                                 FRA=Set to &Known Comment;
                                 NLD=Wijzig naar &bekende opmerking;
                                 DEA=Set to &Known Comment];
                      Image=Edit;
                      OnAction=VAR
                                 OriginalRec@1000 : Record 11102053;
                               BEGIN
                                 OriginalRec := Rec;

                                 CurrPage.SETSELECTIONFILTER(Rec);
                                 IF FINDSET THEN
                                   REPEAT
                                     ValidateKnownComment(Rec, TRUE);
                                   UNTIL NEXT = 0;

                                 Rec := OriginalRec;
                                 CLEARMARKS;
                               END;
                                }
      { 17      ;2   ;Action    ;
                      ShortCutKey=Ctrl+U;
                      CaptionML=[DEU=Set to &Unknown Comment;
                                 ENU=Set to &Unknown Comment;
                                 ESP=Set to &Unknown Comment;
                                 FRA=Set to &Unknown Comment;
                                 NLD=Wijzig naar &onbekende opmerking;
                                 DEA=Set to &Unknown Comment];
                      Image=Edit;
                      OnAction=VAR
                                 OriginalRec@1000 : Record 11102053;
                               BEGIN
                                 OriginalRec := Rec;

                                 CurrPage.SETSELECTIONFILTER(Rec);
                                 IF FINDSET THEN
                                   REPEAT
                                     ValidateKnownComment(Rec, FALSE);
                                   UNTIL NEXT = 0;

                                 Rec := OriginalRec;
                                 CLEARMARKS;
                               END;
                                }
      { 18      ;2   ;Separator  }
      { 19      ;2   ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=[DEU=Apply Guidelines;
                                 ENU=Apply Guidelines;
                                 ESP=Apply Guidelines;
                                 FRA=Apply Guidelines;
                                 NLD=Richtl&ijnen toepassen;
                                 DEA=Apply Guidelines];
                      Image=Approve;
                      OnAction=VAR
                                 OriginalRec@1000 : Record 11102053;
                                 TmpCALHistoryLine@1001 : TEMPORARY Record 11102053;
                                 TmpCheckGuidelinesComment2@1002 : TEMPORARY Record 11102081;
                                 AutoApplyGuidelines@1003 : Codeunit 11102061;
                               BEGIN
                                 OriginalRec := Rec;
                                 OriginalRec.SETVIEW(GETVIEW);

                                 IF AutoApplyGuidelines.AutocorrectCode(Object, Rec, TmpCheckGuidelinesComment, TmpCALHistoryLine) THEN BEGIN

                                   SetCodeChanged(TRUE);

                                   TmpCheckGuidelinesComment.RESET;
                                   IF TmpCheckGuidelinesComment.FINDSET THEN
                                     REPEAT
                                       TmpCheckGuidelinesComment2 := TmpCheckGuidelinesComment;
                                       TmpCheckGuidelinesComment2.INSERT;
                                     UNTIL TmpCheckGuidelinesComment.NEXT = 0;

                                   ResetGuidelinesComments(TmpCheckGuidelinesComment2);

                                 END;

                                 Rec := OriginalRec;
                                 SETVIEW(OriginalRec.GETVIEW);
                               END;
                                }
      { 20      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[DEU=Apply Guidelines to &Selection;
                                 ENU=Apply Guidelines to &Selection;
                                 ESP=Apply Guidelines to &Selection;
                                 FRA=Apply Guidelines to &Selection;
                                 NLD=Richtlijnen toepassen op &selectie;
                                 DEA=Apply Guidelines to &Selection];
                      Image=Approve;
                      OnAction=BEGIN
                                 CheckGuidelinesSelection;
                               END;
                                }
      { 21      ;2   ;Action    ;
                      ShortCutKey=Ctrl+G;
                      CaptionML=[DEU=Glue Line;
                                 ENU=Glue Line;
                                 ESP=Glue Line;
                                 FRA=Glue Line;
                                 NLD=Regel lijmen;
                                 DEA=Glue Line];
                      Image=Attach;
                      OnAction=VAR
                                 AutoApplyGuidelines@1000 : Codeunit 11102061;
                               BEGIN
                                 IF ProceedModify THEN BEGIN
                                   IF AutoApplyGuidelines.GlueLine(TmpCheckGuidelinesComment, Rec, "Line No.") THEN
                                     SetCodeChanged(TRUE);
                                   RESET;
                                 END;
                               END;
                                }
      { 22      ;2   ;Separator  }
      { 23      ;2   ;Action    ;
                      ShortCutKey=Ctrl+Q;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Update Cap&tions;
                                 ENU=Update Cap&tions;
                                 ESP=Update Cap&tions;
                                 FRA=Update Cap&tions;
                                 NLD=Captions bijwerken;
                                 DEA=Update Cap&tions];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CreateSerialNo;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OriginalRec@1000 : Record 11102053;
                                 TranslationMgt@1001 : Codeunit 11102051;
                               BEGIN
                                 IF ProceedModify THEN BEGIN
                                   COMMIT;
                                   OriginalRec := Rec;
                                   IF TranslationMgt.UpdateFromCALLines(Object, Rec, TmpCheckGuidelinesComment, FALSE) THEN
                                     SetCodeChanged(TRUE);
                                   Rec := OriginalRec;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 24  ;0   ;Container ;
                ContainerType=ContentArea }

    { 25  ;1   ;Group     ;
                GroupType=Repeater }

    { 26  ;2   ;Field     ;
                SourceExpr="Line No.";
                Editable=FALSE;
                StyleExpr=StyleExpr }

    { 27  ;2   ;Field     ;
                CaptionML=[DEU=Comment;
                           ENU=Comment;
                           ESP=Comment;
                           FRA=Comment;
                           NLD=Opmerking;
                           DEA=Comment];
                SourceExpr=Comment;
                StyleExpr=StyleExpr }

    { 28  ;2   ;Field     ;
                CaptionML=[DEU=Known Comment;
                           ENU=Known Comment;
                           ESP=Known Comment;
                           FRA=Known Comment;
                           NLD=Bekende opmerking;
                           DEA=Known Comment];
                SourceExpr=KnownComment;
                StyleExpr=StyleExpr;
                OnValidate=VAR
                             ctSaveFirst@1000 : TextConst 'DEU=Please save your code.;ENU=Please save your code.;ESP=Please save your code.;FRA=Please save your code.;NLD=Sla eerst uw wijzigingen op.;DEA=Please save your code.';
                           BEGIN
                             IF CodeChanged THEN
                               MESSAGE(ctSaveFirst)
                             ELSE
                               ValidateKnownComment(Rec, KnownComment);
                           END;
                            }

    { 29  ;2   ;Field     ;
                SourceExpr=Code;
                StyleExpr=StyleExpr }

  }
  CODE
  {
    VAR
      TmpHighLightedLine@1000 : TEMPORARY Record 11102053;
      TmpOriginalCALHistoryLine@1001 : TEMPORARY Record 11102053;
      TmpCheckGuidelinesComment@1002 : TEMPORARY Record 11102081;
      Object@1003 : Record 2000000001;
      StyleExpr@1004 : Text;
      AnswerProceedModify@1005 : ' ,Yes,No';
      CodeChanged@1006 : Boolean;
      KnownComment@1007 : Boolean;
      SaveEnabled@1008 : Boolean INDATASET;
      UndoEnabled@1009 : Boolean INDATASET;

    LOCAL PROCEDURE Comment@1() ExitValue : Text;
    BEGIN
      TmpCheckGuidelinesComment.SETCURRENTKEY("Object Type", "Object No.", "C/AL Line No.");
      TmpCheckGuidelinesComment.SETRANGE("Object Type", Object.Type);
      TmpCheckGuidelinesComment.SETRANGE("Object No.", Object.ID);
      TmpCheckGuidelinesComment.SETRANGE("C/AL Line No.", "Line No.");
      IF TmpCheckGuidelinesComment.FINDSET THEN
        REPEAT
          IF ExitValue = '' THEN
            ExitValue := TmpCheckGuidelinesComment.Comment
          ELSE
            IF STRLEN(ExitValue + TmpCheckGuidelinesComment.Comment) < 1024 THEN
              ExitValue += ', ' + TmpCheckGuidelinesComment.Comment;
        UNTIL TmpCheckGuidelinesComment.NEXT = 0;
    END;

    LOCAL PROCEDURE GoPrevious@2();
    BEGIN
      TmpHighLightedLine := Rec;
      IF TmpHighLightedLine.NEXT(-1) <> 0 THEN BEGIN
        Rec := TmpHighLightedLine;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE GoNext@3();
    BEGIN
      TmpHighLightedLine := Rec;
      IF TmpHighLightedLine.NEXT <> 0 THEN BEGIN
        Rec := TmpHighLightedLine;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE CalcKnownComment@4() ExitValue : Boolean;
    VAR
      CheckGuidelinesComment@1000 : Record 11102081;
    BEGIN
      CheckGuidelinesComment.SETCURRENTKEY("Object Type", "Object No.", "C/AL Line No.");
      CheckGuidelinesComment.SETRANGE("Object Type", Object.Type);
      CheckGuidelinesComment.SETRANGE("Object No.", Object.ID);
      CheckGuidelinesComment.SETRANGE("C/AL Line No.", "Line No.");
      IF NOT CheckGuidelinesComment.ISEMPTY THEN BEGIN
        ExitValue := TRUE;
        CheckGuidelinesComment.SETRANGE("Known Comment", FALSE);
        IF NOT CheckGuidelinesComment.ISEMPTY THEN
          EXIT(FALSE);
      END;
    END;

    LOCAL PROCEDURE ValidateKnownComment@5(VAR CALHistoryLine@1000 : Record 11102053;NewValue@1001 : Boolean);
    VAR
      CheckGuidelinesComment@1002 : Record 11102081;
    BEGIN
      CheckGuidelinesComment.SETCURRENTKEY("Object Type", "Object No.", "C/AL Line No.");
      CheckGuidelinesComment.SETRANGE("Object Type", Object.Type);
      CheckGuidelinesComment.SETRANGE("Object No.", Object.ID);
      CheckGuidelinesComment.SETRANGE("C/AL Line No.", CALHistoryLine."Line No.");
      IF CheckGuidelinesComment.FINDSET THEN
        REPEAT
          CheckGuidelinesComment."Known Comment" := NewValue;
          CheckGuidelinesComment.MODIFY;
        UNTIL CheckGuidelinesComment.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveObject@6();
    VAR
      Setup@1000 : Record 11102035;
      OriginalRec@1001 : Record 11102053;
      ObjectLock@1002 : Record 11102054;
      SourceControl@1003 : Codeunit 11102050;
    BEGIN
      IF NOT CodeChanged THEN
        EXIT;

      Setup.CustomGet;
      IF Setup."Lock Object at Design" THEN
        ObjectLock.InsertLock(Object);

      RESET;

      OriginalRec := Rec;
      SourceControl.UpdateObject(Object, TRUE, TmpOriginalCALHistoryLine, Rec);
      Rec := OriginalRec;
      IF FIND('=') THEN
        ;

      COMMIT;

      Refresh(FALSE);
    END;

    LOCAL PROCEDURE Undo@7();
    VAR
      CALHistoryObject@1000 : Record 11102052;
      CALHistoryLine@1001 : Record 11102053;
      OriginalRec@1002 : Record 11102053;
    BEGIN
      OriginalRec := Rec;

      DELETEALL;

      CALHistoryObject.GoToLastRecord(Object.Type, Object.ID);

      CALHistoryLine.SETRANGE("Entry No.", CALHistoryObject."Entry No.");
      IF CALHistoryLine.FINDSET THEN
        REPEAT
          Rec := CALHistoryLine;
          "Entry No." := 0;
          INSERT;
        UNTIL CALHistoryLine.NEXT = 0;

      Rec := OriginalRec;

      SetCodeChanged(FALSE);
    END;

    LOCAL PROCEDURE SetCodeChanged@8(NewValue@1000 : Boolean);
    BEGIN
      CodeChanged := NewValue;
      UndoEnabled := NewValue;
      SaveEnabled := NewValue;
    END;

    LOCAL PROCEDURE Refresh@9(CheckObjectUpToDate@1000 : Boolean);
    VAR
      OriginalRec@1001 : Record 11102053;
      SourceControl@1002 : Codeunit 11102050;
      CheckGuidelinesMgt@1003 : Codeunit 11102060;
    BEGIN
      OriginalRec := Rec;

      IF CheckObjectUpToDate THEN
        IF SourceControl.LatestHistoryIsOutdated(Object) THEN
          SourceControl.AddObject(Object, 0, FALSE, FALSE, 1, FALSE);

      CheckGuidelinesMgt.SetObjectRange(Object);
      CheckGuidelinesMgt.RUN;

      PopulateTmpRecords;
      SetCodeChanged(FALSE);

      Rec := OriginalRec;
      IF FIND('=') THEN
        ;
    END;

    LOCAL PROCEDURE KeepChanges@10() : Boolean;
    VAR
      ctSaveChanges@1000 : TextConst 'DEU=Your changes will be lost.\Are you sure?;ENU=Your changes will be lost.\Are you sure?;ESP=Your changes will be lost.\Are you sure?;FRA=Your changes will be lost.\Are you sure?;NLD=Uw wijzigingen gaan verloren.\Weet u het zeker?;DEA=Your changes will be lost.\Are you sure?';
    BEGIN
      IF CodeChanged THEN
        IF NOT CONFIRM(ctSaveChanges, TRUE) THEN
          EXIT(TRUE);
    END;

    LOCAL PROCEDURE PopulateTmpRecords@11();
    VAR
      CALHistoryObject@1000 : Record 11102052;
      CALHistoryLine@1001 : Record 11102053;
      OriginalRec@1002 : Record 11102053;
      CheckGuidelinesComment@1003 : Record 11102081;
    BEGIN
      OriginalRec := Rec;

      RESET;
      DELETEALL;

      TmpOriginalCALHistoryLine.RESET;
      TmpOriginalCALHistoryLine.DELETEALL;

      CALHistoryObject.GoToLastRecord(Object.Type, Object.ID);
      CALHistoryLine.SETRANGE("Entry No.", CALHistoryObject."Entry No.");
      IF CALHistoryLine.FINDSET THEN
        REPEAT
          Rec := CALHistoryLine;
          "Entry No." := 0;
          INSERT;

          TmpOriginalCALHistoryLine := CALHistoryLine;
          TmpOriginalCALHistoryLine."Entry No." := 0;
          TmpOriginalCALHistoryLine.INSERT;

        UNTIL CALHistoryLine.NEXT = 0;

      CheckGuidelinesComment.SETRANGE("Object Type", Object.Type);
      CheckGuidelinesComment.SETRANGE("Object No.", Object.ID);
      ResetGuidelinesComments(CheckGuidelinesComment);

      Rec := OriginalRec;
      IF FIND('=') THEN
        ;
    END;

    LOCAL PROCEDURE ResetGuidelinesComments@12(VAR CheckGuidelinesComment@1000 : Record 11102081);
    BEGIN
      TmpCheckGuidelinesComment.RESET;
      TmpCheckGuidelinesComment.DELETEALL;

      TmpHighLightedLine.RESET;
      TmpHighLightedLine.DELETEALL;

      IF CheckGuidelinesComment.FINDSET THEN
        REPEAT

          IF GET(0, CheckGuidelinesComment."C/AL Line No.", 1) THEN BEGIN

            TmpHighLightedLine := Rec;
            IF TmpHighLightedLine.INSERT THEN
              ;
          END;

          TmpCheckGuidelinesComment := CheckGuidelinesComment;
          TmpCheckGuidelinesComment.INSERT;

        UNTIL CheckGuidelinesComment.NEXT = 0;
    END;

    LOCAL PROCEDURE NewLine@13();
    VAR
      OriginalLine@1000 : Record 11102053;
    BEGIN
      TmpCheckGuidelinesComment.SETCURRENTKEY("Object Type", "Object No.", "C/AL Line No.");
      TmpCheckGuidelinesComment.SETRANGE("Object Type", Object.Type);
      TmpCheckGuidelinesComment.SETRANGE("Object No.", Object.ID);
      TmpCheckGuidelinesComment.SETFILTER("C/AL Line No.", '>%1', "Line No.");
      IF TmpCheckGuidelinesComment.FINDLAST THEN
        REPEAT
          TmpCheckGuidelinesComment."C/AL Line No." += 1;
          TmpCheckGuidelinesComment.MODIFY;
        UNTIL TmpCheckGuidelinesComment.NEXT(-1) = 0;
      TmpCheckGuidelinesComment.RESET;

      OriginalLine := Rec;
      RESET;
      SETFILTER("Line No.", '>=%1', "Line No.");
      IF FINDLAST THEN
        REPEAT
          DELETE;
          "Line No." += 1;
          INSERT;
        UNTIL NEXT(-1) = 0;
      Rec := OriginalLine;
      RESET;

      TmpHighLightedLine.SETFILTER("Line No.", '>=%1', "Line No.");
      IF TmpHighLightedLine.FINDLAST THEN
        REPEAT
          TmpHighLightedLine.DELETE;
          TmpHighLightedLine."Line No." += 1;
          TmpHighLightedLine.INSERT;
        UNTIL TmpHighLightedLine.NEXT(-1) = 0;
      TmpHighLightedLine.RESET;

      "Sub Line No." := 1;
      INSERT;

      SetCodeChanged(TRUE);
    END;

    LOCAL PROCEDURE ProceedModify@14() ExitValue : Boolean;
    VAR
      EditorMgt@1000 : Codeunit 11102106;
      RefreshCode@1001 : Boolean;
    BEGIN
      ExitValue := EditorMgt.ProceedModify(Object, AnswerProceedModify, RefreshCode, '');

      IF RefreshCode THEN
        IF NOT KeepChanges THEN
          Refresh(TRUE);
    END;

    LOCAL PROCEDURE EditObject@15();
    VAR
      TmpCALHistoryObject@1000 : TEMPORARY Record 11102052;
      OriginalRec@1001 : Record 11102053;
      TmpRec2@1002 : TEMPORARY Record 11102053;
      Progressbar@1003 : Codeunit 11102044;
      SourceControl@1004 : Codeunit 11102050;
      FileHandler@1005 : Codeunit 11102097;
      EditFile@1006 : Page 11102075;
      ClientTempFileName@1007 : Text;
      ServerTempFileName@1008 : Text;
      ctDefaultFileName@1009 : TextConst 'DEU=EditObjects.txt;ENU=EditObjects.txt;ESP=EditObjects.txt;FRA=EditObjects.txt;NLD=EditObjects.txt;DEA=EditObjects.txt';
      ctEditObject@1010 : TextConst 'DEU=Edit Object;ENU=Edit Object;ESP=Edit Object;FRA=Edit Object;NLD=Object wijzigen;DEA=Edit Object';
      ctQuestion@1011 : TextConst 'DEU=Modify the C/AL code in the external editor and press OK.;ENU=Modify the C/AL code in the external editor and press OK.;ESP=Modify the C/AL code in the external editor and press OK.;FRA=Modify the C/AL code in the external editor and press OK.;NLD=Wijzig de C/AL code in de externe tekst-editor en kies OK.;DEA=Modify the C/AL code in the external editor and press OK.';
    BEGIN
      IF NOT ProceedModify THEN
        EXIT;

      ServerTempFileName := FileHandler.FileNameServerPath(ctDefaultFileName);
      IF EXISTS(ServerTempFileName) THEN
        ERASE(ServerTempFileName);

      OriginalRec := Rec;

      RESET;
      FINDFIRST;

      SourceControl.ExportTextFile(ServerTempFileName, Rec);
      Rec := OriginalRec;

      ClientTempFileName := FileHandler.DownloadToClient(ServerTempFileName, '', ctDefaultFileName);

      SourceControl.OpenInEditor(ClientTempFileName);

      EditFile.SetCaption(ctEditObject);
      EditFile.SetQuestion(ctQuestion);
      EditFile.SetFileName(ClientTempFileName);

      COMMIT;

      IF EditFile.RUNMODAL IN [ACTION::OK, ACTION::Yes] THEN BEGIN
        ServerTempFileName := FileHandler.UploadToServer(ClientTempFileName, ctDefaultFileName);
        DELETEALL;
        SourceControl.ImportTextFile2(ServerTempFileName, 0, Progressbar, 0, TmpCALHistoryObject, TmpRec2, TRUE);
        IF TmpRec2.FINDSET THEN
          REPEAT
            Rec := TmpRec2;
            "Entry No." := 0;
            INSERT;
          UNTIL TmpRec2.NEXT = 0;
        SetCodeChanged(TRUE);
      END;

      Rec := OriginalRec;
    END;

    LOCAL PROCEDURE CaptionExpr@16() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1 %2 %3', Object.Type, Object.ID, Object.Name));
    END;

    LOCAL PROCEDURE CheckGuidelinesSelection@17();
    VAR
      OriginalRec@1000 : Record 11102053;
      TmpCALHistoryLine@1001 : ARRAY [2] OF TEMPORARY Record 11102053;
      TmpSelectedCALHistoryLine@1002 : TEMPORARY Record 11102053;
      TmpCheckGuidelinesComment2@1003 : TEMPORARY Record 11102081;
      TranslationMgt@1004 : Codeunit 11102051;
      AutoApplyGuidelines@1005 : Codeunit 11102061;
      LineNo@1006 : Integer;
      CodeChanged2@1007 : Boolean;
    BEGIN
      OriginalRec := Rec;
      OriginalRec.SETVIEW(GETVIEW);

      CurrPage.SETSELECTIONFILTER(Rec);
      IF FINDSET THEN
        REPEAT
          TmpSelectedCALHistoryLine := Rec;
          TmpSelectedCALHistoryLine.INSERT;
        UNTIL NEXT = 0;

      Rec := OriginalRec;
      CLEARMARKS;
      MARKEDONLY(FALSE);
      SETVIEW(OriginalRec.GETVIEW);

      IF TmpSelectedCALHistoryLine.ISEMPTY THEN BEGIN
        TmpSelectedCALHistoryLine := Rec;
        TmpSelectedCALHistoryLine.INSERT;
      END;

      IF AutoApplyGuidelines.AutocorrectCode(Object, Rec, TmpCheckGuidelinesComment, TmpSelectedCALHistoryLine) THEN
        CodeChanged2 := TRUE;

      RESET;

      TranslationMgt.FillCALLineBuffer(Rec, TmpCALHistoryLine[1], 0);
      IF AutoApplyGuidelines.CorrectCaptions(Object, TmpCALHistoryLine, TmpSelectedCALHistoryLine) THEN BEGIN
        RESET;
        DELETEALL;
        IF TmpCALHistoryLine[1].FINDSET THEN
          REPEAT
            IF TmpCALHistoryLine[1]."Sub Line No." = 1 THEN
              LineNo += 1;

            Rec := TmpCALHistoryLine[1];
            "Line No." := LineNo;
            INSERT;

          UNTIL TmpCALHistoryLine[1].NEXT = 0;
        CodeChanged2 := TRUE;
      END;

      IF CodeChanged2 THEN BEGIN

        SetCodeChanged(TRUE);

        TmpCheckGuidelinesComment.RESET;
        IF TmpCheckGuidelinesComment.FINDSET THEN
          REPEAT
            TmpCheckGuidelinesComment2 := TmpCheckGuidelinesComment;
            TmpCheckGuidelinesComment2.INSERT;
          UNTIL TmpCheckGuidelinesComment.NEXT = 0;

        ResetGuidelinesComments(TmpCheckGuidelinesComment2);

      END;

      Rec := OriginalRec;
    END;

    BEGIN
    END.
  }
}

