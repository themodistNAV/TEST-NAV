OBJECT Codeunit 846 Cash Flow Wksh. -Register Line
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=846;
    Permissions=TableData 847=imd,
                TableData 848=imd;
    OnRun=BEGIN
            GLSetup.GET;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      CFWkshLine@1000 : Record 846;
      CFForecastEntry@1001 : Record 847;
      CashFlowForecast@1003 : Record 840;
      GLSetup@1007 : Record 98;
      CFAccount@1011 : Record 841;
      CFWkshCheckLine@1008 : Codeunit 845;
      NextEntryNo@1010 : Integer;

    PROCEDURE RunWithCheck@1001(VAR CFWkshLine2@1000 : Record 846);
    BEGIN
      CFWkshLine.COPY(CFWkshLine2);
      Code;
      CFWkshLine2 := CFWkshLine;
    END;

    LOCAL PROCEDURE Code@1002();
    BEGIN
      WITH CFWkshLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        CFWkshCheckLine.RunCheck(CFWkshLine);

        IF NextEntryNo = 0 THEN BEGIN
          CFForecastEntry.LOCKTABLE;
          IF CFForecastEntry.FINDLAST THEN
            NextEntryNo := CFForecastEntry."Entry No.";
          NextEntryNo := NextEntryNo + 1;
        END;

        CashFlowForecast.GET("Cash Flow Forecast No.");
        IF "Cash Flow Account No." <> '' THEN BEGIN
          CFAccount.GET("Cash Flow Account No.");
          CFAccount.TESTFIELD(Blocked,FALSE);
        END;

        CFForecastEntry.INIT;
        CFForecastEntry."Cash Flow Forecast No." := "Cash Flow Forecast No.";
        CFForecastEntry."Cash Flow Date" := "Cash Flow Date";
        CFForecastEntry."Document No." := "Document No.";
        CFForecastEntry."Cash Flow Account No." := "Cash Flow Account No.";
        CFForecastEntry."Source Type" := "Source Type";
        CFForecastEntry."Source No." := "Source No.";
        CFForecastEntry."G/L Budget Name" := "G/L Budget Name";
        CFForecastEntry.Description := Description;
        CFForecastEntry."Payment Discount" := ROUND("Payment Discount",0.00001);
        CFForecastEntry."Associated Entry No." := "Associated Entry No.";
        CFForecastEntry."Global Dimension 2 Code" := "Shortcut Dimension 2 Code";
        CFForecastEntry."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
        CFForecastEntry."Dimension Set ID" := "Dimension Set ID";
        CFForecastEntry."Amount (LCY)" := ROUND("Amount (LCY)",0.00001);
        CFForecastEntry.Positive := CFForecastEntry."Amount (LCY)" > 0;

        IF CFForecastEntry.Description = CashFlowForecast.Description THEN
          CFForecastEntry.Description := '';
        CFForecastEntry."User ID" := USERID;
        CFForecastEntry."Entry No." := NextEntryNo;

        CFForecastEntry.INSERT;

        NextEntryNo := NextEntryNo + 1;
      END;
    END;

    BEGIN
    END.
  }
}

