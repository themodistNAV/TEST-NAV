OBJECT Codeunit 1278 Exp. User Feedback Gen. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo=1220;
    Permissions=TableData 1226=rimd;
    OnRun=VAR
            CreditTransferRegister@1001 : Record 1205;
            GenJnlLine@1000 : Record 81;
            PaymentExportData@1002 : Record 1226;
          BEGIN
            GenJnlLine.SETRANGE("Data Exch. Entry No.","Entry No.");
            GenJnlLine.FINDFIRST;

            CreditTransferRegister.SETRANGE("From Bank Account No.",GenJnlLine."Bal. Account No.");
            CreditTransferRegister.FINDLAST;
            SetFileOnCreditTransferRegister(Rec,CreditTransferRegister);
            SetExportFlagOnGenJnlLine(GenJnlLine);

            PaymentExportData.SETRANGE("Data Exch Entry No.","Entry No.");
            PaymentExportData.DELETEALL(TRUE);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE SetFileOnCreditTransferRegister@5(DataExch@1000 : Record 1220;VAR CreditTransferRegister@1001 : Record 1205);
    BEGIN
      CreditTransferRegister.SetStatus(CreditTransferRegister.Status::"File Created");
      CreditTransferRegister.SetFileContent(DataExch);
    END;

    PROCEDURE SetExportFlagOnGenJnlLine@1(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlLine2@1001 : Record 81;
    BEGIN
      GenJnlLine2.COPYFILTERS(GenJnlLine);
      IF GenJnlLine2.FINDSET THEN
        REPEAT
          CASE GenJnlLine2."Account Type" OF
            GenJnlLine2."Account Type"::Vendor:
              SetExportFlagOnAppliedVendorLedgerEntry(GenJnlLine2);
            GenJnlLine2."Account Type"::Customer:
              SetExportFlagOnAppliedCustLedgerEntry(GenJnlLine2);
          END;
          GenJnlLine2.VALIDATE("Exported to Payment File",TRUE);
          GenJnlLine2.MODIFY(TRUE);
        UNTIL GenJnlLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE SetExportFlagOnAppliedVendorLedgerEntry@31(GenJnlLine@1000 : Record 81);
    VAR
      VendLedgerEntry@1001 : Record 25;
    BEGIN
      IF GenJnlLine.IsApplied THEN BEGIN
        VendLedgerEntry.SETRANGE("Vendor No.",GenJnlLine."Account No.");

        IF GenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
          VendLedgerEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
          VendLedgerEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        END;

        IF GenJnlLine."Applies-to ID" <> '' THEN
          VendLedgerEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

        IF VendLedgerEntry.FINDSET THEN
          REPEAT
            VendLedgerEntry.VALIDATE("Exported to Payment File",TRUE);
            CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgerEntry);
          UNTIL VendLedgerEntry.NEXT = 0;
      END;

      VendLedgerEntry.RESET;
      VendLedgerEntry.SETRANGE("Vendor No.",GenJnlLine."Account No.");
      VendLedgerEntry.SETRANGE("Applies-to Doc. Type",GenJnlLine."Document Type");
      VendLedgerEntry.SETRANGE("Applies-to Doc. No.",GenJnlLine."Document No.");
      IF VendLedgerEntry.FINDSET THEN
        REPEAT
          VendLedgerEntry.VALIDATE("Exported to Payment File",TRUE);
          CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgerEntry);
        UNTIL VendLedgerEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE SetExportFlagOnAppliedCustLedgerEntry@2(GenJnlLine@1000 : Record 81);
    VAR
      CustLedgerEntry@1001 : Record 21;
    BEGIN
      IF GenJnlLine.IsApplied THEN BEGIN
        CustLedgerEntry.SETRANGE("Customer No.",GenJnlLine."Account No.");

        IF GenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
          CustLedgerEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
          CustLedgerEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        END;

        IF GenJnlLine."Applies-to ID" <> '' THEN
          CustLedgerEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

        IF CustLedgerEntry.FINDSET THEN
          REPEAT
            CustLedgerEntry.VALIDATE("Exported to Payment File",TRUE);
            CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgerEntry);
          UNTIL CustLedgerEntry.NEXT = 0;
      END;

      CustLedgerEntry.RESET;
      CustLedgerEntry.SETRANGE("Customer No.",GenJnlLine."Account No.");
      CustLedgerEntry.SETRANGE("Applies-to Doc. Type",GenJnlLine."Document Type");
      CustLedgerEntry.SETRANGE("Applies-to Doc. No.",GenJnlLine."Document No.");

      IF CustLedgerEntry.FINDSET THEN
        REPEAT
          CustLedgerEntry.VALIDATE("Exported to Payment File",TRUE);
          CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgerEntry);
        UNTIL CustLedgerEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

