OBJECT Codeunit 9062560 Panel Data Toolkit
{
  OBJECT-PROPERTIES
  {
    Date=28/08/15;
    Time=[ 1:26:46 PM];
    Version List=NA2016.8;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      tDataSource@1000000000 : TextConst 'ENU=Please, select data source.;ENG=Please, select data source.';
      mPanelData@1000000001 : Record 9062432;
      mObjectFieldToolkit@1000000002 : Codeunit 9062343;
      mQuickReportToolkit@1000000003 : Codeunit 9062538;

    PROCEDURE "--- Data From ---"@1000000001();
    BEGIN
    END;

    PROCEDURE DataFromName@1000000000(pDataSource@1000000001 : Integer;VAR pTableId@1000000004 : Integer;VAR pQuickReportCode@1000000003 : Code[60]) : Text;
    VAR
      lName@1000000002 : Text;
    BEGIN
      //DOC NA2015.8 - Returning data from name
      //  -> pDataSource: Data source
      //  -> pTableId: Table ID
      //  -> pQuickReportCode: Quick report code
      //  <- The name

      lName := '';

      CASE (pDataSource) OF
        mPanelData."Data Source"::Table:
          lName := mObjectFieldToolkit.GetTableName(pTableId);
        mPanelData."Data Source"::"Quick Report":
          lName := mQuickReportToolkit.QuickReportName(pQuickReportCode);
      END;

      EXIT(lName);
    END;

    PROCEDURE ValidateDataFrom@1000000002(VAR pDataSource@1000000000 : '0,1,2,3,4,5,6,7,8,9';VAR pDataFrom@1000000005 : Text[50];VAR pTableId@1000000001 : Integer;VAR pQuickReportCode@1000000003 : Code[60]);
    VAR
      lDataSources@1000000002 : ARRAY [2] OF Integer;
      lDataSourceIndex@1000000004 : Integer;
      lDataSourceText@1000000008 : Text;
      lValidated@1000000006 : Boolean;
      eUnknown@1000000007 : TextConst 'ENU=Unknown %1: "%2".;ENG=Unknown %1: "%2".';
    BEGIN
      //DOC NA2015.8 - Validating "Data From"
      //  <> pDataSource: Source / target data source
      //  <> pDataFrom: Source / target data from
      //  <> pTableId: Source / target table ID
      //  <> pQuickReportCode: Source / target quick report code

      CASE (pDataSource) OF
        mPanelData."Data Source"::"Quick Report":
          BEGIN
            lDataSources[1] := mPanelData."Data Source"::"Quick Report";
            lDataSources[2] := mPanelData."Data Source"::Table;
          END;
        ELSE
          BEGIN
            lDataSources[1] := mPanelData."Data Source"::Table;
            lDataSources[2] := mPanelData."Data Source"::"Quick Report";
          END;
      END;

      lValidated := TRUE;
      FOR lDataSourceIndex := 1 TO ARRAYLEN(lDataSources) DO
      BEGIN
        CASE (lDataSources[lDataSourceIndex]) OF
          mPanelData."Data Source"::Table:
            lValidated := mObjectFieldToolkit.TryToValidateTableName(pTableId, pDataFrom, FALSE);
          mPanelData."Data Source"::"Quick Report":
            lValidated := mQuickReportToolkit.TryToValidateQuickReportName(pQuickReportCode, pDataFrom, FALSE);
        END;
        IF (lValidated) THEN
        BEGIN
          pDataSource := lDataSources[lDataSourceIndex];
          lDataSourceIndex := ARRAYLEN(lDataSources);
        END;
      END;

      IF ( NOT (lValidated)) THEN
      BEGIN
        IF (pDataSource <> mPanelData."Data Source"::" ") THEN
          lDataSourceText := LOWERCASE(FORMAT(mPanelData."Data Source"))
        ELSE
          lDataSourceText := LOWERCASE(mPanelData.FIELDCAPTION("Data Source"));
        ERROR(eUnknown, lDataSourceText, pDataFrom);
      END;
    END;

    PROCEDURE DataFromLookup@1000000003(pDataSource@1000000003 : Integer;VAR pDataFrom@1000000002 : Text[50];VAR pTableId@1000000001 : Integer;VAR pQuickReportCode@1000000000 : Code[60]) : Boolean;
    VAR
      lResult@1000000004 : Boolean;
    BEGIN
      //DOC NA2015.8 - "Data From" lookup
      //  -> pDataSource: Data source
      //  <> pDataFrom: Source / target data from
      //  <> pTableId: Source / target table ID
      //  <> pQuickReportCode: Source / target quick report code
      //  <- Lookup result

      lResult := FALSE;

      CASE (pDataSource) OF
        mPanelData."Data Source"::Table:
          lResult := mObjectFieldToolkit.TableLookup(pTableId);
        mPanelData."Data Source"::"Quick Report":
          lResult := mQuickReportToolkit.QuickReportLookup(pQuickReportCode);
      END;

      EXIT(lResult);
    END;

    BEGIN
    {
      //DOC NA2015.8 JH 22/06/2015 - Created
      //DOC NA2016.8 JH 28/08/2015 - Upgraded to 2016
    }
    END.
  }
}

