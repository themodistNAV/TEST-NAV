OBJECT Table 9021692 Web Location
{
  OBJECT-PROPERTIES
  {
    Date=13/07/16;
    Time=[ 1:50:08 PM];
    Version List=EC2016.6;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LabelRec.GET("Label Code");
               LabelRec.TESTFIELD("Default User Country Code");
               "Country Code" := LabelRec."Default User Country Code";
               DateTimeInsertedModified:=CURRENTDATETIME;
             END;

    OnModify=BEGIN
               DateTimeInsertedModified:=CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               IF NOT CONFIRM(Text001) THEN
                 ERROR(Text002);

               WebWorkDay.RESET;
               WebWorkDay.SETRANGE("Label Code","Label Code");
               WebWorkDay.SETRANGE("Web Location Code",Code);
               WebWorkDay.DELETEALL;

               WebLocOS.RESET;
               WebLocOS.SETRANGE("Label Code","Label Code");
               WebLocOS.SETRANGE("Web Location Code",Code);
               WebLocOS.DELETEALL;

               WebLocCS.RESET;
               WebLocCS.SETRANGE("Label Code","Label Code");
               WebLocCS.SETRANGE("Web Location Code",Code);
               WebLocCS.DELETEALL;

               WebLocPC.RESET;
               WebLocPC.SETRANGE("Label Code","Label Code");
               WebLocPC.SETRANGE("Web Location Code",Code);
               WebLocPC.DELETEALL;

               WebAttribValue.RESET;
               WebAttribValue.SETRANGE("Label Code","Label Code");
               WebAttribValue.SETRANGE("Table ID",9021692);
               WebAttribValue.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=[ENU=Web Location;
               ENG=Web Location];
    LookupPageID=Page9021756;
    DrillDownPageID=Page9021756;
  }
  FIELDS
  {
    { 1   ;   ;Label Code          ;Code10        ;TableRelation=Label;
                                                   OnValidate=BEGIN
                                                                //DOC EC2013.4.00 - and removed code from Label Code - OnLookup
                                                                IF "Label Code" <> '' THEN BEGIN
                                                                  LabelRec.GET("Label Code");
                                                                  IF LabelRec."Used-in Company" <> COMPANYNAME THEN
                                                                    ERROR(Text000,LabelRec."Used-in Company");
                                                                END;
                                                                //DOC EC2013.4.00 +
                                                              END;

                                                   CaptionML=[ENU=Label Code;
                                                              ENG=Label Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ENG=Code];
                                                   NotBlank=Yes }
    { 10  ;   ;Name                ;Text80        ;CaptionML=[ENU=Name;
                                                              ENG=Name] }
    { 13  ;   ;DateTimeInsertedModified;DateTime  ;CaptionML=[ENU=DateTimeInsertedModified;
                                                              ENG=DateTimeInsertedModified] }
    { 20  ;   ;NAV Location        ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=NAV Location;
                                                              ENG=NAV Location] }
    { 22  ;   ;Base Calendar       ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[ENU=Base Calendar;
                                                              ENG=Base Calendar] }
    { 28  ;   ;No. of Web Location Post Codes;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Web Location Postcode" WHERE (Label Code=FIELD(Label Code),
                                                                                                    Web Location Code=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Web Location Post Codes;
                                                              ENG=No. of Web Location Post Codes] }
    { 30  ;   ;Country Code        ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country Code;
                                                              ENG=Country Code];
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;Label Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LabelRec@1000000000 : Record 9021618;
      Text000@1000000001 : TextConst 'ENU=You can not have a label that is associated with a different company %1.;ENG=You can not have a label that is associated with a different company %1.';
      Text001@1000000002 : TextConst 'ENU=Deleting the Web Location will delete all the underlying data.\\Are you sure you want to continue?;ENG=Deleting the Web Location will delete all the underlying data.\\Are you sure you want to continue?';
      Text002@1000000003 : TextConst 'ENU=Change Abandoned.;ENG=Change Abandoned.';
      WebLocPC@1000000004 : Record 9021696;
      Text003@1000000006 : TextConst 'ENU=Sorry, you are not allowed to rename a Web Location.  You have to delete it and enter it again.;ENG=Sorry, you are not allowed to rename a Web Location.  You have to delete it and enter it again.';
      WebWorkDay@1000000007 : Record 9021699;
      WebAttribValue@1000000009 : Record 9021702;
      WebLocOS@1000000010 : Record 9021693;
      WebLocCS@1000000011 : Record 9021694;
      DeliveryType@1000000013 : Record 9021700;
      DeliveryType1@1000000014 : Record 9021700;

    PROCEDURE CopyDeliveryTypesFromLabel@1000000000();
    BEGIN
      TESTFIELD(Code);
      TESTFIELD("Label Code");
      DeliveryType.RESET;
      DeliveryType.SETRANGE("Label Code","Label Code");
      DeliveryType.SETFILTER("Web Location Code",'%1','');
      IF DeliveryType.FIND('-') THEN
      REPEAT
        DeliveryType1.INIT;
        DeliveryType1 := DeliveryType;
        DeliveryType1."Web Location Code" := Code;
        IF DeliveryType1.INSERT THEN;
      UNTIL DeliveryType.NEXT = 0;
    END;

    BEGIN
    {
      //DOC EC2013.4.00 MQ 06/11/2013 - GW131106 RTC
      //DOC EC2016.6    JH 05/10/2016 - ECom 2016
    }
    END.
  }
}

