OBJECT Page 9062785 P. Inv. Matching PDF Part
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=P. Inv. Matching PDF Part;
               ENG=P. Inv. Matching PDF Part];
    SourceTable=Table9062272;
    PageType=CardPart;
    OnAfterGetRecord=VAR
                       lLoaded@1000000000 : Boolean;
                     BEGIN
                       IF (LOWERCASE(Rec.Extension) = 'pdf') THEN
                       BEGIN
                         CASE (Rec."Storage Type") OF
                           Rec."Storage Type"::BLOB:
                             BEGIN
                               Rec.CALCFIELDS(Attachment);
                               IF (Rec.Attachment.HASVALUE) THEN
                               BEGIN
                                 mAttachmentBlob.Blob := Rec.Attachment;
                                 mPdfViewerManagement.OpenBlob(mAttachmentBlob, mBlob);
                                 lLoaded := TRUE;
                               END;
                             END;
                           Rec."Storage Type"::FilePath:
                             IF (Rec."Stored FilePath" <> '') THEN
                             BEGIN
                               IF (mFilePath <> '') THEN
                               BEGIN
                                 mFileSystem.DeleteFileIfExists(TRUE, mFilePath);
                                 mFilePath := '';
                               END;
                               mFilePath := mFileSystem.GetTempPathForFile(TRUE, mFileSystem.GetFileName(TRUE, Rec."Stored FilePath"));
                               mFileSystem.DeleteFileIfExists(TRUE, mFilePath);
                               mFilePath := mFileSystem.DownloadSilent(TRUE, Rec."Stored FilePath", mFilePath);
                               mPdfViewerManagement.OpenFile(mFilePath, mBlob);
                               lLoaded := TRUE;
                             END;
                         END;
                       END;
                       IF ( NOT (lLoaded)) THEN
                         ClearBlob();
                     END;

  }
  CONTROLS
  {
    { 1000000000;;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=PDFViewer;
                CaptionML=[ENU=PDF Viewer;
                           ENG=PDF Viewer];
                GroupType=Group }

    { 1000000002;2;Field  ;
                Name=PDF;
                CaptionML=[ENU=PDF;
                           ENG=PDF];
                SourceExpr=mBlob.Blob;
                ControlAddIn=[TheNavPeople.PdfViewer;PublicKeyToken=7ddee031ca97e96c];
                ShowCaption=No }

  }
  CODE
  {
    VAR
      mAttachmentBlob@1000000002 : Record 99008535;
      mBlob@1000000000 : Record 99008535;
      mFilePath@1000000003 : Text;
      mFileSystem@1000000004 : Codeunit 9062264;
      mPdfViewerManagement@1000000001 : Codeunit 9062424;

    LOCAL PROCEDURE ClearBlob@1000000000();
    BEGIN
      IF (mFilePath <> '') THEN
      BEGIN
        mFileSystem.DeleteFileIfExists(TRUE, mFilePath);
        mFilePath := '';
      END;
      CLEAR(mBlob);
    END;

    BEGIN
    {
      //DOC NH2015.1  JH 08/04/2014 - Created
      //DOC NH2016.10 JH 29/10/2015 - Upgraded to 2016
      //DOC NH2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

