OBJECT Page 9086500 Budget Adj. Factor Entries
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NH2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Budget Adj. Factor Entries;
               ENG=Budget Adj. Factor Entries];
    SourceTable=Table9062862;
    PageType=ListPart;
    OnAfterGetRecord=VAR
                       AdjFactor@1100710000 : Record 9062861;
                     BEGIN
                       IF AdjFactor.GET(GetFactor) THEN BEGIN
                         AdjBy_Percentage := AdjFactor."Adjust By" = AdjFactor."Adjust By"::Percentage;
                         AdjBy_Quantity := AdjFactor."Adjust By" = AdjFactor."Adjust By"::Quantity;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1000000004;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000006;1 ;Action    ;
                      Name=Add Current Year;
                      CaptionML=[ENU=Add Current Year;
                                 ENG=Add Current Year];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=CalendarMachine;
                      PromotedCategory=New;
                      OnAction=VAR
                                 Entry@1000000000 : Record 9062862;
                                 WeekStartDate@1000000001 : Date;
                                 Year@1000000002 : Integer;
                                 WeekNo@1000000003 : Integer;
                                 FactorCode@1000000004 : Code[20];
                               BEGIN
                                 FactorCode := GetFactor;

                                 WeekStartDate := CALCDATE('<CD-CY>',TODAY);
                                 REPEAT
                                   WeekNo += 1;
                                   IF WeekNo > 1 THEN WeekStartDate := CALCDATE('<CD-CY+' + FORMAT(WeekNo-1,0,9) + 'W>',TODAY);
                                   Year := DATE2DMY(WeekStartDate,3);
                                   IF Year = DATE2DMY(TODAY,3) THEN BEGIN
                                     IF NOT Entry.GET(FactorCode,WeekStartDate) THEN BEGIN
                                       Entry.INIT;
                                       Entry.VALIDATE("Factor Code",FactorCode);
                                       Entry.VALIDATE("Week Start",WeekStartDate);
                                       Entry.INSERT(TRUE);
                                     END;
                                   END;
                                 UNTIL Year > DATE2DMY(TODAY,3);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                CaptionML=[ENU=Group;
                           ENG=Group];
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                SourceExpr="Factor Code";
                Visible=FALSE }

    { 1000000003;2;Field  ;
                SourceExpr="Week Start" }

    { 1000000005;2;Field  ;
                SourceExpr="Adjustment %";
                Visible=AdjBy_Percentage }

    { 1100710000;2;Field  ;
                SourceExpr="Adjustment Quantity";
                Visible=AdjBy_Quantity }

  }
  CODE
  {
    VAR
      AdjBy_Percentage@1100710000 : Boolean INDATASET;
      AdjBy_Quantity@1100710001 : Boolean INDATASET;

    LOCAL PROCEDURE GetFactor@1000000000() : Code[20];
    VAR
      FactorFilter@1000000001 : Code[20];
      i@1000000000 : Integer;
      Factor@1000000002 : Record 9062861;
      eNoFactorFound@1000000003 : TextConst 'ENU=No factor found;ENG=No factor found';
    BEGIN
      i := -1;
      REPEAT
        i += 1;
        FILTERGROUP(i);
      UNTIL (i >= 10) OR (GETFILTER("Factor Code") <> '');
      FactorFilter := GETFILTER("Factor Code");
      IF FactorFilter = '' THEN ERROR(eNoFactorFound);
      Factor.SETFILTER(Code,FactorFilter);
      IF NOT Factor.FINDFIRST THEN ERROR(eNoFactorFound);
      EXIT(Factor.Code);
    END;

    BEGIN
    {
      //DOC NH2016.15 JH 25/04/2016 - CfMD amendments
    }
    END.
  }
}

