OBJECT XMLport 9062407 Enhanced View - Menu Data
{
  OBJECT-PROPERTIES
  {
    Date=10/05/16;
    Time=12:00:00 PM;
    Version List=NA2016.15;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Enhanced View - Menu Data;
               ENG=Enhanced View - Menu Data];
    Encoding=ISO-8859-2;
    XMLVersionNo=1.1;
    Format/Evaluate=XML Format/Evaluate;
    OnPreXMLport=VAR
                   Column@1000000000 : TEMPORARY Record 9062805;
                   Cell@1000000001 : TEMPORARY Record 9062806;
                 BEGIN
                   AsArray2 := 'true';
                   EnhViewData.GetEnhViewState(EnhViewCalcMgt);
                   EnhViewCalcMgt.GetData(EnhView, SelectedGroupByField, SelectedTotalsField, Column, Cell);
                   EnhViewCalcMgt.GetGroupByFields(GroupByField);
                   EnhViewCalcMgt.GetTotalsFields(TotalsField);
                 END;

  }
  ELEMENTS
  {
    { [{E4C0F020-7258-419D-AF2E-AD24C667C7E0}];  ;EnhView             ;Element ;Table   ;
                                                  VariableName=EnhView;
                                                  SourceTable=Table9062804;
                                                  Temporary=Yes;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             BuildMenuItems();
                                                                           END;
                                                                            }

    { [{911D5024-6469-4A8D-A9C7-EB58B2F520EC}];1 ;HideMenu            ;Element ;Field   ;
                                                  DataType=Boolean;
                                                  SourceField=EnhView::Hide Menu Bar }

    { [{31DC7E6C-B862-4F04-A93B-2FF22BF947DF}];1 ;MenuItem            ;Element ;Table   ;
                                                  VariableName=MenuItem;
                                                  SourceTable=Table9062809;
                                                  Temporary=Yes;
                                                  MinOccurs=Zero;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             MenuItem_Type := FORMAT(MenuItem.Type);
                                                                           END;
                                                                            }

    { [{7851A35E-5494-4E25-AB7B-B8C84183E887}];2 ;asArray             ;Attribute;Text   ;
                                                  VariableName=AsArray2 }

    { [{50D49B15-8982-4166-8F90-DC5F11E736AB}];2 ;Id                  ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=MenuItem::Entry No. }

    { [{E392F8A7-00E1-440B-9E1E-93AA0DBC932B}];2 ;ParentId            ;Element ;Field   ;
                                                  DataType=Integer;
                                                  SourceField=MenuItem::Parent Entry No. }

    { [{B786226B-73B2-43B9-965B-7E1E7FCFC55B}];2 ;ItemType            ;Element ;Text    ;
                                                  VariableName=MenuItem_Type;
                                                  DataType=Option }

    { [{0A01DF3E-A9C1-4E2F-BBC7-171052A339F7}];2 ;Caption             ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=MenuItem::Caption }

    { [{26E48843-4CB7-41BB-80AF-7E35D6FD5B6E}];2 ;Caption2            ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=MenuItem::Caption 2 }

    { [{B13DE3A6-7102-4450-9495-FC09096B2E01}];2 ;Command             ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=MenuItem::Command Name }

    { [{59C29E72-E68B-4D96-851C-DA5BB9643BCA}];2 ;Parameters          ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=MenuItem::Command Parameters }

    { [{AE178437-1CBF-49A5-8AD8-9D7E0A97AFB4}];2 ;Selected            ;Element ;Field   ;
                                                  DataType=Boolean;
                                                  SourceField=MenuItem::Selected }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      SelectedGroupByField@1000000005 : TEMPORARY Record 9062807;
      SelectedTotalsField@1000000006 : TEMPORARY Record 9062807;
      GroupByField@1000000004 : TEMPORARY Record 9062807;
      TotalsField@1000000001 : TEMPORARY Record 9062807;
      EnhViewData@1000000002 : Codeunit 9062678;
      EnhFldMgt@1000000007 : Codeunit 9062298;
      EnhViewMgt@1000000015 : Codeunit 9062679;
      EnhViewCalcMgt@1000000003 : Codeunit 9062674;
      "--- Menu Action Variables ---"@1000000012 : Integer;
      MenuItemId@1000000014 : Integer;
      txtGroupBy@1000000016 : TextConst 'ENU=Group by:;ENG=Group by:';
      txtGroupByDropDown@1000000021 : TextConst 'ENU=Other...;ENG=Other...';
      txtTotals@1000000017 : TextConst 'ENU=Totals:;ENG=Totals:';
      txtChartType@1000000018 : TextConst 'ENU=Chart type:;ENG=Chart type:';
      txtLookup@1000000019 : TextConst 'ENU=Select...;ENG=Select...';
      txtDateGroup@1000000022 : TextConst 'ENU=Date;ENG=Date';

    LOCAL PROCEDURE BuildMenuItems@1000000007();
    VAR
      NameValueBuffer@1000000003 : TEMPORARY Record 9062730;
      NameValueBuffer2@1000000006 : TEMPORARY Record 9062730;
      ChartName@1000000004 : Text;
      GroupByCaption@1000000002 : Text;
      GroupByCaption2@1000000007 : Text;
      ParentMenuId@1000000000 : Integer;
    BEGIN

      //Group by fields >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
      GroupByCaption2 := txtGroupBy;
      GroupByCaption := SelectedGroupByField.Caption;

      GroupByField.RESET();
      //Fast select fields
      GroupByField.SETRANGE("Fast Select", TRUE);
      IF (GroupByField.FINDSET()) THEN BEGIN
        AddMenuItem(0, MenuItem.Type::Caption, txtGroupBy, '', '', '', FALSE);
        REPEAT
          AddMenuItem(0, MenuItem.Type::Action, GroupByField.Caption, '', 'groupby',
            FORMAT(GroupByField."Line No.", 0, 9), SelectedGroupByField.FieldEquals(GroupByField));
        UNTIL (GroupByField.NEXT() = 0);

        //add current field if not in fast select group
        IF (NOT(SelectedGroupByField."Fast Select")) THEN
          AddMenuItem(0, MenuItem.Type::Action, SelectedGroupByField.Caption, '', 'groupby',
            FORMAT(SelectedGroupByField."Line No.", 0, 9), TRUE);

        GroupByCaption2 := '';
        GroupByCaption := txtGroupByDropDown;
      END;
      //other fields
      GroupByField.SETRANGE("Fast Select", FALSE);
      IF (GroupByField.FINDSET()) THEN BEGIN
        ParentMenuId := AddMenuGroup(0, GroupByCaption, GroupByCaption2);
        REPEAT
          AddMenuItem(ParentMenuId, MenuItem.Type::Action, GroupByField.Caption, '', 'groupby',
            FORMAT(GroupByField."Line No.", 0, 9), SelectedGroupByField.FieldEquals(GroupByField));
        UNTIL (GroupByField.NEXT() = 0);
        //Lookup is disabled
        //AddMenuItem(ParentMenuId, MenuItem.Type::Action, txtLookup, 'lookupgroup', '', FALSE);
      END;
      //Group by fields <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

      //Totals >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
      TotalsField.RESET();
      AddSeparator(0);
      ParentMenuId := AddMenuGroup(0, SelectedTotalsField.Caption, txtTotals);
      IF (TotalsField.FINDSET()) THEN BEGIN
        REPEAT
          AddMenuItem(ParentMenuId, MenuItem.Type::Action, TotalsField.Caption, '', 'totals',
            FORMAT(TotalsField."Line No.", 0, 9), SelectedTotalsField.FieldEquals(TotalsField));
        UNTIL (TotalsField.NEXT() = 0);
      END;
      //AddMenuItem(ParentMenuId, MenuItem.Type::Action, txtLookup, 'lookuptotals', '', FALSE);
      //Totals <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

      //Charts >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
      IF (EnhView."View Mode" = EnhView."View Mode"::Chart) THEN BEGIN
        EnhViewMgt.GetChartList(NameValueBuffer);
        ChartName := EnhView."Chart Type";
        NameValueBuffer.RESET();
        NameValueBuffer.SETRANGE(Value, EnhView."Chart Type");
        IF (NameValueBuffer.FINDFIRST()) THEN
          ChartName := NameValueBuffer.Name;
        NameValueBuffer.RESET();

        AddSeparator(0);
        ParentMenuId := AddMenuGroup(0, ChartName, txtChartType);
        IF (NameValueBuffer.FINDSET()) THEN BEGIN
          REPEAT
            AddMenuItem(ParentMenuId, MenuItem.Type::Action, NameValueBuffer.Name, '', 'charttype',
              NameValueBuffer.Value, NameValueBuffer.Value = EnhView."Chart Type");
          UNTIL (NameValueBuffer.NEXT() = 0);
        END;
        AddMenuItem(ParentMenuId, MenuItem.Type::Action, txtLookup, '', 'lookupcharttype', '', FALSE);
      END;
      //Charts <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

      //Date Functions >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
      IF (SelectedGroupByField."Group By Date Function Avail") THEN BEGIN
        EnhViewMgt.GetDateFunctionList(NameValueBuffer2, SelectedGroupByField."Group By Date Function");
        ParentMenuId := AddButtonGroup(0, txtDateGroup);
        IF (NameValueBuffer2.FINDSET()) THEN BEGIN
          REPEAT
            AddMenuItem(ParentMenuId, MenuItem.Type::Action, NameValueBuffer2.Name, '',
              'groupfunc', NameValueBuffer2.Value, NameValueBuffer2.Selected);
          UNTIL (NameValueBuffer2.NEXT() = 0);
        END;
      END;
      //Date Functions <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    END;

    LOCAL PROCEDURE AddMenuGroup@1000000000(ParentItemId@1000000000 : Integer;Caption@1000000001 : Text;Caption2@1000000002 : Text) : Integer;
    BEGIN
      EXIT(AddMenuItem(ParentItemId, MenuItem.Type::Group, Caption, Caption2, '', '', FALSE));
    END;

    LOCAL PROCEDURE AddButtonGroup@1000000002(ParentItemId@1000000000 : Integer;Caption@1000000001 : Text) : Integer;
    BEGIN
      EXIT(AddMenuItem(ParentItemId, MenuItem.Type::ButtonGroup, Caption, '', '', '', FALSE));
    END;

    LOCAL PROCEDURE AddSeparator@1000000005(ParentItemId@1000000000 : Integer) : Integer;
    BEGIN
      EXIT(AddMenuItem(ParentItemId, MenuItem.Type::Separator, '', '', '', '', FALSE));
    END;

    LOCAL PROCEDURE AddMenuItem@1000000003(MenuGroupId@1000000000 : Integer;Type@1000000004 : Integer;Caption@1000000001 : Text;Caption2@1000000006 : Text;Command@1000000002 : Text;Parameters@1000000003 : Text;Selected@1000000005 : Boolean) : Integer;
    BEGIN
      MenuItemId += 1;

      IF (Caption = '') THEN
        Caption := '-';

      MenuItem.INIT();
      MenuItem."Entry No." := MenuItemId;
      MenuItem."Parent Entry No." := MenuGroupId;
      MenuItem.Type := Type;
      MenuItem.Index := 0;
      MenuItem.Caption := Caption;
      MenuItem."Caption 2" := Caption2;
      MenuItem."Command Name" := Command;
      MenuItem."Command Parameters" := Parameters;
      MenuItem.Selected := Selected;
      MenuItem.INSERT();

      EXIT(MenuItem."Entry No.");
    END;

    LOCAL PROCEDURE AddGroupFunc@1000000001(ParentItemId@1000000000 : Integer;GroupFunctionId@1000000001 : Integer;Caption@1000000002 : Text;SelectedGroupFunctionId@1000000003 : Integer) : Integer;
    BEGIN
      EXIT(AddMenuItem(ParentItemId, MenuItem.Type::Action, Caption, '', 'groupfunc',
        FORMAT(GroupFunctionId), GroupFunctionId = SelectedGroupFunctionId));
    END;

    BEGIN
    {
      //DOC NA2016.14 AZ 23/03/2016 - Created
      //DOC NA2016.15 AZ 12/04/2016 - Midifications and bugfixes, Quick Reports support added
      //DOC NA2016.15 JH 10/05/2016 - CfMD amendments
    }
    END.
  }
}

